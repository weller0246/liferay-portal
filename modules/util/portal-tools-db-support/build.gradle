apply plugin: "com.liferay.maven.plugin.builder"

String h2Version = "1.4.193"

buildPluginDescriptor {
	ext {
		autoClean = false
	}

	finalizedBy formatSource

	goalPrefix = "db-support"
}

configurations {
	mavenDistribution
}

dependencies {
	compileInclude group: "com.beust", name: "jcommander", version: "1.58"

	compileOnly group: "com.liferay", name: "com.liferay.portal.tools.service.builder", transitive: false, version: "1.0.135"
	compileOnly group: "org.apache.ant", name: "ant", transitive: false, version: "1.10.11"
	compileOnly group: "org.apache.maven", name: "maven-plugin-api", transitive: false, version: "3.0.4"

	mavenDistribution classifier: "bin", ext: "zip", group: "org.apache.maven", name: "apache-maven", version: "3.3.9"

	testCompile group: "com.h2database", name: "h2", version: h2Version
	testCompile group: "com.liferay", name: "com.liferay.maven.executor", version: "1.0.2"
	testCompile group: "org.apache.ant", name: "ant-testutil", version: "1.9.7"
}

javadoc {
	options.tags = ["goal", "parameter", "required"]
}

liferayOSGi {
	expandCompileInclude = true
}

test {
	dependsOn install

	doFirst {
		systemProperty "maven.distribution.file.name", configurations.mavenDistribution.singleFile
	}

	systemProperty "db.support.version", jar.version
	systemProperty "h2.version", h2Version
	systemProperty "http.proxyHost", System.properties["http.proxyHost"]
	systemProperty "http.proxyPort", System.properties["http.proxyPort"]
	systemProperty "maven.debug", System.properties["maven.debug"]
	systemProperty "maven.repo.local", System.properties["maven.repo.local"]
	systemProperty "repository.url", System.properties["repository.url"]
}

updateFileVersions {
	match(/com\.liferay\.portal\.tools\.db\.support=(\d.+)/, "../../sdk/gradle-plugins/src/main/resources/com/liferay/gradle/plugins/dependencies/portal-tools.properties")
}
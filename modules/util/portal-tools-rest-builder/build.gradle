apply plugin: "com.liferay.maven.plugin.builder"

buildPluginDescriptor {
	ext {
		autoClean = false
	}

	finalizedBy formatSource

	goalPrefix = "rest-builder"
}

dependencies {
	compile group: "com.puppycrawl.tools", name: "checkstyle", version: "8.29"
	compile group: "commons-io", name: "commons-io", version: "2.8.0"
	compile group: "org.freemarker", name: "freemarker", transitive: false, version: "2.3.28"

	compileInclude group: "com.beust", name: "jcommander", version: "1.48"
	compileInclude group: "org.yaml", name: "snakeyaml", version: "1.27"
	compileInclude project(":apps:portal-vulcan:portal-vulcan-api")

	compileOnly group: "com.liferay", name: "com.liferay.petra.lang", version: "1.0.0"
	compileOnly group: "com.liferay", name: "com.liferay.petra.string", version: "5.0.1"
	compileOnly group: "com.liferay", name: "com.liferay.portal.tools.java.parser", version: "1.0.41"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	compileOnly group: "javax.ws.rs", name: "javax.ws.rs-api", version: "2.1"
	compileOnly group: "org.apache.ant", name: "ant", transitive: false, version: "1.10.11"
	compileOnly group: "org.apache.maven", name: "maven-plugin-api", transitive: false, version: "3.0.4"
}

deployDependencies {
	from (configurations.compile) {
		include "checkstyle-*.jar"
	}

	rename(/animal-sniffer-annotations-(.+)\.jar/, "animal-sniffer-annotations" + renameSuffix)
	rename(/antlr-(.+)\.jar/, "antlr" + renameSuffix)
	rename(/checker-qual-(.+)\.jar/, "checker-qual" + renameSuffix)
	rename(/checkstyle-(.+)\.jar/, "checkstyle" + renameSuffix)
	rename(/error_prone_annotations-(.+)\.jar/, "error_prone_annotations" + renameSuffix)
	rename(/j2objc-annotations-(.+)\.jar/, "j2objc-annotations" + renameSuffix)
	rename(/jsr305-(.+)\.jar/, "jsr305" + renameSuffix)
	rename(/Saxon-HE-(.+)\.jar/, "Saxon-HE" + renameSuffix)
}

liferay {
	deployDir = "../../../tools/sdk/dependencies/com.liferay.portal.tools.rest.builder/lib"
}

liferayOSGi {
	expandCompileInclude = true
}

updateFileVersions {
	match(/\t\t\t\t<version>(\d.+)<\/version>/, "samples/pom.xml")
	match(/com\.liferay\.portal\.tools\.rest\.builder=(\d.+)/, "../../sdk/gradle-plugins/src/main/resources/com/liferay/gradle/plugins/dependencies/portal-tools.properties")
	match(/\t\t<dependency name="com.liferay.portal.tools.rest.builder" org="com.liferay" rev="(\d.+)" \/>/, "../../../tools/sdk/dependencies/com.liferay.portal.tools.rest.builder/ivy.xml")
}
@component-name = "portal-commerce"
definition {

	property custom.properties = "jsonws.web.service.paths.excludes=${line.separator}company.security.strangers.verify=false";
	property portal.release = "true";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Order Management";

	setUp {
		CommerceConfiguration.commerceSetUp(minium = "true");

		task ("Setup: Add a Minium page with an Organization Management widget") {
			JSONLayout.addPublicLayout(
				groupName = "Minium",
				layoutName = "Organization Management Widget Page");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Minium",
				layoutName = "Organization Management Widget Page",
				widgetName = "Organization Management");
		}

		task ("Setup: Add an organization") {
			JSONOrganization.addOrganization(
				organizationName = "Organization 1",
				organizationSite = "false");
		}
	}

	tearDown {
		CommerceConfiguration.commerceTearDown();
	}

	@description = "COMMERCE-9403. Verify new users can be added using the Organization Management widget."
	@priority = "3"
	test CanAddNewUserUsingWidget {
		task ("When the Organization Management Widget Page is reached") {
			Navigator.openSitePage(
				pageName = "Organization Management Widget Page",
				siteName = "Minium");

			Click(locator1 = "Button#EXPAND");
		}

		task ("And the organization svg node is clicked") {
			Click(
				key_nodeName = "Organization 1",
				locator1 = "CommerceOrganizationManagementWidget#NODE_TITLE");
		}

		task ("And the plus button is clicked") {
			Click(locator1 = "CommerceOrganizationManagementWidget#PLUS_BUTTON");
		}

		task ("And the user svg node is clicked") {
			WaitForVisible(
				key_orgType = "user",
				locator1 = "CommerceOrganizationManagementWidget#NODE_ACTION");

			Click(
				key_orgType = "user",
				locator1 = "CommerceOrganizationManagementWidget#NODE_ACTION");
		}

		task ("Then the admin is able to create and add a new user to the Organization by adding the user email and selecting the organization role") {
			Type(
				locator1 = "CommerceOrganizationManagementWidget#INVITE_EMAIL_INPUT",
				value1 = "user@liferay.com");

			Click(
				key_roleOption = "Organization User",
				locator1 = "CommerceOrganizationManagementWidget#SELECT_ROLE");

			Button.clickSave();

			Alert.viewSuccessMessage();
		}

		task ("And the user is present in the admin console") {
			AssertElementPresent(
				key_nodeName = "user@life",
				locator1 = "CommerceOrganizationManagementWidget#NODE_TITLE");
		}
	}

	@description = "COMMERCE-9402. Verify the store admin can associate existing accounts to an organization by using the Organization Management widget."
	@priority = "3"
	test CanAssociateExistingAccountUsingWidget {
		task ("Given an account") {
			JSONAccountEntry.addAccountEntry(
				accountEntryName = "Commerce Account",
				accountEntryType = "Business");
		}

		task ("When the Organization Management Widget Page is reached") {
			Navigator.openSitePage(
				pageName = "Organization Management Widget Page",
				siteName = "Minium");

			Click(locator1 = "Button#EXPAND");
		}

		task ("And the organization svg node is clicked") {
			Click(
				key_nodeName = "Organization 1",
				locator1 = "CommerceOrganizationManagementWidget#NODE_TITLE");
		}

		task ("And the plus button is clicked") {
			Click(locator1 = "CommerceOrganizationManagementWidget#PLUS_BUTTON");
		}

		task ("And the account svg node is clicked") {
			WaitForVisible(
				key_orgType = "account",
				locator1 = "CommerceOrganizationManagementWidget#NODE_ACTION");

			Click(
				key_orgType = "account",
				locator1 = "CommerceOrganizationManagementWidget#NODE_ACTION");
		}

		task ("Then the admin is able to select an existing account to be associated with the organization") {
			Click.clickAtNotVisible(
				key_radioOption = "Select Accounts",
				locator1 = "CommerceEntry#RADIO_BUTTON");

			Type(
				locator1 = "CommerceOrganizationManagementWidget#SEARCH_ACCOUNT_INPUT",
				value1 = "Commerce Account");

			KeyPress(
				locator1 = "CommerceOrganizationManagementWidget#SEARCH_ACCOUNT_INPUT",
				value1 = "\ENTER");

			Button.clickSave();

			Alert.viewSuccessMessage();
		}

		task ("And the account is present in the admin console") {
			AssertElementPresent(
				key_nodeName = "Commerce Account",
				locator1 = "CommerceOrganizationManagementWidget#NODE_TITLE");
		}
	}

	@description = "COMMERCE-9403. Verify the store admin can associate existing users to an organization by using the Organization Management widget."
	@priority = "3"
	test CanAssociateExistingUserUsingWidget {
		task ("Given an account with a buyer user") {
			CommerceEntry.addAccountEntryUser(
				accountName = "Commerce Account",
				accountType = "Business",
				agreeToTermsAndAnswerReminderQuery = "true",
				createNewAccount = "true",
				requireReset = "false",
				userEmailAddress = "buyer@liferay.com",
				userFirstName = "Buyer",
				userLastName = "User",
				userRole = "Buyer",
				userScreenName = "buyeruser",
				userSiteMembership = "Minium");
		}

		task ("When the Organization Management Widget Page is reached") {
			Navigator.openSitePage(
				pageName = "Organization Management Widget Page",
				siteName = "Minium");

			Click(locator1 = "Button#EXPAND");
		}

		task ("And the organization svg node is clicked") {
			Click(
				key_nodeName = "Organization 1",
				locator1 = "CommerceOrganizationManagementWidget#NODE_TITLE");
		}

		task ("And the plus button is clicked") {
			Click(locator1 = "CommerceOrganizationManagementWidget#PLUS_BUTTON");
		}

		task ("And the user svg node is clicked") {
			WaitForVisible(
				key_orgType = "user",
				locator1 = "CommerceOrganizationManagementWidget#NODE_ACTION");

			Click(
				key_orgType = "user",
				locator1 = "CommerceOrganizationManagementWidget#NODE_ACTION");
		}

		task ("Then the admin is able to add an existing user's email to be associated with the organization") {
			Type(
				locator1 = "CommerceOrganizationManagementWidget#INVITE_EMAIL_INPUT",
				value1 = "buyer@liferay.com");

			Click(
				key_roleOption = "Organization User",
				locator1 = "CommerceOrganizationManagementWidget#SELECT_ROLE");

			Button.clickSave();

			Alert.viewSuccessMessage();
		}

		task ("And the user is present in the admin console") {
			AssertElementPresent(
				key_nodeName = "Buyer User",
				locator1 = "CommerceOrganizationManagementWidget#NODE_TITLE");
		}
	}

	@description = "COMMERCE-9401. Verify new accounts can be created using the Organization Management widget."
	@priority = "3"
	test CanCreateAccountUsingWidget {
		task ("When the Organization Management Widget Page is reached") {
			Navigator.openSitePage(
				pageName = "Organization Management Widget Page",
				siteName = "Minium");

			Click(locator1 = "Button#EXPAND");
		}

		task ("And the organization svg node is clicked") {
			Click(
				key_nodeName = "Organization 1",
				locator1 = "CommerceOrganizationManagementWidget#NODE_TITLE");
		}

		task ("And the plus button is clicked") {
			Click(locator1 = "CommerceOrganizationManagementWidget#PLUS_BUTTON");
		}

		task ("And the account svg node is clicked") {
			WaitForVisible(
				key_orgType = "account",
				locator1 = "CommerceOrganizationManagementWidget#NODE_ACTION");

			Click(
				key_orgType = "account",
				locator1 = "CommerceOrganizationManagementWidget#NODE_ACTION");
		}

		task ("Then the admin is able to create a new account") {
			Click.clickAtNotVisible(
				key_radioOption = "Create New Account",
				locator1 = "CommerceEntry#RADIO_BUTTON");

			Type(
				locator1 = "TextInput#NAME",
				value1 = "Account 1");

			Button.clickSave();

			Alert.viewSuccessMessage();
		}

		task ("And the new account is present in the admin console") {
			AssertElementPresent(
				key_nodeName = "Account 1",
				locator1 = "CommerceOrganizationManagementWidget#NODE_TITLE");
		}
	}

	@description = "COMMERCE-9400. Verify new child organizations can be created using the Organization Management widget."
	@priority = "3"
	test CanCreateOrganizationUsingWidget {
		task ("When the Organization Management Widget Page is reached") {
			Navigator.openSitePage(
				pageName = "Organization Management Widget Page",
				siteName = "Minium");

			Click(locator1 = "Button#EXPAND");
		}

		task ("And the organization svg node is clicked") {
			Click(
				key_nodeName = "Organization 1",
				locator1 = "CommerceOrganizationManagementWidget#NODE_TITLE");
		}

		task ("And the plus button is clicked") {
			Click(locator1 = "CommerceOrganizationManagementWidget#PLUS_BUTTON");
		}

		task ("And the organization icon is clicked") {
			WaitForVisible(
				key_orgType = "organization",
				locator1 = "CommerceOrganizationManagementWidget#NODE_ACTION");

			Click(
				key_orgType = "organization",
				locator1 = "CommerceOrganizationManagementWidget#NODE_ACTION");
		}

		task ("Then the admin is able to create a new child organization") {
			Type(
				locator1 = "TextInput#NAME",
				value1 = "Child Org 1");

			Button.clickSave();

			Alert.viewSuccessMessage();
		}

		task ("And then the new child organization is present in the admin console") {
			AssertElementPresent(
				key_nodeName = "Child Org 1",
				locator1 = "CommerceOrganizationManagementWidget#NODE_TITLE");
		}
	}

	@description = "COMMERCE-9404. Verify accounts and organizations can be moved using the Organization Management widget."
	@priority = "3"
	test CanMoveAccountsAndOrganizationsInWidget {
		task ("Given a new organization with a child organization") {
			JSONOrganization.addOrganization(
				organizationName = "Organization 2",
				organizationSite = "false");

			JSONOrganization.addOrganization(
				organizationName = "Child Org 1",
				organizationSite = "false",
				parentOrganizationName = "Organization 2");
		}

		task ("And given an account with a buyer user") {
			CommerceEntry.addAccountEntryUser(
				accountName = "Commerce Account",
				accountType = "Business",
				agreeToTermsAndAnswerReminderQuery = "true",
				createNewAccount = "true",
				requireReset = "false",
				userEmailAddress = "buyer@liferay.com",
				userFirstName = "Buyer",
				userLastName = "User",
				userRole = "Buyer",
				userScreenName = "buyeruser",
				userSiteMembership = "Minium");
		}

		task ("And given the account belongs to Organization 2") {
			Accounts.assignOrganizations(
				accountName = "Commerce Account",
				assigneeName = "Organization 2");
		}

		task ("When the Organization Management Widget Page is reached") {
			Navigator.openSitePage(
				pageName = "Organization Management Widget Page",
				siteName = "Minium");

			Click(locator1 = "Button#EXPAND");
		}

		task ("Then the admin is able to drag and drop the account to Organization 1") {
			Click(
				key_nodeName = "Organization 2",
				locator1 = "CommerceOrganizationManagementWidget#NODE_TITLE");

			DragAndDrop.dragAndDropPortletToPortletNoError(
				key_nodeDestination = "Organization 1",
				key_nodeOrigin = "Commerce Account",
				locator1 = "CommerceOrganizationManagementWidget#NODE_ORIGIN",
				locator2 = "CommerceOrganizationManagementWidget#NODE_DESTINATION",
				value1 = "Commerce Account");

			AssertConfirm(value1 = "Commerce Account will be moved into Organization 1.");
		}

		task ("And the admin is able to drag and drop the child organization to Organization 1") {
			DragAndDrop.dragAndDropPortletToPortletNoError(
				key_nodeDestination = "Organization 1",
				key_nodeOrigin = "Child Org 1",
				locator1 = "CommerceOrganizationManagementWidget#NODE_ORIGIN",
				locator2 = "CommerceOrganizationManagementWidget#NODE_DESTINATION",
				value1 = "Child Org 1");

			AssertConfirm(value1 = "Child Org 1 will be moved into Organization 1.");

			Refresh();

			Click(locator1 = "Button#EXPAND");
		}

		task ("And the admin is able to click on the child organization ellipses and click remove to move the child organization outside the parent organization") {
			Click(
				key_nodeName = "Organization 1",
				locator1 = "CommerceOrganizationManagementWidget#NODE_TITLE");

			Click(
				key_nodeName = "Child Org 1",
				locator1 = "CommerceOrganizationManagementWidget#VERTICAL_ELLIPSIS");

			ClickNoError(
				key_menuItem = "Remove",
				locator1 = "CommerceEntry#ANY_MENU_ITEM");

			AssertConfirm(value1 = "Child Org 1 will be removed from Organization 1.");

			Refresh();

			Click(locator1 = "Button#EXPAND");

			AssertElementPresent(
				key_nodeName = "Child Org 1",
				locator1 = "CommerceOrganizationManagementWidget#NODE_TITLE");
		}
	}

	@description = "COMMERCE-9399. Verify organizations can be viewed in the Organization Management widget."
	@priority = "3"
	test CanViewOrganizationInWidget {
		task ("When the Organization Management Widget Page is reached") {
			Navigator.openSitePage(
				pageName = "Organization Management Widget Page",
				siteName = "Minium");

			Click(locator1 = "Button#EXPAND");
		}

		task ("Then the organization is shown within the widget") {
			AssertElementPresent(
				key_nodeName = "Organization 1",
				locator1 = "CommerceOrganizationManagementWidget#NODE_TITLE");
		}
	}

}
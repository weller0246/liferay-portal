@component-name = "portal-commerce"
definition {

	property license.required = "true";
	property portal.release = "true";
	property portal.upstream = "false";
	property test.run.environment = "EE";
	property testray.main.component.name = "Commerce";

	setUp {
		TestCase.setUpPortalInstance();

		SignIn.signInTestSetup();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "This is a test for covering the following LPS-121306 and LPS-128094."
	@priority = "5"
	test CommerceRemainsActiveOnRestarts {
		property commerce.enabled = "true";
		property test.name.skip.portal.instance = "CommerceLicense#CommerceRemainsActiveOnRestarts";

		CommerceLicense.viewCommerceAvailable();

		Portlet.shutdownServer();

		Portlet.startServer(deleteLiferayHome = "false");

		AssertConsoleTextNotPresent(value1 = "Unable to install bundle");

		Navigator.openURL();

		CommerceLicense.viewCommerceAvailable();
	}

	@priority = "3"
	test DeactivateCommerce {
		property commerce.enabled = "true";
		property test.name.skip.portal.instance = "CommerceLicense#DeactivateCommerce";

		ApplicationsMenuHelper.openApplicationsMenu();

		CommerceLicense.viewCommerceAvailable();

		Portlet.shutdownServer();

		AntCommand(
			locator1 = "build-test.xml",
			value1 = "portal-ext-properties-update -Dupdate.properties.original=enterprise.product.commerce.enabled=true -Dupdate.properties=enterprise.product.commerce.enabled=false");

		Portlet.startServer(commerceEnabled = "false");

		User.firstLoginPG();

		CommerceLicense.viewCommerceMissing();
	}

	@priority = "4"
	test DeleteEnterpriseLicenseModule {
		property commerce.enabled = "true";
		property test.name.skip.portal.instance = "CommerceLicense#DeleteEnterpriseLicenseModule";

		Portlet.shutdownServer();

		var liferayHome = PropsUtil.get("liferay.home.dir.name");

		FileUtil.delete("${liferayHome}/osgi/static/com.liferay.portal.license.enterprise.app.jar");
		FileUtil.delete("${liferayHome}/osgi/marketplace/Liferay Foundation - Liferay License Manager - Impl.lpkg");
		FileUtil.delete("${liferayHome}/osgi/core/com.liferay.portal.app.license.api.jar");
		FileUtil.delete("${liferayHome}/osgi/core/com.liferay.portal.app.license.impl.jar");
		FileUtil.delete("${liferayHome}/osgi/core/com.liferay.portal.app.license.resolver.hook.jar");

		AntCommand(
			locator1 = "build-test.xml",
			value1 = "delete-static-impl-license-jar -DliferayHome=${liferayHome}");

		Portlet.startServer(keepOSGiState = "false");

		User.firstLoginPG();

		CommerceLicense.viewCommerceMissing();
	}

	@priority = "5"
	test EnableCommerceAfterPortalStarts {
		property commerce.enabled = "false";
		property test.name.skip.portal.instance = "CommerceLicense#EnableCommerceAfterPortalStarts";

		CommerceLicense.viewCommerceMissing();

		Portlet.shutdownServer();

		AntCommand(
			locator1 = "build-test.xml",
			value1 = "portal-ext-properties-update -Dadd.new.properties=true -Dupdate.properties=enterprise.product.commerce.enabled=true");

		Portlet.startServer(deleteLiferayHome = "false");

		CommerceLicense.executeSmoke();
	}

	@priority = "5"
	test ViewCommerceEnabled {
		property commerce.enabled = "true";
		property test.assert.warning.exceptions = "true";
		property test.name.skip.portal.instance = "CommerceLicense#ViewCommerceEnabled";

		CommerceLicense.executeSmoke();
	}

}
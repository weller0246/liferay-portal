definition {

	macro _addCommerceCart {
		Variables.assertDefined(parameterList = "${accountEntryName}");

		var accountEntryId = JSONAccountEntryAPI._getAccountEntryId(accountEntryName = "${accountEntryName}");
		var baseURL = "${baseURL}";

		if (!(isSet(baseURL))) {
			var baseURL = PropsUtil.get("portal.url");
		}

		var channelId = CommerceJSONChannelsAPI._getCommerceChannelIds();

		if (!(isSet(currencyCode))) {
			var currencyCode = "USD";
		}

		var cartItemsList = "";

		if (isSet(cartItems)) {
			for (var cartItem : list "${cartItems}") {
				var itemSku = StringUtil.extractFirst("${cartItem}", ":");
				var itemQuantity = StringUtil.extractLast("${cartItem}", ":");

				if ("${itemSku}" != "") {
					var itemSkuId = CommerceJSONProductsAPI._getSKUId(productSKU = "${itemSku}");
				}
				else {
					var itemSkuId = "";
				}

				if ("${cartItemsList}" != "") {
					var comma = ",";
				}
				else {
					var comma = "";
				}

				var cartItemsList = '''
						${cartItemsList}${comma}
						{
							"quantity": "${itemQuantity}",
      						"skuId": "${itemSkuId}"
						}
				''';
			}
		}

		var curl = '''
			${baseURL}/o/headless-commerce-delivery-cart/v1.0/channels/${channelId}/carts \
				-u test@liferay.com:test \
				-H 'accept: application/json' \
				-H 'Content-Type: application/json' \
				-d '{
					"accountId": ${accountEntryId},
					"cartItems": [${cartItemsList}],
					"currencyCode": "${currencyCode}"
				}'
		''';

		var responseBody = JSONCurlUtil.post("${curl}");

		return "${responseBody}";
	}

	macro getOrderItemPostRequestStatus {
		var responseBody = CommerceJSONDeliveryCart._addCommerceCart(
			accountEntryName = "${accountEntryName}",
			cartItems = "${cartItems}");

		var status = JSONUtil.getWithJSONPath("${responseBody}", "$..['status']");

		return "${status}";
	}

}
definition {

	macro addPriceToSKU {
		Type(
			locator1 = "CommerceEntry#PRICE_FIELD",
			value1 = "${price}");
	}

	macro addSKU {
		for (var optionValues : list "${optionValuesList}") {
			var optionValue = StringUtil.extractFirst("${optionValues}", ":");
			var price = StringUtil.extractLast("${optionValues}", ":");
			var optionValueUppercase = StringUtil.upperCase("${optionValue}");

			Refresh();

			Click(locator1 = "Button#PLUS");

			if (IsElementPresent(key_menuItem = "Add SKU", locator1 = "MenuItem#DROPDOWN_MENU_ITEM")) {
				AssertClick(
					key_menuItem = "Add SKU",
					locator1 = "MenuItem#DROPDOWN_MENU_ITEM",
					value1 = "${key_menuItem}");
			}

			SelectFrame.selectFrameNoLoading(locator1 = "CommerceEntry#IFRAME_SIDE_PANEL");

			CommerceEntry.addSKUsEntry(
				noOption = "${noOption}",
				optionValue = "${optionValue}",
				productSku = "${optionValueUppercase}SKU");

			WaitForElementNotPresent(locator1 = "TextInput#REQUIRED_ALERT");

			Check.checkToggleSwitch(
				key_toggleSwitchLabel = "Purchasable",
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

			CommerceSKUs.addPriceToSKU(price = "${price}");

			Button.clickPublish();

			AssertVisible(locator1 = "Message#SUCCESS");

			Click(locator1 = "Icon#CLOSE");
		}

		Refresh();
	}

	macro editSkuPricing {
		SelectFrame.selectFrameNoLoading(locator1 = "CommerceEntry#IFRAME_SIDE_PANEL");

		if (isSet(basePrice)) {
			Type(
				key_text = "Base Price",
				locator1 = "TextInput#ANY",
				value1 = "${basePrice}");
		}

		if (isSet(salePrice)) {
			Type(
				key_text = "Sale Price",
				locator1 = "TextInput#ANY",
				value1 = "${salePrice}");
		}

		if (isSet(cost)) {
			Type(
				key_text = "Cost",
				locator1 = "TextInput#ANY",
				value1 = "${cost}");
		}

		Button.clickPublish();

		Click(locator1 = "Icon#CLOSE");

		SelectFrame.selectFrameTop();
	}

}
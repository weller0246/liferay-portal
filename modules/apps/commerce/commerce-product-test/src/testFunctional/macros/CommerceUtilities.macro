definition {

	macro addFullDescriptionUsingSource {
		FormFields.toggleCKEditorContentStyle(optionTool = "Source");

		SelectFrameTop();

		CommerceEntry.addFullDescriptionViaCKEditor(entryFullDescription = "${sourceFullDescription}");

		Click(
			key_titleName = "Source",
			locator1 = "FormFields#TOOLBAR_ANY_ITEM_BUTTON");
	}

	macro checkSuccessAlertAppearsAndDisappears {
		WaitForElementPresent(locator1 = "Message#SUCCESS");

		Alert.closeMessage(messageType = "SUCCESS");

		WaitForElementNotPresent(locator1 = "Message#SUCCESS");
	}

	@description = "Compare two dates, if the first is greater than the second it returns the string"
	macro compareDates {
		var javaScript = '''
			CompareDates = {
				compareDates: function (dateString1, dateString2) {
					date1 = new Date(dateString1);
					date2 = new Date(dateString2);

					if (date1.getTime() > date2.getTime()) {
						return "True";
					}
					else if (date1.getTime() < date2.getTime()) {
						return "False";
					}
					else {
						return "Equal";
					}
				}
			};

			compare = CompareDates.compareDates(arguments[0], arguments[1]);

			return compare;
		''';

		var compareDatesResult = selenium.getJavaScriptResult("${javaScript}", "${date1}", "${date2}");

		return "${compareDatesResult}";
	}

	macro convertStringToKey {
		var string = StringUtil.lowerCase("${string}");

		var string = StringUtil.replace("${string}", " ", "-");

		return "${string}";
	}

	macro convertWholeNumberToDecimal {
		Variables.assertDefined(parameterList = "${number}");

		var isDecimal = StringUtil.contains("${number}", ".");

		if ("${isDecimal}" == "true") {
			return "${number}";
		}
		else {
			var decimalNumber = StringUtil.concat("${number}", ".00");

			return "${decimalNumber}";
		}
	}

	macro getConvertedBase64File {
		Variables.assertDefined(parameterList = "${file}");

		var projectDir = PropsUtil.get("project.dir");
		var fileDir = "modules/apps/commerce/commerce-product-test/src/testFunctional/tests/dependencies";

		if ("${file}" == "Commerce_Black.jpg") {
			var fileBase64 = FileUtil.read("${projectDir}/${fileDir}/commerce_black_t_shirt_encoded_base64.txt");
		}

		return "${fileBase64}";
	}

	macro getFutureDate {
		if (isSet(monthFormat)) {
			var dateMonth = DateUtil.getFormattedCurrentDate("${monthFormat}");
		}
		else {
			var dateMonth = DateUtil.getCurrentMonth();
		}

		if (isSet(dayFormat)) {
			var dateDay = DateUtil.getFormattedCurrentDate("${dayFormat}");
		}
		else {
			var dateDay = DateUtil.getCurrentDate();
		}

		if (isSet(yearFormat)) {
			var dateYear = DateUtil.getFormattedCurrentDate("${yearFormat}");
		}
		else {
			var dateYear = DateUtil.getCurrentYear();
		}

		if (isSet(increaseMonths)) {
			var dateMonth = ${dateMonth} + ${increaseMonths};
		}

		if (isSet(increaseDays)) {
			var dateDay = ${dateDay} + ${increaseDays};
		}

		if (isSet(increaseYears)) {
			var dateYear = ${dateYear} + ${increaseYears};
		}

		var futureDate = "${dateMonth}/${dateDay}/${dateYear}";

		return "${futureDate}";
	}

	@description = "Create ${payload} from a list of fields passed for an API call and returns it"
	macro getPayloadFromFieldsList {
		Variables.assertDefined(parameterList = "${fieldsList}");

		var count = "0";
		var list = ListUtil.newListFromString("${fieldsList}");

		var listSize = ListUtil.size("${list}");
		var payload = "";
		var isLessThanSize = MathUtil.isLessThan("${count}", "${listSize}");

		while (("${isLessThanSize}" == "true")) {
			var field = ListUtil.get("${list}", "${count}");

			var isAllowedOrderQuantities = StringUtil.matches("${field}", "(.*)allowedOrderQuantities(.*)");

			if ("${isAllowedOrderQuantities}" == "true") {
				var field = StringUtil.replace("${field}", "-", ",");
			}

			if ("${count}" == "0") {
				var comma = "";
			}
			else {
				var comma = ",";
			}

			var payload = '''${payload}${comma}
				${field}''';
			var count = ${count} + 1;

			var count = StringUtil.valueOf("${count}");

			var isLessThanSize = MathUtil.isLessThan("${count}", "${listSize}");
		}

		return "${payload}";
	}

	macro uploadSingleFile {
		Variables.assertDefined(parameterList = "${uploadFileName}");

		if (isSet(selectButtonName)) {
			CommerceEntry.clickButtonWithName(
				buttonName = "${selectButtonName}",
				text = "Select");
		}
		else {
			Button.clickSelect();
		}

		SelectFrameTop();

		CommerceItemSelector.uploadSingleFile(uploadFileName = "${uploadFileName}");

		SelectFrameTop();
	}

}
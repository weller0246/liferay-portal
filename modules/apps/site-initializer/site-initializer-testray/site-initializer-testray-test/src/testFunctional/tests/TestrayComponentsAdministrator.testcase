@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	setUp {
		task ("Login and create a Testray Site") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();

			Testray.createTestraySite(siteName = "Testray 2");

			Refresh();
		}

		task ("Go to Testray, Create Project and change pagination") {
			ApplicationsMenu.gotoSite(site = "Testray 2");

			Testray.createProjectTestray(
				description = "Projects_1 Description",
				key_name = "Projects_1 Project");

			Testray.changePagination(
				numberItems = "20",
				valuePagination = "150");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		Testray.deleteAllProjects();

		Testray.tearDownCP();

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "This is a test for LPS-156998 - Verify if as an Administrator I should be able to add and delete a Component that doesn't have a Team"
	@priority = "5"
	test AdminCanAddAndDeleteComponentWithoutTeam {
		task ("Given that the user add a new Component") {
			TestrayContextMenu.clickContextMenuProjects(name = "Projects_1 Project");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Manage Components");

			Testray.addNewComponent(componentName = "Components_1 Project");

			Testray.assertItemName(key_name = "Components_1 Project");
		}

		task ("And when a component without team is deleted") {
			Testray.deleteComponent(itemName = "Components_1 Project");
		}

		task ("Then the component should not appear on the list") {
			Testray.assertItemNotPresent(itemName = "Components_1 Project");
		}
	}

	@description = "This is a test for LPS-156997 - Verify if as an Administrator I should be able to add and delete a Component that has a Team"
	@priority = "5"
	test AdminCanAddAndDeleteComponentWithTeam {
		task ("Given that the user adds a new Team") {
			TestrayContextMenu.clickContextMenuProjects(name = "Projects_1 Project");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Manage Teams");

			Testray.addNewTeam(teamName = "Components_1 Team");

			Testray.assertItemName(key_name = "Components_1 Team");
		}

		task ("When the user goes to add a new component and selects the team") {
			TestrayManage.closeModalInOptions();

			TestrayContextMenu.clickContextMenuProjects(name = "Projects_1 Project");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Manage Components");

			Testray.addNewComponent(
				componentName = "Components_1 Project",
				teamName = "Components_1 Team");
		}

		task ("Then the component and team should appear on the list") {
			Testray.assertItemName(key_name = "Components_1 Project");

			Testray.assertItemName(key_name = "Components_1 Team");
		}

		task ("And when a component is deleted") {
			Testray.deleteComponent(itemName = "Components_1 Project");
		}

		task ("Then the component should not appear on the list") {
			Testray.assertItemNotPresent(itemName = "Components_1 Project");
		}
	}

	@description = "This is a test for LPS-156982 - Verify if as an Administrator I am able to add Components"
	@priority = "5"
	test AdminCanAddComponent {
		task ("Given that the user add a new Component") {
			TestrayContextMenu.clickContextMenuProjects(name = "Projects_1 Project");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Manage Components");

			Testray.addNewComponent(componentName = "Components_1 Project");
		}

		task ("Then the component should appear on the list") {
			Testray.assertItemName(key_name = "Components_1 Project");
		}
	}

	@description = "This is a test for LPS-156994 - Verify if as an Administrator I am able to edit Components and change the assigned Team"
	@priority = "4"
	test AdminCanEditChangeComponent {
		task ("Given that the user adds a new Team") {
			TestrayContextMenu.clickContextMenuProjects(name = "Projects_1 Project");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Manage Teams");

			Testray.addNewTeam(teamName = "Components_1 Team,Components_1 Team Second");

			Testray.assertItemName(key_name = "Components_1 Team");

			Testray.assertItemName(key_name = "Components_1 Team Second");
		}

		task ("And given that the user goes to add a new component and selects the team") {
			TestrayManage.closeModalInOptions();

			TestrayContextMenu.clickContextMenuProjects(name = "Projects_1 Project");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Manage Components");

			Testray.addNewComponent(
				componentName = "Components_1 Project",
				teamName = "Components_1 Team");

			Testray.assertItemName(key_name = "Components_1 Project");

			Testray.assertItemName(key_name = "Components_1 Team");
		}

		task ("When the user goes to edit the component and change the team") {
			SelectFrameTop();

			TestrayContextMenu.clickContextMenuProjects(name = "Components_1 Project");

			Testray.editComponent(
				componentName = "Components_1 Project",
				newComponentName = "Components_1 Project",
				newTeamName = "Components_1 Team Second");
		}

		task ("Then the team should be updated on the list") {
			Testray.assertItemName(key_name = "Components_1 Team Second");
		}
	}

	@description = "This is a test for LPS-156988 - Verify that as an Administrator I am able to edit Components while connecting a Team"
	@priority = "5"
	test AdminCanEditComponent {
		task ("Given that the user add a new Component") {
			TestrayContextMenu.clickContextMenuProjects(name = "Projects_1 Project");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Manage Components");

			Testray.addNewComponent(componentName = "Components_1 Project");

			Testray.assertItemName(key_name = "Components_1 Project");
		}

		task ("When the user goes to edit the component and change the name") {
			Testray.editComponent(
				componentName = "Components_1 Project",
				newComponentName = "Components_1 Project Edited");
		}

		task ("Then the component should be update on the list") {
			Testray.assertItemName(key_name = "Components_1 Project Edited");
		}
	}

	@description = "This is a test for LPS-162504 - Verify if as an Administrator I should not be able to add a duplicate component"
	@priority = "5"
	test AdminCanNotAddDuplicatedComponent {
		task ("Given that the user adds a new component") {
			TestrayContextMenu.clickContextMenuProjects(name = "Projects_1 Project");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Manage Components");

			Testray.addNewComponent(componentName = "Components_1 Project");
		}

		task ("When the user try to add a component with duplicated name") {
			Testray.assertItemName(key_name = "Components_1 Project");

			Testray.addNewComponent(componentName = "Components_1 Project");
		}

		task ("Then an error message is displayed") {
			Alert.viewErrorMessage(errorMessage = "The Component name already exists");
		}
	}

	@description = "This is a test for LPS-156995 - Verify if as an Administrator when I search for a Component the system should return that component"
	@ignore = "Test Stub"
	@priority = "4"
	test AdminCanSearchComponent {

		// TODO LPS-156995 AdminCanSearchComponent pending implementation

	}

	@description = "This is a test for LPS-156996 - Verify if as an Administrator when I search for a Component that does not exist the system should not return that component"
	@ignore = "Test Stub"
	@priority = "4"
	test AdminCanSearchComponentNonExistent {

		// TODO LPS-156996 AdminCanSearchComponentNonExistent pending implementation

	}

	@description = "This is a test for LPS-162505 - Verify if as an Administrator I can see the warning message 'This field is required' when trying to save without a name"
	@priority = "4"
	test AdminCanViewWarningMessage {
		task ("Given that the user goes to component management") {
			TestrayContextMenu.clickContextMenuProjects(name = "Projects_1 Project");

			TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Manage Components");
		}

		task ("When the user try to add a component without name") {
			Testray.addNewComponent(componentName = "");
		}

		task ("Then a warning message is displayed") {
			Testray.viewErrorMessage(
				fieldLabel = "Name",
				warningMessage = "name is a required field");
		}
	}

}
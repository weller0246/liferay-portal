@component-name = "portal-solutions"
definition {

	property custom.properties = "feature.flag.LPS-163118=true";
	property osgi.modules.includes = "site-initializer-testray";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Testray";

	setUp {
		task ("Login and create a Testray Site") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();

			Testray.addTestraySite();

			Refresh();
		}

		task ("Enable the Changed of password, Go to Testray and go to Change password") {
			ApplicationsMenu.gotoSite(site = "Testray 2");

			TestrayLayout.manageAccountSidebar(valueName = "Manage Account");

			Button.click(button = "Change Password");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		Testray.tearDownCP();

		ObjectAdmin.deleteAllCustomObjectsViaAPI();

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "This is a test for LPS-162648 - Verify that can log in with the new password after changing it"
	@priority = "5"
	test CanLoginWithChangedPassword {
		task ("Enable the Changed of password, Go to Testray") {
			TestraySignIn.enableChangePassword();

			ApplicationsMenu.gotoSite(site = "Testray 2");

			TestrayLayout.manageAccountSidebar(valueName = "Manage Account");

			Button.click(button = "Change Password");
		}

		task ("When the user changes the password correctly") {
			Type(
				locator1 = "TextInput#CURRENT_PASSWORD",
				value1 = "test");

			Type(
				locator1 = "TextInput#NEW_PASSWORD",
				value1 = "TestTest");

			Type(
				locator1 = "TestrayUser#CONFIRM_PASSWORD",
				value1 = "TestTest");

			Button.clickSave();

			Testray.signOutTestray();
		}

		task ("Then the user can log in to Testray 2 with modified password") {
			Navigator.openSpecificURL(url = "http://localhost:8080/web/testray-2");

			TestraySignIn.addEmailTestray(key_itemvalue = "test@liferay.com");

			TestraySignIn.addPasswordTestray(key_itemvalue = "TestTest");

			TestraySignIn.buttonSignInTestray();

			AssertElementPresent(locator1 = "TestrayLayout#TESTRAY_LOGO_SIDEBAR");
		}
	}

	@description = "This is a test for LPS-162647 - Verify that the message 'The current password is incorrect' appears, when enter an incorrect current password"
	@priority = "5"
	test CanViewCurrentPasswordIncorrect {
		task ("When the user tries to save password with current passwords incorrect") {
			Type(
				locator1 = "TextInput#CURRENT_PASSWORD",
				value1 = "test123");

			Type(
				locator1 = "TextInput#NEW_PASSWORD",
				value1 = "TestTest");

			Type(
				locator1 = "TestrayUser#CONFIRM_PASSWORD",
				value1 = "TestTest");

			Button.clickSave();
		}

		task ("Then an incorrect current password message is displayed") {
			Testray.viewErrorMessage(
				fieldLabel = "Current Password",
				warningMessage = "Current password is incorrect.");
		}
	}

	@description = "This is a test for LPS-162644 - Verify if an alert will appear when user does not fill a mandatory field"
	@priority = "3"
	test CanViewFieldRequired {
		task ("When the user tries to save password without filling out the required fields") {
			Button.click(button = "Save");
		}

		task ("Then a field required message is displayed") {
			Testray.viewErrorMessage(
				fieldLabel = "Current Password",
				warningMessage = "This field is required.");

			Testray.viewErrorMessage(
				fieldLabel = "New Password",
				warningMessage = "This field is required.");

			Testray.viewErrorMessage(
				fieldLabel = "Confirm Password",
				warningMessage = "This field is required.");
		}
	}

	@description = "This is a test for LPS-162645 - Verify that the New Password field displays the message 'minimum 8 characters.' if less than 8 characters are entered"
	@priority = "5"
	test CanViewMinimumEightCharacters {
		task ("When the user tries to save password without Minimum Eight Characters") {
			Type(
				locator1 = "TextInput#NEW_PASSWORD",
				value1 = "Test");

			Button.clickSave();
		}

		task ("Then an error message appears requiring a password with a minimum of eight characters") {
			Testray.viewErrorMessage(
				fieldLabel = "New Password",
				warningMessage = "Minimum 8 characters.");
		}
	}

	@description = "This is a test for LPS-162646 - Verify that the message 'Passwords do not match' appears, when the passwords are different"
	@priority = "5"
	test CanViewPasswordNotMatch {
		task ("When the user tries to save password with passwords different") {
			Type(
				locator1 = "TextInput#NEW_PASSWORD",
				value1 = "TestTest");

			Type(
				locator1 = "TestrayUser#CONFIRM_PASSWORD",
				value1 = "Test");

			Button.clickSave();
		}

		task ("Then a different password error message appears") {
			Testray.viewErrorMessage(
				fieldLabel = "Confirm Password",
				warningMessage = "Passwords do not match.");
		}
	}

}
definition {

	macro addSuite {
		LexiconEntry.gotoAdd();

		TestraySuites.typeSuiteField(
			suiteDescription = "${suiteDescription}",
			suiteName = "${suiteName}");

		if (isSet(smartSuite)) {
			Click.clickAtNotVisible(
				checkboxName = "Smart Suite",
				locator1 = "Checkbox#ANY_CHECKBOX");

			Click(
				locator1 = "Button#BUTTON_WITH_VALUE",
				value = "Select Case Parameters");

			if (isSet(teamName)) {
				Click(
					key_parameterType = "Available Teams",
					locator1 = "TestraySuites#SMART_SUITE_PARAMETER_TYPE",
					value1 = "${teamName}");

				Click(
					key_arrowDirection = "Right",
					key_parameterType = "Available Teams",
					locator1 = "TestraySuites#SMART_SUITE_ARROW");
			}

			if (isSet(caseType)) {
				Click(
					key_parameterType = "Available Case Types",
					locator1 = "TestraySuites#SMART_SUITE_PARAMETER_TYPE",
					value1 = "${caseType}");

				Click(
					key_arrowDirection = "Right",
					key_parameterType = "Available Case Types",
					locator1 = "TestraySuites#SMART_SUITE_ARROW");
			}

			if (isSet(mainComponent)) {
				Click(
					key_parameterType = "Available Main Components",
					locator1 = "TestraySuites#SMART_SUITE_PARAMETER_TYPE",
					value1 = "${mainComponent}");

				Click(
					key_arrowDirection = "Right",
					key_parameterType = "Available Main Components",
					locator1 = "TestraySuites#SMART_SUITE_ARROW");
			}

			if (isSet(subComponent)) {
				Click(
					key_parameterType = "Available Sub Components",
					locator1 = "TestraySuites#SMART_SUITE_PARAMETER_TYPE",
					value1 = "${subComponent}");

				Click(
					key_arrowDirection = "Right",
					key_parameterType = "Available Sub Components",
					locator1 = "TestraySuites#SMART_SUITE_ARROW");
			}

			if (isSet(priorityNumber)) {
				Click(
					key_parameterType = "Available Priorities",
					locator1 = "TestraySuites#SMART_SUITE_PARAMETER_TYPE",
					value1 = "${priorityNumber}");

				Click(
					key_arrowDirection = "Right",
					key_parameterType = "Available Priorities",
					locator1 = "TestraySuites#SMART_SUITE_ARROW");
			}

			if (isSet(requirementName)) {
				Click(
					key_parameterType = "Available Requirements",
					locator1 = "TestraySuites#SMART_SUITE_PARAMETER_TYPE",
					value1 = "${requirementName}");

				Click(
					key_arrowDirection = "Right",
					key_parameterType = "Available Requirements",
					locator1 = "TestraySuites#SMART_SUITE_ARROW");
			}

			Click(locator1 = "TestraySuites#MODAL_SELECT_CASES");
		}

		if (isSet(caseName)) {
			Click(
				locator1 = "Button#BUTTON_WITH_VALUE",
				value = "Select Case");

			Click.clickAtNotVisible(
				key_caseName = "${caseName}",
				locator1 = "TestraySuites#CASE_CHECKBOX");

			Click(locator1 = "TestraySuites#MODAL_SELECT_CASES");
		}

		Button.clickSave();
	}

	macro deleteSuite {
		TestrayContextMenu.clickContextMenuProjects(name = "${suiteName}");

		TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Delete");

		AssertConfirm(value1 = "Are you sure you want to delete this item?");

		Alert.viewSuccessMessage();
	}

	macro editSuite {
		TestrayContextMenu.clickContextMenuProjects(name = "${suiteName}");

		TestrayContextMenu.selectItemFromProjectsContextMenu(item = "Edit");

		TestraySuites.typeSuiteField(
			suiteDescription = "${newDescription}",
			suiteName = "${newName}");

		Button.clickSave();
	}

	macro goToSuiteDetails {
		Click(
			key_projectName = "${suiteName}",
			locator1 = "Testray#ITEM_NAME");
	}

	macro typeSuiteField {
		Type(
			locator1 = "TestrayManage#NEW_OPTION_NAME",
			value1 = "${suiteName}");

		Type(
			key_text = "description",
			locator1 = "TextArea#ANY",
			value1 = "${suiteDescription}");
	}

	macro verifySuite {
		AssertElementPresent(
			key_suiteDescription = "${suiteDescription}",
			key_suiteName = "${suiteName}",
			key_suiteType = "${suiteType}",
			locator1 = "TestraySuites#SUITE_ON_LIST");
	}

}
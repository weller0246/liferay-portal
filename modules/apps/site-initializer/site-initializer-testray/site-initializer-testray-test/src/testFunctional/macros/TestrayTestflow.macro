definition {

	macro addTask {
		Click(locator1 = "Button#PLUS");

		TestrayTestflow.editBreadcrumb(
			build = "${selectBuild}",
			project = "${selectProject}",
			routine = "${selectRoutine}");

		Button.click(button = "Analyze");

		if (isSet(taskName)) {
			Type(
				locator1 = "TestrayManage#NEW_OPTION_NAME",
				value1 = "${taskName}");
		}

		if (isSet(checkCaseType)) {
			Click(
				key_checkboxOption = "${checkCaseType}",
				locator1 = "Checkbox#ENABLE_CHECKBOX_VIA_SPAN");
		}

		Button.click(button = "Assign Users");

		if (isSet(userName)) {
			Click.clickAtNotVisible(
				key_checkboxOption = "${userName}",
				locator1 = "Checkbox#SPECIFIC_CHECKBOX_IN_TABLE");
		}

		Button.click(button = "Select Users");

		Testray.checkSuccessAlertAppearsAndDisappears();

		Button.click(button = "Save");
	}

	macro assertAssignTask {
		AssertElementPresent(
			key_email = "${email}",
			key_userName = "${userName}",
			locator1 = "TestrayTestflow#ASSIGN_INFORMATIONS");
	}

	macro assertStatus {
		AssertElementPresent(
			key_label = "${label}",
			key_status = "${status}",
			locator1 = "TestrayTestflow#TESTFLOW_STATUS");
	}

	macro assertTaskDetails {
		AssertElementPresent(
			key_build = "${build}",
			key_nameTask = "${nameTask}",
			key_project = "${project}",
			key_routine = "${routine}",
			key_status = "${status}",
			locator1 = "TestrayTestflow#TASK_DETAILS");
	}

	macro assignUsers {
		SelectFrame(locator1 = "Testray#MODAL_BODY");

		Testray.goToProjectPage(projectName = "${userName}");

		Alert.viewSuccessMessage();
	}

	macro editBreadcrumb {
		Click(locator1 = "TestrayTestflow#BREADCRUMB_EDIT");

		Type(
			locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
			value1 = "${project}");

		if (isSet(project)) {
			Click(locator1 = "TestrayTestflow#BREADCRUMB_SELECT_OPTIONS");
		}

		Type(
			locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
			value1 = "${routine}");

		if (isSet(routine)) {
			Click(locator1 = "TestrayTestflow#BREADCRUMB_SELECT_OPTIONS");
		}

		Type(
			locator1 = "TestrayTestflow#BREADCRUMB_EDIT",
			value1 = "${build}");

		if (isSet(build)) {
			Click(locator1 = "TestrayTestflow#BREADCRUMB_SELECT_OPTIONS");
		}
	}

	macro editStatus {
		Select(
			locator1 = "TestrayTestflow#SELECT_STATUS_CASE_RESULTS",
			value1 = "${selectStatus}");

		if (isSet(issues)) {
			Type(
				locator1 = "TestrayTestflow#INPUT_ISSUES",
				value1 = "${issues}");
		}

		if (isSet(comment)) {
			Type(
				locator1 = "TestrayTestflow#INPUT_COMMENT",
				value1 = "${comment}");
		}

		Button.click(button = "Save");
	}

	macro goToTask {
		Click(
			key_projectName = "${taskName}",
			locator1 = "Testray#ITEM_NAME");
	}

	macro goToTestflow {
		Click(locator1 = "TestrayTestflow#TESTFLOW_SIDEBAR");
	}

	macro taskOptions {
		if (isSet(taskName)) {
			Type(
				locator1 = "TestrayManage#NEW_OPTION_NAME",
				value1 = "${taskName}");
		}

		Button.click(button = "Save");
	}

	macro userAvatarNotPresent {
		AssertElementNotPresent(
			label = "${label}",
			locator1 = "TestrayTestflow#USER_AVATAR_IMAGE");
	}

	macro userAvatarPresent {
		AssertElementPresent(
			label = "${label}",
			locator1 = "TestrayTestflow#USER_AVATAR_IMAGE");
	}

}
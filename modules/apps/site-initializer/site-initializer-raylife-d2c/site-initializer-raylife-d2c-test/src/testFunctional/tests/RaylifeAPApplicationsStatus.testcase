@component-name = "portal-solutions"
definition {

	property osgi.modules.includes = "site-initializer-raylife-d2c";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "Site Initializer Raylife D2C";

	var testSiteName = "Raylife AP";
	var testSiteURL = "raylife-ap";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		Raylife.createRaylifeSite(siteName = "${testSiteName}");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		Raylife.tearDownCP();

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.logoutPG();
		}
	}

	@description = "LPS-154592 - Check if when making an incomplete Flow until the section Business in the Raylife d2c, the status 'Open' appears in the Applications status of Raylife ap"
	@priority = "4"
	test IfStatusOpenAppearsInApplicationsStatus {
		ApplicationsMenu.gotoSite(site = "${testSiteName}");

		RaylifeAPNavigator.gotoSidebarItem(itemName = "Applications");

		var open = RaylifeAPApplications.getApplicationLegendValue(legendItem = "Open");

		ApplicationsMenu.gotoSite(site = "Raylife D2C");

		RaylifeHomePage.startFlow();

		RaylifeContactInfo.typeAllBasicsInfoFields();

		Click(locator1 = "RaylifeSaveExit#SAVE_EXIT");

		Click(locator1 = "RaylifeSaveExit#SEND_LINK_EXIT");

		ApplicationsMenu.gotoSite(site = "${testSiteName}");

		RaylifeAPNavigator.gotoSidebarItem(itemName = "Applications");

		var newValue = ${open} + 1;

		AssertTextEquals(
			key_legendItem = "Open",
			locator1 = "RaylifeAPApplications#STATUS_LEGEND",
			value1 = "${newValue}");
	}

	@description = "LPS-154593 - Check if when making an incomplete flow until the page Quote Comparison in Raylife d2c, the status 'Quoted' appears in the Applications status of Raylife ap"
	@priority = "4"
	test IfStatusQuotedAppearsInApplicationsStatus {
		ApplicationsMenu.gotoSite(site = "${testSiteName}");

		RaylifeAPNavigator.gotoSidebarItem(itemName = "Applications");

		var quoted = RaylifeAPApplications.getApplicationLegendValue(legendItem = "Quoted");

		ApplicationsMenu.gotoSite(site = "Raylife D2C");

		RaylifeHomePage.startFlow();

		Raylife.formsFlow();

		ApplicationsMenu.gotoSite(site = "${testSiteName}");

		RaylifeAPNavigator.gotoSidebarItem(itemName = "Applications");

		var newValue = ${quoted} + 1;

		AssertTextEquals(
			key_legendItem = "Quoted",
			locator1 = "RaylifeAPApplications#STATUS_LEGEND",
			value1 = "${newValue}");
	}

	@description = "LPS-154587 - Verify that the number in the center of the graph is equal to the total amount of registered status"
	@priority = "4"
	test NumberCenterGraphEqualNumberOfStatus {
		ApplicationsMenu.gotoSite(site = "${testSiteName}");

		RaylifeAPNavigator.gotoSidebarItem(itemName = "Applications");

		RaylifeAPApplications.verifyApplicationsStatus();
	}

}
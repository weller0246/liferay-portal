definition {

	macro _editWalkthroughJSON {
		var jsonSnippet = Walkthrough.getWalkthroughSteps(fileName = "${fileName}");

		SystemSettings.editTextAreaSetting(
			settingName = "Steps",
			settingValue = "${jsonSnippet}");
	}

	macro _enableWalkthrough {
		SystemSettings.configureSystemSetting(
			enableSetting = "true",
			settingFieldName = "Enable Walkthrough");
	}

	macro addWalkthrough {
		Walkthrough.gotoSiteConfigWalkthrough();

		Walkthrough._enableWalkthrough();

		Walkthrough._editWalkthroughJSON(fileName = "${fileName}");

		SystemSettings.saveConfiguration();
	}

	macro assertHighlightedSpecificObject {
		AssertAttributeValue.assertAttributeNotPresent(
			attribute1 = "fill-opacity",
			key_stepId = "${key_stepId}",
			locator1 = "Walkthrough#STEP_ID",
			value1 = "0.8");

		AssertAttributeValue(
			attribute1 = "fill",
			locator1 = "Walkthrough#POPOVER_OVERLAY",
			value1 = "#393a4a");
	}

	macro enablePopoverMode {
		if (IsElementNotPresent(locator1 = "Walkthrough#POPOVER")) {
			if (IsElementPresent(key_portletName = "JS Walkthrough Sample", locator1 = "Portlet#TITLE")) {
				Click(locator1 = "Walkthrough#HOTSPOT");
			}
			else {
				Click(locator1 = "Walkthrough#SAMPLE_PORTLET_HOTSPOT");
			}
		}
	}

	macro getWalkthroughSteps {
		var dependencyFilePath = TestCase.getDependenciesDirPath(fileName = "${fileName}");

		var fileContent = FileUtil.read("${dependencyFilePath}");

		return "${fileContent}";
	}

	macro goToNextStep {
		Click(locator1 = "Button#OK");

		VerifyElementPresent(
			key_title = "Step ${key_step}",
			locator1 = "Walkthrough#POPOVER_TITLE");
	}

	macro goToPreviousStep {
		Click(locator1 = "Button#PREVIOUS");

		VerifyElementPresent(
			key_title = "Step ${key_step}",
			locator1 = "Walkthrough#POPOVER_TITLE");
	}

	macro gotoSiteConfigWalkthrough {
		ProductMenu.gotoPortlet(
			category = "Configuration",
			portlet = "Site Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "Walkthrough",
			configurationName = "Walkthrough",
			configurationScope = "Site Scope");
	}

	macro goToSpecificStep {
		while (IsElementNotPresent(key_title = "Step ${key_step}", locator1 = "Walkthrough#POPOVER_TITLE")) {
			Click(locator1 = "Button#OK");
		}
	}

	macro setupWalkthroughPage {
		task ("Given there is a site-level JSON defined for current site") {
			Walkthrough.addWalkthrough(fileName = "${filename}");

			JSONLayout.addPublicLayout(
				groupName = "${siteName}",
				layoutName = "Walkthrough Page 1");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "${siteName}",
				layoutName = "Walkthrough Page 1",
				widgetName = "JS Walkthrough Sample");

			if ("${filename}" == "walkthrough_configuration_multiple_page.json") {
				JSONLayout.addPublicLayout(
					groupName = "${siteName}",
					layoutName = "Walkthrough Page 2");

				JSONLayout.addWidgetToPublicLayout(
					groupName = "${siteName}",
					layoutName = "Walkthrough Page 2",
					widgetName = "JS Walkthrough Sample");
			}
		}

		task ("When instantiate the walkthrough") {
			Navigator.openSitePage(
				pageName = "Walkthrough Page 1",
				siteName = "${siteName}");

			Walkthrough.enablePopoverMode();
		}
	}

}
definition {

	macro addAccountWithDomain {
		LexiconEntry.gotoAdd();

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Add Account");

		PortletEntry.inputName(name = "${accountName}");

		AssertClick(
			locator1 = "Account#ACCOUNT_DOMAIN_ADD_LINK",
			value1 = "Add");

		SelectFrame(locator1 = "IFrame#ADD_DOMAIN");

		PortletEntry.inputText(
			fieldLabel = "Domain",
			text = "${domainNames}");

		Button.clickSave();

		for (var domainName : list "${domainNames}") {
			AssertElementPresent(
				key_domainName = "${domainName}",
				locator1 = "Account#DOMAIN_TABLE_NAME");
		}

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

	macro addAddress {
		LexiconEntry.gotoAdd();

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Add Address");

		PortletEntry.inputName(name = "${addressName}");

		if (isSet(addressDescription)) {
			PortletEntry.inputDescription(description = "${addressDescription}");
		}

		if (isSet(addressType)) {
			SelectField.select(
				selectFieldLabel = "Type",
				selectFieldValue = "${addressType}");
		}
		else {
			AssertTextEquals(
				key_typeName = "Billing and Shipping",
				locator1 = "Select#TYPE_SELECTED",
				value1 = "Billing and Shipping");
		}

		Type(
			locator1 = "TextInput#STREET_1",
			value1 = "${addressStreet1}");

		if (isSet(addressStreet2)) {
			Type(
				key_streetNumber = "2",
				locator1 = "TextInput#STREET_N",
				value1 = "${addressStreet2}");
		}

		if (isSet(addressStreet3)) {
			Type(
				key_streetNumber = "3",
				locator1 = "TextInput#STREET_N",
				value1 = "${addressStreet3}");
		}

		Select(
			locator1 = "Select#COUNTRY",
			value1 = "${addressCountry}");

		Type(
			locator1 = "TextInput#CITY",
			value1 = "${addressCity}");

		if (isSet(addressRegion)) {
			Select(
				locator1 = "Select#REGION",
				value1 = "${addressRegion}");
		}

		Type(
			locator1 = "TextInput#POSTAL_CODE",
			value1 = "${addressPostalCode}");

		if (isSet(phoneNumber)) {
			Type(
				locator1 = "TextInput#NUMBER",
				value1 = "${phoneNumber}");
		}

		if (isSet(customFieldText)) {
			Type(
				locator1 = "TextInput#CUSTOM_FIELD",
				value1 = "${customFieldText}");
		}

		Button.clickSave();

		Alert.viewSuccessMessage();

		AssertElementPresent(
			key_tab = "Addresses",
			locator1 = "NavTab#ACTIVE_TAB_LINK");
	}

	macro addBusinessAccount {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${accountName}");

		if (isSet(accountDescription)) {
			PortletEntry.inputDescription(description = "${accountDescription}");
		}

		if (isSet(accountLogo)) {
			User.changeProfileImage(imageName = "${accountLogo}");
		}

		if (isSet(accountTaxID)) {
			PortletEntry.inputText(
				fieldLabel = "Tax ID",
				text = "${accountTaxID}");
		}

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

	macro addInactiveBusinessAccount {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${accountName}");

		if (isSet(accountDescription)) {
			PortletEntry.inputDescription(description = "${accountDescription}");
		}

		Uncheck.uncheckToggleSwitch(locator1 = "ToggleSwitch#ACCOUNT_STATUS");

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

	macro addNewUserToBusinessAccount {
		Accounts.gotoAssigneeTab(assigneeTab = "Users");

		LexiconEntry.gotoAdd();

		Pause(locator1 = "3000");

		SelectFrame(
			key_title = "Assign Users to ${accountName}",
			locator1 = "IFrame#MODAL_ANY");

		LexiconEntry.gotoAdd();

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_SCREEN_NAME_FIELD",
			value1 = "${userScreenName}");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_EMAIL_ADDRESS_FIELD",
			value1 = "${userEmailAddress}");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_FIRST_NAME_FIELD",
			value1 = "${userFirstName}");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_LAST_NAME_FIELD",
			value1 = "${userLastName}");

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");

		Alert.viewSuccessMessage();
	}

	macro addNewUserToPersonAccount {
		AssertElementPresent(locator1 = "Account#USER_SELECT");

		if (!(IsElementPresent(locator1 = "Message#EMPTY_INFO_DEPOT_HIDE"))) {
			Alert.viewMutedMessage(muteMessage = "Assign a user to this person account.");
		}

		AssertClick(
			locator1 = "Account#USER_SELECT",
			value1 = "Select");

		SelectFrame(locator1 = "IFrame#ASSIGN_USER");

		LexiconEntry.gotoAdd();

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_SCREEN_NAME_FIELD",
			value1 = "${userScreenName}");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_EMAIL_ADDRESS_FIELD",
			value1 = "${userEmailAddress}");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_FIRST_NAME_FIELD",
			value1 = "${userFirstName}");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_LAST_NAME_FIELD",
			value1 = "${userLastName}");

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");

		SelectFrame(value1 = "relative=top");

		AssertElementPresent(
			key_userName = "${accountUserName}",
			locator1 = "Account#USER_TABLE_NAME");

		Button.clickSave();

		Alert.viewSuccessMessage();

		AssertElementPresent(
			key_userName = "${accountUserName}",
			locator1 = "Account#USER_TABLE_NAME");
	}

	macro addNewUserWithBlockedDomain {
		Accounts.gotoAssigneeTab(assigneeTab = "Users");

		LexiconEntry.gotoAdd();

		Pause(locator1 = "3000");

		SelectFrame(
			key_title = "Assign Users to ${accountName}",
			locator1 = "IFrame#MODAL_ANY");

		LexiconEntry.gotoAdd();

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_SCREEN_NAME_FIELD",
			value1 = "${userScreenName}");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_EMAIL_ADDRESS_FIELD",
			value1 = "${userEmailAddress}");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_FIRST_NAME_FIELD",
			value1 = "${userFirstName}");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_LAST_NAME_FIELD",
			value1 = "${userLastName}");

		AssertTextEquals(
			key_alertMessage = "${emailDomainName} is a blocked domain. Account administrators cannot add users with this domain to their accounts. You can still manually assign this email address to this user.",
			locator1 = "Message#ERROR_ENTER_A_VALID_VALUE",
			value1 = "${emailDomainName} is a blocked domain. Account administrators cannot add users with this domain to their accounts. You can still manually assign this email address to this user.");

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");

		Alert.viewSuccessMessage();
	}

	macro addNewUserWithInvalidDomain {
		Accounts.gotoAssigneeTab(assigneeTab = "Users");

		LexiconEntry.gotoAdd();

		Pause(locator1 = "3000");

		SelectFrame(
			key_title = "Assign Users to ${accountName}",
			locator1 = "IFrame#MODAL_ANY");

		LexiconEntry.gotoAdd();

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_SCREEN_NAME_FIELD",
			value1 = "${userScreenName}");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_EMAIL_ADDRESS_FIELD",
			value1 = "${userEmailAddress}");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_FIRST_NAME_FIELD",
			value1 = "${userFirstName}");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_LAST_NAME_FIELD",
			value1 = "${userLastName}");

		AssertTextEquals(
			key_alertMessage = "${emailDomainName} is not a valid domain for the following accounts: ${accountName}. Account Administrators cannot add users with this domain to their accounts. You can still manually assign this email address to this user.",
			locator1 = "Message#ERROR_ENTER_A_VALID_VALUE",
			value1 = "${emailDomainName} is not a valid domain for the following accounts: ${accountName}. Account Administrators cannot add users with this domain to their accounts. You can still manually assign this email address to this user.");

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");

		Alert.viewSuccessMessage();
	}

	macro addNewUserWithSameDomain {
		Accounts.gotoAssigneeTab(assigneeTab = "Users");

		LexiconEntry.gotoAdd();

		Pause(locator1 = "3000");

		SelectFrame(
			key_title = "Assign Users to ${accountName}",
			locator1 = "IFrame#MODAL_ANY");

		LexiconEntry.gotoAdd();

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_SCREEN_NAME_FIELD",
			value1 = "${userScreenName}");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_EMAIL_ADDRESS_FIELD",
			value1 = "${userEmailAddress}");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_FIRST_NAME_FIELD",
			value1 = "${userFirstName}");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_LAST_NAME_FIELD",
			value1 = "${userLastName}");

		AssertElementNotPresent(
			key_alertMessage = "${emailDomainName} is",
			locator1 = "Message#ERROR_ENTER_A_VALID_VALUE");

		AccountUsers.viewValidDomains(validDomainNames = "${validDomainNames}");

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");

		Alert.viewSuccessMessage();
	}

	macro addPersonAccount {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${accountName}");

		SelectField.select(
			selectFieldLabel = "Type",
			selectFieldValue = "Person");

		if (isSet(accountDescription)) {
			PortletEntry.inputDescription(description = "${accountDescription}");
		}

		if (isSet(accountLogo)) {
			User.changeProfileImage(imageName = "${accountLogo}");
		}

		if (isSet(accountTaxID)) {
			PortletEntry.inputText(
				fieldLabel = "Tax ID",
				text = "${accountTaxID}");
		}

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

	macro assignExistingUserToPersonAccount {
		AssertElementPresent(locator1 = "Account#USER_SELECT");

		if (!(IsElementPresent(locator1 = "Message#EMPTY_INFO_DEPOT_HIDE"))) {
			Alert.viewMutedMessage(muteMessage = "Assign a user to this person account.");
		}

		AssertClick(
			locator1 = "Account#USER_SELECT",
			value1 = "Select");

		SelectFrame(locator1 = "IFrame#ASSIGN_USER");

		Search.searchCP(searchTerm = "${accountUserName}");

		AssertClick(
			key_accountUserName = "${accountUserName}",
			locator1 = "Account#CHOOSE_ACCOUNT_USER",
			value1 = "Choose");

		SelectFrame(value1 = "relative=top");

		AssertElementPresent(
			key_userName = "${accountUserName}",
			locator1 = "Account#USER_TABLE_NAME");

		Button.clickSave();

		Alert.viewSuccessMessage();

		AssertElementPresent(
			key_userName = "${accountUserName}",
			locator1 = "Account#USER_TABLE_NAME");
	}

	macro assignOrganizations {
		Accounts.gotoAssigneeTab(assigneeTab = "Organizations");

		LexiconEntry.gotoAdd();

		Pause(locator1 = "3000");

		SelectFrame(
			key_title = "Assign Organizations to ${accountName}",
			locator1 = "IFrame#MODAL_ANY");

		Search.searchCP(searchTerm = "${assigneeName}");

		Check(
			key_tableEntry = "${assigneeName}",
			locator1 = "LexiconTable#TABLE_ENTRY_CHECKBOX");

		SelectFrameTop();

		Button.click(button = "Assign");

		Alert.viewSuccessMessage();
	}

	macro assignUsers {
		Accounts.gotoAssigneeTab(assigneeTab = "Users");

		LexiconEntry.gotoAdd();

		Pause(locator1 = "3000");

		SelectFrame(
			key_title = "Assign Users to ${accountName}",
			locator1 = "IFrame#MODAL_ANY");

		Search.searchCP(searchTerm = "${assigneeName}");

		Check(
			key_tableEntry = "${assigneeName}",
			locator1 = "LexiconTable#TABLE_ENTRY_CHECKBOX");

		SelectFrameTop();

		Button.click(button = "Assign");

		Alert.viewSuccessMessage();
	}

	macro deactivateAccount {
		Search.searchCP(searchTerm = "${accountName}");

		if ("${deactivateType}" == "Hide Icon") {
			LexiconEntry.selectEntry(key_rowEntry = "${accountName}");

			ClickNoError(locator1 = "Icon#DEACTIVATE");

			AssertConfirm(value1 = "Are you sure you want to deactivate this?");

			Alert.viewSuccessMessage();
		}
		else if ("${deactivateType}" == "Management Toolbar") {
			LexiconEntry.selectEntry(key_rowEntry = "${accountName}");

			Click(locator1 = "Icon#TOOLBAR_ELLIPSIS");

			MenuItem.clickNoError(menuItem = "Deactivate");

			AssertConfirm(value1 = "Are you sure you want to deactivate this?");

			Alert.viewSuccessMessage();
		}
		else {
			LexiconEntry.gotoEntryMenuItemNoError(
				menuItem = "Deactivate",
				rowEntry = "${accountName}");

			AssertConfirm(value1 = "Are you sure you want to deactivate this?");

			Alert.viewSuccessMessage();
		}

		LexiconEntry.viewNoEntry(rowEntry = "${accountName}");
	}

	macro deleteAccount {
		Search.searchCP(searchTerm = "${accountName}");

		if ("${deleteType}" == "Delete Icon") {
			LexiconEntry.selectEntry(key_rowEntry = "${accountName}");

			ClickNoError(locator1 = "Icon#DELETE");

			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();
		}
		else if ("${deleteType}" == "Management Toolbar") {
			LexiconEntry.selectEntry(key_rowEntry = "${accountName}");

			Click(locator1 = "Icon#TOOLBAR_ELLIPSIS");

			MenuItem.clickNoError(menuItem = "Delete");

			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();
		}
		else {
			LexiconEntry.gotoEntryMenuItemNoError(
				menuItem = "Delete",
				rowEntry = "${accountName}");

			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();
		}

		LexiconEntry.viewNoEntry(rowEntry = "${accountName}");
	}

	macro editAddress {
		LexiconEntry.gotoEntry(rowEntry = "${addressName}");

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Edit Address");

		if (isSet(addressName)) {
			PortletEntry.inputName(name = "${addressName}");
		}

		if (isSet(addressDescription)) {
			PortletEntry.inputDescription(description = "${addressDescription}");
		}

		if (isSet(addressType)) {
			SelectField.select(
				selectFieldLabel = "Type",
				selectFieldValue = "${addressType}");
		}

		if (isSet(addressStreet1)) {
			Type(
				locator1 = "TextInput#STREET_1",
				value1 = "${addressStreet1}");
		}

		if (isSet(addressStreet2)) {
			Type(
				key_streetNumber = "2",
				locator1 = "TextInput#STREET_N",
				value1 = "${addressStreet2}");
		}

		if (isSet(addressStreet3)) {
			Type(
				key_streetNumber = "3",
				locator1 = "TextInput#STREET_N",
				value1 = "${addressStreet3}");
		}

		if (isSet(addressCity)) {
			Type(
				locator1 = "TextInput#CITY",
				value1 = "${addressCity}");
		}

		if (isSet(addressRegion)) {
			Select(
				locator1 = "Select#REGION",
				value1 = "${addressRegion}");
		}

		if (isSet(addressPostalCode)) {
			Type(
				locator1 = "TextInput#POSTAL_CODE",
				value1 = "${addressPostalCode}");
		}

		if (isSet(addressCountry)) {
			Select(
				locator1 = "Select#COUNTRY",
				value1 = "${addressCountry}");
		}

		if (isSet(phoneNumber)) {
			Type(
				locator1 = "TextInput#NUMBER",
				value1 = "${phoneNumber}");
		}

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

	macro editCP {
		Accounts.gotoEdit(accountName = "${accountName}");

		PortletEntry.inputName(name = "${accountNameEdit}");

		if (isSet(accountDescriptionEdit)) {
			PortletEntry.inputDescription(description = "${accountDescriptionEdit}");
		}

		if (isSet(accountLogoEdit)) {
			User.changeProfileImage(imageName = "${accountLogoEdit}");
		}

		if ("${accountStatusEdit}" == "Inactive") {
			Uncheck.uncheckToggleSwitch(locator1 = "ToggleSwitch#ACCOUNT_STATUS");
		}
		else if ("${accountStatusEdit}" == "Active") {
			Check.checkToggleSwitch(locator1 = "ToggleSwitch#ACCOUNT_STATUS");
		}

		if (isSet(customFieldText)) {
			Type(
				locator1 = "TextInput#CUSTOM_FIELD",
				value1 = "${customFieldText}");
		}

		Button.clickSave();

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Edit ${accountNameEdit}");

		Alert.viewSuccessMessage();
	}

	macro gotoAssigneeTab {
		Navigator.gotoNavTab(navTab = "${assigneeTab}");
	}

	macro gotoEdit {
		ApplicationsMenu.gotoPortlet(
			category = "Accounts",
			panel = "Applications",
			portlet = "Accounts");

		Search.searchCP(searchTerm = "${accountName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${accountName}");

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Edit ${accountName}");
	}

	macro openAccountsAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/control_panel/manage?p_p_id=com_liferay_account_admin_web_internal_portlet_AccountEntriesAdminPortlet");
	}

	macro removeAddress {
		Click(
			key_addressDataType = "${addressDataType}",
			locator1 = "Account#REMOVE_ACCOUNT_DEFAULT_ADDRESS");

		PortletEntry.save();

		AssertTextEquals(
			key_addressDataType = "${addressDataType}",
			locator1 = "Account#ACCOUNT_ADDRESSES",
			value1 = "Not Set");
	}

	macro removeAssignee {
		var key_assigneeName = "${assigneeName}";

		Search.searchCP(searchTerm = "${assigneeName}");

		ClickNoError(
			locator1 = "Icon#REMOVE_ASSIGNEE",
			value1 = "${assigneeName}");

		if ("${assigneeType}" == "Users") {
			var assigneeType = "user";
		}
		else if ("${assigneeType}" == "Organizations") {
			var assigneeType = "organization";
		}

		AssertConfirm(value1 = "Are you sure you want to remove this ${assigneeType}?");

		Alert.viewSuccessMessage();
	}

	macro removeDomain {
		LexiconEntry.gotoEntry(rowEntry = "${accountName}");

		Click(
			key_domainName = "${domainName}",
			locator1 = "Account#DOMAIN_REMOVE_DOMAIN");

		PortletEntry.save();

		AssertElementNotPresent(
			key_domainName = "${domainName}",
			locator1 = "Account#DOMAIN_REMOVE_DOMAIN");
	}

	macro setDefaultAddress {
		AssertElementPresent(locator1 = "Account#DEFAULT_ACCOUNT_ADDRESSES_SECTION");

		if (IsElementPresent(key_addressDataType = "${addressDataType}",key_text = "Set Default Address", locator1 = "Account#SET_ACCOUNT_DEFAULT_ADDRESS")) {
			AssertClick(
				key_addressDataType = "${addressDataType}",
				key_text = "Set Default Address",
				locator1 = "Account#SET_ACCOUNT_DEFAULT_ADDRESS",
				value1 = "Set Default Address");
		}
		else {
			AssertClick(
				key_addressDataType = "${addressDataType}",
				key_text = "Change",
				locator1 = "Account#SET_ACCOUNT_DEFAULT_ADDRESS",
				value1 = "Change");
		}

		Pause(locator1 = "3000");

		SelectFrame(
			key_title = StringUtil.upperCaseFirstLetter("${addressDataType}"),
			locator1 = "IFrame#MODAL_ANY");

		AssertElementPresent(
			key_addressDataType = StringUtil.upperCaseFirstLetter("${addressDataType}"),
			key_filterScope = "Type: ${key_addressDataType}",
			locator1 = "ManagementBar#FILTER_RESULTS_SCOPE");

		Click(
			key_addressName = "${addressName}",
			locator1 = "Account#SELECT_ADDRESS_RADIO");

		SelectFrameTop();

		AssertClick(
			locator1 = "Account#SAVE_BUTTON",
			value1 = "Save");

		Alert.viewSuccessMessage();
	}

	macro tearDownCP {
		ApplicationsMenu.gotoPortlet(
			category = "Accounts",
			panel = "Applications",
			portlet = "Accounts");

		PortletEntry.selectAll();

		ClickNoError(locator1 = "Icon#DELETE");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();
	}

	macro viewAccountDetails {
		LexiconEntry.gotoEntry(rowEntry = "${accountName}");

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "${accountName}");

		if (isSet(accountDescription)) {
			AssertTextEquals(
				locator1 = "TextArea#DESCRIPTION",
				value1 = "${accountDescription}");
		}

		if (isSet(accountType)) {
			AssertTextEquals(
				key_typeName = "${accountType}",
				locator1 = "Select#TYPE_SELECTED",
				value1 = "${accountType}");
		}

		AssertElementPresent(
			key_selectFieldLabel = "Type",
			locator1 = "Select#ANY_DISABLED");

		if (isSet(accountTaxID)) {
			AssertTextEquals(
				key_fieldLabel = "Tax ID",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "${accountTaxID}");
		}

		AssertTextEquals(
			key_fieldLabel = "Tax ID",
			locator1 = "Icon#HELPER",
			value1 = "Enter the account's tax ID (EIN, VAT number, etc.).");

		if (isSet(domainName)) {
			LexiconEntry.viewEntryName(rowEntry = "${domainName}");
		}

		if (isSet(parentAccount)) {
			LexiconEntry.viewEntryName(rowEntry = "${parentAccount}");
		}

		if (isSet(customFieldText)) {
			AssertTextEquals(
				locator1 = "TextInput#CUSTOM_FIELD",
				value1 = "${customFieldText}");
		}
	}

	macro viewAddress {
		if ("${addressDataType}" == "billing") {
			AssertElementPresent(
				key_addressDataType = "${addressDataType}",
				locator1 = "Account#ACCOUNT_ADDRESS_BILLING");
		}
		else {
			AssertElementPresent(
				key_addressDataType = "${addressDataType}",
				locator1 = "Account#ACCOUNT_ADDRESS_SHIPPING");
		}

		AssertTextEquals.assertPartialText(
			key_addressDataType = "${addressDataType}",
			locator1 = "Account#ACCOUNT_ADDRESSES",
			value1 = "${addressName}");

		AssertTextEquals.assertPartialText(
			key_addressDataType = "${addressDataType}",
			locator1 = "Account#ACCOUNT_ADDRESSES",
			value1 = "${addressStreet1}");

		AssertTextEquals.assertPartialText(
			key_addressDataType = "${addressDataType}",
			locator1 = "Account#ACCOUNT_ADDRESSES",
			value1 = "${addressCountry}");

		AssertTextEquals.assertPartialText(
			key_addressDataType = "${addressDataType}",
			locator1 = "Account#ACCOUNT_ADDRESSES",
			value1 = "${addressCity}");

		AssertTextEquals.assertPartialText(
			key_addressDataType = "${addressDataType}",
			locator1 = "Account#ACCOUNT_ADDRESSES",
			value1 = "${addressPostalCode}");

		if (isSet(addressRegion)) {
			AssertTextEquals.assertPartialText(
				key_addressDataType = "${addressDataType}",
				locator1 = "Account#ACCOUNT_ADDRESSES",
				value1 = "${addressRegion}");
		}

		if (isSet(addressStreet2)) {
			AssertTextEquals.assertPartialText(
				key_addressDataType = "${addressDataType}",
				locator1 = "Account#ACCOUNT_ADDRESSES",
				value1 = "${addressStreet2}");
		}

		if (isSet(addressStreet3)) {
			AssertTextEquals.assertPartialText(
				key_addressDataType = "${addressDataType}",
				locator1 = "Account#ACCOUNT_ADDRESSES",
				value1 = "${addressStreet3}");
		}

		if (isSet(phoneNumber)) {
			AssertTextEquals.assertPartialText(
				key_addressDataType = "${addressDataType}",
				locator1 = "Account#ACCOUNT_ADDRESSES",
				value1 = "${phoneNumber}");
		}
	}

	macro viewAssignee {
		AssertTextEquals.assertPartialText(
			key_tableEntry = "${assigneeName}",
			locator1 = "LexiconTable#TABLE_ENTRY",
			value1 = "${assigneeName}");
	}

	macro viewAssigneeTab {
		AssertElementPresent(
			key_tab = "${navTab}",
			locator1 = "NavTab#TAB_LINK");
	}

	macro viewInactiveAccount {
		ManagementBar.setFilterAndOrder(filterBy = "Inactive");

		Search.searchCP(searchTerm = "${accountName}");

		LexiconEntry.viewEntryStatus(
			rowEntry = "${accountName}",
			rowStatus = "Inactive");
	}

	macro viewNoAssignee {
		LexiconEntry.gotoEntry(rowEntry = "${accountName}");

		Accounts.gotoAssigneeTab(assigneeTab = "${assigneeType}");

		var assigneeType = StringUtil.lowerCase("${assigneeType}");

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "There are no ${assigneeType} associated with this account.");
	}

	macro viewNoAssigneeTab {
		AssertElementNotPresent(
			key_tab = "${navTab}",
			locator1 = "NavTab#TAB_LINK");
	}

}
definition {

	macro addLanguageKey {
		LexiconEntry.gotoAdd();

		PortletEntry.inputText(
			fieldLabel = "Key",
			text = "${languageKey}");

		var j = "0";

		for (var translationOverride : list "${translationOverrideList}") {
			var k = "0";

			for (var translationOverrideValue : list "${translationOverrideValueList}") {
				if ("${j}" == "${k}") {
					PortletEntry.inputText(
						fieldLabel = "${translationOverride}",
						text = "${translationOverrideValue}");
				}

				var k = ${k} + 1;
			}

			var j = ${j} + 1;
		}

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

	@description = "Assert the filter scope label in management toolbar"
	macro assertFilterScope {
		AssertElementPresent(
			key_filterScope = "${filterScope}",
			locator1 = "ManagementBar#FILTER_RESULTS_SCOPE");
	}

	@description = "Assert values for existing language keys"
	macro assertLanguageKeyWithoutOverride {
		LexiconList.viewListEntryPresent(listEntry = "${languageKey}");

		LexiconList.viewListEntrySubtitle(
			listEntry = "${languageKey}",
			listEntrySubtitle = "${translation}");
	}

	@description = "Assert the following values for language key with override when a language is NOT selected: language key, languages with override but not highlighted"
	macro assertLanguageKeyWithOverrideInListView {
		LexiconList.viewListEntryPresent(listEntry = "${languageKey}");

		LexiconList.viewListEntrySubtitle(
			listEntry = "${languageKey}",
			listEntrySubtitle = "Languages With Override: ${languagesWithOverride}");
	}

	@description = "Assert the following values for language key with override when a language is selected: language key, current translation, and the highlighted language with override"
	macro assertSelectedLanguageInListView {
		LexiconList.viewListEntryPresent(listEntry = "${languageKey}");

		LexiconList.viewListEntrySubtitle(
			listEntry = "${languageKey}",
			listEntrySubtitle = "${currentTranslationOverride}");

		if (isSet(languagesWithOverride)) {
			LexiconList.viewListEntrySubtitle(
				listEntry = "${languageKey}",
				listEntrySubtitle = "Languages With Override: ${languagesWithOverride}");
		}

		AssertElementPresent(
			key_language = "${selectedLanguage}",
			key_languageKey = "${languageKey}",
			locator1 = "LanguageOverride#FILTER_BY_OVERRIDE_BOLD_LANGUAGE_WITH_OVERRIDE");
	}

	macro clearAllOverridesViaEditPage {
		ClickNoError(
			key_text = "Clear All Overrides",
			locator1 = "Link#ANY");

		AssertConfirm(value1 = "Are you sure you want to reset all translation overrides?");

		Alert.viewSuccessMessage();
	}

	macro exportOverriddenTranslations {
		Portlet.gotoPortletOptions(portletOption = "Export Overridden Translations");

		var tempFileName = TestCase.getDownloadedTempFileName(fileNamePattern = "*.zip");
		var tempFileDirectory = selenium.getOutputDirName();

		var exportedZipFile = FileUtil.exists("${tempFileDirectory}/${tempFileName}");

		if ("${exportedZipFile}" == "true") {
			echo("Overridden translations are exported");
		}
		else {
			fail("FAIL! Cannot find the exported zip file for overridden translations");
		}
	}

	macro goToLanguageKey {
		LexiconList.clickListEntryTitle(listEntry = "${languageKey}");
	}

	macro openLanguageOverridePortlet {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/control_panel/manage?p_p_id=com_liferay_portal_language_override_web_internal_portlet_PLOPortlet");
	}

	macro removeAllTranslationOverrides {
		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Remove all translation overrides",
			rowEntry = "${languageKey}");

		AssertConfirm(value1 = "Do you want to reset all translation overrides?");

		Alert.viewSuccessMessage();
	}

	macro removeTranslationOverrideForSelectedLanguage {
		PageEditor.changeLocale(
			currentLocale = "${currentLanguage}",
			locale = "${newLanguage}");

		var newLanguage = StringUtil.replace("${newLanguage}", "-", "_");

		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Remove translation override for ${newLanguage}",
			rowEntry = "${languageKey}");

		AssertConfirm(value1 = "Do you want to reset the translation override for ${newLanguage}?");

		Alert.viewSuccessMessage();
	}

	macro updateTranslationOverrideValues {
		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Edit",
			rowEntry = "${languageKey}");

		var j = "0";

		for (var translationOverride : list "${translationOverrideList}") {
			var k = "0";

			for (var translationOverrideValue : list "${translationOverrideValueList}") {
				if ("${j}" == "${k}") {
					PortletEntry.inputText(
						fieldLabel = "${translationOverride}",
						text = "${translationOverrideValue}");
				}

				var k = ${k} + 1;
			}

			var j = ${j} + 1;
		}

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

}
@component-name = "data-engine"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Data Engine";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This test ensures default Web Content title is displayed on Web content list for a web content created from a structure with a fieldset."
	@priority = "5"
	test CheckDefaultWebContentTitleOnWebContentList {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.createNewFieldset(
			fieldNameList = "Text",
			fieldsetName = "New Fieldset");

		DataEngine.addFieldsetToBuilder(fieldsetName = "New Fieldset");

		PortletEntry.changeLocale(locale = "pt-BR");

		DataEngine.editFieldsetsLabel(
			fieldsetLabel = "New Fieldset",
			fieldsetLabelEdit = "Novo Fieldset");

		DataEngine.gotoFieldsetItemEllipsis(fieldsetName = "New Fieldset");

		MenuItem.click(menuItem = "Edit");

		PortletEntry.changeLocale(
			locale = "pt-BR",
			modal = "true");

		DataEngine.editFieldLabel(
			fieldFieldLabel = "Text",
			fieldFieldLabelEdit = "Texto",
			fieldsetModal = "true");

		Click(locator1 = "Modal#SAVE_BUTTON");

		Click(
			key_text = "Propagate",
			locator1 = "Button#ANY");

		WebContentStructures.saveCP();

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		PortletEntry.inputTitle(title = "WC Title");

		PortletEntry.changeLocale(locale = "pt-BR");

		PortletEntry.inputTitle(title = "WC Titulo");

		PortletEntry.publish();

		WebContent.viewTitle(webContentTitle = "WC Title");
	}

	@description = "This test ensures locale selector is displayed and is functional on the fieldsets modal."
	@ignore = "true"
	@priority = "4"
	test CheckLanguageSelectorAndFieldsetSummaryLocalization {
		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "Structure Description",
			structureName = "Structure Name");

		DataEngine.createNewFieldset(
			fieldNameList = "Text",
			fieldsetName = "New Fieldset");

		DataEngine.addFieldsetToBuilder(fieldsetName = "New Fieldset");

		DataEngine.addTranslationToFieldset(
			fieldNameList = "Text",
			fieldsetName = "New Fieldset",
			localeList = "pt-BR");

		Refresh();

		PortletEntry.changeLocale(locale = "pt-BR");

		DataEngine.editFieldsetsLabel(
			fieldsetLabel = "New Fieldset",
			fieldsetLabelEdit = "Novo Fieldset");

		Click.clickNoMouseOver(locator1 = "Button#LOCALIZATION_GENERIC");

		AssertElementPresent(locator1 = "WCD#WEB_CONTENT_TRANSLATION_SPAN_LANGUAGE_ENGLISH");

		WebContentStructures.saveCP();

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "Structure Name");

		PortletEntry.inputTitle(title = "WC Title");

		PortletEntry.inputText(
			fieldLabel = "Text",
			text = "This is a Text");

		PortletEntry.changeLocaleSummary(locale = "pt_BR");

		PortletEntry.viewLocale(locale = "pt-BR");

		PortletEntry.inputText(
			fieldLabel = "Text",
			text = "Este é um texto");

		PortletEntry.changeLocaleSummary(locale = "en-US");

		Click.clickNoMouseOver(locator1 = "Button#LOCALIZATION_GENERIC");

		AssertElementPresent(locator1 = "WCD#WEB_CONTENT_TRANSLATION_SPAN_LANGUAGE_ENGLISH");

		PortletEntry.publish();

		WebContentNavigator.gotoEditCP(webContentTitle = "WC Title");

		AssertAttributeValue(
			attribute1 = "value",
			key_fieldFieldLabel = "Text",
			locator1 = "WCEditWebContent#TEXT_INPUT",
			value1 = "This is a Text");

		PortletEntry.changeLocaleSummary(locale = "pt_BR");

		PortletEntry.viewLocale(locale = "pt-BR");

		AssertAttributeValue(
			attribute1 = "value",
			key_fieldFieldLabel = "Text pt-BR",
			locator1 = "WCEditWebContent#TEXT_INPUT",
			value1 = "Este é um texto");
	}

	@description = "This test validates the fieldset translations are displayed on the Web Content."
	@priority = "5"
	test CheckStructureTranslationOnWebContent {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.createNewFieldset(
			fieldHelp = "true",
			fieldNameList = "Text",
			fieldsetName = "New Fieldset");

		DataEngine.addTranslationToFieldset(
			fieldHelp = "true",
			fieldNameList = "Text",
			fieldsetName = "New Fieldset",
			localeList = "pt-BR");

		DataEngine.addFieldsetToBuilder(fieldsetName = "New Fieldset");

		WebContentStructures.saveCP();

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addCP(webContentTitle = "Web Content Title");

		PortletEntry.publish();

		WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "Web Content Title");

		AssertVisible(
			key_fieldsetLabel = "New Fieldset",
			locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");

		AssertVisible(
			key_fieldFieldLabel = "Text",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");

		AssertTextEquals(
			key_fieldFieldLabel = "Text",
			locator1 = "DDMEditStructure#SETTINGS_TEXT",
			value1 = "Text Help Text");

		PortletEntry.changeLocale(locale = "pt-BR");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "Web Content Title");

		AssertVisible(
			key_fieldsetLabel = "New Fieldset pt-BR",
			locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");

		AssertVisible(
			key_fieldFieldLabel = "Text pt-BR",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");

		AssertTextEquals(
			key_fieldFieldLabel = "Text pt-BR",
			locator1 = "DDMEditStructure#SETTINGS_TEXT",
			value1 = "Text Help Text pt-BR");
	}

	@description = "This is a test for LPS-123539. As Content Creator, I want to leverage the structures’ translations with fildset when creating a new Web Content"
	@priority = "5"
	test CheckTranslationOfDuplicatedFieldsetOnWebContent {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.createNewFieldset(
			fieldNameList = "Text,Color",
			fieldsetName = "New Fieldset");

		DataEngine.addFieldsetToBuilder(fieldsetName = "New Fieldset");

		DataEngine.addTranslationToFieldset(
			fieldNameList = "Text,Color",
			fieldsetName = "New Fieldset",
			localeList = "pt-BR");

		DataEngine.toggleFieldRepeatable(fieldsetLabel = "New Fieldset");

		PortletEntry.changeLocale(locale = "pt_BR");

		WebContentStructures.addName(structureName = "WC Structure Name pt-BR");

		WebContentStructures.saveCP();

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addCP(webContentTitle = "Web Content Title");

		DataEngine.addRepeatableField(fieldLabel = "New Fieldset");

		PortletEntry.changeLocale(locale = "pt_BR");

		WebContent.addCP(webContentTitle = "Web Content Title pt-BR");

		PortletEntry.publish();

		WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title");

		PortletEntry.changeLocale(locale = "pt_BR");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "Web Content Title pt-BR");

		for (var i : list "1,2") {
			AssertElementPresent(
				key_fieldFieldLabel = "New Fieldset pt-BR",
				key_index = "${i}",
				locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL_ANY");

			AssertElementPresent(
				key_fieldFieldLabel = "Text pt-BR",
				key_index = "${i}",
				locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL_ANY");

			AssertElementPresent(
				key_fieldFieldLabel = "Color pt-BR",
				key_index = "${i}",
				locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL_ANY");
		}
	}

	@description = "This test ensures add fields, translate, edit fieldset, check propagation, translate from different language."
	@priority = "5"
	test CreateEditAndDeleteLocalizedFieldset {
		property portal.acceptance = "true";

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.createNewFieldset(
			fieldNameList = "Text,Numeric",
			fieldsetName = "New Fieldset");

		DataEngine.addFieldsetToBuilder(fieldsetName = "New Fieldset");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Name");

		DataEngine.gotoEditFieldset(fieldsetName = "New Fieldset");

		DEBuilder.addField(
			fieldLabel = "Date",
			fieldName = "Date");

		FormViewBuilder.saveButtonFieldset();

		FormViewBuilder.propagateChange();

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Name");

		DataEngine.addTranslationToFieldset(
			fieldNameList = "Text,Numeric,Date",
			fieldsetName = "New Fieldset",
			localeList = "pt-BR");

		PortletEntry.changeLocale(locale = "pt-BR");

		WebContentStructures.addName(structureName = "WC Structure Name pt-BR");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Name");

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "WC Structure Name");

		DataEngine.viewFieldset(
			fieldNameList = "Text,Numeric,Date",
			fieldsetName = "New Fieldset");

		DataEngine.viewFieldsetTranslation(
			fieldNameList = "Text,Numeric,Date",
			fieldsetName = "New Fieldset",
			localeList = "pt-BR");

		WebContentStructures.saveCP();

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addWithStructureCP(
			webContentDate = "02/22/2021",
			webContentNumeric = "1",
			webContentText = "Text",
			webContentTitle = "Web Content Title");

		PortletEntry.changeLocale(locale = "pt-BR");

		WebContent.addWithStructureCP(
			webContentDate = "23/02/2021",
			webContentDateFieldLabel = "Date pt-BR",
			webContentNumeric = "2",
			webContentNumericFieldLabel = "Numeric pt-BR",
			webContentText = "Text - pt-BR",
			webContentTextFieldLabel = "Text pt-BR",
			webContentTitle = "Web Content Title pt-BR");

		PortletEntry.publish();

		WebContent.viewWithStructureCP(
			webContentDate = "02/22/2021",
			webContentNumeric = "1",
			webContentText = "Text",
			webContentTitle = "Web Content Title");

		WebContent.viewWithStructureCP(
			skipGotoEditCP = "true",
			webContentDate = "23/02/2021",
			webContentDateFieldLabel = "Date pt-BR",
			webContentLocaleFieldLabel = "pt-BR",
			webContentNumeric = "2",
			webContentNumericFieldLabel = "Numeric pt-BR",
			webContentText = "Text - pt-BR",
			webContentTextFieldLabel = "Text pt-BR",
			webContentTitle = "Web Content Title pt-BR");

		Navigator.gotoBack();

		NavItem.gotoStructures();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Name");

		DataEngine.delete(fieldsetLabel = "New Fieldset");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		WebContentStructures.saveCP();

		DataEngine.deleteFieldsetFromSideBar(fieldsetName = "New Fieldset");

		Button.clickCancel();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Name");

		AssertElementNotPresent(
			key_fieldsetName = "New Fieldset",
			locator1 = "Sidebar#FIELDSET_ITEM_ELLIPSIS");

		WebContentStructures.saveCP();

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title");

		for (var fieldName : list "Text,Numeric,Date") {
			AssertElementNotPresent(
				key_fieldsetName = "${fieldName}",
				locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");
		}

		AssertElementNotPresent(
			key_fieldsetName = "New Fieldset",
			locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");

		AssertVisible(
			key_fieldFieldLabel = "Text",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");
	}

	@description = "This is a test for LPS-123539. As Content Creator, I want to leverage the structures’ translations with fieldset when creating a new Web Content"
	@priority = "4"
	test DefaultTitleIsDisplayedUponLocaleChange {
		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "Structure Description",
			structureName = "Structure Name");

		DataEngine.createNewFieldset(
			fieldHelp = "true",
			fieldNameList = "Text",
			fieldsetName = "New Fieldset");

		DataEngine.addFieldsetToBuilder(fieldsetName = "New Fieldset");

		WebContentStructures.saveCP(structureName = "Structure Name");

		NavItem.gotoWebContent();

		WebContentNavigator.gotoAddWithStructureCP(structureName = "Structure Name");

		PortletEntry.inputTitle(title = "WC Title");

		PortletEntry.changeLocale(locale = "zh-CN");

		PortletEntry.inputTitle(title = "测试标题");

		PortletEntry.publish();

		WebContentNavigator.gotoEditCP(webContentTitle = "WC Title");

		PortletEntry.changeLocale(locale = "pt_BR");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "WC Title");

		PortletEntry.changeLocale(locale = "es_ES");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "WC Title");
	}

	@description = "This test checks when user translates the title, the default language title persists"
	@priority = "4"
	test DefaultTitlePersistsAfterTranslatingForAnotherLocale {
		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.gotoCreateNewFieldset();

		Type(
			locator1 = "FormViewBuilder#FIELD_SET_NAME",
			value1 = "Fieldset Default Title");

		DataEngine.addFields(fieldNameList = "Text");

		PortletEntry.changeLocale(
			locale = "zh-CN",
			modal = "true");

		Type(
			locator1 = "FormViewBuilder#FIELD_SET_NAME",
			value1 = "测试标题");

		Click(locator1 = "Modal#SAVE_BUTTON");

		DataEngine.addFieldsetToBuilder(fieldsetName = "Fieldset Default Title");

		WebContentStructures.saveCP();

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Title");

		WebContent.addCP(webContentTitle = "Web Content Title");

		PortletEntry.publish();

		WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title");

		AssertVisible(
			key_fieldsetLabel = "Fieldset Default Title",
			locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");
	}

	@description = "This test ensures the default fieldset title persists upon translating it on a Web Content."
	@priority = "4"
	test DefaultTitlePersistsUponFirstlyTranslatingForAnotherLocale {
		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "Structure Description",
			structureName = "Structure Name");

		DataEngine.createNewFieldset(
			fieldHelp = "True",
			fieldNameList = "Text",
			fieldsetName = "New Fieldset");

		DataEngine.addFieldsetToBuilder(fieldsetName = "New Fieldset");

		DataEngine.addTranslationToFieldset(
			fieldHelp = "true",
			fieldNameList = "Text",
			fieldsetName = "New Fieldset",
			localeList = "pt-BR");

		WebContentStructures.saveCP(structureName = "Structure Name");

		NavItem.gotoWebContent();

		WebContentNavigator.gotoAddWithStructureCP(structureName = "Structure Name");

		PortletEntry.changeLocale(locale = "pt-BR");

		PortletEntry.inputTitle(title = "WC Titulo");

		PortletEntry.changeLocale(locale = "en-US");

		PortletEntry.inputTitle(title = "WC Title");

		PortletEntry.changeLocale(locale = "pt-BR");

		PortletEntry.publish();

		WebContentNavigator.gotoEditCP(webContentTitle = "WC Title");

		PortletEntry.changeLocale(locale = "pt-BR");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "WC Titulo");
	}

	@description = "This test ensures create, translate and edit with different instance language."
	@priority = "5"
	test LocalizedFieldsetWithDifferentInstanceLanguage {
		property portal.acceptance = "quarantine";

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Instance Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "Localization",
			configurationName = "Language",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.configureCurrentLanguagesCP(defaultPortalLanguage = "Portuguese (Brazil)");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.createNewFieldset(
			fieldNameList = "Text,Numeric",
			fieldsetName = "New Fieldset Test");

		DataEngine.addFieldsetToBuilder(fieldsetName = "New Fieldset Test");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Name");

		DataEngine.gotoEditFieldset(fieldsetName = "New Fieldset Test");

		DataEngine.addFields(fieldNameList = "Date");

		Click(locator1 = "Modal#SAVE_BUTTON");

		Click(
			key_text = "Propagate",
			locator1 = "Button#ANY");

		Alert.viewSuccessMessage();

		DataEngine.addTranslationToFieldset(
			fieldNameList = "Text,Numeric,Date",
			fieldsetName = "New Fieldset Test",
			localeList = "en-US");

		Refresh();

		PortletEntry.changeLocale(locale = "en-US");

		WebContentStructures.addName(structureName = "WC Structure Name en-US");

		DataEngine.editFieldsetsLabel(
			fieldsetLabel = "New Fieldset Test",
			fieldsetLabelEdit = "New Fieldset Test en-US");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Name en-US");

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "WC Structure Name");

		DataEngine.viewFieldset(
			fieldNameList = "Text,Numeric,Date",
			fieldsetName = "New Fieldset Test");

		DataEngine.viewFieldsetTranslation(
			fieldNameList = "Text,Numeric,Date",
			fieldsetName = "New Fieldset Test",
			localeList = "en-US");

		WebContentStructures.saveCP();

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name en-US");

		WebContent.addWithStructureCP(
			webContentDate = "02/02/2021",
			webContentDateFieldLabel = "Date",
			webContentNumeric = "1",
			webContentNumericFieldLabel = "Numeric",
			webContentText = "Text",
			webContentTextFieldLabel = "Text",
			webContentTitle = "Web Content Title");

		PortletEntry.changeLocale(locale = "en-US");

		WebContent.addWithStructureCP(
			webContentDate = "02/03/2021",
			webContentDateFieldLabel = "Date en-US",
			webContentNumeric = "2",
			webContentNumericFieldLabel = "Numeric en-US",
			webContentText = "Text - en-US",
			webContentTextFieldLabel = "Text en-US",
			webContentTitle = "Web Content Title en-US");

		PortletEntry.publish();

		WebContent.viewWithStructureCP(
			webContentDate = "02/02/2021",
			webContentDateFieldLabel = "Date",
			webContentNumeric = "1",
			webContentNumericFieldLabel = "Numeric",
			webContentText = "Text",
			webContentTextFieldLabel = "Text",
			webContentTitle = "Web Content Title");

		PortletEntry.publish();

		WebContent.viewWithStructureCP(
			webContentDate = "02/03/2021",
			webContentDateFieldLabel = "Date en-US",
			webContentLocaleFieldLabel = "en-US",
			webContentNumeric = "2",
			webContentNumericFieldLabel = "Numeric en-US",
			webContentText = "Text - en-US",
			webContentTextFieldLabel = "Text en-US",
			webContentTitle = "Web Content Title",
			webContentTitleTranslated = "Web Content Title en-US");

		PortletEntry.publish();

		NavItem.gotoStructures();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Name en-US");

		DataEngine.delete(fieldsetLabel = "New Fieldset Test");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");
		WebContentStructures.saveCP();

		DataEngine.deleteFieldsetFromSideBar(fieldsetName = "New Fieldset Test");

		Button.clickCancel();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Name en-US");

		AssertElementNotPresent(
			key_fieldsetName = "New Fieldset Test",
			locator1 = "Sidebar#FIELDSET_ITEM_ELLIPSIS");

		WebContentStructures.saveCP();

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title en-US");

		for (var fieldName : list "Text,Numeric,Date") {
			AssertElementNotPresent(
				key_fieldsetName = "${fieldName}",
				locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");
		}

		AssertElementNotPresent(
			key_fieldsetName = "New Fieldset",
			locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");

		AssertVisible(
			key_fieldFieldLabel = "Text",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Web Content");

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Instance Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "Localization",
			configurationName = "Language",
			configurationScope = "Virtual Instance Scope");

		PortalSettings.configureCurrentLanguagesCP(defaultPortalLanguage = "English (United States)");
	}

	@description = "This test ensures is possible to translate a web content from a structure with a fieldset with a field group."
	@priority = "5"
	test TranslateWebContentFromStructureWithFieldsetWithFieldGroup {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.gotoCreateNewFieldset();

		Type(
			locator1 = "FormViewBuilder#FIELD_SET_NAME",
			value1 = "New Fieldset");

		DataEngine.addFields(fieldNameList = "Text");

		DataEngine.addFieldNested(
			fieldFieldLabel = "Rich Text",
			fieldName = "Rich Text",
			targetFieldLabel = "Text");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Text",
			fieldHelp = "Text Field Help",
			fieldsetModal = "true");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Rich Text",
			fieldHelp = "Rich Text Field Help",
			fieldsetModal = "true");

		PortletEntry.changeLocale(
			locale = "pt-BR",
			modal = "true");

		Type(
			locator1 = "FormViewBuilder#FIELD_SET_NAME",
			value1 = "Novo Fieldset");

		DataEngine.editFieldsGroupLabel(
			fieldsetModal = "true",
			fieldsGroupLabel = "Fields Group",
			fieldsGroupLabelEdit = "Grupo de Campos");

		DataEngine.editFieldLabel(
			fieldFieldLabel = "Text",
			fieldFieldLabelEdit = "Texto",
			fieldsetModal = "true");

		DataEngine.editFieldLabel(
			fieldFieldLabel = "Rich Text",
			fieldFieldLabelEdit = "Texto Rico",
			fieldsetModal = "true");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Texto",
			fieldHelp = "Texto de ajuda do campo de texto");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Texto Rico",
			fieldHelp = "Texto de ajuda do campo Texto Rico",
			fieldsetModal = "true");

		Click(locator1 = "Modal#SAVE_BUTTON");

		Alert.viewSuccessMessage();

		DataEngine.addFieldsetToBuilder(fieldsetName = "New Fieldset");

		WebContentStructures.saveCP();

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addCP(webContentTitle = "Web Content Title");

		PortletEntry.publish();

		WebContentNavigator.gotoEditCP(webContentTitle = "Web Content Title");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "Web Content Title");

		AssertVisible(
			key_fieldsetLabel = "New Fieldset",
			locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");

		AssertVisible(
			key_fieldFieldLabel = "Fields Group",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");

		for (var fieldFieldLabel : list "Text,Rich Text") {
			AssertVisible(
				key_fieldFieldLabel = "${fieldFieldLabel}",
				key_fieldsGroupLabel = "Fields Group",
				locator1 = "DataEngineBuilder#FORM_FIELD_NESTED");
		}

		for (var fieldFieldLabel : list "Text,Rich Text") {
			AssertTextEquals(
				key_fieldFieldLabel = "${fieldFieldLabel}",
				locator1 = "DDMEditStructure#SETTINGS_TEXT",
				value1 = "${fieldFieldLabel} Field Help");
		}

		PortletEntry.changeLocale(locale = "pt-BR");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "Web Content Title");

		AssertVisible(
			key_fieldsetLabel = "Novo Fieldset",
			locator1 = "DDMEditStructure#FORM_FIELD_SETS_CONTAINER_LABEL");

		AssertVisible(
			key_fieldFieldLabel = "Grupo de Campos",
			locator1 = "DDMEditStructure#FORM_FIELD_CONTAINER_LABEL");

		for (var fieldFieldLabel : list "Texto,Texto Rico") {
			AssertVisible(
				key_fieldFieldLabel = "${fieldFieldLabel}",
				key_fieldsGroupLabel = "Grupo de Campos",
				locator1 = "DataEngineBuilder#FORM_FIELD_NESTED");
		}

		AssertTextEquals(
			key_fieldFieldLabel = "Texto",
			locator1 = "DDMEditStructure#SETTINGS_TEXT",
			value1 = "Texto de ajuda do campo de texto");

		AssertTextEquals(
			key_fieldFieldLabel = "Texto Rico",
			locator1 = "DDMEditStructure#SETTINGS_TEXT",
			value1 = "Texto de ajuda do campo Texto Rico");
	}

	@description = "This test ensures translation of a web content from a structure with a fieldset persists after translating for another locale."
	@priority = "5"
	test TranslationPersistsAfterEditingAnother {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Name");

		DataEngine.createNewFieldset(
			fieldHelp = "True",
			fieldNameList = "Text",
			fieldsetName = "New Fieldset");

		DataEngine.addFieldsetToBuilder(fieldsetName = "New Fieldset");

		DataEngine.addTranslationToFieldset(
			fieldHelp = "true",
			fieldNameList = "Text",
			fieldsetName = "New Fieldset",
			localeList = "pt-BR");

		WebContentStructures.saveCP();

		NavItem.click(navItem = "Web Content");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "New Fieldset");

		WebContent.addWithStructureCP(
			webContentText = "This is a text",
			webContentTitle = "WC Title");

		PortletEntry.changeLocale(locale = "pt_BR");

		WebContent.addWithStructureCP(webContentTitle = "Título do conteúdo da web");

		PortletEntry.inputText(
			fieldLabel = "Text pt-BR",
			text = "Este é um campo de texto");

		PortletEntry.changeLocale(locale = "es_ES");

		WebContent.addWithStructureCP(webContentTitle = "WC Title ES");

		PortletEntry.inputText(
			fieldLabel = "Text",
			text = "This is a text in ES");

		PortletEntry.publish();

		WebContentNavigator.gotoEditCP(webContentTitle = "WC Title");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "WC Title");

		AssertTextEquals(
			key_fieldLabel = "Text",
			locator1 = "TextInput#GENERIC_TEXT_INPUT",
			value1 = "This is a text");

		PortletEntry.changeLocale(locale = "pt_BR");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "Título do conteúdo da web");

		AssertTextEquals(
			key_fieldLabel = "Text pt-BR",
			locator1 = "TextInput#GENERIC_TEXT_INPUT",
			value1 = "Este é um campo de texto");

		PortletEntry.changeLocale(locale = "es_ES");

		AssertTextEquals(
			locator1 = "TextInput#TITLE",
			value1 = "WC Title ES");

		AssertTextEquals(
			key_fieldLabel = "Text",
			locator1 = "TextInput#GENERIC_TEXT_INPUT",
			value1 = "This is a text in ES");
	}

}
@component-name = "portal-document-management"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Bulk Editing";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.enableActionsMenuOnPortlet();

		LexiconEntry.changeDisplayStyle(displayStyle = "list");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			DMDocument.tearDownCP();

			DMFolder.tearDownCP();

			User.logoutPG();
		}
	}

	@description = "This ensures that indeterminate checkboxes appear in the permissions modal when editing multiple folders with different permissions."
	@priority = "5"
	test CanAddPermissionsForFoldersWithDifferentPermissions {
		JSONFolder.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name1",
			groupName = "Guest");

		JSONFolder.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name2",
			groupName = "Guest");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoFolderPermissions(dmFolderName = "DM Folder Name2");

		PermissionsInline.addOrRemoveAnyPermissionsFromSpecificRole(
			addOrRemoveViewPermission = "Remove",
			permissionsKeyList = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
			roleTitle = "Guest");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMFolder.selectFolders(dmFolderName = "DM Folder Name1,DM Folder Name2");

		DMNavigator.gotoItemViaManagementBar(
			item = "dropdownMenu",
			menuItem = "Permissions");

		PermissionsInline.addOrRemoveAnyPermissionsFromSpecificRole(
			addOrRemoveViewPermission = "Add",
			permissionsBulkEditing = "true",
			permissionsKeyList = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
			roleTitle = "Guest");

		User.logoutPG();

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMFolder.viewEntryPresent(dmFolderName = "DM Folder Name1");

		DMFolder.viewEntryPresent(dmFolderName = "DM Folder Name2");
	}

	@description = "This test adds a tag for a single document via management toolbar"
	@priority = "4"
	test CanAddTagToSingleAsset {
		for (var entryCount : list "1,2") {
			JSONDocument.addFileWithUploadedFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "Document_${entryCount}",
				groupName = "Guest",
				mimeType = "application/msword",
				sourceFileName = "Document_${entryCount}.doc");
		}

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.selectDocuments(dmDocumentTitle = "Document_1");

		DMNavigator.gotoItemViaManagementBar(item = "Edit Tags");

		DMDocument.editTags(tagName = "newtag");

		DMNavigator.gotoDocumentPG(dmDocumentTitle = "Document_1");

		DMDocument.viewTagInfo(tagName = "newtag");

		SearchPortlets.searchEmbedded(searchTerm = "newtag");

		SearchResultPortlet.viewSearchResults(
			searchAssetTitle = "Document_1",
			searchAssetType = "Document");
	}

	@description = "This test covers LPS-94956. It ensures that users can delete common category tags by using the bulk append action."
	@priority = "5"
	test CanDeleteCommonCategoryTags {
		for (var entryCount : list "1,2") {
			JSONDocument.addFileWithUploadedFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "Document_${entryCount}",
				groupName = "Guest",
				mimeType = "application/msword",
				sourceFileName = "Document_${entryCount}.doc");
		}

		for (var categoryName : list "Books,Plants,Pets") {
			JSONCategory.addCategory(
				groupName = "Guest",
				title = "${categoryName}",
				vocabularyName = "Topic");
		}

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoAddCategoriesPG(dmDocumentTitleList = "Document_1");

		DMDocument.editCategoryTags(categoryNameList = "Books,Plants");

		DMDocument.unselectDocuments(dmDocumentTitle = "Document_1");

		DMNavigator.gotoAddCategoriesPG(dmDocumentTitleList = "Document_2");

		DMDocument.editCategoryTags(categoryNameList = "Books,Pets");

		DMDocument.unselectDocuments(dmDocumentTitle = "Document_2");

		DMNavigator.gotoAddCategoriesPG(dmDocumentTitleList = "Document_1,Document_2");

		DMDocument.removeCategories(categoriesNameList = "Books");

		DMNavigator.gotoDocumentPG(dmDocumentTitle = "Document_1");

		DMDocument.viewCategoriesInfo(categoryNameList = "Plants");

		DMDocument.viewNoCategoriesPG(categoryName = "Books");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoDocumentPG(dmDocumentTitle = "Document_2");

		DMDocument.viewCategoriesInfo(categoryNameList = "Pets");

		DMDocument.viewNoCategoriesPG(categoryName = "Books");
	}

	@description = "This test covers LPS-94955. It ensures that users can edit category tags by using the bulk append action."
	@priority = "5"
	test CanEditAndAppendCategoryTags {
		property portal.acceptance = "true";

		JSONCategory.addVocabulary(
			groupName = "Guest",
			title = "Vocabulary Name");

		JSONCategory.addCategory(
			groupName = "Guest",
			title = "Books",
			vocabularyName = "Vocabulary Name");

		for (var entryCount : list "1,2") {
			JSONDocument.addFileWithUploadedFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "Document_${entryCount}",
				groupName = "Guest",
				mimeType = "application/msword",
				sourceFileName = "Document_${entryCount}.doc");
		}

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoAddCategoriesPG(dmDocumentTitleList = "Document_1,Document_2");

		DMDocument.editCategoryTags(
			categoryNameList = "Books",
			itemNumber = "2",
			multipleDocuments = "true");

		for (var key_dmDocumentTitle : list "Document_1,Document_2") {
			DMNavigator.gotoDocumentPG(dmDocumentTitle = "${key_dmDocumentTitle}");

			DMDocument.viewCategoriesInfo(categoryName = "Books");

			Navigator.gotoPage(pageName = "Documents and Media Page");
		}
	}

	@description = "This test covers LPS-91749. It ensures that a user can edit tags by using the bulk append action for checked out document files."
	@priority = "4"
	test CanEditAndAppendTagForCheckedOutFiles {
		for (var entryCount : list "1,2") {
			JSONDocument.addFileWithUploadedFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "Document_${entryCount}.doc",
				groupName = "Guest",
				mimeType = "application/msword",
				sourceFileName = "Document_${entryCount}.doc");
		}

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.selectDocuments(dmDocumentTitle = "Document_1.doc,Document_2.doc");

		DMDocument.checkoutViaManagementBar();

		DMDocument.selectDocuments(dmDocumentTitle = "Document_1.doc,Document_2.doc");

		DMNavigator.gotoItemViaManagementBar(item = "Edit Tags");

		DMDocument.editTags(
			itemNumber = "2",
			multipleDocuments = "true",
			tagName = "newtag");

		DMNavigator.openToEntryInAdmin(
			dmDocumentTitle = "Document_1.doc",
			groupName = "Guest",
			siteURLKey = "guest");

		DMDocument.viewTagInfo(tagName = "newtag");

		DMNavigator.openToEntryInAdmin(
			dmDocumentTitle = "Document_2.doc",
			groupName = "Guest",
			siteURLKey = "guest");

		DMDocument.viewTagInfo(tagName = "newtag");
	}

	@description = "This test edits tags by using the bulk append action."
	@priority = "4"
	test CanEditAndAppendTags {
		for (var entryCount : list "1,2") {
			JSONDocument.addFileWithUploadedFile(
				assetTagNames = "tagone",
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "Document_${entryCount}",
				groupName = "Guest",
				mimeType = "application/msword",
				sourceFileName = "Document_${entryCount}.doc");
		}

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.selectDocuments(dmDocumentTitle = "Document_1,Document_2");

		DMNavigator.gotoItemViaManagementBar(item = "Edit Tags");

		DMDocument.editTags(
			itemNumber = "2",
			multipleDocuments = "true",
			tagName = "tagtwo");

		for (var key_dmDocumentTitle : list "Document_1,Document_2") {
			DMNavigator.gotoDocumentPG(dmDocumentTitle = "${key_dmDocumentTitle}");

			DMDocument.viewTagInfo(tagName = "tagone,tagtwo");

			Navigator.gotoPage(pageName = "Documents and Media Page");
		}
	}

	@description = "This test edits tags by using the bulk append action, also covers LPS-91586."
	@priority = "4"
	test CanEditAndReplaceTags {
		for (var entryCount : list "1,2,3") {
			JSONDocument.addFileWithUploadedFile(
				assetTagNames = "Tag${entryCount}",
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "Document_${entryCount}",
				groupName = "Guest",
				mimeType = "application/msword",
				sourceFileName = "Document_${entryCount}.doc");
		}

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.selectDocuments(dmDocumentTitle = "Document_1,Document_3");

		DMNavigator.gotoItemViaManagementBar(item = "Edit Tags");

		DMDocument.editTags(
			editAction = "Replace",
			itemNumber = "2",
			multipleDocuments = "true",
			tagName = "newTag");

		for (var key_dmDocumentTitle : list "Document_1,Document_3") {
			DMNavigator.gotoDocumentPG(dmDocumentTitle = "${key_dmDocumentTitle}");

			DMDocument.viewTagInfo(tagName = "newTag");

			Navigator.gotoPage(pageName = "Documents and Media Page");
		}

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.selectDocuments(dmDocumentTitle = "Document_1,Document_2");

		DMNavigator.gotoItemViaManagementBar(item = "Edit Tags");

		DMDocument.editTags(
			editAction = "Replace",
			itemNumber = "2",
			multipleDocuments = "true",
			tagName = "");

		for (var key_dmDocumentTitle : list "Document_1,Document_2") {
			DMNavigator.gotoDocumentPG(dmDocumentTitle = "${key_dmDocumentTitle}");

			DMDocument.viewNoTagInfo();

			Navigator.gotoPage(pageName = "Documents and Media Page");
		}
	}

	@description = "This ensures that the files permissions can be edited via bulk editing."
	@priority = "5"
	test CanEditFilesPermissions {
		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document_1",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document_2",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_2.doc");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoDocumentPermissionsCP(dmDocumentTitle = "Document_1");

		PermissionsInline.addOrRemoveAnyPermissionsFromSpecificRole(
			addOrRemoveViewPermission = "Remove",
			permissionsKeyList = "INLINE_PERMISSIONS_VIEW_CHECKBOX,INLINE_PERMISSIONS_ADD_DISCUSSION_CHECKBOX",
			roleTitle = "Guest");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.selectDocuments(dmDocumentTitle = "Document_1,Document_2");

		DMNavigator.gotoItemViaManagementBar(
			item = "dropdownMenu",
			menuItem = "Permissions");

		PermissionsInline.addOrRemoveAnyPermissionsFromSpecificRole(
			addOrRemoveViewPermission = "Add",
			permissionsBulkEditing = "true",
			permissionsKeyList = "INLINE_PERMISSIONS_VIEW_CHECKBOX,INLINE_PERMISSIONS_ADD_DISCUSSION_CHECKBOX",
			roleTitle = "Guest");

		User.logoutPG();

		DMDocument.validateGuestUserViewPermission(
			dmDocumentTitle = "Document_1",
			groupName = "Guest",
			guestWithViewPermission = "true");

		DMDocument.validateGuestUserViewPermission(
			dmDocumentTitle = "Document_2",
			groupName = "Guest",
			guestWithViewPermission = "true");
	}

	@description = "This ensures that multiple folders with the same permissions can be edited via bulk editing."
	@priority = "5"
	test CanEditFoldersWithSamePermissions {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		JSONFolder.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name1",
			groupName = "Guest");

		JSONFolder.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name2",
			groupName = "Guest");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMFolder.selectFolders(dmFolderName = "DM Folder Name1,DM Folder Name2");

		DMNavigator.gotoItemViaManagementBar(
			item = "dropdownMenu",
			menuItem = "Permissions");

		PermissionsInline.addOrRemoveAnyPermissionsFromSpecificRole(
			addOrRemoveViewPermission = "Remove",
			permissionsBulkEditing = "true",
			permissionsKeyList = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
			roleTitle = "Guest");

		PermissionsInline.addOrRemoveAnyPermissionsFromSpecificRole(
			addOrRemoveViewPermission = "Add",
			permissionsBulkEditing = "true",
			permissionsKeyList = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
			roleTitle = "User");

		User.logoutPG();

		Navigator.gotoPage(pageName = "Documents and Media Page");

		LexiconCard.viewCardNotPresent(card = "DM Folder Name1");

		LexiconCard.viewCardNotPresent(card = "DM Folder Name2");

		User.logoutPG();

		User.firstLoginUI(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMFolder.viewEntryPresent(dmFolderName = "DM Folder Name1");

		DMFolder.viewEntryPresent(dmFolderName = "DM Folder Name2");
	}

	@description = "This test selects multiple documents and moves them into one folder."
	@priority = "5"
	test CanMoveDocumentsToFolder {
		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Documents and Media Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Site Name",
			layoutName = "Documents and Media Page",
			widgetName = "Documents and Media");

		Navigator.openSitePage(
			pageName = "Documents and Media Page",
			siteName = "Site Name");

		DMDocument.enableActionsMenuOnPortlet();

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		JSONFolder.addFolder(
			dmFolderDescription = "Folder 1",
			dmFolderName = "Folder 1",
			groupName = "Site Name");

		for (var entryCount : list "1,2") {
			JSONDocument.addFileWithUploadedFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "Document_${entryCount}",
				groupName = "Site Name",
				mimeType = "application/msword",
				sourceFileName = "Document_${entryCount}.doc");
		}

		Navigator.openSitePage(
			pageName = "Documents and Media Page",
			siteName = "Site Name");

		DMDocument.selectDocuments(dmDocumentTitle = "Document_1,Document_2");

		DMDocument.moveToFolderPG(
			bulkSelection = "2 of 3 Selected",
			dmFolderName = "Folder 1");

		for (var key_dmDocumentTitle : list "Document_1,Document_2") {
			Navigator.openSitePage(
				pageName = "Documents and Media Page",
				siteName = "Site Name");

			DMNavigator.gotoFolder(dmFolderName = "Folder 1");

			DMDocument.viewPG(dmDocumentTitle = "${key_dmDocumentTitle}");
		}
	}

	@description = "This ensures that the permissions option in the bulk editing is disabled when editing file and folder simultaneously."
	@priority = "4"
	test CannotEditFileAndFolderPermissionsSimultaneously {
		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document_1",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		JSONFolder.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name",
			groupName = "Guest");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.selectDocuments(dmDocumentTitle = "Document_1,DM Folder Name");

		Click(locator1 = "Icon#TOOLBAR_ELLIPSIS");

		MenuItem.viewDisabled(menuItem = "Permissions");
	}

	@description = "This ensures that the files permissions can be removed via bulk editing."
	@priority = "5"
	test CanRemoveFilesPermissions {
		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document_1",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Document_2",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_2.doc");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoDocumentPermissionsCP(dmDocumentTitle = "Document_1");

		PermissionsInline.addOrRemoveAnyPermissionsFromSpecificRole(
			addOrRemoveViewPermission = "Remove",
			permissionsKeyList = "INLINE_PERMISSIONS_VIEW_CHECKBOX,INLINE_PERMISSIONS_ADD_DISCUSSION_CHECKBOX",
			roleTitle = "Guest");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.selectDocuments(dmDocumentTitle = "Document_1,Document_2");

		DMNavigator.gotoItemViaManagementBar(
			item = "dropdownMenu",
			menuItem = "Permissions");

		PermissionsInline.addOrRemoveAnyPermissionsFromSpecificRole(
			addOrRemoveViewPermission = "Remove",
			permissionsBulkEditing = "true",
			permissionsKeyList = "INLINE_PERMISSIONS_VIEW_CHECKBOX,INLINE_PERMISSIONS_ADD_DISCUSSION_CHECKBOX",
			roleTitle = "Guest");

		User.logoutPG();

		DMDocument.validateGuestUserViewPermission(
			dmDocumentTitle = "Document_1",
			groupName = "Guest",
			guestWithoutViewPermission = "true");

		DMDocument.validateGuestUserViewPermission(
			dmDocumentTitle = "Document_2",
			groupName = "Guest",
			guestWithoutViewPermission = "true");
	}

	@description = "This ensures that the indeterminate permissions in the permissions modal can be unchecked and saved when editing the multiple folders."
	@priority = "5"
	test CanRemovePermissionsForFoldersWithDifferentPermissions {
		JSONFolder.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name1",
			groupName = "Guest");

		JSONFolder.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name2",
			groupName = "Guest");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMNavigator.gotoFolderPermissions(dmFolderName = "DM Folder Name2");

		PermissionsInline.addOrRemoveAnyPermissionsFromSpecificRole(
			addOrRemoveViewPermission = "Remove",
			permissionsKeyList = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
			roleTitle = "Guest");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMFolder.selectFolders(dmFolderName = "DM Folder Name1,DM Folder Name2");

		DMNavigator.gotoItemViaManagementBar(
			item = "dropdownMenu",
			menuItem = "Permissions");

		PermissionsInline.addOrRemoveAnyPermissionsFromSpecificRole(
			addOrRemoveViewPermission = "Remove",
			permissionsBulkEditing = "true",
			permissionsKeyList = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
			roleTitle = "Guest");

		User.logoutPG();

		Navigator.gotoPage(pageName = "Documents and Media Page");

		LexiconCard.viewCardNotPresent(card = "DM Folder Name1");

		LexiconCard.viewCardNotPresent(card = "DM Folder Name2");
	}

	@description = "This test covers LPS-89636. It saves tags without clicking Enter or adding a Comma."
	@priority = "4"
	test CanSaveTags {
		for (var entryCount : list "1,2,3,4") {
			JSONDocument.addFileWithUploadedFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "Document_${entryCount}",
				groupName = "Guest",
				mimeType = "image/jpeg",
				sourceFileName = "Document_${entryCount}.jpg");
		}

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMDocument.selectDocuments(dmDocumentTitle = "Document_1,Document_2,Document_3,Document_4");

		DMNavigator.gotoItemViaManagementBar(item = "Edit Tags");

		DMDocument.editTags(
			itemNumber = "4",
			multipleDocuments = "true",
			tagName = "tagone");

		for (var key_dmDocumentTitle : list "Document_1,Document_2,Document_3,Document_4") {
			DMNavigator.gotoDocumentPG(dmDocumentTitle = "${key_dmDocumentTitle}");

			DMDocument.viewTagInfo(tagName = "tagone");

			Navigator.gotoPage(pageName = "Documents and Media Page");
		}
	}

	@priority = "4"
	@refactordone
	test SingleDocumentSelectionDoesNotCheckAllEntries {
		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document1 Title",
			groupName = "Guest",
			mimeType = "image/jpeg",
			sourceFileName = "Document_1.jpg");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document2 Title",
			groupName = "Guest",
			mimeType = "image/jpeg",
			sourceFileName = "Document_2.jpg");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		DMDocument.checkDocumentCheckboxCP(dmDocumentTitle = "DM Document1 Title");

		AssertElementPresent(locator1 = "Checkbox#SELECT_ALL_CHECKBOX_TICK");

		DMDocument.uncheckDocumentCheckboxCP(dmDocumentTitle = "DM Document1 Title");

		AssertElementNotPresent(locator1 = "Checkbox#SELECT_ALL_CHECKBOX_TICK");
	}

	@description = "This test covers LPS-91524. It asserts that the Edit Tags icon is disabled when workflow is enabled. "
	@priority = "4"
	test TagsCannotBeEditedForUnapprovedEntries {
		JSONFolder.addFolder(
			dmFolderDescription = "DM Folder Description",
			dmFolderName = "DM Folder Name",
			groupName = "Guest");

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMFolder.configureWorkflowCP(
			dmFolderName = "DM Folder Name",
			workflowDefinition = "Single Approver",
			workflowRestriction = "Default Workflow");

		DMNavigator.openToFolderInAdmin(
			dmFolderName = "DM Folder Name",
			groupName = "Guest",
			siteURLKey = "guest");

		for (var entryCount : list "1,2") {
			DMDocument.addWithWorkflowCP(
				dmDocumentDescription = "DM Document 1 in Folder",
				dmDocumentFile = "Document_${entryCount}.jpg",
				dmDocumentTitle = "DM Document ${entryCount} in Folder");
		}

		Navigator.gotoPage(pageName = "Documents and Media Page");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		DMNavigator.gotoFolder(dmFolderName = "DM Folder Name");

		DMDocument.selectDocuments(dmDocumentTitle = "DM Document 1 in Folder,DM Document 2 in Folder");

		AssertElementPresent(locator1 = "Icon#EDIT_TAGS_DISABLED");
	}

	@description = "This test covers LPS-94994. It ensures that users can edit category tags by using the bulk append action."
	@priority = "5"
	test UncommonCategoryTagsCanBeReplacedUsingBulkAction {
		DMDocument.addMultiplePG(dmDocumentFileList = "Document_1.doc,Document_2.jpg");

		for (var categoryName : list "Books,Plants,Pets,Furniture") {
			JSONCategory.addCategory(
				groupName = "Guest",
				title = "${categoryName}",
				vocabularyName = "Topic");
		}

		Navigator.gotoPage(pageName = "Documents and Media Page");

		DMNavigator.gotoAddCategoriesPG(dmDocumentTitleList = "Document_1");

		DMDocument.editCategoryTags(categoryNameList = "Books,Plants");

		DMDocument.unselectDocuments(dmDocumentTitle = "Document_1");

		DMNavigator.gotoAddCategoriesPG(dmDocumentTitleList = "Document_2");

		DMDocument.editCategoryTags(categoryNameList = "Books,Pets");

		DMDocument.unselectDocuments(dmDocumentTitle = "Document_2");

		DMNavigator.gotoAddCategoriesPG(dmDocumentTitleList = "Document_1,Document_2");

		DMDocument.editCategoryTags(
			categoryNameList = "Furniture",
			editAction = "Replace",
			itemNumber = "2",
			multipleDocuments = "true");

		Navigator.gotoPage(pageName = "Documents and Media Page");

		for (var key_dmDocumentTitle : list "Document_1,Document_2") {
			DMNavigator.gotoDocumentPG(dmDocumentTitle = "${key_dmDocumentTitle}");

			DMDocument.viewCategoriesInfo(categoryNameList = "Books,Furniture");

			Navigator.gotoPage(pageName = "Documents and Media Page");
		}
	}

}
@component-name = "portal-lima"
definition {

	property portal.acceptance = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Friendly URL Service";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Source Page");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.logoutPG();
		}
	}

	@description = "This ensures that accessing an old friendlyURL in the default language after publishing the new friendly URL will redirect the user to the new URL."
	@priority = "5"
	test CanAccessOldFriendlyURLAfterPublishing {
		var portalURL = PropsUtil.get("portal.url");

		JSONStaging.enableLocalStaging(groupName = "Test Site Name");

		PagesAdmin.openToConfigurePublicPage(
			groupName = "Test Site Name (Staging)",
			layoutName = "Test Source Page",
			site = "false",
			siteURLKey = "test-site-name-staging");

		PagesAdmin.editFriendlyURL(friendlyURLEdit = "/new-test-page");

		Navigator.gotoStagedSitePage(
			pageName = "Test Source Page",
			siteName = "Test Site Name");

		Staging.gotoPublishToLive();

		Staging.publishToLive();

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/test-source-page");

		AssertLocation(value1 = "${portalURL}/en/web/test-site-name/new-test-page");

		AssertVisible(
			key_pageName = "Test Source Page",
			locator1 = "Home#PAGE_ACTIVE");
	}

	@description = "This ensures that accessing an old localized friendlyURL after publishing the new friendly URL will redirect the user to the new URL."
	@priority = "5"
	test CanAccessOldLocalizedFriendlyURLAfterPublishing {
		var portalURL = PropsUtil.get("portal.url");

		JSONStaging.enableLocalStaging(groupName = "Test Site Name");

		PagesAdmin.openToConfigurePublicPage(
			groupName = "Test Site Name (Staging)",
			layoutName = "Test Source Page",
			site = "false",
			siteURLKey = "test-site-name-staging");

		PagesAdmin.addLocalizedFriendlyURL(
			friendlyURLLocalizedList = "es-ES",
			localizedFriendlyURLList = "/pagina-de-prueba",
			localizedNameList = "Pagina de prueba");

		Navigator.gotoStagedSitePage(
			pageName = "Test Source Page",
			siteName = "Test Site Name");

		Staging.gotoPublishToLive();

		Staging.publishToLive();

		PagesAdmin.openToConfigurePublicPage(
			groupName = "Test Site Name (Staging)",
			layoutName = "Test Source Page",
			site = "false",
			siteURLKey = "test-site-name-staging");

		PagesAdmin.addLocalizedFriendlyURL(
			friendlyURLLocalizedList = "es-ES",
			localizedFriendlyURLList = "/nueva-pagina-de-prueba",
			localizedNameList = "-");

		Navigator.gotoStagedSitePage(
			pageName = "Test Source Page",
			siteName = "Test Site Name");

		Staging.gotoPublishToLive();

		Staging.publishToLive();

		Navigator.openSpecificURL(url = "${portalURL}/es/web/test-site-name/pagina-de-prueba");

		AssertLocation(value1 = "${portalURL}/es/web/test-site-name/nueva-pagina-de-prueba");

		AssertVisible(
			key_pageName = "Pagina de prueba",
			locator1 = "Home#PAGE_ACTIVE");
	}

	@description = "This test covers LPS-125475. The default page will be displayed when accessing a page through a language without a specific layout."
	@priority = "3"
	test CanAccessWithSpecificDisabledLanguageAfterSettingLocally {
		property custom.properties = "locales.enabled=zh_CN,en_US,en_AU,fr_FR,de_DE,it_IT,ja_JP,pt_BR,es_ES${line.separator}locales=zh_CN,en_US,en_AU,fr_FR,de_DE,it_IT,ja_JP,pt_BR,es_ES,ru_RU,pl_PL,sv_SE,hu_HU,ar_SA,nb_NO,hr_HR,fi_FI,nl_NL,cs_CZ";

		var portalURL = PropsUtil.get("portal.url");

		Site.openSiteSettingsAdmin(siteURLKey = "test-site-name");

		Site.configureCurrentLanguagesCP(
			currentSiteLanguages = "English (United States),Japanese (Japan)",
			defaultSiteLanguage = "English (United States)");

		Navigator.openSpecificURL(url = "${portalURL}/en_AU/web/test-site-name/test-source-page");

		AssertTextNotPresent(value1 = "The requested resource could not be found.");
	}

	@description = "This ensures that a user can add friendlyURL with escape character in a web content."
	@priority = "3"
	test CanAddFriendlyURLWithEscapeCharacter {
		var portalURL = PropsUtil.get("portal.url");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Source Page",
			widgetName = "Asset Publisher");

		Navigator.openSitePage(
			pageName = "Test Source Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.configureDisplaySettings(displaySettings = "Set as the Default Asset Publisher for This Page");

		WebContentNavigator.openToAddBasicArticle(
			groupName = "Test Site Name",
			siteURLKey = "test-site-name");

		WebContent.addWithFriendlyURL(
			friendlyURL = '''link&''',
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title");

		WebContentNavigator.openToEditWCInSite(
			groupName = "Test Site Name",
			siteURLKey = "test-site-name",
			urlTitle = '''link-''',
			webContentTitle = "WC WebContent Title");

		WebContent.editDisplayPage(
			pageName = "Test Source Page",
			pageType = "Pages");

		PortletEntry.publish();

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/-/link-");

		AssetPublisherPortlet.viewAssetPG(assetTitle = "WC WebContent Title");
	}

	@description = "This ensures that a user is redirected to the updated friendlyURL when he accesses the former friendlyURL."
	@priority = "5"
	test CanRedirectToUpdatedFriendlyURL {
		var portalURL = PropsUtil.get("portal.url");

		PagesAdmin.openToConfigurePublicPage(
			groupName = "Test Site Name",
			layoutName = "Test Source Page",
			siteURLKey = "test-site-name");

		PagesAdmin.editFriendlyURL(friendlyURLEdit = "/new-test-page");

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/test-source-page");

		AssertLocation(value1 = "${portalURL}/en/web/test-site-name/new-test-page");

		AssertVisible(
			key_pageName = "Test Source Page",
			locator1 = "Home#PAGE_ACTIVE");
	}

	@description = "This ensures that a user is redirected to the updated localized friendlyURL when he accesses the former localized friendlyURL."
	@priority = "5"
	test CanRedirectToUpdatedLocalizedFriendlyURL {
		var portalURL = PropsUtil.get("portal.url");

		PagesAdmin.openToConfigurePublicPage(
			groupName = "Test Site Name",
			layoutName = "Test Source Page",
			siteURLKey = "test-site-name");

		PagesAdmin.addLocalizedFriendlyURL(
			friendlyURLLocalizedList = "es-ES,es-ES",
			localizedFriendlyURLList = "/pagina-de-prueba,/nueva-pagina-de-prueba",
			localizedNameList = "Pagina de prueba,-");

		Navigator.openSpecificURL(url = "${portalURL}/es/web/test-site-name/pagina-de-prueba");

		AssertLocation(value1 = "${portalURL}/es/web/test-site-name/nueva-pagina-de-prueba");

		AssertVisible(
			key_pageName = "Pagina de prueba",
			locator1 = "Home#PAGE_ACTIVE");
	}

	@description = "This ensures that the old friendlyURL can be removed from the history."
	@priority = "5"
	test CanRemoveOldFriendlyURL {
		var portalURL = PropsUtil.get("portal.url");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page");

		PagesAdmin.openToConfigurePublicPage(
			groupName = "Test Site Name",
			layoutName = "Test Page",
			siteURLKey = "test-site-name");

		PagesAdmin.editFriendlyURL(friendlyURLEdit = "/new-test-page");

		PagesAdmin.gotoFriendlyURLHistory();

		PagesAdmin.removeOldFriendlyURL(oldFriendlyURL = "/test-page");

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/test-page");

		AssertTextPresent(value1 = "The requested resource could not be found.");
	}

	@description = "This ensures that the friendlyURL of web content can be reset to default."
	@priority = "4"
	test CanResetFriendlyURLToDefault {
		var portalURL = PropsUtil.get("portal.url");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Source Page",
			widgetName = "Asset Publisher");

		Navigator.openSitePage(
			pageName = "Test Source Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.configureDisplaySettings(displaySettings = "Set as the Default Asset Publisher for This Page");

		JSONWebcontent.addWebContent(
			content = "Web Content Content",
			groupName = "Test Site Name",
			title = "Web Content Article");

		WebContentNavigator.openToEditWCInSite(
			groupName = "Test Site Name",
			siteURLKey = "test-site-name",
			version = "1.1",
			webContentTitle = "Web Content Article");

		WebContent.editDisplayPage(
			pageName = "Test Source Page",
			pageType = "Pages");

		WebContent.editFriendlyURL(webContentFriendlyURL = "friendlyUrl");

		PortletEntry.publish();

		WebContentNavigator.openToEditWCInSite(
			groupName = "Test Site Name",
			siteURLKey = "test-site-name",
			urlTitle = "friendlyurl",
			version = "1.1",
			webContentTitle = "Web Content Article");

		WebContent.editFriendlyURL(webContentFriendlyURL = "web-content-article");

		PortletEntry.publish();

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/-/web-content-article");

		AssetPublisherPortlet.viewAssetPG(assetTitle = "Web Content Article");
	}

	@description = "This ensures that the old friendlyURL can be restored from the history."
	@priority = "5"
	test CanRestoreOldFriendlyURL {
		var portalURL = PropsUtil.get("portal.url");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page");

		PagesAdmin.openToConfigurePublicPage(
			groupName = "Test Site Name",
			layoutName = "Test Page",
			siteURLKey = "test-site-name");

		PagesAdmin.editFriendlyURL(friendlyURLEdit = "/new-test-page");

		PagesAdmin.gotoFriendlyURLHistory();

		PagesAdmin.restoreOldFriendlyURL(
			activeURL = "/new-test-page",
			friendlyURLEdit = "/test-page",
			oldFriendlyURL = "/test-page",
			restoredURL = "/test-page");

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/test-page");

		AssertLocation(value1 = "${portalURL}/web/test-site-name/test-page");
	}

	@description = "This ensures that both the active URL and old friendlyURL can be viewed from the history."
	@priority = "5"
	test CanViewFriendlyURLsFromHistory {
		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page");

		PagesAdmin.openToConfigurePublicPage(
			groupName = "Test Site Name",
			layoutName = "Test Page",
			siteURLKey = "test-site-name");

		PagesAdmin.editFriendlyURL(friendlyURLEdit = "/new-test-page");

		PagesAdmin.gotoFriendlyURLHistory();

		PagesAdmin.viewActiveURL(activeURL = "/new-test-page");

		AssertElementNotPresent(
			key_activeURL = "/test-page",
			locator1 = "PagesAdmin#FRIENDLY_URL_ACTIVE_URL");

		PagesAdmin.viewOldFriendlyURL(oldFriendlyURL = "/test-page");

		AssertElementNotPresent(
			key_oldFriendlyURL = "/new-test-page",
			locator1 = "PagesAdmin#FRIENDLY_URL_HISTORY_ITEM");
	}

	@description = "This test covers LPS-116068. It ensures that both the active URL and old friendlyURL can be viewed from the history of a default page."
	@priority = "5"
	test CanViewFriendlyURLsViaDefaultPage {
		PagesAdmin.openToConfigurePublicPage(
			groupName = "Guest",
			layoutName = "Search",
			siteURLKey = "guest");

		PagesAdmin.editFriendlyURL(friendlyURLEdit = "/new-search-page");

		PagesAdmin.gotoFriendlyURLHistory();

		PagesAdmin.viewActiveURL(activeURL = "/new-search-page");

		AssertElementNotPresent(
			key_activeURL = "/search",
			locator1 = "PagesAdmin#FRIENDLY_URL_ACTIVE_URL");

		PagesAdmin.viewOldFriendlyURL(oldFriendlyURL = "/search");

		AssertElementNotPresent(
			key_oldFriendlyURL = "/new-search-page",
			locator1 = "PagesAdmin#FRIENDLY_URL_HISTORY_ITEM");
	}

	@description = "This test covers LPS-119836. It ensures the localized URL with special characters can be escaped correctly and viewed in friendly URL history."
	@priority = "4"
	test CanViewFriendlyURLWithSpecialCharacters {
		property portal.acceptance = "false";

		PagesAdmin.openToConfigurePublicPage(
			groupName = "Test Site Name",
			layoutName = "Test Source Page",
			siteURLKey = "test-site-name");

		PagesAdmin.addLocalizedFriendlyURL(
			friendlyURLLocalizedList = "es-ES",
			localizedFriendlyURLList = "/^_^",
			localizedNameList = "Pagina de prueba");

		var portalURL = PropsUtil.get("portal.url");

		Navigator.openWithAppendToBaseURL(urlAppend = "es/web/test-site-name/test-source-page");

		AssertLocation(value1 = "${portalURL}/es/web/test-site-name/%5E_%5E");

		AssertTextNotPresent(value1 = "The requested resource could not be found");

		Navigator.openWithAppendToBaseURL(urlAppend = "en/web/test-site-name/test-source-page");

		PagesAdmin.openToConfigurePublicPage(
			groupName = "Test Site Name",
			layoutName = "Test Source Page",
			siteURLKey = "test-site-name");

		PagesAdmin.addLocalizedFriendlyURL(
			friendlyURLLocalizedList = "es-ES",
			localizedFriendlyURLList = "/página-de-prueba",
			localizedNameList = "-");

		PagesAdmin.gotoFriendlyURLHistory();

		PortletEntry.changeLocale(
			friendlyURLHistory = "true",
			localizedFriendlyURL = "es-ES");

		PagesAdmin.viewActiveURL(activeURL = "/página-de-prueba");

		PagesAdmin.viewOldFriendlyURL(oldFriendlyURL = "/^_^");

		Navigator.openWithAppendToBaseURL(urlAppend = "es/web/test-site-name/test-source-page");

		AssertLocation(value1 = "${portalURL}/es/web/test-site-name/%5E_%5E");
	}

	@description = "This ensures that both the localized active URL and old friendlyURL can be viewed from the history."
	@priority = "5"
	test CanViewLocalizedFriendlyURLsFromHistory {
		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page");

		PagesAdmin.openToConfigurePublicPage(
			groupName = "Test Site Name",
			layoutName = "Test Page",
			siteURLKey = "test-site-name");

		PagesAdmin.editFriendlyURL(friendlyURLEdit = "/new-test-page");

		PagesAdmin.addLocalizedFriendlyURL(
			friendlyURLLocalizedList = "es-ES,es-ES",
			localizedFriendlyURLList = "/pagina-de-prueba,/nueva-pagina-de-prueba",
			localizedNameList = "Pagina de prueba,-");

		PagesAdmin.gotoFriendlyURLHistory();

		PortletEntry.changeLocale(
			friendlyURLHistory = "true",
			localizedFriendlyURL = "es-ES");

		PagesAdmin.viewActiveURL(activeURL = "/nueva-pagina-de-prueba");

		AssertElementNotPresent(
			key_activeURL = "/pagina-de-prueba",
			locator1 = "PagesAdmin#FRIENDLY_URL_ACTIVE_URL");

		PagesAdmin.viewOldFriendlyURL(oldFriendlyURL = "/pagina-de-prueba");

		AssertElementNotPresent(
			key_oldFriendlyURL = "/nueva-pagina-de-prueba",
			locator1 = "PagesAdmin#FRIENDLY_URL_HISTORY_ITEM");
	}

	@description = "This ensures that the multiple old friendlyURLs can be viewed from the history. This test also covers the first old friendlyURL is the last one."
	@priority = "5"
	test CanViewMultipleOldFriendlyURLsFromHistory {
		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page");

		PagesAdmin.openToConfigurePublicPage(
			groupName = "Test Site Name",
			layoutName = "Test Page",
			siteURLKey = "test-site-name");

		PagesAdmin.editFriendlyURL(friendlyURLEdit = "/new-test-page");

		PagesAdmin.editFriendlyURL(friendlyURLEdit = "/new-test-page1");

		PagesAdmin.gotoFriendlyURLHistory();

		PagesAdmin.viewActiveURL(activeURL = "/new-test-page1");

		PagesAdmin.viewOldFriendlyURL(
			lastOldFriendlyURL = "/test-page",
			oldFriendlyURL = "/new-test-page");
	}

	@description = "This ensures that only the active URL can be viewed by default language from the history after localization without friendlyURL."
	@priority = "5"
	test CanViewURLsAfterLocalizationWithoutFriendlyURL {
		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Page");

		PagesAdmin.openToConfigurePublicPage(
			groupName = "Test Site Name",
			layoutName = "Test Page",
			siteURLKey = "test-site-name");

		PagesAdmin.editFriendlyURL(friendlyURLEdit = "/new-test-page");

		PortletEntry.changeLocale(
			fieldName = "Friendly URL",
			locale = "es-ES");

		PortletEntry.inputName(name = "Pagina de prueba");

		Button.clickSave();

		PortletEntry.changeLocale(
			fieldName = "Friendly URL",
			locale = "es-ES");

		PagesAdmin.gotoFriendlyURLHistory();

		PagesAdmin.viewActiveURL(activeURL = "/new-test-page");

		PagesAdmin.viewOldFriendlyURLNotPresent(oldFriendlyURLList = "/test-page");
	}

	@description = "This ensures that a previous friendlyURL of web content can be deleted when updating."
	@priority = "4"
	test CanViewWCWithLatestFriendlyURL {
		var portalURL = PropsUtil.get("portal.url");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Source Page",
			widgetName = "Asset Publisher");

		Navigator.openSitePage(
			pageName = "Test Source Page",
			siteName = "Test Site Name");

		AssetPublisherPortlet.configureDisplaySettings(displaySettings = "Set as the Default Asset Publisher for This Page");

		WebContentNavigator.openToAddBasicArticle(
			groupName = "Test Site Name",
			siteURLKey = "test-site-name");

		WebContent.addWithFriendlyURL(
			friendlyURL = "friendlyUrl",
			webContentContent = "WC WebContent Content",
			webContentTitle = "WC WebContent Title");

		WebContentNavigator.openToEditWCInSite(
			groupName = "Test Site Name",
			siteURLKey = "test-site-name",
			urlTitle = "friendlyurl",
			webContentTitle = "Web Content Article");

		WebContent.editFriendlyURL(webContentFriendlyURL = "editedfriendlyUrl");

		WebContent.editDisplayPage(
			pageName = "Test Source Page",
			pageType = "Pages");

		PortletEntry.publish();

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/-/friendlyUrl");

		AssertTextEquals(
			locator1 = "Home#ERROR_MESSAGE_ALERT_ERROR",
			value1 = "Not Found");

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/-/editedfriendlyUrl");

		AssetPublisherPortlet.viewAssetPG(assetTitle = "WC WebContent Title");
	}

	@description = "This ensures that the default URL of the document will be displayed when accessing a document stored in the current site asset."
	@priority = "5"
	test DefaultURLIsDisplayedWhenAccessingDocumentInCurrentSite {
		var portalURL = PropsUtil.get("portal.url");

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Test Page");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Guest",
			layoutName = "Test Page",
			widgetName = "Asset Publisher");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			groupName = "Guest",
			mimeType = "image/jpeg",
			sourceFileName = "Document_1.jpg");

		JSONLayoutpagetemplate.addDisplayPageTemplateEntry(
			contentType = "Document",
			displayPageTemplateEntryName = "Document DPT",
			groupName = "Guest",
			subType = "Basic Document");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "guest");

		DisplayPageTemplatesAdmin.gotoDisplayPage(displayPageName = "Document DPT");

		PageEditor.publish();

		DisplayPageTemplatesAdmin.markDisplayPageAsDefault(displayPageName = "Document DPT");

		Navigator.gotoPage(pageName = "Test Page");

		AssetPublisherPortlet.configureDynamicAssetSelectionPG(portletName = "Asset Publisher");

		Navigator.gotoPage(pageName = "Test Page");

		AssetPublisherPortlet.clickAssetTitle(assetTitle = "DM Document Title");

		AssertLocation.assertPartialLocation(value1 = "${portalURL}/web/guest/d/dm-document-title");
	}

	@description = "This ensures that the permanent redirection can be performed when accessing the old friendly URL of a blog entry displayed with a display page template."
	@priority = "4"
	test PermanentRedirectionCanBePerformedWhenAccessingBlogOldFriendlyURL {
		property custom.properties = "locale.prepend.friendly.url.style=0";
		property portal.acceptance = "false";
		property test.name.skip.portal.instance = "FriendlyURLService#PermanentRedirectionCanBePerformedWhenAccessingBlogOldFriendlyURL";

		var portalURL = PropsUtil.get("portal.url");

		Redirect.configureFriendlyURLRedirection(redirectionType = "Permanent");

		JSONLayoutpagetemplate.addDisplayPageTemplateEntry(
			contentType = "Blogs Entry",
			displayPageTemplateEntryName = "Display Page Name",
			groupName = "Test Site Name");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

		DisplayPageTemplatesAdmin.gotoDisplayPage(displayPageName = "Display Page Name");

		PageEditor.publish();

		DisplayPageTemplatesAdmin.markDisplayPageAsDefault(displayPageName = "Display Page Name");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blogs Entry Title",
			groupName = "Test Site Name");

		BlogsNavigator.openToEditEntryInSite(
			entryTitle = "Blogs Entry Title",
			groupName = "Test Site Name",
			siteURLKey = "test-site-name");

		Panel.expandPanel(panel = "Configuration");

		BlogsEntry.addCustomURL(customURL = "blogs-entry-new");

		PortletEntry.publish();

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/b/blogs-entry-title");

		AssertLocation(value1 = "${portalURL}/web/test-site-name/b/blogs-entry-new");

		Redirect.viewURLRedirectionType(
			permanent = "true",
			url = "${portalURL}/web/test-site-name/b/blogs-entry-title");
	}

	@description = "This ensures that the permanent redirection can be performed when accessing the old friendly URL of a page."
	@priority = "4"
	test PermanentRedirectionCanBePerformedWhenAccessingPageOldFriendlyURL {
		property custom.properties = "locale.prepend.friendly.url.style=0";
		property test.name.skip.portal.instance = "FriendlyURLService#PermanentRedirectionCanBePerformedWhenAccessingPageOldFriendlyURL";

		var portalURL = PropsUtil.get("portal.url");

		Redirect.configureFriendlyURLRedirection(
			redirectionType = "Permanent",
			systemSettingsInstanceScope = "true");

		PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

		PagesAdmin.gotoPageEllipsisMenuItem(
			menuItem = "Configure",
			pageName = "Test Source Page");

		PagesAdmin.editFriendlyURL(friendlyURLEdit = "/new-page-url");

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/test-source-page");

		AssertLocation(value1 = "${portalURL}/web/test-site-name/new-page-url");

		AssertVisible(
			key_pageName = "Test Source Page",
			locator1 = "Home#PAGE_ACTIVE");

		Redirect.viewURLRedirectionType(
			permanent = "true",
			url = "${portalURL}/web/test-site-name/test-source-page");
	}

	@description = "This ensures that the temporary redirection can be performed when accessing the old friendly URL of a blog entry displayed with a display page template."
	@priority = "4"
	test TemporaryRedirectionCanBePerformedWhenAccessingBlogOldFriendlyURL {
		property custom.properties = "locale.prepend.friendly.url.style=0";
		property portal.acceptance = "false";
		property test.name.skip.portal.instance = "FriendlyURLService#TemporaryRedirectionCanBePerformedWhenAccessingBlogOldFriendlyURL";

		var portalURL = PropsUtil.get("portal.url");

		Redirect.configureFriendlyURLRedirection(
			redirectionType = "Temporary",
			systemSettingsInstanceScope = "true");

		JSONLayoutpagetemplate.addDisplayPageTemplateEntry(
			contentType = "Blogs Entry",
			displayPageTemplateEntryName = "Display Page Name",
			groupName = "Test Site Name");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

		DisplayPageTemplatesAdmin.gotoDisplayPage(displayPageName = "Display Page Name");

		PageEditor.publish();

		DisplayPageTemplatesAdmin.markDisplayPageAsDefault(displayPageName = "Display Page Name");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blogs Entry Title",
			groupName = "Test Site Name");

		BlogsNavigator.openToEditEntryInSite(
			entryTitle = "Blogs Entry Title",
			groupName = "Test Site Name",
			siteURLKey = "test-site-name");

		Panel.expandPanel(panel = "Configuration");

		BlogsEntry.addCustomURL(customURL = "blogs-entry-new");

		PortletEntry.publish();

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/b/blogs-entry-title");

		AssertLocation(value1 = "${portalURL}/web/test-site-name/b/blogs-entry-new");

		Redirect.viewURLRedirectionType(
			temporary = "true",
			url = "${portalURL}/web/test-site-name/b/blogs-entry-title");
	}

	@description = "This ensures that the temporary redirection can be performed when accessing the old friendly URL of a page."
	@priority = "4"
	test TemporaryRedirectionCanBePerformedWhenAccessingPageOldFriendlyURL {
		property custom.properties = "locale.prepend.friendly.url.style=0";
		property test.name.skip.portal.instance = "FriendlyURLService#TemporaryRedirectionCanBePerformedWhenAccessingPageOldFriendlyURL";

		var portalURL = PropsUtil.get("portal.url");

		Redirect.configureFriendlyURLRedirection(redirectionType = "Temporary");

		PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

		PagesAdmin.gotoPageEllipsisMenuItem(
			menuItem = "Configure",
			pageName = "Test Source Page");

		PagesAdmin.editFriendlyURL(friendlyURLEdit = "/new-page-url");

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/test-source-page");

		AssertLocation(value1 = "${portalURL}/web/test-site-name/new-page-url");

		AssertVisible(
			key_pageName = "Test Source Page",
			locator1 = "Home#PAGE_ACTIVE");

		Redirect.viewURLRedirectionType(
			temporary = "true",
			url = "${portalURL}/web/test-site-name/test-source-page");
	}

	@description = "This ensures that the friendly URL will contain the group info to identify the attribution of the document when accessing other site documents."
	@priority = "5"
	test URLIsDisplayedWithGroupWhenAccessingDocumentInOtherSite {
		var portalURL = PropsUtil.get("portal.url");

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Test Page");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Guest",
			layoutName = "Test Page",
			widgetName = "Asset Publisher");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "DM Document Title",
			groupName = "Test Site Name",
			mimeType = "image/jpeg",
			sourceFileName = "Document_1.jpg");

		JSONLayoutpagetemplate.addDisplayPageTemplateEntry(
			contentType = "Document",
			displayPageTemplateEntryName = "Document DPT",
			groupName = "Test Site Name",
			subType = "Basic Document");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

		DisplayPageTemplatesAdmin.gotoDisplayPage(displayPageName = "Document DPT");

		PageEditor.publish();

		DisplayPageTemplatesAdmin.markDisplayPageAsDefault(displayPageName = "Document DPT");

		Navigator.gotoPage(pageName = "Test Page");

		AssetPublisherPortlet.configureDynamicAssetSelectionPG(portletName = "Asset Publisher");

		AssetPublisherPortlet.configureScope(otherSite = "Test Site Name");

		Navigator.gotoPage(pageName = "Test Page");

		AssetPublisherPortlet.clickAssetTitle(assetTitle = "DM Document Title");

		AssertLocation.assertPartialLocation(value1 = "${portalURL}/web/test-site-name/d/dm-document-title");
	}

}
@component-name = "portal-frontend-infrastructure"
definition {

	property osgi.modules.includes = "frontend-data-set-sample-web";
	property portal.acceptance = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Frontend Dataset";
	property testray.main.component.name = "Frontend Dataset";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		task ("Given Frontend Dataset sample portlet") {
			JSONLayout.addPublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page",
				widgetName = "Frontend Data Set Sample");

			JSONLayout.updateLayoutTemplateOfPublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page",
				layoutTemplate = "1 Column");

			Navigator.gotoPage(pageName = "Frontend Data Set Test Page");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONLayout.deletePublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page");
		}
	}

	@description = "LPS-155309. Assert all filters can be removed."
	@priority = "4"
	test AllFiltersCanBeRemoved {
		task ("When there is a filter already established") {
			AssertElementPresent(
				key_filter = "Color: Blue, Green, Yellow",
				locator1 = "FrontendDataSet#FILTER_SUMMARY_LABEL");
		}

		task ("Then all of them can be removed at once") {
			FDSFilters.resetFilter();
		}

		task ("Then the results will be displayed updated accordingly to it") {
			AssertTextEquals(
				locator1 = "FrontendDataSet#PAGINATION_RESULTS",
				value1 = "Showing 1 to 10 of 100");
		}

		task ("And the Filters Summary boxes will be updated accordingly to it") {
			AssertElementNotPresent(
				key_filter = "Color: Blue, Green, Yellow",
				locator1 = "FrontendDataSet#FILTER_SUMMARY_LABEL");
		}
	}

	@description = "LPS-155305. Assert results can be correctly filtered when selecting available filters."
	@priority = "5"
	test AvailableFiltersCanBeApplied {
		task ("When click on the filter button") {
			FDSFilters.openFilters();
		}

		task ("And selecting one of the available filters") {
			FDSFilters.disableStatusFilters(key_status = "Approved,Draft");

			FDSFilters.enableStatusFilters(key_status = "Pending");

			Click(locator1 = "FrontendDataSet#ADD_FILTER_BUTTON");
		}

		task ("Then the results will be displayed accordingly to the filter selected") {
			AssertElementPresent(locator1 = "FrontendDataSet#EMPTY_FDS_TABLE_MESSAGE");
		}

		task ("And the Filters Summary boxes will be updated accordingly to it") {
			AssertElementPresent(
				key_filter = "Status: Pending",
				locator1 = "FrontendDataSet#FILTER_SUMMARY_LABEL");
		}
	}

	@description = "LPS-155313. Assert results can be correctly filtered when exclude is enabled."
	@priority = "5"
	test ExcludeCanBeEnabled {
		task ("When click on the filter button") {
			FDSFilters.openFilters();
		}

		task ("And When adding an option from the status list filter") {
			FDSFilters.enableStatusFilters(key_status = "Approved,Draft");
		}

		task ("And When the exclude option is active") {
			Check.checkToggleSwitch(locator1 = "FrontendDataSet#EXCLUDE_TOGGLE");

			Click(locator1 = "FrontendDataSet#ADD_FILTER_BUTTON");
		}

		task ("Then the filter will be added to the Filters Summary") {
			AssertElementPresent(
				key_filter = "Status: (Exclude) Approved, Draft",
				locator1 = "FrontendDataSet#FILTER_SUMMARY_LABEL");
		}

		task ("And Then the results will be displayed according to the filter") {
			AssertElementPresent(locator1 = "FrontendDataSet#EMPTY_FDS_TABLE_MESSAGE");
		}
	}

	@description = "LPS-155306. Assert results can be correctly filtered by editing the filter summary boxes."
	@priority = "5"
	test FilterCanBeEdited {
		task ("When there is a filter already established") {
			AssertElementPresent(
				key_filter = "Color: Blue, Green, Yellow",
				locator1 = "FrontendDataSet#FILTER_SUMMARY_LABEL");
		}

		task ("And clicking on the Filters Summary") {
			ClickNoError(
				key_filter = "Color: Blue, Green, Yellow",
				locator1 = "FrontendDataSet#FILTER_SUMMARY_LABEL");
		}

		task ("And the filter is edited") {
			FDSFilters.disableStatusFilters(key_status = "Blue,Green");

			FDSFilters.enableStatusFilters(key_status = "Red");

			Click(locator1 = "FrontendDataSet#EDIT_FILTER_BUTTON");
		}

		task ("Then the results will be displayed updated accordingly to it") {
			AssertElementNotPresent(
				key_itemName = "Green",
				locator1 = "FrontendDataSet#TABLE_ITEM_ROW");

			AssertElementNotPresent(
				key_itemName = "Blue",
				locator1 = "FrontendDataSet#TABLE_ITEM_ROW");

			AssertElementPresent(
				key_itemName = "Red",
				locator1 = "FrontendDataSet#TABLE_ITEM_ROW");

			AssertElementPresent(
				key_itemName = "Yellow",
				locator1 = "FrontendDataSet#TABLE_ITEM_ROW");
		}

		task ("And the Filters Summary boxes will be updated accordingly to it") {
			AssertElementPresent(
				key_filter = "Color: Yellow, Red",
				locator1 = "FrontendDataSet#FILTER_SUMMARY_LABEL");
		}
	}

	@description = "LPS-155310. Assert results can be correctly filtered when searching for a filter."
	@priority = "5"
	test FilterCanBeSearched {
		task ("When click on the filter button") {
			FDSFilters.openFilters();
		}

		task ("And When typing a key word in the category search box") {
			FDSFilters.searchFilter(searchTerm = "status");
		}

		task ("And When there is a matching filter ") {
			VerifyElementPresent(
				key_filter = "Status",
				locator1 = "FrontendDataSet#FILTER_OPTION");
		}

		task ("Then the results will be displayed updated accordingly to it") {
			FDSFilters.enableStatusFilters(key_status = "Pending");

			Click(locator1 = "FrontendDataSet#ADD_FILTER_BUTTON");
		}

		task ("And Then the Filters Summary boxes will be updated accordingly to it") {
			AssertElementPresent(
				key_filter = "Status: Pending",
				locator1 = "FrontendDataSet#FILTER_SUMMARY_LABEL");
		}
	}

	@description = "LPS-155311. Assert the filter cannot be filtered by searching."
	@priority = "3"
	test FilterNotFound {
		task ("When clicking on the filter button") {
			FDSFilters.openFilters();
		}

		task ("And typing a key word in the category search box") {
			FDSFilters.searchFilter(searchTerm = "Pending");
		}

		task ("And there is no matching filter") {
			AssertElementNotPresent(
				key_filter = "Pending",
				locator1 = "FrontendDataSet#FILTER_OPTION");
		}

		task ("Then the user will be warned about it") {
			AssertElementPresent(locator1 = "FrontendDataSet#FILTER_WARN_MESSAGE");
		}
	}

	@description = "LPS-155303. Assert management bar will render."
	@priority = "3"
	test ManagementBarWillRender {
		task ("Then the Management Bar will be displayed correctly") {
			AssertElementPresent(locator1 = "FrontendDataSet#MANAGEMENT_BAR");

			takeScreenshot();
		}
	}

	@description = "LPS-155308. Assert one filter can be removed."
	@priority = "4"
	test OneFilterCanBeRemoved {
		task ("When there are several filters already established") {
			FDSFilters.openFilters();

			FDSFilters.addDateRangeFilters(
				key_fromDate = "01/01/2021",
				key_toDate = "02/02/2021");

			VerifyElementPresent(
				key_filter = "Color: Blue, Green, Yellow",
				locator1 = "FrontendDataSet#FILTER_SUMMARY_LABEL");

			VerifyElementPresent(
				key_filter = "Date Range: 1/1/2021 - 2/2/2021",
				locator1 = "FrontendDataSet#FILTER_SUMMARY_LABEL");
		}

		task ("Then it can be removed one by one") {
			FDSFilters.closeFilters();

			FDSFilters.removeFilter(key_filter = "Color");

			FDSFilters.removeFilter(key_filter = "Date Range");
		}

		task ("Then the results will be displayed updated accordingly to it") {
			AssertTextEquals(
				locator1 = "FrontendDataSet#PAGINATION_RESULTS",
				value1 = "Showing 1 to 10 of 100");
		}

		task ("And the Filters Summary boxes will be updated accordingly to it") {
			AssertElementNotPresent(
				key_filter = "Color",
				locator1 = "FrontendDataSet#FILTER_SUMMARY_LABEL");

			AssertElementNotPresent(
				key_filter = "Date Range",
				locator1 = "FrontendDataSet#FILTER_SUMMARY_LABEL");
		}
	}

	@description = "LPS-155312. Assert results can be correctly filtered when using the status filter."
	@priority = "5"
	test StatusFilterCanBeAdded {
		task ("Given no filters applied") {
			FDSFilters.removeAllFilters();
		}

		task ("When click on the filter button") {
			FDSFilters.openFilters();
		}

		task ("And When adding an option from the status list filter") {
			FDSFilters.enableStatusFilters(key_status = "Pending");

			Click(locator1 = "FrontendDataSet#ADD_FILTER_BUTTON");
		}

		task ("Then the filter will be added to the Filters Summary") {
			AssertElementPresent(
				key_filter = "Status: Pending",
				locator1 = "FrontendDataSet#FILTER_SUMMARY_LABEL");
		}

		task ("And Then the results will be displayed according to the filter") {
			AssertElementPresent(locator1 = "FrontendDataSet#EMPTY_FDS_TABLE_MESSAGE");
		}
	}

	@description = "LPS-155304. Assert filter is preloaded when entering on a FDS page for first time."
	@priority = "5"
	test WillRenderAsInitialState {
		task ("And When viewing FDS Sample portlet dataset") {
			Click(
				key_sampleTab = "Customized",
				locator1 = "FrontendDataSet#FDS_DATASET");

			Click(locator1 = "FrontendDataSet#ITEMS_VIEW_MODE_BUTTON");

			Click(
				key_viewMode = "Table",
				locator1 = "FrontendDataSet#ITEMS_VIEW_TYPE_DROPDOWN");
		}

		task ("Then the results will be displayed accordingly to the filter selected") {
			AssertElementPresent(
				key_statusName = "Approved",
				locator1 = "FrontendDataSet#TABLE_STATUS");

			AssertElementNotPresent(
				key_statusName = "Pending",
				locator1 = "FrontendDataSet#TABLE_STATUS");

			AssertTextEquals(
				locator1 = "FrontendDataSet#PAGINATION_RESULTS",
				value1 = "Showing 1 to 10 of 75");
		}

		task ("And Then the Filters Summary boxes will be displayed accordingly to it") {
			AssertElementPresent(
				key_filter = "Color: Blue, Green, Yellow",
				locator1 = "FrontendDataSet#FILTER_SUMMARY_LABEL");
		}
	}

}
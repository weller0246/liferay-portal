@component-name = "portal-frontend-infrastructure"
definition {

	property custom.properties = "upgrade.database.auto.run=false";
	property osgi.modules.includes = "frontend-data-set-sample-web";
	property portal.acceptance = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Frontend Dataset";
	property testray.main.component.name = "Frontend Dataset";

	setUp {
		TestCase.setUpPortalInstance();

		task ("Given Custom view enabled for a dataset display instance") {
			User.firstLoginPG();

			JSONLayout.addPublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page",
				widgetName = "Frontend Data Set Sample");

			JSONLayout.updateLayoutTemplateOfPublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page",
				layoutTemplate = "1 Column");

			Navigator.gotoPage(pageName = "Frontend Data Set Test Page");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONLayout.deletePublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page");
		}
	}

	@description = "LPS-159575 Automation Test Creation - Allow users to delete their custom views"
	@priority = "5"
	test CanBeDeleted {
		task ("Given A custom view is created") {
			FDSCustomView.createNewView(key_nameView = "New View");
		}

		task ("When Deleting the custom view") {
			FDSCustomView.deleteView();
		}

		task ("Then Assert the custom view no longer exists") {
			FDSCustomView.assertCustomViewName(key_nameCustomView = "New View");
		}
	}

	@description = "LPS-164617. Assert it is possible to edit the view settings of a custom view"
	@priority = "4"
	test CanBeEdited {
		task ("And Given a custom view is created and selected") {
			FDSCustomView.createNewView(key_nameView = "New View");
		}

		task ("When make edits to the view settings") {
			FDSCustomView.selectColumn(key_itemName = "Description");

			FDSFilters.editFilters(
				key_disableFilters = "Blue",
				key_enableFilters = "Red",
				key_filter = "Color");
		}

		task ("And When save the custom view") {
			FDSCustomView.saveCustomView();
		}

		task ("Then the view settings are applied to current custom view") {
			FDSCustomView.assertColumnNotPresent(key_field = "Description");

			FDSCustomView.assertFilterSummaryLabel(key_filter = "Green, Yellow, Red");
		}

		task ("And Then edits are not applied to default view") {
			Click(locator1 = "FrontendDataSet#SELECT_CUSTOM_VIEW");

			FDSCustomView.selectView(key_itemName = "Default View");

			FDSCustomView.assertColumnPresent(key_field = "Description");

			FDSCustomView.assertFilterSummaryLabel(key_filter = "Blue, Green, Yellow");
		}
	}

	@description = "LPS-164616. Assert that it's possible to create A new custom view and selected as current view"
	@priority = "4"
	test CanBeSaved {
		task ("When Make some changes to dataset display") {
			FDSFilters.editFilters(
				key_disableFilters = "Blue,Green,Yellow",
				key_enableFilters = "Red",
				key_filter = "Color");

			Click(
				key_field = "ID",
				locator1 = "FrontendDataSet#SORT_FIELD");

			Pagination.changePagination(itemsPerPage = "4 items");

			Pagination.viewResults(results = "Showing 1 to 4 of 25");

			Click(locator1 = "FrontendDataSet#SELECT_FIELD");

			Click(
				key_itemName = "Description",
				locator1 = "FrontendDataSet#SELECT_OPTION_CUSTOM_VIEW");
		}

		task ("And When Save As the custom view") {
			FDSCustomView.createNewView(key_nameView = "TEST");

			Click(locator1 = "FrontendDataSet#SELECT_OPTION_HEADER");

			Click(
				key_itemName = "Save View",
				locator1 = "FrontendDataSet#SELECT_OPTION_HEADER");
		}

		task ("Then A new custom view is created and selected as current view") {
			Click(locator1 = "FrontendDataSet#SELECT_CUSTOM_VIEW");

			FDSCustomView.selectView(key_itemName = "Default View");

			AssertElementPresent(
				columnName = "Description",
				locator1 = "FrontendDataSet#COLUMN_NAME");

			Click(locator1 = "FrontendDataSet#SELECT_CUSTOM_VIEW");

			FDSCustomView.selectView(key_itemName = "TEST");

			ObjectCustomViews.assertColumnNotPresent(columnName = "Description");
		}
	}

	@description = "LPS-160159. Assert that it's possible to select a new custom view and selected the default view"
	@priority = "4"
	test CanBeSelected {
		task ("And Given FDS sample portlet deployed and given customized tab active") {
			AssertElementPresent(
				key_tabName = "Customized",
				locator1 = "StagingPublishToLive#ACTIVE_TAB_NAME");
		}

		task ("And Given a created customized view") {
			FDSFilters.editFilters(
				key_disableFilters = "Blue,Green,Yellow",
				key_enableFilters = "Red",
				key_filter = "Color");

			FDSCustomView.createNewView(key_nameView = "Custom View Name");
		}

		task ("When open menu to select created custom view") {
			Click(locator1 = "FrontendDataSet#SELECT_CUSTOM_VIEW");
		}

		task ("Then option to select default view is available") {
			AssertTextPresent(
				locator1 = "FrontendDataSet#SELECT_CUSTOM_VIEW",
				value1 = "Default View");
		}

		task ("And Then option to select created custom view is available") {
			AssertTextPresent(
				locator1 = "FrontendDataSet#SELECT_OPTION_CUSTOM_VIEW",
				value1 = "Custom View Name");
		}
	}

	@description = "LPS-164618. Assert that it's possible can edit a new instance was previously created"
	@priority = "4"
	test CanCreateMultiple {
		task ("And Given A custom view is created and selected") {
			FDSCustomView.createNewView(key_nameView = "FrontendDataSet View");
		}

		task ("When Make some changes to dataset display (filter, sorting, page size or visible columns if data display in table)") {
			FDSFilters.editFilters(
				key_disableFilters = "Blue,Green,Yellow",
				key_enableFilters = "Red",
				key_filter = "Color");

			Click(
				key_text = "order-arrow-down",
				locator1 = "Icon#ANY");

			Click(
				key_text = "order-arrow-down",
				locator1 = "Icon#ANY");

			FDSCustomView.selectColumn(key_itemName = "Description");

			Pagination.changePagination(itemsPerPage = "4 items");
		}

		task ("And Click Save as button, provide a name") {
			FDSCustomView.createNewView(key_nameView = "FrontendDataSet View 2");
		}

		task ("Then A new custom view is created, and selected as current view") {
			FDSCustomView.assertCustomViewName(key_nameCustomView = "FrontendDataSet View 2");

			FDSCustomView.changeCustomView(key_itemName = "FrontendDataSet View");

			FDSCustomView.assertFilterSummaryLabel(key_filter = "Color: Blue, Green, Yellow");

			FDSCustomView.assertColumnPresent(key_field = "Description");

			FDSCustomView.changeCustomView(key_itemName = "FrontendDataSet View 2");

			Pagination.viewResults(results = "Showing 1 to 4 of 25");

			FDSCustomView.assertFilterSummaryLabel(key_filter = "Color: Red");

			FDSCustomView.assertColumnNotPresent(key_field = "Description");

			FDSCustomView.compareValue(
				key_columnIndex = "2",
				key_valueIndex1 = "1",
				key_valueIndex2 = "2");
		}
	}

	@description = "LPS-165672 - Simplify how actions are shown in the custom views section"
	@priority = "3"
	test HasActionsOnlyOnEllipsis {
		task ("And Given created custom view") {
			FDSCustomView.createNewView(key_nameView = "New View");
		}

		task ("When open ellipsis next to custom view") {
			Click(locator1 = "FrontendDataSet#SELECT_OPTION_HEADER");
		}

		task ("Then actions to save and delete views are available in dropdown menu") {
			FDSCustomView.assertMenuItem(key_menuItem = "Save View");

			FDSCustomView.assertMenuItem(key_menuItem = "Delete View");
		}

		task ("When open select custom view button") {
			FDSCustomView.changeCustomView(key_itemName = "Default View");

			Click(locator1 = "FrontendDataSet#SELECT_OPTION_HEADER");
		}

		task ("Then actions to save and delete views are not available in dropdown menu") {
			AssertElementNotPresent(
				key_itemName = "Save View",
				locator1 = "FrontendDataSet#SELECT_OPTION_CUSTOM_VIEW");

			AssertElementNotPresent(
				key_itemName = "Delete View",
				locator1 = "FrontendDataSet#SELECT_OPTION_CUSTOM_VIEW");
		}
	}

	@description = "LPS-168120. Allow users to rename a custom view"
	@priority = "4"
	test CanUpdateName {
		task ("And Given created custom view") {

			FDSCustomView.createNewView(key_nameView = "TEST");

			Click(locator1 = "FrontendDataSet#SELECT_OPTION_HEADER");

			Click(
				key_itemName = "Save View",
				locator1 = "FrontendDataSet#SELECT_OPTION_HEADER");
		}

		task ("When edit custom view name") {
			FDSCustomView.editView();
		}

		task ("Then name of custom view is updated in select custom view") {
			AssertTextPresent(
				locator1 = "FrontendDataSet#SELECT_CUSTOM_VIEW",
				value1 = "Edit View Name");
		}
	}

	@description = "LPS-164616. Assert that it's possible to create A new custom view and selected as current view"
	@priority = "4"
	test HasOptionToEditName {
		task ("And Given created custom view") {
			FDSCustomView.createNewView(key_nameView = "TEST");
		}

		task ("When open ellisis next to custom view") {
			Click(locator1 = "FrontendDataSet#ELLIPSIS");
		}

		task ("Then actions to edit custom view name is available") {
			AssertElementPresent(
				locator1 = "FrontendDataSet#SELECT_OPTION_HEADER",
				value1 = "Rename View");
		}
	}

	@description = "LPS-164602 Frontend Dataset - column labels are not localized "
	@priority = "4"
	test ManagementBarCanBeLocalized {
		task ("And Given: site URL localized to Spanish //localhost:8080/es/ and when on a customized tab view") {
			var portalURL = PropsUtil.get("portal.url");

			Navigator.openSpecificURL(url = "${portalURL}/es/web/guest/frontend-data-set-test-page");
		}

		task ("Then Filter button is localized ") {
			AssertTextEquals(
				locator1 = "FrontendDataSet#FILTER_BUTTON",
				value1 = "Filtro");
		}

		task ("And Then Search help text is localized ") {
			AssertAttributeValue(
				attribute1 = "placeholder",
				key_placeholder = "Buscar",
				locator1 = "FrontendDataSet#PLACEHOLDER",
				value1 = "Buscar");
		}

		task ("And Then Change Display dropdown items are localized") {
			AssertTextEquals(
				locator1 = "FrontendDataSet#FILTER_BUTTON",
				value1 = "Filtro");
		}

		task ("And Then Custom Views dropdown items are localized") {
			Click(locator1 = "FrontendDataSet#SELECT_CUSTOM_VIEW");

			AssertTextEquals(
				locator1 = "FrontendDataSet#SELECT_CUSTOM_VIEW",
				value1 = "Vista por defecto");
		}

		task ("And Then Ellipsis dropdown 'Save' is localized") {
			Click(locator1 = "FrontendDataSet#ELLIPSIS");

			AssertElementPresent(
				key_itemName = "Guardar vista comoâ€¦",
				locator1 = "FrontendDataSet#SELECT_OPTION_CUSTOM_VIEW");
		}

		task ("And Then Reset Filters link is localized") {
			AssertElementPresent(
				locator1 = "FrontendDataSet#RESET_FILTERS",
				reset_filter = "Restablecer filtros");
		}
	}

}
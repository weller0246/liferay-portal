@component-name = "portal-frontend-infrastructure"
definition {

	property custom.properties = "upgrade.database.auto.run=false";
	property osgi.modules.includes = "frontend-data-set-sample-web";
	property portal.acceptance = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Frontend Dataset";
	property testray.main.component.name = "Frontend Dataset";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		task ("Given Frontend Dataset sample portlet") {
			JSONLayout.addPublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page",
				widgetName = "Frontend Data Set Sample");

			JSONLayout.updateLayoutTemplateOfPublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page",
				layoutTemplate = "1 Column");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONLayout.deletePublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page");
		}
	}

	@description = "LPS-165727. Assert the details shown in the FDS table"
	@priority = "4"
	test HeadersCanBeLocalized {
		task ("And Given: site URL localized to Spanish //localhost:8080/es/*") {
			var portalURL = PropsUtil.get("portal.url");

			Navigator.openSpecificURL(url = "${portalURL}/es/web/guest/frontend-data-set-test-page");
		}

		task ("When: on classic tab view") {
			Click(
				key_sampleTab = "Clásico",
				locator1 = "FrontendDataSet#FDS_DATASET");
		}

		task ("Then: There is no hyphen in any header text") {
			WaitForElementPresent(locator1 = "FrontendDataSet#HEADER_ROW");

			var header = selenium.getText("FrontendDataSet#HEADER_ROW");

			var containsHyphen = StringUtil.contains("${header}", "-");

			TestUtils.assertEquals(
				actual = "${containsHyphen}",
				expected = "false");
		}

		task ("And Then: First letter is capitalized for all headers and localized Spanish headers are present") {
			AssertTextEquals.assertTextCaseSensitive(
				key_field = "Nombre",
				locator1 = "FrontendDataSet#TABLE_ITEM_COLUMN",
				value1 = "Nombre");

			AssertTextEquals.assertTextCaseSensitive(
				key_field = "Apellido",
				locator1 = "FrontendDataSet#TABLE_ITEM_COLUMN",
				value1 = "Apellido");

			AssertTextEquals.assertTextCaseSensitive(
				key_field = "Dirección de correo",
				locator1 = "FrontendDataSet#TABLE_ITEM_COLUMN",
				value1 = "Dirección de correo");
		}
	}

}
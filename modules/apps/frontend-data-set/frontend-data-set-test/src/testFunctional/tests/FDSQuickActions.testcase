@component-name = "portal-frontend-infrastructure"
definition {

	property custom.properties = "upgrade.database.auto.run=false";
	property osgi.modules.includes = "frontend-data-set-sample-web";
	property portal.acceptance = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Frontend Dataset";
	property testray.main.component.name = "Frontend Dataset";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		task ("Given Frontend Dataset sample portlet") {
			JSONLayout.addPublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page",
				widgetName = "Frontend Data Set Sample");

			JSONLayout.updateLayoutTemplateOfPublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page",
				layoutTemplate = "1 Column");

			Navigator.gotoPage(pageName = "Frontend Data Set Test Page");

			WaitForElementPresent(
				key_itemName = "Sample1",
				locator1 = "FrontendDataSet#TABLE_ITEM_ROW");

			MouseOver(
				key_itemName = "Sample1",
				locator1 = "FrontendDataSet#TABLE_ITEM_ROW");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONLayout.deletePublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page");
		}
	}

	@description = "LPS-155465. Assert that clicking quick action is equivalent to clicking the ellipsis dropdown menu."
	@priority = "5"
	test BehaviorIsEqualToEllipsisDropdownEquivalentAction {
		property custom.properties = "upgrade.database.auto.run=false";
		property test.name.skip.portal.instance = "FDSQuickActions#BehaviorIsEqualToEllipsisDropdownEquivalentAction";

		var portalURL = PropsUtil.get("portal.url");

		task ("When click on Pencil Icon") {
			ClickNoError(
				key_title = "Sample1",
				locator1 = "FrontendDataSet#EDIT_ACTION_BUTTON");
		}

		task ("And when copy the browser URL (#test-pencil is appended)") {
			var browserURL = selenium.getLocation();
		}

		task ("And when click on ellipsis of 1st row item in FDS table") {
			Click(
				key_title = "Sample1",
				locator1 = "FrontendDataSet#ENTRY_VERTICAL_ELLIPSIS");
		}

		task ("And when click on Pencil Icon") {
			Click(locator1 = "FrontendDataSet#DROPDOWN_MENU_SHOW_ITEM_EDIT_ICON");
		}

		task ("Then browser URL matches copied URL (#test-pencil)") {
			AssertLocation(value1 = "${browserURL}");
		}
	}

	@description = "LPS-155457. Assert that hover over mouse off of the table body quick action menu is not visible."
	@priority = "5"
	test CanDisappearWhenNotHovered {
		property test.name.skip.portal.instance = "FDSQuickActions#CanDisappearWhenNotHovered";

		task ("When hover mouse off of the table body") {
			MouseOver(locator1 = "FrontendDataSet#TABLE_BODY");
		}

		task ("Then quick action menu is not visible") {
			FDSQuickActions.viewQuickActionMenuNotPresent(title = "Sample1");
		}
	}

	@description = "LPS-155455: When hovering over the first line item and the quick action menu is displayed on the 1st line"
	@priority = "5"
	test CanDisplayOnHover {
		property custom.properties = "upgrade.database.auto.run=false";
		property test.name.skip.portal.instance = "FDSQuickActions#CanDisplayOnHover";

		task ("Then the quick action menu is displayed on the 1st line") {
			FDSQuickActions.viewQuickActionMenuPresent(title = "Sample1");
		}
	}

	@description = "LPS-155459: When clicking on the ellipsis and hovering over another row, multiple quick action menus are displayed"
	@ignore = "true"
	@priority = "3"
	test CanDisplayOnMultipleActiveRows {
		property test.name.skip.portal.instance = "FDSQuickActions#CanDisplayOnMultipleActiveRows";

		task ("When click on ellipsis button 2nd row item") {
			Click(
				key_title = "Sample2",
				locator1 = "FrontendDataSet#VERTICAL_ELLIPSIS_BUTTON");
		}

		task ("Then the quick action menu is displayed on the 2nd row") {
			FDSQuickActions.viewQuickActionMenuPresent(title = "Sample2");
		}

		task ("When hovering over the 1st row item") {
			MouseOver(
				key_itemName = "Sample1",
				locator1 = "FrontendDataSet#TABLE_ITEM_ROW");
		}

		task ("Then the quick action menu is displayed on the 1st row") {
			FDSQuickActions.viewQuickActionMenuPresent(title = "Sample1");
		}

		// This test is being ignored due to bug that doesn't display multiple quick actions in LPS-157838.

		task ("And Then the quick action menu is displayed on the 2nd row") {
			FDSQuickActions.viewQuickActionMenuPresent(title = "Sample2");
		}
	}

	@description = "LPS-155458. Assert can be displayed on one an active row."
	@priority = "4"
	test CanDisplayOnOneActiveRow {
		property test.name.skip.portal.instance = "FDSQuickActions#CanDisplayOnOneActiveRow";

		task ("When hover over 3rd row item") {
			MouseOver(
				key_itemName = "Sample3",
				locator1 = "FrontendDataSet#TABLE_ITEM_ROW");
		}

		task ("Then quick action menu is displayed on the 3rd row") {
			FDSQuickActions.viewQuickActionMenuPresent(title = "Sample3");
		}

		task ("Then quick action menu is not displayed on 1st row") {
			FDSQuickActions.viewQuickActionMenuNotPresent(title = "Sample1");
		}
	}

	@description = "LPS-155463. Assert that Quick actions icons list should be limited to three actions."
	@priority = "4"
	test CanDisplayUpToThreeIcons {
		property test.name.skip.portal.instance = "FDSQuickActions#CanDisplayUpToThreeIcons";

		task ("Then quick action menu only has 3 actions icons available") {
			AssertElementPresent(
				key_index = "1",
				key_title = "Sample1",
				locator1 = "FrontendDataSet#QUICK_ACTION_ITEM_INDEX");

			AssertElementPresent(
				key_index = "2",
				key_title = "Sample1",
				locator1 = "FrontendDataSet#QUICK_ACTION_ITEM_INDEX");

			AssertElementPresent(
				key_index = "3",
				key_title = "Sample1",
				locator1 = "FrontendDataSet#QUICK_ACTION_ITEM_INDEX");

			AssertElementNotPresent(
				key_index = "4",
				key_title = "Sample1",
				locator1 = "FrontendDataSet#QUICK_ACTION_ITEM_INDEX");
		}
	}

	@description = "LPS-155466. Assert that test-pencil is appended to browser URL after clicking."
	@priority = "5"
	test CanExecuteAnAction {
		property test.name.skip.portal.instance = "FDSQuickActions#CanExecuteAnAction";

		task ("When click on Pencil Icon ") {
			ClickNoError(
				key_title = "Sample1",
				locator1 = "FrontendDataSet#EDIT_ACTION_BUTTON");
		}

		task ("Then #test-pencil is appended to browser URL") {
			AssertLocation.assertPartialLocation(value1 = "frontend-data-set-test-page#test-pencil");
		}
	}

	@description = "LPS-155467. Assert the quick action is not visible when the row checkbox is checked."
	@priority = "3"
	test IsNotVisibleOnCheckedRow {
		property test.name.skip.portal.instance = "FDSQuickActions#IsNotVisibleOnCheckedRow";

		task ("When mark a row checkbox as checked") {
			Check.checkToggleSwitch(
				key_title = "Sample1",
				locator1 = "FrontendDataSet#ENTRY_CHECKBOX");
		}

		task ("And when hover mouse over checked row") {
			MouseOver(
				key_itemName = "Sample1",
				locator1 = "FrontendDataSet#TABLE_ITEM_ROW");
		}

		task ("Then the quick action icons are not visible") {
			FDSQuickActions.viewQuickActionMenuNotPresent(title = "Sample1");
		}
	}

}
definition {

	macro activateScheduleTrigger {
		LexiconEntry.gotoEntry(rowEntry = "${trigger}");

		Navigator.gotoNavTab(navTab = "Job Scheduler Trigger");

		Check.checkToggleSwitch(locator1 = "CommerceEntry#ACTIVE_CHECKBOX");

		Type(
			key_text = "Cron Expression",
			locator1 = "TextInput#ANY",
			value1 = "${cronExpression}");

		Button.clickSave();

		Alert.viewSuccessMessage();

		Button.clickCancel();
	}

	macro addScheduleTrigger {
		Click(locator1 = "Button#NEW");

		MenuItem.click(menuItem = "${taskExecutorType}");

		Type(
			locator1 = "TextInput#NAME",
			value1 = "${triggerName}");

		if (isSet(triggerProperties)) {
			Type.typeAceEditor(
				locator1 = "TextArea#ACE_EDITOR",
				value1 = "${triggerProperties}");
		}

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

	macro deleteJobItem {
		Variables.assertDefined(parameterList = "${jobItemName}");

		Click(
			key_name = "${jobItemName}",
			locator1 = "JobScheduler#JOB_ITEM_KEBAB_MENU");

		ClickNoError(
			key_option = "Delete",
			locator1 = "JobScheduler#KEBAB_MENU_OPTION");

		AssertConfirm.waitForConfirmation(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		AssertElementNotPresent(
			key_name = "${jobItemName}",
			locator1 = "JobScheduler#JOB_ITEM_NAME");
	}

	macro openJobSchedulerAdmin {
		Navigator.openWithAppendToBaseURL(
			baseURL = "${baseURL}",
			urlAppend = "group/control_panel/manage?p_p_id=com_liferay_dispatch_web_internal_portlet_DispatchPortlet");
	}

	macro setJobSchedulerTrigger {
		Variables.assertDefined(parameterList = "${key_name},${selectFieldLabel},${selectFieldValue}");

		Click(
			key_name = "${key_name}",
			locator1 = "JobScheduler#JOB_ITEM_NAME");

		Click(
			key_tab = "Job Scheduler Trigger",
			locator1 = "NavTab#TAB_LINK");

		SelectField.select(
			selectFieldLabel = "${selectFieldLabel}",
			selectFieldValue = "${selectFieldValue}");

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

}
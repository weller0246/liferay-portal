import com.liferay.gradle.plugins.patcher.PatchTask

apply plugin: "com.liferay.patcher"

task jarManifest
task jarPatched(type: Zip)
task patch(type: PatchTask)

dependencies {
	compileOnly group: "com.liferay", name: "org.eclipse.equinox.http.servlet", transitive: false, version: "1.2.2.v20211119-2358-LIFERAY-CACHED"
	compileOnly group: "org.apache.felix", name: "org.apache.felix.http.servlet-api", transitive: false, version: "1.1.2"
	compileOnly group: "org.osgi", name: "org.osgi.service.http.whiteboard", transitive: false, version: "1.0.0"
	compileOnly group: "org.osgi", name: "osgi.core", transitive: false, version: "6.0.0"
}

jar {
	setActions([])

	dependsOn jarPatched
	finalizedBy jarManifest
}

jarManifest {
	doLast {
		ant.jar(destfile: jar.archivePath, update: true)
	}
}

jarPatched {
	archiveName = jar.archiveName
	destinationDir = jar.destinationDir
	duplicatesStrategy = "exclude"

	from sourceSets.main.output

	from {
		zipTree(
			configurations.compileOnly.find {
				it.name.startsWith "org.eclipse.equinox.http.servlet-"
			})
	}
}

patch {
	ext {
		autoClean = false
	}

	fileNames "org/eclipse/equinox/http/servlet/internal/context/ContextController.java"

	originalLibModuleName = "org.eclipse.equinox.http.servlet"
	originalLibSrcBaseUrl = "https://repository-cdn.liferay.com/nexus/content/groups/public/com/liferay/"
}
@component-name = "portal-solutions"
definition {

	property osgi.modules.includes = "osb-site-initializer-customer-portal";
	property portal.release = "false";
	property portal.upstream = "true";
	property release.feature.flags.disable.DISABLE_PRIVATE_LAYOUTS = "true";
	property testray.main.component.name = "OSB Site Initializer Customer Portal";

	var testSiteName = "Customer Portal Site";
	var testSiteURL = "customer-portal-site";
	var testAccounts = "Test Account LXC - SM,Test Account Analytics Cloud,Test Account DXP,Test Account Enterprise Search,Test Account Partnership";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Create a new Customer Portal Site") {
			CustomerPortal.addCustomerPortalSite(siteName = "${testSiteName}");
		}

		task ("Add test user access to all Accounts") {
			CustomerPortal.assignUserToOrganization();

			CustomerPortal.addUserToOneOrMoreAccounts(accountsList = "${testAccounts}");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		CustomerPortal.customerPortalApisTearDown();

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "${testSiteName}");
		}
	}

	@description = "Automation ID: LPS-144906 | Test Summary: Verify if when Hide button is clicked by user, the Quick Links panel collapses"
	@priority = "3"
	test HideButtonClickedPanelCollapses {
		task ("Go to CP2.0 and join a project") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(title = "Test Account LXC - SM");
		}

		task ("Click the Hide button and assert that the quick links panel collapses") {
			CP2NavigationMenu.clickOnHideButton();

			CustomerPortal.assertQuickLinksPanelIsHidden();
		}
	}

	@description = "Automation ID: LPS-144933 | Test Summary: Verify if Activate Analytics Cloud doesn't appear in Quick Links panel when Analytics Cloud is Active"
	@ignore = "true"
	@priority = "4"
	test IfACActiveNoActivateAC {
		task ("Creating objects for the Analytics Cloud") {
			CustomerPortal.showCPObject(title = "Account Subscription Group");

			CustomerPortal.addCPObject(title = "Analytics Cloud Workspace");
		}

		task ("Change the user role in the project") {
			Accounts.openAccountsAdmin();

			CustomerPortal.accountTableProject(title = "Test Account Analytics Cloud");

			Accounts.addAssigneeToRoleViaUsersTab(
				accountRoleTitle = "Account Administrator",
				accountUserName = "Test Test");
		}

		task ("Go to CP2.0 and join a project") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(title = "Test Account Analytics Cloud");
		}

		task ("Going through onboarding") {
			CustomerPortal.clickOnStartProjectSetup();

			CustomerPortal.clickOnSkipForNowButton();

			CustomerPortal.clickOnSkipForNowButton();
		}

		task ("Assert the Activate Analytics Cloud card") {
			CustomerPortal.assertQuickLinksPanelInfo(title = "Activate Analytics Cloud");
		}

		task ("Go to the product activation page and open the card activation modal") {
			CP2NavigationMenu.goToProductActivation();

			CP2NavigationMenu.clickOnProductButton(product = "Analytics Cloud");

			CustomerPortal.clickOnFinishActivationButton();
		}

		task ("Fill out the activation form and submit") {
			CP2AnalyticsCloudSetup.typeAllSetupAnalyticsCloudInfoFields();

			Button.click(button = "Submit");
		}

		task ("Change the activation card status to Active") {
			CustomerPortal.activeStatus();
		}

		task ("Go back to CP2.0 and enter the project again") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(title = "Test Account Analytics Cloud");
		}

		task ("Assert that the Activate Analytics Cloud card is no longer present") {
			AssertElementNotPresent(
				locator1 = "CP2NavigationMenu#QUICK_LINKS",
				title = "Activate Analytics Cloud");
		}

		task ("Delete the entries and objects created") {
			CustomerPortal.deleteCPObject(title = "Analytics Cloud Workspace");

			CustomerPortal.deleteCPObject(title = "Account Subscription Group");
		}
	}

	@description = "Automation ID: LPS-144932 | Test Summary: Verify if 'Activate Analytics Cloud' appears in Quick Links panel when Analytics Cloud is Not Active"
	@priority = "5"
	test IfACNotActiveShowActivateAC {
		task ("Go to CP2.0 and join a project") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(
				status = "Expired",
				title = "Test Account Analytics Cloud");
		}

		task ("Go to the product activation page") {
			CP2NavigationMenu.goToProductActivation();

			CP2NavigationMenu.clickOnProductButton(product = "Analytics Cloud");
		}

		task ("Assert the Activate Analytics Cloud card") {
			CustomerPortal.assertQuickLinksPanelInfo(title = "Activate Analytics Cloud");
		}
	}

	@description = "Automation ID: LPS-144912 | Test Summary: Verify if 'Environment Details' appears in Quick Links panel when DXP is Active"
	@priority = "5"
	test IfDXPActiveShowsEnvironmentDetails {
		task ("Go to CP2.0 and join a project") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(title = "Test Account DXP");
		}

		task ("Go to the product activation page") {
			CP2NavigationMenu.goToProductActivation();

			CP2NavigationMenu.clickOnProductButton(product = "DXP");
		}

		task ("Assert the Environment Details card") {
			CustomerPortal.assertQuickLinksPanelInfo(title = "Environment Details");
		}
	}

	@description = "Automation ID: LPS-144928 | Test Summary: Verify if 'Source Code Access' appears in Quick Links panel when DXP is 'Active'"
	@priority = "5"
	test IfDXPActiveShowsSourceCode {
		task ("Go to CP2.0 and join a project") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(title = "Test Account DXP");
		}

		task ("Go to the product activation page") {
			CP2NavigationMenu.goToProductActivation();

			CP2NavigationMenu.clickOnProductButton(product = "DXP");
		}

		task ("Assert the Source Code Access card") {
			CustomerPortal.assertQuickLinksPanelInfo(title = "Source Code Access");
		}
	}

	@description = "Automation ID: LPS-144930 | Test Summary: Verify if Latest Fix Packs appears in Quick Links panel when DXP Cloud is Active"
	@ignore = "true"
	@priority = "5"
	test IfDXPCloudActiveShowLatestFix {
		task ("Go to CP2.0 and join a project") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(title = "Test Account LXC - SM");
		}

		task ("Go to the product activation page") {
			CP2NavigationMenu.goToProductActivation();

			CP2NavigationMenu.clickOnProductButton(product = "LXC - SM");
		}

		task ("Assert the Latest Fix Packs card") {
			CustomerPortal.assertQuickLinksPanelInfo(title = "Latest Fix Packs");
		}
	}

	@description = "Automation ID: LPS-144924 | Test Summary: Verify if when DXP Cloud is present Environment Details doesn't show up unless Commerce is Active too"
	@priority = "4"
	test IfDXPCloudAndCommerceActiveShowsEnvironmentDetails {
		task ("Width desktop") {
			SetWindowSize(value1 = "1920,1080");
		}

		task ("Go to CP2.0 and join a project") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(title = "Test Account LXC - SM");
		}

		task ("Assert that the card is not present in the panel") {
			CustomerPortal.cannotAppearsQuickLinksPanelInfo(title = "Environment Details");
		}

		task ("Go back to the homepage and join a project") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(title = "Test Account Commerce");
		}

		task ("Check that subscriptions are present") {
			CustomerPortal.viewSubscriptionManagementTab(productTab = "LXC - SM,Commerce");
		}

		task ("Assert that the desired card appears in the panel") {
			CustomerPortal.assertQuickLinksPanelInfo(title = "Environment Details");
		}
	}

	@description = "Automation ID: LPS-144929 | Test Summary: Verify if when DXP Cloud is not active, Source Code Access doesn't show up. DXP cannot be active otherwise, Source Code Access will appear"
	@priority = "4"
	test IfDXPCloudNotActiveNoSourceCode {
		task ("Go to CP2.0 and join a project") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(
				status = "Expired",
				title = "Test Account Analytics Cloud");
		}

		task ("Check that the following subscriptions are not part of the project") {
			CustomerPortal.notViewProduct(productTab = "DXP,DXP Cloud");
		}

		task ("Assert that the card is not present in the panel") {
			CustomerPortal.cannotAppearsQuickLinksPanelInfo(title = "Source Code Access");
		}
	}

	@description = "Automation ID: LPS-144931 | Automation ID: Verify if 'Latest Fix Packs' does not appear when DXP is not active. DXP Cloud cannot be active otherwise, Latest Fix Packs will appear"
	@priority = "4"
	test IfDXPNotActiveNoLatestFix {
		task ("Go to CP2.0 and join a project") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(title = "Test Account LXC - SM");
		}

		task ("Check that the following subscription are not part of the project") {
			CustomerPortal.notViewProduct(productTab = "DXP");
		}

		task ("Assert that the card is not present in the panel") {
			CustomerPortal.cannotAppearsQuickLinksPanelInfo(title = "Latest Fix Packs");
		}
	}

	@description = "Automation ID: LPS-144926 | Test Summary: Verify if when Limited SLA is Active, Large File Attachments doesn't show up"
	@priority = "4"
	test IfLimitedSLAActiveNoLargeFile {
		task ("Go to CP2.0 and join a project") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(title = "Test Account Enterprise Search");
		}

		task ("Assert the SLA card") {
			CustomerPortal.assertSlaCard(type = "Limited");
		}

		task ("Assert that the card is not present in the panel") {
			CustomerPortal.cannotAppearsQuickLinksPanelInfo(title = "Large File Attachments");
		}
	}

	@description = "Automation ID: LPS-144914 | Test Summary: Verify if when Partnership is Active, Environment Details doesn't appear. Note: DXP, Portal and Commerce cannot be active otherwise, Environment Details will appear"
	@ignore = "true"
	@priority = "4"
	test IfPartnershipActiveNoEnvironmentDetails {
		task ("Go to CP2.0 and join a project") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(title = "Test Account Partnership");
		}

		task ("Check that the following subscriptions are not part of the project") {
			CustomerPortal.notViewProduct(productTab = "DXP,Portal,Commerce");
		}

		task ("Assert that the desired card appears in the panel") {
			CustomerPortal.assertQuickLinksPanelInfo(title = "Environment Details");
		}
	}

	@description = "Automation ID: LPS-144925 | Test Summary: Verify if Large File Attachments appears in Quick Links panel when there is no Limited SLA or Partnership Active"
	@priority = "5"
	test IfPartnershipNorLimitedSLAShowsLargeFile {
		task ("Go to CP2.0 and join a project") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(title = "Test Account LXC - SM");
		}

		task ("Assert that the project does not have a Partnership subscription and does not have a limited SLA card") {
			CustomerPortal.assertSlaCardNotAppears(typeCard = "Limited");

			CustomerPortal.notViewProduct(productTab = "Partnership");
		}

		task ("Assert that the desired card appears in the panel") {
			CustomerPortal.assertQuickLinksPanelInfo(title = "Large File Attachments");
		}
	}

	@description = "Automation ID: LPS-144934 | Test Summary: Verify if Activate Analytics Cloud don't show up if Portal is active and if DXP or DXP Cloud aren't Active"
	@ignore = "true"
	@priority = "4"
	test IfPortalActiveButNoDXPNoActivateAC {
		task ("Go to CP2.0 and join a project") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(title = "Test Account LXC - SM");
		}
	}

	@description = "Automation ID: LPS-144741 | Test Summary: Verify if the Quick Links panel is visible on the Overview page"
	@priority = "5"
	test IsQuickLinksOnOverviewPage {
		task ("Go to CP2.0 and join a project") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(title = "Test Account LXC - SM");
		}

		task ("Assert that the desired card appears in the panel") {
			CustomerPortal.assertQuickLinksPanelInfo(title = "Quick Links");
		}
	}

	@description = "Automation ID: LPS-144742 | Test Summary: Verify if the Quick Links panel is visible on the Product Activation pages"
	@priority = "5"
	test IsQuickLinksOnProductActivation {
		task ("Go to CP2.0 and join a project") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(title = "Test Account LXC - SM");
		}

		task ("Go to the product activation page") {
			CP2NavigationMenu.goToProductActivation();

			CP2NavigationMenu.clickOnProductButton(product = "LXC - SM");
		}

		task ("Assert that the desired card appears in the panel") {
			CustomerPortal.assertQuickLinksPanelInfo(title = "Quick Links");
		}
	}

	@description = "Automation ID: LPS-154791 | Test Summary: Verify that the Quick Links Panel does not appear in Team Members"
	@priority = "3"
	test QuickLinksDoesNotAppearInTeamMembers {
		task ("Go to CP2.0 and join a project") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(title = "Test Account LXC - SM");
		}

		task ("Go to the Team Members page") {
			CP2NavigationMenu.goToTeamMembers();
		}

		task ("Assert that the card is not present in the panel") {
			CustomerPortal.cannotAppearsQuickLinksPanelInfo(title = "Quick Links");
		}
	}

	@description = "Automation ID: LPS-144907 | Test Summary: Verify if when user clicks on Show, the Quick Links panel expands"
	@priority = "3"
	test ShowButtonClickedPanelExpands {
		task ("Go to CP2.0 and join a project") {
			ApplicationsMenu.gotoSite(key_site = "${testSiteName}");

			CustomerPortal.enterOnProject(title = "Test Account LXC - SM");
		}

		task ("Make the quick links panel collapse and then expand again") {
			CP2NavigationMenu.clickOnHideButton();

			CP2NavigationMenu.clickOnShowButton();
		}

		task ("Assert that the desired card appears in the panel") {
			CustomerPortal.assertQuickLinksPanelInfo(title = "Quick Links");
		}
	}

}
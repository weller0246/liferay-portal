definition {

	@summary = "Add a new MDF Request filling the goals and activity pages"
	macro createNewRequest {
		PRMMDFRequest.fillGoalsPage(
			accountName = "${accountName}",
			additionalOptions = "${additionalOptions}",
			campaignDescription = "${campaignDescription}",
			campaignName = "${campaignName}",
			countryName = "${countryName}",
			goalsOptions = "${goalsOptions}",
			targetMarketOptions = "${targetMarketOptions}",
			targetRolesOptions = "${targetRolesOptions}");

		Button.click(button = "Continue");

		Click(
			key_buttonText = "Add Activity",
			locator1 = "PRMMDFRequest#ADD_BUTTON");

		PRMMDFRequest.newActivity(
			activityName = "${activityName}",
			budgetList = "${budgetList}",
			followUpStrategy = "${followUpStrategy}",
			leadFollowUpList = "${leadFollowUpList}",
			leadList = "${leadList}",
			tactic = "${tactic}",
			targetOfLeads = "${targetOfLeads}",
			typeOfActivity = "${typeOfActivity}");

		Button.click(button = "Continue");

		Button.click(button = "Continue");

		ScrollWebElementIntoView(locator1 = "Button#SUBMIT");

		Button.click(button = "Submit");

		WaitForPageLoad();
	}

	@summary = "Fill the goals page form"
	macro fillGoalsPage {
		Select(
			key_fieldName = "company",
			locator1 = "PRMMDFRequest#SELECT",
			value1 = "${accountName}");

		Select(
			key_fieldName = "country",
			locator1 = "PRMMDFRequest#SELECT",
			value1 = "${countryName}");

		Type(
			key_fieldName = "overallCampaignName",
			locator1 = "PRMMDFRequest#TEXT_FIELD",
			value1 = "${campaignName}");

		Type(
			key_fieldName = "overallCampaignDescription",
			locator1 = "PRMMDFRequest#TEXT_FIELD",
			value1 = "${campaignDescription}");

		for (var goalsList : list "${goalsOptions}") {
			Click.clickAtNotVisible(
				key_fieldName = "liferayBusinessSalesGoals",
				key_optionValue = "${goalsList}",
				locator1 = "PRMMDFRequest#CHECKBOX");
		}

		for (var targetMarketList : list "${targetMarketOptions}") {
			Click.clickAtNotVisible(
				key_fieldName = "targetMarkets",
				key_optionValue = "${targetMarketList}",
				locator1 = "PRMMDFRequest#CHECKBOX");
		}

		if (isSet(additionalOptions)) {
			for (var optionsList : list "${additionalOptions}") {
				Click.clickAtNotVisible(
					key_buttonText = "${optionsList}",
					key_fieldName = "additionalOption",
					locator1 = "PRMMDFRequest#INPUT_BUTTON");
			}
		}

		for (var targetRolesList : list "${targetRolesOptions}") {
			Click.clickAtNotVisible(
				key_fieldName = "targetAudienceRoles",
				key_optionValue = "${targetRolesList}",
				locator1 = "PRMMDFRequest#CHECKBOX");
		}
	}

	@summary = "Format the date that will be inputed on start and end date"
	macro formatDate {
		var nextDayDate = DateUtil.getFormattedDate("+1", "MMddyyyy");

		if (isSet(endDate)) {
			var nextDayDate = DateUtil.getFormattedDate("+2", "MMddyyyy");
		}

		return "${nextDayDate}";
	}

	@summary = "Add a new activity"
	macro newActivity {
		if (!(isSet(activityIndex))) {
			var activityIndex = "0";
		}

		Click(
			key_buttonText = "Add Activity",
			locator1 = "PRMMDFRequest#ADD_BUTTON");

		Type(
			key_fieldName = "activities[${activityIndex}].name",
			locator1 = "PRMMDFRequest#TEXT_FIELD",
			value1 = "${activityName}");

		Select(
			key_fieldName = "activities[${activityIndex}].typeActivity",
			locator1 = "PRMMDFRequest#SELECT",
			value1 = "${typeOfActivity}");

		Select(
			key_fieldName = "activities[${activityIndex}].tactic",
			locator1 = "PRMMDFRequest#SELECT",
			value1 = "${tactic}");

		var currentDay = DateUtil.getCurrentDate();
		var currentMonth = DateUtil.getCurrentMonth();
		var currentYear = DateUtil.getCurrentYear();
		var nextDayStartDate = DateUtil.getFormattedDate("+1", "dd");

		if ("${nextDayStartDate}" == "01") {
			var currentMonth = ${currentMonth} + 1;

			if ("${currentMonth}" == "13") {
				fail("The selected date will be on next year. The start date can be only on current year");
			}
		}

		var currentDate = "${currentMonth}${nextDayStartDate}${currentYear}";

		Type.sendKeys(
			key_fieldName = "activities[${activityIndex}].startDate",
			locator1 = "PRMMDFRequest#TEXT_FIELD",
			value1 = "${currentDate}");

		var nextDayEndDate = DateUtil.getFormattedDate("+2", "dd");

		var endIsLessThanStart = MathUtil.isLessThan("${nextDayEndDate}", "${nextDayStartDate}");

		if ("${endIsLessThanStart}" == "true") {
			var currentMonth = ${currentMonth} + 1;

			if ("${currentMonth}" == "13") {
				fail("The end date can not be different from start date.");
			}
		}

		var futureDate = "${currentMonth}${nextDayEndDate}${currentYear}";

		Type.sendKeys(
			key_fieldName = "activities[${activityIndex}].endDate",
			locator1 = "PRMMDFRequest#TEXT_FIELD",
			value1 = "${futureDate}");

		var budgetIndex = "0";

		for (var bugdetValues : list "${budgetList}") {
			var expenseOption = StringUtil.extractFirst("${bugdetValues}", "-");
			var bugdetValue = StringUtil.extractLast("${bugdetValues}", "-");

			Click(
				key_buttonText = "Add Expense",
				locator1 = "PRMMDFRequest#ADD_BUTTON");

			Select(
				key_fieldName = "activities[${activityIndex}].budgets[${budgetIndex}].expense",
				locator1 = "PRMMDFRequest#SELECT",
				value1 = "${expenseOption}");

			Type(
				key_fieldName = "activities[${activityIndex}].budgets[${budgetIndex}].cost",
				locator1 = "PRMMDFRequest#TEXT_FIELD",
				value1 = "${bugdetValue}");

			var budgetIndex = ${budgetIndex} + 1;
		}

		Click.clickAtNotVisible(
			key_fieldName = "activities[${activityIndex}].leadGenerated",
			key_optionValue = "${leadList}",
			locator1 = "PRMMDFRequest#CHECKBOX");

		Type(
			key_fieldName = "activities[${activityIndex}].targetOfLeads",
			locator1 = "PRMMDFRequest#TEXT_FIELD",
			value1 = "${targetOfLeads}");

		for (var leadFollowUp : list "${leadFollowUpList}") {
			Click.clickAtNotVisible(
				key_fieldName = "activities[${activityIndex}].leadFollowUpStrategies",
				key_optionValue = "${leadFollowUp}",
				locator1 = "PRMMDFRequest#CHECKBOX");
		}

		Type(
			key_fieldName = "activities[${activityIndex}].detailsLeadFollowUp",
			locator1 = "PRMMDFRequest#TEXT_FIELD",
			value1 = "${followUpStrategy}");
	}

}
@component-name = "portal-workflow-ee"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property test.run.environment = "EE";
	property testray.main.component.name = "Workflow Metrics";

	setUp {
		TestCase.setUpPortalInstance();

		var testPortalInstance = PropsUtil.get("test.portal.instance");

		User.firstLoginPG();

		Navigator.openURL();

		ApplicationsMenu.gotoPortlet(
			category = "Workflow",
			panel = "Applications",
			portlet = "Metrics");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "LPS-98233 - Verify that the default date format for en-US in Custom Range filter is MM/DD/YYYY"
	@priority = "3"
	test DefaultDateFormatIsMMDDYYYY {
		WorkflowMetrics.goToWorkflowProcessMetrics(workflowProcessName = "Single Approver");

		Click(locator1 = "WorkflowMetrics#METRICS_PERFORMANCE_TAB");

		Click(locator1 = "WorkflowMetrics#METRICS_COMPLETION_VELOCITY_DATE_RANGE_DROPDOWN");

		Click(
			completionVelocityDateRange = "Custom Range",
			locator1 = "WorkflowMetrics#METRICS_COMPLETION_VELOCITY_DATE_RANGE_DROPDOWN_OPTION");

		Pause(locator1 = "1000");

		AssertElementPresent(
			key_dateFormat = "Default date format is MM/DD/YYYY.",
			locator1 = "WorkflowMetricsCompletionVelocity#DEFAULT_DATE_FORMAT_MESSAGE");
	}

	@description = "LPS-98233 - Verify that the fields From and To are required"
	@priority = "3"
	test FromAndToAreRequiredFields {
		WorkflowMetrics.goToWorkflowProcessMetrics(workflowProcessName = "Single Approver");

		Navigator.gotoNavItem(navItem = "Performance");

		WorkflowMetrics.selectDateRange(
			completionVelocityDateRange = "Custom Range",
			dateRange = "Last 30 Days",
			endDateRange = "06/14/2021",
			startDateRange = " ",
			title = "Completion Velocity");

		WorkflowMetrics.viewDateRangeErrorMessage(errorMessage = "Please enter a valid date.");

		Refresh();

		WorkflowMetrics.selectDateRange(
			completionVelocityDateRange = "Custom Range",
			dateRange = "Last 30 Days",
			endDateRange = " ",
			startDateRange = "06/14/2021",
			title = "Completion Velocity");

		WorkflowMetrics.viewDateRangeErrorMessage(errorMessage = "Please enter a valid date.");
	}

	@description = "LPS-98233 - Verify that the user can not insert a future date"
	@priority = "3"
	test FutureDatesAreNotAllowed {
		WorkflowMetrics.goToWorkflowProcessMetrics(workflowProcessName = "Single Approver");

		Navigator.gotoNavItem(navItem = "Performance");

		WorkflowMetrics.selectDateRange(
			completionVelocityDateRange = "Custom Range",
			dateRange = "Last 30 Days",
			endDateRange = "06/14/2024",
			startDateRange = "06/14/2023",
			title = "Completion Velocity");

		WorkflowMetrics.viewDateRangeErrorMessage(errorMessage = "Please enter a valid date.");
	}

	@description = "LPS-97665 - Verify that info icon hover state and tooltip message"
	@priority = "2"
	test InfoIconDisplaysMessageOnHover {
		WorkflowMetrics.goToWorkflowProcessMetrics(workflowProcessName = "Single Approver");

		Click(locator1 = "WorkflowMetrics#METRICS_PERFORMANCE_TAB");

		AssertElementPresent(
			key_sectionName = "Completion Velocity",
			key_tooltipMessage = "Completion velocity tracks the number of completed workflow instances over the selected time period.",
			locator1 = "WorkflowMetricsProcessListView#METRICS_WORKFLOW_TOOLTIP_MESSAGE");
	}

	@description = "LPS-97665 - Verify that an item out of the timeframe selected is not considered in the velocity result"
	@priority = "4"
	test ItemOutOfTimeFrameIsNotConsideredInVelocityResult {
		WorkflowMetrics.goToWorkflowProcessMetrics(workflowProcessName = "Single Approver");

		ApplicationsMenu.gotoPortlet(
			category = "Workflow",
			panel = "Applications",
			portlet = "Process Builder");

		Navigator.gotoNavItem(navItem = "Configuration");

		Workflow.configureWorkflow(
			workflowDefinition = "Single Approver",
			workflowResourceValue = "Blogs Entry");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blogs Entry Title");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Blogs Entry Title",
			workflowAssetType = "Blogs Entry",
			workflowTask = "Review");

		Workflow.approveTaskByActions(
			workflowAssetTitle = "Blogs Entry Title",
			workflowAssetType = "Blogs Entry",
			workflowTask = "Review");

		WorkflowMetrics.goToWorkflowProcessMetrics(workflowProcessName = "Single Approver");

		Click(locator1 = "WorkflowMetrics#METRICS_PERFORMANCE_TAB");

		WorkflowMetrics.verifyDefaultUnitsForEachDateRange(
			completionVelocityDateRange = "Today",
			isNotPresent = "INST_DAY,INST_WEEK,INST_MONTH,INST_YEAR",
			isPresent = "INST_HOUR");

		WorkflowMetrics.assertChartTotalValueNotEquals(velocityValueTotal = "0");

		WorkflowMetrics.verifyDefaultUnitsForEachDateRange(
			completionVelocityDateRange = "Yesterday",
			isNotPresent = "INST_DAY,INST_WEEK,INST_MONTH,INST_YEAR",
			isPresent = "INST_HOUR");

		WorkflowMetrics.assertChartTotalValue(
			velocityUnitTotal = "inst / hour",
			velocityValueTotal = "0");
	}

	@description = "LPS-97665 - Verify the label from Yesterday dropdown timeframe option is read as <dd MM>, 12 AM - <dd MM>, 11:59 PM"
	@priority = "3"
	test LabelFromYesterdayFilter {
		WorkflowMetrics.goToWorkflowProcessMetrics(workflowProcessName = "Single Approver");

		Navigator.gotoNavItem(navItem = "Performance");

		Click(
			key_dateRange = "Last 30 Days",
			key_title = "Completion Velocity",
			locator1 = "WorkflowMetrics#METRICS_DATE_RANGE_DROPDOWN_BY_TITLE");

		AssertElementPresent(locator1 = "WorkflowMetrics#METRICS_DATE_FORMAT_DROPDOWN_YESTERDAY");
	}

	@description = "LPS-98233 - Verify that at the custom range modal, the oldest date accepted is 1970"
	@priority = "2"
	test OldestDataAcceptedIs1970 {
		WorkflowMetrics.goToWorkflowProcessMetrics(workflowProcessName = "Single Approver");

		Navigator.gotoNavItem(navItem = "Performance");

		WorkflowMetrics.selectDateRangePerformanceByStep(
			completionVelocityDateRange = "Custom Range",
			endDateRange = "06/14/2021",
			startDateRange = "01/01/1969");

		WorkflowMetrics.viewDateRangeErrorMessage(errorMessage = "The date cannot be earlier than 1970.");

		Refresh();

		WorkflowMetrics.selectDateRangePerformanceByStep(
			completionVelocityDateRange = "Custom Range",
			endDateRange = "06/14/2021",
			startDateRange = "01/01/1970");

		AssertElementPresent(
			completionVelocityDateRange = "Jan 01, 1970 - Jun 14, 2021",
			locator1 = "WorkflowMetrics#METRICS_COMPLETION_VELOCITY_DATE_RANGE_DROPDOWN_OPTION");
	}

	@description = "LPS-98233 - Verify that when there are no results for a filter selection, the table should display the respective empty state"
	@priority = "3"
	test PageProvidesEmptyStateWhenThereAreNoResultForFilterSelection {
		WorkflowMetrics.goToWorkflowProcessMetrics(workflowProcessName = "Single Approver");

		Navigator.gotoNavItem(navItem = "Performance");

		Click(
			key_dateRange = "Last 30 Days",
			key_title = "Completion Velocity",
			locator1 = "WorkflowMetrics#METRICS_DATE_RANGE_DROPDOWN_BY_TITLE");

		Click(
			completionVelocityDateRange = "Yesterday",
			locator1 = "WorkflowMetrics#METRICS_COMPLETION_VELOCITY_DATE_RANGE_DROPDOWN_OPTION");

		WorkflowMetrics.assertChartTotalValue(
			velocityUnitTotal = "inst / hour",
			velocityValueTotal = "0");
	}

	@description = "LPS-98233 - Verify that the To date is greater than the From date"
	@priority = "3"
	test ToDateMustBeGreaterThanFromDate {
		WorkflowMetrics.goToWorkflowProcessMetrics(workflowProcessName = "Single Approver");

		Navigator.gotoNavItem(navItem = "Performance");

		WorkflowMetrics.selectDateRange(
			completionVelocityDateRange = "Custom Range",
			dateRange = "Last 30 Days",
			endDateRange = "01/01/2020",
			startDateRange = "01/01/2021",
			title = "Completion Velocity");

		WorkflowMetrics.viewDateRangeErrorMessage(errorMessage = "The start date cannot be later than the end date.");

		WorkflowMetrics.viewDateRangeErrorMessage(errorMessage = "The end date cannot be earlier than the start date.");
	}

}
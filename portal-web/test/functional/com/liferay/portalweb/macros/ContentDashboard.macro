definition {

	macro addMimeType {
		Click(
			ariaLabel = "Add Duplicate Field Code File MIME types",
			locator1 = "Button#ANY_WITH_ARIA_LABEL");

		Type(
			key_indexNumber = "2",
			key_label = "Code File MIME types",
			locator1 = "TextArea#ANY_LABELED_N",
			value1 = "${typeName}");

		Button.clickSave();
	}

	macro applySelectedFilter {
		SelectFrame(value1 = "relative=top");

		AssertClickNoError(
			locator1 = "Button#SELECT",
			value1 = "Select");

		SelectFrame(value1 = "relative=top");

		WaitForElementPresent(locator1 = "ContentDashboard#ROW_PANEL");
	}

	macro cancelSelectFilterTreeview {
		Portlet.expandTree();

		for (var optionName : list "${optionsList}") {
			Check.checkNotVisible(
				key_nodeName = "${optionName}",
				locator1 = "Treeview#NODE_CHECKBOX_2");
		}

		ClickNoError(
			key_text = "Clear All",
			locator1 = "Link#ANY");

		AssertElementNotPresent(
			key_text = "Clear All",
			locator1 = "Link#ANY");

		SelectFrame(value1 = "relative=top");

		Button.clickCancel();
	}

	macro checkMimeExtensionDisplayed {
		if (isSet(typeName)) {
			AssertVisible(
				key_nodeName = "${typeName}",
				locator1 = "Treeview#NODE_ITEM_2");
		}
		else {
			for (var typeName : list "${typeNameList}") {
				AssertVisible(
					key_nodeName = "${typeName}",
					locator1 = "Treeview#NODE_ITEM_2");
			}
		}

		SelectFrame(value1 = "relative=top");

		Button.clickCancel();
	}

	macro clearFilters {
		Click(
			key_text = "Clear",
			locator1 = "Link#ANY");
	}

	macro clickOnFirstBar {
		if (!(isSet(fillColor))) {
			var fillColor = "4B9FFF";
		}

		Click(
			fillColor = "${fillColor}",
			locator1 = "ContentDashboard#BAR_FROM_NO_VOCABULARY_SPECIFIED");
	}

	macro clickQuickActionIcon {
		if (isSet(assetName)) {
			Click.clickNoMouseOver(
				key_assetName = "${assetName}",
				locator1 = "ContentDashboard#ROW_PANEL_ASSET_NAME");
		}
		else {
			Click.clickNoMouseOver(locator1 = "ContentDashboard#ROW_PANEL");
		}

		Click.javaScriptClick(
			locator1 = "ContentDashboard#QUICK_ACTION_ICON",
			title = "${iconName}");
	}

	macro deleteSecondCategoryFilter {
		Click(locator1 = "ContentDashboard#DELETE_SECOND_FILTER_SELECTED");
	}

	macro editAsset {
		LexiconTable.clickEllipsisItem(
			item = "Edit",
			tableEntry = "${assetTitle}");

		if (isSet(assetNewTitle)) {
			if (IsElementPresent(locator1 = "TextInput#TITLE")) {
				Click(locator1 = "TextInput#TITLE");

				PortletEntry.inputTitle(title = "${assetNewTitle}");
			}
			else {
				BlogsEntry.addTitle(entryTitle = "${assetNewTitle}");
			}
		}

		if (isSet(saveAsDraft)) {
			PortletEntry.saveAsDraft();

			Click(locator1 = "Icon#BACK");
		}
		else {
			Button.clickPublish();
		}
	}

	macro filterByAuthor {
		ContentDashboard.openFilterModal(filterOption = "Author");

		if (!(isSet(authorNameList))) {
			var authorNameList = "Test Test";
		}

		if (!(isSet(viewOption))) {
			for (var authorName : list "${authorNameList}") {
				LexiconTable.checkTableCheckbox(tableEntry = "${authorName}");
			}
		}
		else if ("${viewOption}" == "Cards") {
			LexiconEntry.changeExportDisplayStyle(displayStyle = "${viewOption}");

			for (var authorName : list "${authorNameList}") {
				LexiconCard.checkCardCheckbox(card = "${authorName}");
			}
		}
		else if ("${viewOption}" == "List") {
			LexiconEntry.changeExportDisplayStyle(displayStyle = "${viewOption}");

			for (var authorName : list "${authorNameList}") {
				Check(
					key_listEntry = "${authorName}",
					locator1 = "LexiconList#LIST_ENTRY_CHECKBOX");
			}
		}

		ContentDashboard.applySelectedFilter();
	}

	macro filterByAuthorAll {
		ContentDashboard.openFilterAndOrder();

		Click(locator1 = "ContentDashboard#FILTER_BY_OPTION");
	}

	macro filterByAuthorMe {
		ContentDashboard.openFilterAndOrder();

		Click(locator1 = "ContentDashboard#FILTER_BY_OPTION");
	}

	macro filterByCategory {
		ContentDashboard.openFilterModal(filterOption = "Categories");

		ContentDashboard.useTreeviewFilter(optionsList = "${categoryNameList}");

		ContentDashboard.applySelectedFilter();
	}

	macro filterByExtension {
		ContentDashboard.openFilterModal(filterOption = "Extension");

		if (!(isSet(extensionNameList))) {
			var extensionNameList = "${extensionName}";
		}

		ContentDashboard.useTreeviewFilter(optionsList = "${extensionNameList}");

		ContentDashboard.applySelectedFilter();
	}

	macro filterByMimeParent {
		ContentDashboard.openFilterModal(filterOption = "Extension");

		ContentDashboard.useTreeviewFilter(optionsList = "${parentExtension}");

		ContentDashboard.applySelectedFilter();
	}

	macro filterBySite {
		ContentDashboard.openFilterModal(filterOption = "Site or Asset Library");

		AssertClick(
			locator1 = "NavBar#MY_SITES",
			value1 = "My Sites");

		LexiconCard.clickCardTitle(card = "${siteName}");

		SelectFrame(value1 = "relative=top");
	}

	macro filterByStatus {
		ContentDashboard.openFilterAndOrder();

		Click(locator1 = "ContentDashboard#FILTER_BY_OPTION");
	}

	macro filterByTag {
		ContentDashboard.openFilterModal(filterOption = "Tag");

		if (!(isSet(tagNameList))) {
			var tagNameList = "${tagName}";
		}

		for (var tableEntry : list "${tagNameList}") {
			LexiconTable.checkTableCheckbox(tableEntry = "${tableEntry}");
		}

		ContentDashboard.applySelectedFilter();
	}

	macro filterByType {
		ContentDashboard.openFilterModal(filterOption = "Type");

		if (!(isSet(typeNameList))) {
			var typeNameList = "${typeName}";
		}

		ContentDashboard.useTreeviewFilter(optionsList = "${typeNameList}");

		ContentDashboard.applySelectedFilter();
	}

	macro flipAxesButtonClick {
		Click(locator1 = "ContentDashboard#FLIP_AXES_BUTTON");
	}

	macro goToContentDashboard {
		ApplicationsMenu.gotoPortlet(
			category = "Content",
			panel = "Applications",
			portlet = "Content Dashboard");

		WaitForPageLoad();

		WaitForElementPresent(locator1 = "ContentDashboard#CONTENT_CHART_SVG");
	}

	macro moveToPage {
		Click(locator1 = "ContentDashboard#PAGINATION_MOVE_TO_PAGE");
	}

	macro moveVocabularyDisplayConfiguration {
		Click(locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_BUTTON");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		if (IsElementPresent(locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_VOCABULARY_IN_USE")) {
			var key_vocabularyName = "${vocabularyName}";

			Click(locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_VOCABULARY_IN_USE");

			Click(locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_LEFT_ARROW_BUTTON");
		}
		else if (IsElementPresent(locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_VOCABULARY_AVAILABLE")) {
			var key_vocabularyName = "${vocabularyName}";

			Click(locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_VOCABULARY_AVAILABLE");

			Click(locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_RIGHT_ARROW_BUTTON");
		}

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");

		if (!(isSet(notCloseFrame))) {
			Alert.viewSuccessMessage();

			IFrame.closeFrame();
		}
	}

	macro openAuditGraphConfiguration {
		Click(locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_BUTTON");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");
	}

	macro openFilterAndOrder {
		if (IsElementPresent(locator1 = "ContentDashboard#FILTER_AND_ORDER_DROPDOWN")) {
			AssertClickNoError(
				locator1 = "ContentDashboard#FILTER_AND_ORDER_DROPDOWN",
				value1 = "Filter and Order");
		}
	}

	macro openFilterModal {
		ContentDashboard.openFilterAndOrder();

		Click(locator1 = "ContentDashboard#FILTER_BY_OPTION");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	macro orderByModificationDate {
		ContentDashboard.openFilterAndOrder();

		Click(locator1 = "ContentDashboard#FILTER_BY_OPTION");
	}

	macro orderByTitle {
		ContentDashboard.openFilterAndOrder();

		Click(locator1 = "ContentDashboard#FILTER_BY_OPTION");
	}

	macro searchValue {
		Click(locator1 = "ContentDashboard#SEARCH_FIELD");

		Type(
			locator1 = "ContentDashboard#SEARCH_FIELD",
			value1 = "${webContentTitle}");

		KeyPress(
			locator1 = "ContentDashboard#SEARCH_FIELD",
			value1 = "\ENTER");
	}

	macro setUpGlobalCategories {
		var audienceCategory = JSONCategory.addCategory(
			groupName = "Global",
			title = "Audience 1",
			vocabularyName = "Audience");
		var stageCategory = JSONCategory.addCategory(
			groupName = "Global",
			title = "Stage 1",
			vocabularyName = "Stage");
		static var audienceId1 = "${audienceCategory}";
		static var stageId1 = "${stageCategory}";

		return "${audienceId1}";

		return "${stageId1}";
	}

	macro setVocabularyInUseSecondPosition {
		Click(locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_BUTTON");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Click(locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_VOCABULARY_IN_USE");

		Click(locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_DOWN_ARROW_BUTTON");

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");

		IFrame.closeFrame();
	}

	macro setVocabularyInUseTopPosition {
		Click(locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_BUTTON");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Click(locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_VOCABULARY_IN_USE");

		Click(locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_UP_ARROW_BUTTON");

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");

		IFrame.closeFrame();
	}

	macro stageFirstVocabularyConfiguration {
		Click(locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_BUTTON");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Click(locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_STAGE_VOCABULARY");

		Click(locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_UP_ARROW_BUTTON");

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");

		Alert.viewSuccessMessage();

		IFrame.closeFrame();
	}

	macro swapVocabularies {
		Click(locator1 = "ContentDashboard#SWAP_VOCABULARIES_BUTTON");
	}

	macro tearDown {
		Category.openCategoriesAdmin(siteURLKey = "global");

		Navigator.gotoNavNested(navNested = "Audience");

		var key_categoryName = "Audience 1";

		if (IsElementPresent(locator1 = "Categories#VOCABULARY_CATEGORY")) {
			Category.deleteCategoriesCP(vocabularyName = "Audience");
		}

		Navigator.gotoNavNested(navNested = "Stage");

		var key_categoryName = "Stage 1";

		if (IsElementPresent(locator1 = "Categories#VOCABULARY_CATEGORY")) {
			Category.deleteCategoriesCP(vocabularyName = "Stage");
		}
	}

	macro uncheckCheckbox {
		var checkboxLabel = "${auditGraphCheckboxName}";

		Click.javaScriptClick(
			checkboxName = "Stage 1",
			locator1 = "Checkbox#ANY_CHECKBOX");
	}

	macro useTreeviewFilter {
		Portlet.expandTree();

		for (var optionName : list "${optionsList}") {
			Check.checkNotVisible(
				key_nodeName = "${optionName}",
				locator1 = "Treeview#NODE_CHECKBOX_2");
		}
	}

	macro viewAppliedFilters {
		for (var filterLabel : list "${filterLabelList}") {
			AssertElementPresent(
				filterLabel = "${filterLabel}",
				locator1 = "ContentDashboard#FILTER_LABEL");
		}
	}

	macro viewAssetsTranslatedVersion {
		Panel.expandPanel(panel = "Details");

		ClickNoError(
			key_language = "${language}",
			locator1 = "ContentDashboard#VIEW_TRANSLATED_ASSET_ICON");
	}

}
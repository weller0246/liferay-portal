definition {

	var orgName = "Organization Name";

	macro activateMemberCP {
		Organization.gotoMemberActionsCmd(
			memberName = "${userFirstName}",
			orgName = "${orgName}");

		MenuItem.clickNoError(menuItem = "Activate");

		Alert.viewSuccessMessage();

		var key_userFirstName = "${userFirstName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#USER_TABLE_STATUS",
			value1 = "Active");
	}

	macro addCategorizationCP {
		AssetCategorization.addTag(tagName = "${tagName}");

		PortletEntry.save();

		UsersAndOrganizationsNavigator.gotoMenuBarInformation();

		AssertTextEquals(
			key_tagName = "${tagName}",
			locator1 = "AssetCategorization#TAG",
			value1 = "${tagName}");
	}

	macro addCommentCP {
		AssertElementPresent(locator1 = "TextArea#COMMENTS");

		Type(
			locator1 = "TextArea#COMMENTS",
			value1 = "${orgComment}");

		PortletEntry.save();

		AssertTextEquals(
			locator1 = "TextArea#COMMENTS",
			value1 = "${orgComment}");
	}

	macro addContactInformation {
		if (isSet(contactType)) {
			var key_contactType = "${contactType}";

			Click.mouseDownMouseUp(locator1 = "UsersAndOrganizationsEditOrganization#CONTACT_ADD_BUTTON");

			if ("${makePrimary}" == "true") {
				Check(
					checkboxName = "Make Primary",
					locator1 = "Checkbox#ANY_CHECKBOX");
			}

			// Addresses

			if (isSet(addressType)) {
				Select(
					locator1 = "Select#TYPE",
					value1 = "${addressType}");
			}

			if (isSet(street1)) {
				Type(
					locator1 = "TextInput#STREET_1",
					value1 = "${street1}");
			}

			if (isSet(city)) {
				Type(
					locator1 = "TextInput#CITY",
					value1 = "${city}");
			}

			if (isSet(country)) {
				Select(
					locator1 = "Select#COUNTRY",
					value1 = "${country}");
			}

			if (isSet(region)) {
				Select(
					locator1 = "Select#REGION",
					value1 = "${region}");
			}

			if (isSet(postalCode)) {
				Type(
					locator1 = "TextInput#POSTAL_CODE",
					value1 = "${postalCode}");
			}

			// Phone Numbers

			if (isSet(number)) {
				Type(
					locator1 = "TextInput#NUMBER",
					value1 = "${number}");
			}

			if (isSet(extension)) {
				Type(
					locator1 = "TextInput#EXTENSION",
					value1 = "${extension}");
			}

			// Additional Email Addresses

			if (isSet(address)) {
				Type(
					locator1 = "TextInput#ADDRESS",
					value1 = "${address}");
			}

			// Websites

			if (isSet(url)) {
				Type(
					locator1 = "TextInput#URL",
					value1 = "${url}");
			}

			// Opening Hours

			if (isSet(mondayOpen)) {
				Select(
					locator1 = "Select#MONDAY_OPEN",
					value1 = "${mondayOpen}");
			}

			if (isSet(mondayClose)) {
				Select(
					locator1 = "Select#MONDAY_CLOSE",
					value1 = "${mondayClose}");
			}
		}

		Click(locator1 = "Button#SAVE");

		Alert.viewSuccessMessage();
	}

	macro addCP {
		AssertClick(
			locator1 = "NavBar#ORGANIZATIONS",
			value1 = "Organizations");

		var key_navItem = "Organizations";

		AssertElementPresent(locator1 = "NavBar#ACTIVE_NAV_ITEM_LINK");

		LexiconEntry.gotoAdd();

		if ("${orgType}" == "Location") {
			MenuItem.click(menuItem = "Location");
		}
		else if (IsElementPresent(locator1 = "MenuItem#ANY_MENU_ITEM", key_menuItem = "Organization")) {
			MenuItem.click(menuItem = "Organization");
		}

		Type(
			locator1 = "TextInput#NAME",
			value1 = "${orgName}");

		ScrollWebElementIntoView(locator1 = "Button#SAVE");

		Button.clickSave();

		if ("${organizationName}" == "Duplicate") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_1",
				value1 = "The organization name is already taken.");
		}
		else {
			var key_orgType = "${orgType}";

			Alert.viewSuccessMessage();

			AssertTextEquals(
				locator1 = "TextInput#NAME",
				value1 = "${orgName}");

			AssertTextEquals.assertValue(
				locator1 = "UsersAndOrganizationsEditOrganization#DETAILS_TYPE",
				value1 = "${orgType}");
		}
	}

	macro addSecurityQuestionsCP {
		MenuBar.gotoSecurityQuestions();

		Type(
			locator1 = "TextArea#SECURITY_QUESTIONS",
			value1 = "${securityQuestions}");

		if (isSet(localizedLanguage)) {
			PortletEntry.changeLocale(
				fieldName = "Security Questions",
				locale = "${localizedLanguage}");

			Type(
				locator1 = "TextArea#SECURITY_QUESTIONS",
				value1 = "${localizedSecurityQuestions}");
		}

		PortletEntry.save();

		MenuBar.gotoSecurityQuestions();

		AssertTextEquals(
			locator1 = "TextArea#SECURITY_QUESTIONS",
			value1 = "${securityQuestions}");

		if (isSet(localizedLanguage)) {
			PortletEntry.changeLocale(
				fieldName = "Security Questions",
				locale = "${localizedLanguage}");

			AssertTextEquals(
				locator1 = "TextArea#SECURITY_QUESTIONS",
				value1 = "${securityQuestions}");
		}
	}

	macro addSiteCP {
		Organization.editOrganizationCP(orgName = "${orgName}");

		UsersAndOrganizationsNavigator.gotoMenuBarOrganizationSite();

		Check.checkToggleSwitch(
			key_toggleSwitchLabel = "Create Site",
			locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

		if (isSet(siteTemplateName)) {
			Select(
				locator1 = "UserGroupsEditUserGroup#PUBLIC_PAGES_SELECT",
				value1 = "${siteTemplateName}");

			if ("${checkEnablePropagationCheckbox}" == "true") {
				AssertChecked(locator1 = "UserGroupsEditUserGroup#ENABLE_PROPAGATION_PUBLIC_PAGE_CHECKBOX");
			}
			else {
				Uncheck(locator1 = "UserGroupsEditUserGroup#ENABLE_PROPAGATION_PUBLIC_PAGE_CHECKBOX");
			}
		}

		PortletEntry.save();
	}

	macro addSitePG {
		Organization.editOrganizationPG(orgName = "${orgName}");

		UsersAndOrganizationsNavigator.gotoMenuBarOrganizationSite();

		Check.checkToggleSwitch(
			key_toggleSwitchLabel = "Create Site",
			locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

		if (("${organizationSite}" == "Community Site") || ("${organizationSite}" == "Intranet Site")) {
			Select(
				locator1 = "UserGroupsEditUserGroup#PUBLIC_PAGES_SELECT",
				value1 = "${organizationSite}");

			if ("${checkEnablePropagationCheckbox}" == "true") {
				AssertChecked(locator1 = "UserGroupsEditUserGroup#ENABLE_PROPAGATION_PUBLIC_PAGE_CHECKBOX");
			}
			else {
				Uncheck(locator1 = "UserGroupsEditUserGroup#ENABLE_PROPAGATION_PUBLIC_PAGE_CHECKBOX");
			}
		}

		PortletEntry.save();
	}

	macro addWithLocalizedCountryCP {
		Navigator.gotoNavItem(navItem = "${navItem}");

		LexiconEntry.gotoAdd();

		if (IsElementPresent(locator1 = "MenuItem#ANY_MENU_ITEM", key_menuItem = "${localizedOption}")) {
			MenuItem.click(menuItem = "${localizedOption}");
		}

		Type(
			locator1 = "TextInput#NAME",
			value1 = "${orgName}");

		Select(
			key_countryName = "${countryName}",
			locator1 = "UsersAndOrganizations#ORGANIZATION_COUNTRY_NAME",
			value1 = "${countryName}");

		AssertClick(
			key_saveButton = "${saveButton}",
			locator1 = "Button#SAVE_LOCALIZED",
			value1 = "${saveButton}");

		Alert.viewSuccessMessage();
	}

	macro assignOrgRoleCP {
		Organization.gotoActionsCmd(orgName = "${orgName}");

		Pause(locator1 = "3000");

		MenuItem.click(menuItem = "Assign Organization Roles");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		AssertTextEquals(
			key_roleTitle = "${roleTitle}",
			locator1 = "UsersAndOrganizationsAssignOrganizationalRoles#ROLE_TITLE",
			value1 = "${roleTitle}");

		Click(
			key_roleTitle = "${roleTitle}",
			locator1 = "UsersAndOrganizationsAssignOrganizationalRoles#ROLE_TITLE");

		Navigator.gotoNavItem(navItem = "Available");

		Search.searchCP(searchTerm = "${userScreenName}");

		AssertTextEquals(
			key_userScreenName = "${userScreenName}",
			locator1 = "UserGroupsAssignUsers#USER_TABLE_SCREEN_NAME",
			value1 = "${userScreenName}");

		Check(
			key_userScreenName = "${userScreenName}",
			locator1 = "UserGroupsAssignUsers#USER_TABLE_CHECKBOX");

		AssertClick(
			locator1 = "Button#UPDATE_ASSOCIATIONS",
			value1 = "Update Associations");

		Alert.viewSuccessMessage();

		Navigator.gotoNavItem(navItem = "Current");

		AssertTextEquals(
			key_userScreenName = "${userScreenName}",
			locator1 = "UsersAndOrganizationsAssignUsers#USER_TABLE_USER_NAME",
			value1 = "${userFirstName} ${userLastName}");

		AssertTextEquals(
			key_userScreenName = "${userScreenName}",
			locator1 = "UserGroupsAssignUsers#USER_TABLE_SCREEN_NAME",
			value1 = "${userScreenName}");

		IFrame.selectTopFrame();

		Click(locator1 = "Icon#CLOSE");
	}

	macro assignUser {
		Organization.gotoCP(
			orgName = "${orgName}",
			searchToken = "${orgName}");

		LexiconEntry.gotoAdd();

		MenuItem.click(menuItem = "Assign Users");

		AssertElementPresent.assertVisible(locator1 = "Button#DONE");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		AssertTextEquals(
			key_userScreenName = "${userScreenName}",
			locator1 = "UserGroupsAssignUsers#USER_TABLE_SCREEN_NAME",
			value1 = "${userScreenName}");

		Check(
			key_userScreenName = "${userScreenName}",
			locator1 = "UserGroupsAssignUsers#USER_TABLE_CHECKBOX");

		Pause(locator1 = "3000");

		SelectFrameTop();

		AssertClick.assertTextClickAtWaitForLastScript(
			locator1 = "Button#DONE",
			value1 = "Done");

		Alert.viewSuccessMessage();
	}

	macro deactivateMemberCP {
		Organization.gotoMemberActionsCmd(
			memberName = "${userFirstName}",
			orgName = "${orgName}");

		MenuItem.clickNoError(menuItem = "Deactivate");

		AssertConfirm(value1 = "Are you sure you want to deactivate this?");

		Alert.viewSuccessMessage();

		var key_userFirstName = "${userFirstName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#USER_TABLE_STATUS",
			value1 = "Inactive");
	}

	macro deleteCP {
		Organization.gotoActionsCmd(orgName = "${orgName}");

		var key_orgName = "${orgName}";

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		AssertElementNotPresent(locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME");
	}

	macro deletePG {
		var key_orgName = "${orgName}";

		Search.searchPG(
			portletName = "My Organizations",
			searchTerm = "${orgName}");

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}");

		LexiconEntry.openEntryMenu(rowEntry = "${orgName}");

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		AssertElementNotPresent(locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME");
	}

	macro editCategorizationCP {

		//AssertElementPresent(locator1 = "UsersAndOrganizationsEditUser#CATEGORIZATION_LABEL");

		AssertElementPresent(
			key_vocabularyName = "${vocabularyName}",
			locator1 = "UsersAndOrganizationsEditUser#VOCABULARY_LABEL");

		AssertElementPresent.assertVisible(
			key_vocabularyName = "${vocabularyName}",
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_CATEGORY_SELECT",
			value1 = "Select");

		AssertClick(
			key_vocabularyName = "${vocabularyName}",
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_CATEGORY_SELECT",
			value1 = "Select");

		AssertTextEquals(
			locator1 = "Modal#HEADER",
			value1 = "Select ${vocabularyName}");

		AssetCategorization.selectCategory(categoryName = "${categoryName}");

		PortletEntry.save();
	}

	macro editOrganizationCP {
		UsersAndOrganizationsNavigator.gotoOrganizations();

		Organization.gotoActionsCmd(orgName = "${orgName}");

		var key_orgName = "${orgName}";

		MenuItem.click(menuItem = "Edit");
	}

	macro editOrganizationPG {
		var key_orgName = "${orgName}";

		Search.searchPG(
			portletName = "My Organizations",
			searchTerm = "${orgName}");

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}");

		LexiconEntry.openEntryMenu(rowEntry = "${orgName}");

		MenuItem.clickNoError(menuItem = "Edit");
	}

	macro editSiteCP {
		Organization.gotoActionsCmd(orgName = "${orgName}");

		var key_orgName = "${orgName}";

		MenuItem.click(menuItem = "Edit");

		Type(
			locator1 = "TextInput#NAME",
			value1 = "${orgNameEdit}");

		PortletEntry.save();

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "${orgNameEdit}");
	}

	macro gotoActionsCmd {
		Pause(locator1 = "1000");

		Search.searchCP(searchTerm = "${orgName}");

		AssertVisible(locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME_GENERIC");

		var key_orgName = "${orgName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}");

		LexiconEntry.openEntryMenu(rowEntry = "${orgName}");
	}

	macro gotoContact {
		Navigator.gotoNavTab(navTab = "Contact");
	}

	macro gotoCP {
		Search.searchCP(searchTerm = "${searchToken}");

		var key_orgName = "${orgName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}");

		AssertClick(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}");

		Breadcrumb.viewActiveEntry(breadcrumbName = "${orgName}");
	}

	macro gotoMemberActionsCmd {
		Organization.gotoCP(
			orgName = "${orgName}",
			searchToken = "${orgName}");

		Search.searchCP(searchTerm = "${memberName}");

		LexiconEntry.openEntryMenu(rowEntry = "${memberName}");
	}

	macro openOrganizationsAdmin {
		Navigator.openWithAppendToBaseURL(
			baseURL = "${baseURL}",
			urlAppend = "group/control_panel/manage?p_p_id=com_liferay_users_admin_web_portlet_UsersAdminPortlet&p_p_lifecycle=0&p_p_state=maximized&p_p_mode=view&_com_liferay_users_admin_web_portlet_UsersAdminPortlet_toolbarItem=view-all-organizations&_com_liferay_users_admin_web_portlet_UsersAdminPortlet_usersListView=flat-organizations");
	}

	macro removeMemberCP {
		Organization.gotoMemberActionsCmd(
			memberName = "${userFullName}",
			orgName = "${orgName}");

		MenuItem.click(menuItem = "Remove");

		Alert.viewSuccessMessage();

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No results were found.");
	}

	macro searchCP {
		Search.searchCP(searchTerm = "${searchTerm}");

		var key_orgName = "${orgName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}");
	}

	macro selectParentOrganizationCP {
		var key_orgName = "${orgName}";

		LexiconEntry.openEntryMenu(rowEntry = "${orgName}");

		MenuItem.click(menuItem = "Edit");

		AssertClick(
			locator1 = "UsersAndOrganizationsEditOrganization#PARENT_ORGANIZATION_SELECT_BUTTON",
			value1 = "Select");

		SelectFrame(locator1 = "UsersAndOrganizationsSelectOrganization#SELECT_ORGANIZATION_IFRAME");

		AssertTextEquals(
			locator1 = "ContentRow#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}");

		var key_orgName = "${parentOrgName}";

		AssertClick(
			locator1 = "UsersAndOrganizationsSelectOrganization#ORGANIZATION_TABLE_CHOOSE_BUTTON",
			value1 = "Choose");

		SelectFrame(value1 = "relative=top");

		AssertTextEquals(
			locator1 = "UsersAndOrganizationsEditOrganization#PARENT_ORGANIZATION_TABLE_NAME",
			value1 = "${parentOrgName}");

		PortletEntry.save();
	}

	macro tearDownCP {
		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		AssertClick(
			locator1 = "NavBar#ORGANIZATIONS",
			value1 = "Organizations");

		if (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			if (isSet(orgName)) {
				Search.searchCP(searchTerm = "${orgName}");

				PortletEntry.selectAll();

				ClickNoError(locator1 = "Icon#DELETE");

				AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

				Alert.viewSuccessMessage();
			}
			else {
				while (IsElementPresent(locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME_GENERIC")) {
					Click(
						locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME_GENERIC",
						value1 = "${orgName}");

					AssertClick(
						locator1 = "UsersAndOrganizationsOrganization#MENU_ASSIGN_USERS",
						value1 = "Assign Users");

					AssertClick(
						locator1 = "UsersAndOrganizationsAssignUsers#CURRENT_TAB",
						value1 = "Current");

					if ((IsElementPresent(locator1 = "Checkbox#SELECT_ALL")) && (IsVisible(locator1 = "Checkbox#SELECT_ALL"))) {
						Uncheck(locator1 = "Checkbox#SELECT_ALL");

						AssertClick(
							locator1 = "Button#UPDATE_ASSOCIATIONS",
							value1 = "Update Associations");

						AssertTextEquals(
							locator1 = "Message#INFO",
							value1 = "No users were found.");
					}

					AssertClick.assertPartialTextClickAt(
						locator1 = "UsersAndOrganizationsAssignUsers#BACK",
						value1 = "Back");

					AssertClickNoError(
						locator1 = "UsersAndOrganizationsOrganization#MENU_DELETE",
						value1 = "Delete");

					AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");
				}
			}
		}
	}

	macro viewAssignMemberCP {
		if (isSet(userMiddleName)) {
			var key_userName = "${userFirstName} ${userMiddleName} ${userLastName}";

			AssertTextEquals(
				locator1 = "UsersAndOrganizationsOrganization#USER_TABLE_NAME",
				value1 = "${userFirstName} ${userMiddleName} ${userLastName}");
		}
		else {
			var key_userName = "${userName}";

			AssertTextEquals(
				locator1 = "UsersAndOrganizationsOrganization#USER_TABLE_NAME",
				value1 = "${userName}");
		}
	}

	macro viewCP {
		var key_orgName = "${orgName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}");

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_TYPE",
			value1 = "${orgType}");

		LexiconEntry.openEntryMenu(rowEntry = "${orgName}");

		MenuItem.click(menuItem = "Edit");

		var key_orgType = "${orgType}";

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Edit ${orgName}");

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "${orgName}");

		AssertTextEquals.assertValue(
			locator1 = "UsersAndOrganizationsEditOrganization#DETAILS_TYPE",
			value1 = "${orgType}");
	}

	macro viewLocalizedCountryCP {
		AssertClick(
			key_navItem = "${navItem}",
			locator1 = "NavBar#NAV_ITEM_LINK",
			value1 = "${navItem}");

		LexiconEntry.openEntryMenu(rowEntry = "${orgName}");

		MenuItem.click(menuItem = "${editLocalized}");

		AssertTextEquals.assertPartialText(
			key_countryName = "${countryName}",
			locator1 = "UsersAndOrganizations#ORGANIZATION_COUNTRY_NAME",
			value1 = "${countryName}");
	}

	macro viewMembersNotFoundCP {
		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No results were found.");
	}

	macro viewNoOrganizationsFoundCP {
		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No organizations were found.");
	}

	macro viewParentOrganizationCP {
		Organization.gotoActionsCmd(orgName = "${orgName}");

		var key_orgName = "${orgName}";

		MenuItem.click(menuItem = "Edit");

		if (isSet(parentOrgName)) {
			var key_orgName = "${subOrgName}";

			AssertClick(
				locator1 = "UsersAndOrganizationsEditOrganization#PARENT_ORGANIZATION_SELECT_BUTTON",
				value1 = "Change");

			SelectFrame(locator1 = "UsersAndOrganizationsSelectOrganization#SELECT_ORGANIZATION_IFRAME");

			AssertTextEquals(
				locator1 = "ContentRow#ORGANIZATION_TABLE_NAME",
				value1 = "${subOrgName}");

			AssertTextEquals(
				locator1 = "ContentRow#ORGANIZATION_TABLE_NAME_SECOND_COLUMN",
				value1 = "${parentOrgName}");
		}
		else {
			AssertClick(
				locator1 = "UsersAndOrganizationsEditOrganization#PARENT_ORGANIZATION_SELECT_BUTTON",
				value1 = "Select");

			SelectFrame(locator1 = "UsersAndOrganizationsSelectOrganization#SELECT_ORGANIZATION_IFRAME");

			AssertTextEquals(
				locator1 = "ContentRow#ORGANIZATION_TABLE_NAME",
				value1 = "${orgName}");

			AssertTextEquals(
				locator1 = "ContentRow#ORGANIZATION_TABLE_NAME_SECOND_COLUMN",
				value1 = "");
		}

		IFrame.closeFrame();
	}

	macro viewRemoveMemberCP {
		Search.searchCP(searchTerm = "${orgName}");

		var key_orgName = "${orgName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}");

		AssertClick(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}");

		AssertTextNotPresent(value1 = "${userFirstName}");
	}

	macro viewSubOrganizationCP {
		var key_orgName = "${orgName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_TYPE",
			value1 = "${orgType}");

		AssertClick(
			locator1 = "UsersAndOrganizations#ORGANIZATION_TABLE_NAME",
			value1 = "${orgName}");

		var key_suborgName = "${suborgName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizationsOrganization#SUBORGANIZATION_TABLE_NAME",
			value1 = "${suborgName}");

		AssertTextEquals(
			locator1 = "UsersAndOrganizationsOrganization#SUBORGANIZATION_TABLE_TYPE",
			value1 = "${suborgType}");

		AssertElementPresent(locator1 = "UsersAndOrganizationsOrganization#SUBORGANIZATION_TABLE_ACTIONS");
	}

}
definition {

	@summary = "Helper function to add a WC Folder via JSON"
	macro _addFolder {
		Variables.assertDefined(parameterList = "${groupId},${parentFolderId},${folderName}");

		var portalURL = JSONCompany.getPortalURL();

		if (isSet(folderDescription)) {
			var description = "${folderDescription}";
		}
		else {
			var description = "";
		}

		if (!(isSet(userEmailAddress))) {
			var userEmailAddress = "test@liferay.com";
		}

		if (!(isSet(userPassword))) {
			var userPassword = "test";
		}

		var curl = '''
			${portalURL}/api/jsonws/journal.journalfolder/add-folder \
				-u ${userEmailAddress}:${userPassword} \
				-d groupId=${groupId} \
				-d parentFolderId=${parentFolderId} \
				-d name=${folderName} \
				-d description=${description}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	@summary = "Helper function to add a WC Article via JSON"
	macro _addWebContent {
		Variables.assertDefined(parameterList = "${groupId},${folderId},${titleMap},${descriptionMap},${contentXML},${serviceContext}");

		var portalURL = JSONCompany.getPortalURL();

		if (!(isSet(userEmailAddress))) {
			var userEmailAddress = "test@liferay.com";
		}

		if (!(isSet(userPassword))) {
			var userPassword = "test";
		}

		var curl = '''
			${portalURL}/api/jsonws/journal.journalarticle/add-article \
				-u ${userEmailAddress}:${userPassword} \
				-d externalReferenceCode= \
				-d groupId=${groupId} \
				-d folderId=${folderId} \
				-d titleMap=${titleMap} \
				-d descriptionMap=${descriptionMap} \
				-d content=${contentXML} \
				-d ddmStructureKey=BASIC-WEB-CONTENT \
				-d ddmTemplateKey=BASIC-WEB-CONTENT \
				-d serviceContext=${serviceContext}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	@summary = "Helper function to delete a WC Article via JSON"
	macro _deleteWebContent {
		Variables.assertDefined(parameterList = "${articleId},${groupId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/journal.journalarticle/delete-article \
				-u test@liferay.com:test \
				-d groupId=${groupId} \
				-d articleId=${articleId} \
				-d articleURL='' \
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	macro _getArticleIdByURLTitle {
		Variables.assertDefined(parameterList = "${groupName},${urlTitle}");

		var groupId = JSONCategorySetter.setGroupId(
			site = "${site}",
			groupName = "${groupName}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/journal.journalarticle/get-article-by-url-title \
				-u test@liferay.com:test \
				-d groupId=${groupId} \
				-d "urlTitle=${urlTitle}"
		''';

		var articleId = JSONCurlUtil.post("${curl}", "$.[?(@['urlTitle'] == '${urlTitle}')]['articleId']");

		return "${articleId}";
	}

	@summary = "Helper function to get the 'folderId' for the folder '${folderName}'"
	macro _getFolderIdByFolderName {
		Variables.assertDefined(parameterList = "${groupId},${folderName}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/journal.journalfolder/get-folders \
				-u test@liferay.com:test \
				-d groupId=${groupId}
		''';

		var folderId = JSONCurlUtil.post("${curl}", "$.[?(@['name'] == '${folderName}')]['folderId']");

		if ("${folderId}" == "") {
			fail("FAIL. Cannot find folder.");
		}
		else {
			return "${folderId}";
		}
	}

	macro _getResourcePrimKeyByURLTitle {
		Variables.assertDefined(parameterList = "${groupName},${urlTitle}");

		var groupId = JSONCategorySetter.setGroupId(groupName = "${groupName}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/journal.journalarticle/get-display-article-by-url-title \
				-u test@liferay.com:test \
				-d groupId=${groupId} \
				-d urlTitle=${urlTitle}
		''';

		var resourcePrimKey = JSONCurlUtil.post("${curl}", "$.[?(@['urlTitle'] == '${urlTitle}')]['resourcePrimKey']");

		return "${resourcePrimKey}";
	}

	@summary = "Helper function to move a WC Article to the trash via JSON"
	macro _moveWebContentToTrash {
		Variables.assertDefined(parameterList = "${articleId},${groupId}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/journal.journalarticle/move-article-to-trash \
				-u test@liferay.com:test \
				-d groupId=${groupId} \
				-d articleId=${articleId}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	@summary = "Helper function to update status of WC Article via JSON"
	macro _updateStatus {
		Variables.assertDefined(parameterList = "${articleId},${groupId},${status},${version}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/journal.journalarticle/update-status \
				-u test@liferay.com:test \
				-d groupId=${groupId} \
				-d articleId=${articleId} \
				-d version=${version} \
				-d status=${status} \
				-d articleURL=
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

}
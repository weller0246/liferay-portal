definition {

	macro setArticleId {
		Variables.assertDefined(parameterList = "${groupName},${title}");

		var groupId = JSONWebcontentSetter.setGroupId(
			site = "${site}",
			groupName = "${groupName}");

		var urlTitle = StringUtil.replace("${title}", " ", "-");
		var urlTitle = StringUtil.lowerCase("${urlTitle}");

		var articleId = JSONWebcontentAPI._getArticleIdByURLTitle(
			site = "${site}",
			groupName = "${groupName}",
			urlTitle = "${urlTitle}");

		return "${articleId}";
	}

	macro setContentXML {
		JSONUtil2._exclusiveOrCheck(
			A = "${content}",
			B = "${contentMap}",
			failMessage = "Please only set &apos;content&apos; or only &apos;contentMap&apos; but not both or none.");

		if (isSet(contentMap)) {
			var contentXML = JSONWebcontentUtil._localizedConvertToXML(contentMap = "${contentMap}");
		}
		else {
			var contentXML = JSONWebcontentUtil._convertToXML(
				content = "${content}",
				source = "${source}");
		}

		return "${contentXML}";
	}

	macro setDescriptionMap {
		if (isSet(description)) {
			var descriptionMap = '''{"en_US":"%3Cp%3E${description}%3C%2Fp%3E"}''';
		}
		else {
			var descriptionMap = "{}";
		}

		return "${descriptionMap}";
	}

	macro setFolderId {
		if (isSet(folderName)) {
			Variables.assertDefined(parameterList = "${groupId}");

			var folderId = JSONWebcontentAPI._getFolderIdByFolderName(
				folderName = "${folderName}",
				groupId = "${groupId}");
		}
		else {
			var folderId = "0";
		}

		return "${folderId}";
	}

	macro setGroupId {
		Variables.assertDefined(parameterList = "${groupName}");

		if (!(isSet(site))) {
			var site = "true";
		}

		var groupId = JSONGroupAPI._getGroupIdByName(groupName = "${groupName}", site = "${site}");

		return "${groupId}";
	}

	macro setServiceContext {
		if (!(isSet(serviceContext))) {
			var serviceContext = JSONServiceContextUtil.setServiceContext(
				addGroupPermissions = "true",
				addGuestPermissions = "true",
				assetCategoryIds = "${assetCategoryIds}",
				assetTagNames = "${assetTagNames}",
				workflowAction = "${workflowAction}");
		}

		return "${serviceContext}";
	}

	macro setStatus {
		Variables.assertDefined(parameterList = "${statusKey}");

		if ("${statusKey}" == "APPROVE") {
			var status = "0";
		}

		if ("${statusKey}" == "DRAFT") {
			var status = "2";
		}

		if ("${statusKey}" == "EXPIRE") {
			var status = "3";
		}

		return "${status}";
	}

	macro setTitleMap {
		JSONUtil2._exclusiveOrCheck(
			A = "${title}",
			B = "${titleMap}",
			failMessage = "Please only set 'title' or only 'titleMap' but not both or none.");

		if (isSet(titleMap)) {
			var titleMap = JSONUtil2._convertLocalizedListToMap(listMap = "${titleMap}");
		}
		else {
			var titleMap = '''{"en_US":"${title}"}''';
		}

		return "${titleMap}";
	}

}
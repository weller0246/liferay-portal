definition {

	macro changeWebContent {
		Portlet.gotoPortletOptions(
			portletName = "Web Content Display",
			portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Pause(locator1 = "3000");

		Click(locator1 = "Button#CHANGE");

		WaitForElementPresent(locator1 = "IFrame#MODAL_BODY");

		SelectFrame.selectFrameNoLoading(locator1 = "IFrame#MODAL_BODY");

		if (isSet(depotName)) {
			var key_rowEntry = "${webContentTitle}";

			AssertClick(
				key_breadcrumbName = "Sites and Libraries",
				locator1 = "Breadcrumb#BREADCRUMB_ENTRY",
				value1 = "Sites and Libraries");

			AssertClick(
				key_groupTab = "Asset Library",
				locator1 = "ItemSelector#NAVIGATION_GROUP_TAB",
				value1 = "Asset Library");

			Click(
				key_card = "${depotName}",
				locator1 = "Card#CARD_TITLE");

			Click(locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");

			SelectFrame(locator1 = "IFrame#MODAL_BODY");

			AssertTextEquals.assertPartialText(
				key_card = "${webContentTitle}",
				locator1 = "Card#CARD_TITLE",
				value1 = "${webContentTitle}");
		}

		PortletEntry.save();
	}

	macro configureScopePG {
		Portlet.gotoPortletOptions(
			portletName = "${portletName}",
			portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Navigator.gotoNavItem(navItem = "Scope");

		Select(
			locator1 = "Select#SCOPE",
			value1 = "regexp:.*${scopeSelection}.*");

		PortletEntry.save();
	}

	macro configureSetupPG {
		Pause(locator1 = "3000");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		if (isSet(userToolNameList)) {
			for (var userToolName : list "${userToolNameList}") {
				Pause(locator1 = "3000");

				Check.checkToggleSwitch(
					key_fieldName = "${userToolName}",
					locator1 = "Portlet#CONFIGURATION_CHECKBOX");
			}
		}

		if (isSet(metadataNameList)) {
			for (var metadataName : list "${metadataNameList}") {
				Pause(locator1 = "3000");

				Check.checkToggleSwitch(
					key_fieldName = "${metadataName}",
					locator1 = "Portlet#CONFIGURATION_CHECKBOX");
			}
		}

		if ("${enableViewCountIncrement}" == "true") {
			Check.checkToggleSwitch(locator1 = "WCDConfiguration#ENABLE_VIEW_COUNT_INCREMENT_CHECKBOX");
		}

		SelectFrame(value1 = "relative=top");
	}

	macro editTemplate {
		if ("${editTemplateButton}" == "true") {
			WebContentDisplayPortlet.gotoEditTemplate(webContentTitle = "${webContentTitle}");
		}
		else {
			WebContentDisplayPortletNavigator.gotoEdit(
				menuItem = "${menuItem}",
				webContentTitle = "${webContentTitle}");

			Panel.expandPanel(panel = "Default Template");

			AssertTextEquals.assertPartialText(
				locator1 = "WCEditWebContent#SELECTED_STRUCTURE",
				value1 = "${structureName}");

			ClickNoError(locator1 = "WCEditWebContent#EDIT_TEMPLATE");

			AssertConfirm(value1 = "Editing the current template deletes all unsaved content.");
		}

		SelectFrameTop();

		Type.clickAtType(
			locator1 = "TextInput#NAME",
			value1 = "Template Edit");

		if (isSet(templateScript)) {
			WebContentTemplates.inputScript(templateScript = "${templateScript}");
		}

		Button.clickSave();

		if ("${editTemplateButton}" == "true") {
			SelectFrame(value1 = "relative=top");
		}
		else {
			Pause(locator1 = "2000");

			Click(locator1 = "Button#CLOSE_IFRAME");

			SelectFrame(value1 = "relative=top");

			SelectFrame(locator1 = "IFrame#MODAL_IFRAME");

			Panel.expandPanel(panel = "Default Template");

			AssertTextEquals(
				locator1 = "WCEditWebContent#SELECTED_TEMPLATE",
				value1 = "${templateName}");

			AssertClick.pauseAssertTextClickAt(
				locator1 = "Button#PUBLISH",
				value1 = "Publish");

			SelectFrame(value1 = "relative=top");
		}

		if (isSet(templateScript)) {
			AssertTextEquals(
				locator1 = "WCD#WEB_CONTENT_CONTENT",
				value1 = "${templateScript}");
		}
		else {
			AssertTextEquals(
				locator1 = "WCD#WEB_CONTENT_CONTENT",
				value1 = "${webContentContent}");
		}
	}

	macro gotoEditTemplate {
		WebContentDisplayPortletNavigator.gotoArticleEllipsisMenu(webContentTitle = "${webContentTitle}");

		MenuItem.click(menuItem = "Edit Template");
	}

	macro gotoPermissions {
		WebContentDisplayPortletNavigator.gotoArticleEllipsisMenu(webContentTitle = "${webContentTitle}");

		MenuItem.click(menuItem = "Permissions");
	}

	macro gotoSelectWebContent {
		Portlet.gotoPortletOptions(
			portletName = "Web Content Display",
			portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		AssertClick.assertTextClickAtAndPause(
			locator1 = "WCDConfiguration#SELECT_WEB_CONTENT_BUTTON",
			value1 = "Select");

		SelectFrame.selectSecondFrame(locator1 = "IFrame#MODAL_BODY");

		if (isSet(depotName)) {
			var key_rowEntry = "${webContentTitle}";

			AssertClick(
				key_breadcrumbName = "Sites and Libraries",
				locator1 = "Breadcrumb#BREADCRUMB_ENTRY",
				value1 = "Sites and Libraries");

			AssertClick(
				key_groupTab = "Asset Library",
				locator1 = "ItemSelector#NAVIGATION_GROUP_TAB",
				value1 = "Asset Library");

			Click(
				key_card = "${depotName}",
				locator1 = "Card#CARD_TITLE");
		}
	}

	macro removeWebContent {
		Portlet.gotoPortletOptions(
			portletName = "${portletName}",
			portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Button.clickRemove();

		AssertElementNotPresent(
			key_text = "Remove",
			locator1 = "Button#ANY");

		PortletEntry.save();
	}

	macro saveConfiguration {
		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.save();

		SelectFrame(value1 = "relative=top");
	}

	macro searchForNoWebContent {
		Portlet.gotoPortletOptions(
			portletName = "${portletName}",
			portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		AssertClick(
			locator1 = "WCDConfiguration#SELECT_WEB_CONTENT_BUTTON",
			value1 = "Select");

		SelectFrame.selectSecondFrame(locator1 = "IFrame#MODAL_BODY");

		if (isSet(depotName)) {
			var key_rowEntry = "${webContentTitle}";

			AssertClick(
				key_breadcrumbName = "Sites and Libraries",
				locator1 = "Breadcrumb#BREADCRUMB_ENTRY",
				value1 = "Sites and Libraries");

			AssertClick(
				key_groupTab = "Asset Library",
				locator1 = "ItemSelector#NAVIGATION_GROUP_TAB",
				value1 = "Asset Library");

			Click(
				key_card = "${depotName}",
				locator1 = "Card#CARD_TITLE");

			AssertElementNotPresent(
				key_contentName = "${webContentTitle}",
				locator1 = "ItemSelector#SELECT_FILE_WEB_CONTENT");

			AssertTextEquals.assertPartialText(
				locator1 = "Message#INFO",
				value1 = "Info:Web Content is disabled for this scope.");
		}
		else {
			Search.searchCP(searchTerm = "${webContentTitle}");

			LexiconEntry.changeDisplayStyle(displayStyle = "list");

			var key_webContentTitle = "${webContentTitle}";

			AssertElementNotPresent(
				key_listEntry = "${webContentTitle}",
				locator1 = "LexiconList#LIST_ENTRY_TITLE");
		}

		SelectFrame(value1 = "relative=top");
	}

	macro searchWebContent {
		Portlet.gotoPortletOptions(
			portletName = "${portletName}",
			portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		AssertClick(
			locator1 = "WCDConfiguration#SELECT_WEB_CONTENT_BUTTON",
			value1 = "Select");

		SelectFrame.selectSecondFrame(locator1 = "IFrame#MODAL_BODY");

		var searchTerm = "${webContentTitle}";

		if (isSet(webContentSearchValue)) {
			var searchTerm = "${webContentSearchValue}";
		}

		Search.searchCP(searchTerm = "${searchTerm}");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		var key_webContentTitle = "${webContentTitle}";

		AssertTextEquals(
			key_listEntry = "${webContentTitle}",
			locator1 = "LexiconList#LIST_ENTRY_TITLE",
			value1 = "${webContentTitle}");

		if ("${addSearchedContent}" == "true") {
			AssertClick(
				locator1 = "Button#WEB_CONTENT_CHOOSE",
				value1 = "Choose");

			SelectFrame(value1 = "relative=top");

			SelectFrame(locator1 = "IFrame#MODAL_BODY");

			AssertTextEquals(
				locator1 = "Message#INFO_ARTICLE",
				value1 = "${webContentTitle} (Modified)");

			PortletEntry.save();
		}

		SelectFrame(value1 = "relative=top");
	}

	macro selectOtherSiteWebContent {
		Portlet.gotoPortletOptions(
			portletName = "Web Content Display",
			portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		AssertClick(
			locator1 = "WCDConfiguration#SELECT_WEB_CONTENT_BUTTON",
			value1 = "Select");

		Pause(locator1 = "5000");

		SelectFrame.selectFrameNoLoading(locator1 = "IFrame#MODAL_BODY");

		AssertClick(
			key_breadcrumbName = "Sites and Libraries",
			locator1 = "Breadcrumb#BREADCRUMB_ENTRY_LINK",
			value1 = "Sites and Libraries");

		Click(
			key_card = "${otherSiteName}",
			locator1 = "Card#CARD_TITLE_LINK");

		var key_listEntry = "${webContentTitle}";

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		AssertClick(
			locator1 = "LexiconList#LIST_ENTRY_TITLE_LINK",
			value1 = "${webContentTitle}");

		SelectFrame(value1 = "relative=top");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		AssertTextEquals.assertPartialText(
			key_card = "${webContentTitle}",
			locator1 = "Card#CARD_TITLE",
			value1 = "${webContentTitle}");

		PortletEntry.save();

		SelectFrame(value1 = "relative=top");

		Click(locator1 = "Modal#CLOSE_BUTTON");
	}

	macro selectWebContent {
		Portlet.gotoPortletOptions(
			portletName = "Web Content Display",
			portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Pause(locator1 = "3000");

		AssertClick(
			locator1 = "WCDConfiguration#SELECT_WEB_CONTENT_BUTTON",
			value1 = "Select");

		Pause(locator1 = "5000");

		SelectFrame.selectSecondFrame(locator1 = "IFrame#MODAL_BODY");

		if (isSet(depotName)) {
			var key_rowEntry = "${webContentTitle}";

			AssertClick(
				key_breadcrumbName = "Sites and Libraries",
				locator1 = "Breadcrumb#BREADCRUMB_ENTRY",
				value1 = "Sites and Libraries");

			AssertClick(
				key_groupTab = "Asset Library",
				locator1 = "ItemSelector#NAVIGATION_GROUP_TAB",
				value1 = "Asset Library");

			Click(
				key_card = "${depotName}",
				locator1 = "Card#CARD_TITLE");

			Click(locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME");

			SelectFrame(value1 = "relative=top");

			SelectFrame(locator1 = "IFrame#MODAL_BODY");

			AssertTextEquals.assertPartialText(
				key_card = "${webContentTitle}",
				locator1 = "Card#CARD_TITLE",
				value1 = "${webContentTitle}");
		}

		else {
			var key_listEntry = "${webContentTitle}";
			var key_webContentTitle = "${webContentTitle}";

			LexiconEntry.changeDisplayStyle(displayStyle = "list");

			if (IsElementPresent.pauseIsElementPresent(locator1 = "LexiconList#LIST_ENTRY_TITLE_LINK")) {
				AssertClick(
					locator1 = "LexiconList#LIST_ENTRY_TITLE_LINK",
					value1 = "${webContentTitle}");
			}

			SelectFrame(value1 = "relative=top");

			SelectFrame(locator1 = "IFrame#MODAL_BODY");

			AssertTextEquals(
				key_card = "${webContentTitle}",
				locator1 = "Card#CARD_TITLE",
				value1 = "${webContentTitle}");
		}

		PortletEntry.save();

		SelectFrame(value1 = "relative=top");

		if ("${contentPage}" == "true") {
			Click(locator1 = "Icon#CLOSE");
		}
	}

	macro undoMoveToRecycleBin {
		AssertClick(
			locator1 = "Link#UNDO",
			value1 = "Undo");
	}

	macro viewAllSettingsEnabledPG {
		Click(locator1 = "WCD#WEB_CONTENT_TRANSLATION_DROPDOWN");

		Click(
			key_locale = "${locale}",
			locator1 = "WCD#WEB_CONTENT_TRANSLATION_DROPDOWN_MENU_ITEM");

		AssertTextEquals(
			locator1 = "WCD#WEB_CONTENT_TITLE",
			value1 = "${webContentTitleLocalized}");

		AssertTextEquals.assertPartialText(
			locator1 = "WCD#WEB_CONTENT_CONTENT",
			value1 = "${webContentContentLocalized}");

		AssertTextEquals.assertPartialText(
			locator1 = "Icon#PRINT",
			value1 = "Print");

		AssertTextEquals.assertPartialText(
			locator1 = "Button#SUBSCRIBE",
			value1 = "Subscribe");

		AssertTextEquals(
			key_relatedAssetTitle = "${relatedAssetTitle}",
			locator1 = "AssetRelatedAssets#ASSET_TITLE",
			value1 = "${relatedAssetTitle}");

		SelectFrame.selectFrameNoLoading(locator1 = "CKEditor#BODY_FIELD_IFRAME");

		Type(
			locator1 = "CKEditor#BODY",
			value1 = "Hello!");

		SelectFrame(value1 = "relative=top");

		AssertClick(
			locator1 = "Button#REPLY",
			value1 = "Reply");

		AssertElementPresent(
			key_ratingResult = "Rate this as good",
			locator1 = "Ratings#THUMB");

		AssertElementPresent(
			key_ratingResult = "Rate this as bad",
			locator1 = "Ratings#THUMB");
	}

	macro viewConfigureScopePG {
		var key_portletName = "Web Content Display";

		if ("${scopeSelectionDefault}" == "true") {
			AssertTextEquals(
				locator1 = "Portlet#TITLE",
				value1 = "Web Content Display");
		}
		else {
			AssertTextEquals(
				locator1 = "Portlet#TITLE",
				value1 = "Web Content Display (${scopeSelection})");
		}

		Portlet.gotoPortletOptions(
			portletName = "${portletName}",
			portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Navigator.gotoNavItem(navItem = "Scope");

		AssertSelectedLabel(
			locator1 = "Select#SCOPE",
			value1 = "${scopeSelection}");
	}

	macro viewContentPageBreak {
		AssertClick(
			locator1 = "Pagination#PAGE_2",
			value1 = "2");

		AssertTextEquals(
			locator1 = "WCD#WEB_CONTENT_CONTENT",
			value1 = "${webContentContent}");

		AssertClick(
			locator1 = "Pagination#PREVIOUS_LINK",
			value1 = "Previous");
	}

	macro viewDefaultMessage {
		Alert.viewInfoMessageSpecific(infoMessage = "This application is not visible to users yet");

		Alert.viewInfoMessageSpecific(infoMessage = "Select web content to make it visible");
	}

	macro viewDeletedArticle {
		Alert.viewWarningSpecific(warningMessage = "The selected web content no longer exists.");

		Alert.viewWarningSpecific(warningMessage = "Do you want to Select Another web content?");
	}

	macro viewEnabledFileTypesPGViaWCD {
		for (var fileType : list "${fileTypes}") {
			var key_fileType = "${fileType}";

			AssertTextEquals.assertPartialText(
				locator1 = "WCD#DOCUMENT_CONVERSION_TYPE",
				value1 = "${fileType}");
		}
	}

	macro viewImageFieldViaWCD {
		AssertElementPresent(locator1 = "WCD#WEB_CONTENT_IMAGE_PNG");
	}

	macro viewLinkToPageFieldViaWCD {
		var key_contentText = "${linkName}";
		var key_pageName = "${linkedPageName}";

		AssertTextEquals.assertPartialText(
			locator1 = "WCD#WEB_CONTENT_TEXT_LINK_URL",
			value1 = "${linkName}");

		AssertClick(
			locator1 = "WCD#WEB_CONTENT_TEXT_LINK_URL",
			value1 = "${linkName}");

		AssertVisible(locator1 = "Home#PAGE_ACTIVE");
	}

	macro viewRecycleBinMessage {
		var key_webContentTitle = "${webContentTitle}";

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR_3",
			value1 = "The web content article ${webContentTitle} was moved to the Recycle Bin");
	}

}
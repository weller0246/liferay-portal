definition {

	macro addSiteAndPage {
		JSONGroup.addGroup(groupName = "Site Name");

		if (!(isSet(layoutName))) {
			layoutName = "AC Page";
		}

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "${layoutName}");
	}

	macro addWidgetToPage {
		if (!(isSet(layoutName))) {
			layoutName = "AC Page";
		}

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "${layoutName}",
			widgetName = "${widgetName}");

		Navigator.gotoSitePage(
			pageName = "${layoutName}",
			siteName = "Site Name");
	}

	macro assertAudienceCard {
		var key_index = "${index}";

		for (var percentage : list "${percentageList}") {
			var key_percentage = "${percentage}";

			AssertElementPresent(locator1 = "ACUtils#METRIC_VALUE_PERCENTAGE");
		}
	}

	macro assertDataSourceName {
		var itemName = "${dataSourceName}";

		AssertElementPresent(locator1 = "ACSettings#ITEM_ON_LIST");
	}

	macro assertDatePattern {
		if ("${format}" == "MMM d") {
			var pattern = "[A-Z][a-z][a-z]\s\d+";
		}

		if ("${format}" == "MMM") {
			var pattern = "[A-Z][a-z][a-z]";
		}

		if ("${format}" == "MMM d - d") {
			var pattern = "[A-Z][a-z][a-z]\s\d+\s-\s\d+";
		}

		if ("${format}" == "yyyy MMM dd") {
			var pattern = "\d\d\d\d\s[A-Z][a-z][a-z]\s\d+";
		}

		var match = StringUtil.matches("${date}", "${pattern}");

		if ("${match}" == "true") {
			echo("PASSED");
		}

		else {
			fail("The ${date} doesn't follow the expected format ${format}");
		}
	}

	macro assertDropdownItem {
		var key_dropdownItem = "${menuItem}";

		AssertTextEquals(
			locator1 = "ACUtils#DROPDOWN_ITEM",
			value1 = "${menuItem}");
	}

	macro assertEventNotPresent {
		var postDataText = ProxyUtil.getHarRecording("$..request..postData..text");
		var actual = JSONUtil.getWithJSONPath("[${postDataText}]","$..[?(@['applicationId'] == '${applicationId}' && @['eventId'] == '${eventId}')]['eventId']");

		TestUtils.assertEquals(
			actual = "${actual}",
			expected = "");
	}

	macro assertEventPresent {
		var postDataText = ProxyUtil.getHarRecording("$..request..postData..text");
		var actual = JSONUtil.getWithJSONPath("[${postDataText}]","$..[?(@['applicationId'] == '${applicationId}' && @['eventId'] == '${eventId}')]['eventId']");

		TestUtils.assertEquals(
			actual = "${actual}",
			expected = "${eventId}");
	}

	macro assertEventPropertyValue {
		var postDataText = ProxyUtil.getHarRecording("$..request..postData..text");
		var actual = JSONUtil.getWithJSONPath("[${postDataText}]","$..[?(@['applicationId'] == '${applicationId}' && @['eventId'] == '${eventId}')]..${property}");

		TestUtils.assertEquals(
			actual = "${actual}",
			expected = "${value}");
	}

	macro assertIdentityRequestPropertiesValue {
		var actualDataSourceId = ACUtils.getPageRefRequestPropertyValue(
			pageRef = "identity",
			property = "dataSourceId");

		TestUtils.assertEquals(
			actual = "${actualDataSourceId}",
			expected = "${expectedDataSourceId}");

		var emailAddressHashed = ACUtils.getPageRefRequestPropertyValue(
			pageRef = "identity",
			property = "emailAddressHashed");

		TestUtils.assertNotEquals(
			actual = "${emailAddressHashed}",
			expected = "");

		var id = ACUtils.getPageRefRequestPropertyValue(
			pageRef = "identity",
			property = "id");

		TestUtils.assertNotEquals(
			actual = "${id}",
			expected = "");

		var userId = ACUtils.getPageRefRequestPropertyValue(
			pageRef = "identity",
			property = "userId");

		TestUtils.assertNotEquals(
			actual = "${userId}",
			expected = "");
	}

	@summary = "Asserts a list of items by the name"
	macro assertItemsName {
		for (var key_itemName : list "${itemList}") {
			AssertElementPresent(
				itemName = "${key_itemName}",
				locator1 = "ACSettings#ITEM_ON_LIST");
		}
	}

	macro assertPageTitle {
		var key_pageTitle = "${pageTitle}";
		AssertTextEquals(
			locator1 = "ACUtils#PAGE_TITLE",
			value1 = "${pageTitle}");
	}

	macro assertRequestCommonPropertiesValue {
		var actualChannelId = ACUtils.getRequestCommonPropertyValue(property = "channelId");

		TestUtils.assertEquals(
			actual = "${actualChannelId}",
			expected = "${expectedChannelId}");

		var actualDataSourceId = ACUtils.getRequestCommonPropertyValue(property = "dataSourceId");

		TestUtils.assertEquals(
			actual = "${actualDataSourceId}",
			expected = "${expectedDataSourceId}");

		var id = ACUtils.getRequestCommonPropertyValue(property = "id");

		TestUtils.assertNotEquals(
			actual = "${id}",
			expected = "");

		var userId = ACUtils.getRequestCommonPropertyValue(property = "userId");

		TestUtils.assertNotEquals(
			actual = "${userId}",
			expected = "");
	}

	macro assertTimePattern {
		if ("${format}" == "h tt") {
			var pattern = "\d+\s[A-Z][A-Z]";
		}

		var match = StringUtil.regexReplaceFirst("${time}", "${pattern}", "true");

		if ("${match}" == "true") {
			echo("PASSED");
		}

		else {
			fail("The ${time} doesn't follow the expected format ${format}");
		}
	}

	macro assertXAxisTimedGraph {
		var key_parentName = "${parentName}";
		AssertElementPresent(locator1 = "ACUtils#SVG_TIMED_GRAPH");
	}

	macro changePagination {
		Click(locator1 = "ACUtils#PAGINATION_ITEMS_PER_PAGE_SELECT");

		MenuItem.click(menuItem = "${itemsPerPage}");

		ACUtils.viewPaginationItemsPerPage(itemsPerPage = "${itemsPerPage}");
	}

	macro checkAnyPage {
		var key_pageNumber = "${pageNumber}";

		Click(locator1 = "ACUtils#ANY_PAGE");
	}

	macro checkPreviousNextButton {
		if (isSet(previousButton)) {
			Click(locator1 = "ACUtils#PREVIOUS_PAGES");
		}

		if (isSet(nextButton)) {
			Click(locator1 = "ACUtils#NEXT_PAGES");
		}
	}

	macro clearSearchBar {
		Click(locator1 = "ACUtils#CLEAR_SEARCH_BAR_BUTTON");
	}

	macro clickAnyButton {
		var buttonName = "${button}";
		Click(
			key_buttonName = "${buttonName}",
			locator1 = "ACUtils#GENERIC_BUTTON");
	}

	macro clickKebab {
		Click(locator1 = "ACSegments#SEGMENT_ELLIPSIS");
	}

	macro closeAllSessions {
		var analyticsCloudAsahCuratorURL = PropsUtil.get("analytics.cloud.asah.curator.url");

		var cleanCurl = '''
			${analyticsCloudAsahCuratorURL}/admin/sessions/close \
			-H 'Content-Type: application/json' \
			-d '{
				"query": {
					"match_all": {}
				}
			}'
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${cleanCurl}");
	}

	macro closeAllSessionsAndWait {
		ACUtils.closeAllSessions();

		Pause(locator1 = "30000");
	}

	macro closeModal {
		Click(locator1 = "ACUtils#MODAL_CLOSE_BUTTON");
	}

	macro createBlogsAndAddToPage {
		if (!(isSet(layoutName))) {
			layoutName = "AC Page";
		}

		Navigator.gotoSitePage(
			pageName = "${layoutName}",
			siteName = "Site Name");

		if (IsElementNotPresent(key_portletTitleName = "Blogs", locator1 = "Portlet#SPECIFIC_TITLE")) {
			ACUtils.addWidgetToPage(
				layoutName = "${layoutName}",
				widgetName = "Blogs");
		}

		if (!(isSet(entryTitle))) {
			entryTitle = "Blogs AC Title";
		}

		JSONBlog.addEntry(
			entryContent = "This is the blog test for AC",
			entryTitle = "${entryTitle}",
			groupName = "Site Name");
	}

	macro createDMAndAddToPage {
		if (!(isSet(layoutName))) {
			layoutName = "AC Page";
		}

		Navigator.gotoSitePage(
			pageName = "${layoutName}",
			siteName = "Site Name");

		if (IsElementNotPresent(key_portletTitleName = "Documents and Media", locator1 = "Portlet#SPECIFIC_TITLE")) {
			ACUtils.addWidgetToPage(
				layoutName = "${layoutName}",
				widgetName = "Documents and Media");
		}

		if (!(isSet(dmDocumentTitle))) {
			dmDocumentTitle = "DM AC Title";
		}

		if (!(isSet(dmDocumentFile))) {
			dmDocumentFile = "Document_1.jpg";
		}

		JSONDocument.addFileWithUploadedFile(
			dmDocumentTitle = "${dmDocumentTitle}",
			groupName = "Site Name",
			mimeType = "image/jpeg",
			sourceFileName = "${dmDocumentFile}");
	}

	macro createFormAndAddToPage {
		if (!(isSet(formACTitle))) {
			var formACTitle = "Created Form Name";
		}

		if (!(isSet(layoutName))) {
			layoutName = "AC Page";
		}

		ACUtils.addWidgetToPage(
			layoutName = "${layoutName}",
			widgetName = "Form");

		FormsAdmin.openFormsAdmin(siteURLKey = "site-name");

		FormsAdminNavigator.gotoAddForm();

		Form.editName(formTitle = "${formACTitle}");

		Form.addTextField(fieldPositionNumber = "0");

		Form.closeSidebar();

		Button.clickPublish();

		Navigator.gotoSitePage(
			pageName = "${layoutName}",
			siteName = "Site Name");

		FormPortletNavigator.gotoSelectForm();

		FormPortletConfiguration.selectForm(formTitle = "${formACTitle}");

		PortletEntry.save();
	}

	macro createWCAndAddToPage {
		if (!(isSet(layoutName))) {
			layoutName = "AC Page";
		}

		if (!(isSet(webContentTitle))) {
			webContentTitle = "Web Content AC Title";
		}

		if (!(isSet(webContentContent))) {
			webContentContent = "This is the web content test for AC";

			JSONWebcontent.addWebContent(
				content = "${webContentContent}",
				groupName = "Site Name",
				title = "${webContentTitle}");
		}
		else {
			JSONWebcontent.addWebContent(
				content = "${webContentContent}",
				groupName = "Site Name",
				source = "true",
				title = "${webContentTitle}");
		}

		ACUtils.addWidgetToPage(
			layoutName = "${layoutName}",
			widgetName = "Web Content Display");

		WebContentDisplayPortlet.selectWebContent(webContentTitle = "${webContentTitle}");

		Navigator.gotoSitePage(
			pageName = "${layoutName}",
			siteName = "Site Name");
	}

	macro getEventPropertyValue {
		var postDataText = ProxyUtil.getHarRecording("$..request..postData..text");
		var value = JSONUtil.getWithJSONPath("[${postDataText}]","$..[?(@['applicationId'] == '${applicationId}' && @['eventId'] == '${eventId}')]..${property}");

		return "${value}";
	}

	macro getIPCountry {
		var curl = '''https://ipinfo.io''';

		var code = JSONCurlUtil.get("${curl}", "$.country");

		if ("${code}" == "GB") {
			var country = "United Kingdom";
		}

		if ("${code}" == "BR") {
			var country = "Brazil";
		}

		if ("${code}" == "US") {
			var country = "United States";
		}

		if ("${code}" == "CN") {
			var country = "China";
		}

		if ("${code}" == "ES") {
			var country = "Spain";
		}

		if ("${code}" == "NZ") {
			var country = "New Zealand";
		}

		return "${country}";
	}

	macro getPageRefRequestPropertyValue {
		var postDataText = ProxyUtil.getHarRecording("$..[?(@['pageref'] == '${pageRef}')].request[?(@.method == 'POST' && @.url contains '/${pageRef}')]..text");
		var value = JSONUtil.getWithJSONPath("[${postDataText}]","$..${property}");

		return "${value}";
	}

	macro getRequestCommonPropertyValue {
		var postDataText = ProxyUtil.getHarRecording("$..request..postData..text");
		var value = JSONUtil.getWithJSONPath("[${postDataText}]","$.[-1:].${property}");

		return "${value}";
	}

	macro launchAC {
		var analyticsCloudURL = PropsUtil.get("analytics.cloud.url");

		Open(locator1 = "${analyticsCloudURL}");

		if (!(isSet(emailAddress))) {
			emailAddress = "test@liferay.com";
		}

		if (IsElementPresent(locator1 = "ACLogin#EMAIL_LOGIN_FIELD")) {
			if (!(isSet(password))) {
				ACLogin.loginAs(emailAddress = "${emailAddress}");
			}
			else {
				ACLogin.loginAs(
					emailAddress = "${emailAddress}",
					password = "${password}");
			}
		}

		ACWorkspace.openWorkspace();
	}

	macro launchDXP {
		Navigator.openURL();

		if (!(isSet(userEmailAddress))) {
			userEmailAddress = "test@liferay.com";
		}

		if (IsElementNotPresent(locator1 = "ApplicationsMenu#APPLICATIONS_MENU")) {
			User.firstLoginPG(userEmailAddress = "${userEmailAddress}");
		}
	}

	macro orderArrow {
		var type = "${type}";

		if ("${type}" == "descending") {
			if (IsElementPresent(locator1 = "ACUtils#ORDER_ARROW_BUTTON_ASC")) {
				Click(locator1 = "ACUtils#ORDER_ARROW_BUTTON_ASC");
			}
		}

		if ("${type}" == "ascending") {
			if (IsElementPresent(locator1 = "ACUtils#ORDER_ARROW_BUTTON_DES")) {
				Click(locator1 = "ACUtils#ORDER_ARROW_BUTTON_DES");
			}
		}
	}

	macro orderTableTitleArrow {
		var type = "${type}";

		if ("${type}" == "down") {
			if (IsElementPresent(locator1 = "ACUtils#ORDER_ARROW_TABLE_BUTTON_UP")) {
				Click(locator1 = "ACUtils#ORDER_ARROW_TABLE_BUTTON_UP");
			}
		}

		if ("${type}" == "up") {
			if (IsElementPresent(locator1 = "ACUtils#ORDER_ARROW_TABLE_BUTTON_DOWN")) {
				Click(locator1 = "ACUtils#ORDER_ARROW_TABLE_BUTTON_DOWN");
			}
		}
	}

	macro searchBar {
		if (isSet(cardTitle)) {
			Type(
				key_cardTitle = "${cardTitle}",
				locator1 = "ACSettings#SEARCH_BAR_IN_SPECIFIC_CARD",
				value1 = "${searchItem}");

			KeyPress(
				key_cardTitle = "${cardTitle}",
				locator1 = "ACSettings#SEARCH_BAR_IN_SPECIFIC_CARD",
				value1 = "\ENTER");
		}
		else {
			Type(
				locator1 = "ACSettings#SEARCH_BAR",
				value1 = "${searchItem}");

			KeyPress(
				locator1 = "ACSettings#SEARCH_BAR",
				value1 = "\ENTER");
		}
	}

	macro tearDownAC {
		ACUtils.launchAC();

		ACSettings.deleteAllProperties();

		ACSettings.deleteAllDataSource();
	}

	macro tearDownDXP {
		ACUtils.launchDXP();

		ACDXPSettings.disconnectDXPFromAnalyticsCloud();
	}

	macro viewActivePage {
		AssertTextEquals(
			locator1 = "ACUtils#ACTIVE_PAGE",
			value1 = "${pageNumber}");
	}

	macro viewAlertInfo {
		AssertTextEquals.assertPartialText(
			locator1 = "Message#ALERT_DISMISSIBLE",
			value1 = "${infoMessage}");
	}

	macro viewNoResultsMessage {
		var key_noResultMessage = "${noResultMessage}";

		AssertTextEquals(
			locator1 = "ACAssets#NO_RESULTS_CONTENT_MESSAGE",
			value1 = "${noResultMessage}");
	}

	macro viewNoResultsMessageNotPresent {
		AssertElementNotPresent(
			key_noResultMessage = "${noResultMessage}",
			locator1 = "ACAssets#NO_RESULTS_CONTENT_MESSAGE");
	}

	macro viewPaginationItemsPerPage {
		AssertTextEquals(
			locator1 = "ACUtils#PAGINATION_ITEMS_PER_PAGE_SELECT",
			value1 = "${itemsPerPage} Items");
	}

	macro viewResultsMessage {
		AssertTextEquals(
			locator1 = "ACUtils#RESULT_MESSAGE",
			value1 = "${results}");
	}

	macro viewResultsPages {
		var key_pageNumber = "${pageNumber}";

		AssertTextEquals(
			locator1 = "ACUtils#ANY_PAGE",
			value1 = "${key_pageNumber}");
	}

	macro viewSearchResultsInfo {
		AssertTextEquals(
			locator1 = "ACIndividualsDashboard#KNOWN_INDIVIDUAL_ATTRIBUTE_SEARCH_INFO",
			value1 = "${count} Results for ${searchTerm}");
	}

	macro waitForLoading {
		WaitForElementNotPresent(locator1 = "ACUtils#LOADING_ANIMATION");
	}

}
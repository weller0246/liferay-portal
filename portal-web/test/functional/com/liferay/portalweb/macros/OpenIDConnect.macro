definition {

	macro acceptConditions {
		if (IsElementPresent.pauseIsElementPresent(locator1 = "GoogleLogin#ALLOW")) {
			Click(locator1 = "GoogleLogin#ALLOW");
		}

		if (IsElementPresent(locator1 = "Button#I_AGREE")) {
			SignIn.agreeToTermsOfUse();
		}

		if (IsElementPresent.pauseIsElementPresent(locator1 = "TextInput#ANSWER")) {
			Type(
				locator1 = "TextInput#ANSWER",
				value1 = "test");

			KeyPress(
				locator1 = "TextInput#ANSWER",
				value1 = "\RETURN");
		}
	}

	macro accountSignIn {
		if (contains("${specificURL}", "{specificURL}")) {
			Navigator.openURL();
		}
		else {
			Navigator.openSpecificURL(url = "${specificURL}");
		}

		AssertClick(
			locator1 = "UserBar#USER_SIGN_IN",
			value1 = "Sign In");

		Click(locator1 = "Icon#OPENID_CONNECT");

		Click(locator1 = "Button#SIGN_IN");

		Type(
			locator1 = "OpenIDConnect#LOGIN_USERNAME_FIELD",
			value1 = "${userEmailAddress}");

		Type(
			locator1 = "OpenIDConnect#LOGIN_PASSWORD_FIELD",
			value1 = "${userPassword}");

		if (IsElementPresent(locator1 = "OpenIDConnect#LOGIN_SIGN_IN_BUTTON")) {
			Click(locator1 = "OpenIDConnect#LOGIN_SIGN_IN_BUTTON");
		}
		else {
			Click(locator1 = "GoogleLogin#LOGIN_NEXT_BUTTON");
		}
	}

	macro addProvider {
		Button.clickAdd();

		FormFields.editTextMultiline(
			fieldName = "providerName",
			fieldValue = "${providerName}");

		FormFields.editTextMultiline(
			fieldName = "openIdConnectClientId",
			fieldValue = "${openIDConnectClientID}");

		FormFields.editTextMultiline(
			fieldName = "openIdConnectClientSecret",
			fieldValue = "${openIDConnectClientSecret}");

		FormFields.editTextMultiline(
			fieldName = "discoveryEndPoint",
			fieldValue = "https://accounts.google.com/.well-known/openid-configuration");

		PortletEntry.save();
	}

	macro copyOSGIConnectionConfiguration {
		var companyId = JSONCompany.getCompanyId();
		var liferayHome = PropsUtil.get("liferay.home.dir.name");
		var OSGIPreConfigContent = FileUtil.read("test/functional/com/liferay/portalweb/tests/coreinfrastructure/security/openidconnect/dependencies/OpenIdConnectProviderConfiguration_default.config");
		var OSGIConfig = "${liferayHome}/osgi/configs/${OSGiConfigFileName}";

		FileUtil.write("${OSGIConfig}", "${OSGIPreConfigContent}");

		//WaitForConsoleTextPresent(value1 = "Configuration Updater");
	}

	macro enableOpenIdConnect {

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "System Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "SSO",
			configurationName = "OpenID Connect",
			configurationScope = "System Scope");

		FormFields.enableCheckbox(fieldName = "Enabled");

    	AssertClick(
    		locator1 = "Button#SUBMIT",
    		value1 = "Save");

   		WaitForElementPresent(locator1 = "OpenIDConnect#TOKEN_REFRESH_OFFSET");
	}

	macro setTokenRefreshOffset {
		AssertElementPresent(locator1 = "OpenIDConnect#TOKEN_REFRESH_OFFSET");

		AssertTextEquals(
        	locator1 = "OpenIDConnect#TOKEN_REFRESH_OFFSET",
        	value1 = "30");

		Type(
    		locator1 = "OpenIDConnect#TOKEN_REFRESH_OFFSET",
    		value1 = "${expirationTime}");

    	AssertClick(
    		locator1 = "Button#SUBMIT",
    		value1 = "Update");

    	AssertTextEquals(
    		locator1 = "OpenIDConnect#TOKEN_REFRESH_OFFSET",
    		value1 = "${expirationTime}");
	}

	macro viewProvider {
		AssertClick(
			locator1 = "UserBar#USER_SIGN_IN",
			value1 = "Sign In");

		AssertElementPresent(locator1 = "Icon#OPENID_CONNECT");

		Click(locator1 = "Icon#OPENID_CONNECT");

		AssertElementPresent(locator1 = "OpenIDConnectLogin#OPENID_CONNECT_PROVIDER_NAME_LABEL");

		Click(locator1 = "OpenIDConnectLogin#OPENID_CONNECT_DROPDOWN_MENU");

		AssertElementPresent(
			key_providerNname = "Google",
			locator1 = "OpenIDConnectLogin#OPENID_CONNECT_DROPDOWN_OPTION");
	}

}
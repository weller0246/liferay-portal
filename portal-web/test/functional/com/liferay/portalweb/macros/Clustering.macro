definition {

	macro _viewLiveSession {
		var key_navItem = "${navItem}";
		var key_index = "${count}";

		AssertElementPresent(locator1 = "LexiconTable#TABLE_SESSION_ID");
	}

	macro _viewNoLiveSession {
		var key_index = "${count}";

		AssertElementNotPresent(locator1 = "LexiconTable#TABLE_SESSION_ID");
	}

	@description = "Deploy the following jar into Liferay Portal server with port ${nodePort}: ${liferayDependencyJars}"
	macro deployJarOnSpecificServer {
		var leadingPortNumber = StringUtil.replace("${nodePort}", "080", "");
		var bundleIndex = ${leadingPortNumber} - 8;

		FileInstall.deployFileOnServer(
			bundleIndex = "${bundleIndex}",
			liferayDependencyJars = "${liferayDependencyJars}");
	}

	macro enableDebugging {
		Navigator.openSpecificURL(url = "http://localhost:${nodePort}");

		if (IsElementPresent(locator1 = "UserBar#USER_SIGN_IN")) {
			User.firstLoginUI(
				password = "test",
				specificURL = "http://localhost:${nodePort}",
				userEmailAddress = "test@liferay.com");
		}

		ApplicationsMenu.gotoPortlet(
			category = "System",
			panel = "Control Panel",
			portlet = "Server Administration");

		ServerAdministration.addCategoryLogLevels(
			categoryName = "${categoryName}",
			categoryPriority = "DEBUG");
	}

	@description = "Get cluster node control address in console"
	macro getClusterNodeControlAddress {
		var leadingPortNumber = StringUtil.replace("${nodePort}", "080", "");
		var bundleIndex = ${leadingPortNumber} - 8;

		var liferayHome = PropsUtil.get("liferay.home.dir.name");
		var fileContent = FileUtil.read("${liferayHome}/cluster${bundleIndex}.log");

		var controlAddress = StringUtil.extractLast("${fileContent}", "liferay-channel-control, localAddress:");
		var controlAddress = StringUtil.extractFirst("${controlAddress}", "properties");
		var controlAddress = StringUtil.replace("${controlAddress}", ",", "");
		var controlAddress = StringUtil.trim("${controlAddress}");
		return "${controlAddress}";

		echo("control Address of ${nodePort} is ${controlAddress}");
	}

	@description = "Get cluster node outgoing ip address in console"
	macro getClusterNodeOutgoingIPAddress {
		var leadingPortNumber = StringUtil.replace("${nodePort}", "080", "");
		var bundleIndex = ${leadingPortNumber} - 8;

		AntCommand(
			locator1 = "build-test.xml",
			value1 = "copy-cluster-log -Dapp.server.bundle.index=${bundleIndex}");

		var liferayHome = PropsUtil.get("liferay.home.dir.name");
		var fileContent = FileUtil.read("${liferayHome}/cluster${bundleIndex}.log");

		var ipAddress = StringUtil.extractLast("${fileContent}", "Setting JGroups outgoing IP address to");
		var ipAddress = StringUtil.extractFirst("${ipAddress}", "and interface to");
		var ipAddress = StringUtil.trim("${ipAddress}");
		return "${ipAddress}";

		echo("outgoing IP Address of ${nodePort} is ${ipAddress}");
	}

	@description = "Get cluster node transport address in console"
	macro getClusterNodeTransportAddress {
		var leadingPortNumber = StringUtil.replace("${nodePort}", "080", "");
		var bundleIndex = ${leadingPortNumber} - 8;

		var liferayHome = PropsUtil.get("liferay.home.dir.name");
		var fileContent = FileUtil.read("${liferayHome}/cluster${bundleIndex}.log");

		var transportAddress = StringUtil.extractLast("${fileContent}", "liferay-channel-transport-0, localAddress:");
		var transportAddress = StringUtil.extractFirst("${transportAddress}", "properties");
		var transportAddress = StringUtil.replace("${transportAddress}", ",", "");
		var transportAddress = StringUtil.trim("${transportAddress}");
		return "${transportAddress}";

		echo("transport Address of ${nodePort} is ${transportAddress}");
	}

	@description = "This will get the log content for a specific clustered server bundle"
	macro getLogFileSpecificServer {
		var leadingPortNumber = StringUtil.replace("${nodePort}", "080", "");

		if ("${leadingPortNumber}" != "0") {
			var bundleIndex = ${leadingPortNumber} - 8;
		}
		else {
			var bundleIndex = "${leadingPortNumber}";
		}

		AntCommand(
			locator1 = "build-test.xml",
			value1 = "copy-cluster-log -Dapp.server.bundle.index=${bundleIndex}");

		var liferayHome = PropsUtil.get("liferay.home.dir.name");
		var fileContent = FileUtil.read("${liferayHome}/cluster${bundleIndex}.log");

		return "${fileContent}";
	}

	macro runGroovyScript {
		Navigator.openSpecificURL(url = "http://localhost:${nodePort}");

		if (IsElementPresent(locator1 = "UserBar#USER_SIGN_IN")) {
			User.firstLoginUI(
				password = "test",
				specificURL = "http://localhost:${nodePort}",
				userEmailAddress = "test@liferay.com");
		}

		ApplicationsMenu.gotoPortlet(
			category = "System",
			panel = "Control Panel",
			portlet = "Server Administration");

		var script = TestCase.getFileContent(fileName = "${scriptFile}");

		ServerAdministration.executeScript(
			language = "Groovy",
			script = "${script}");

		if (isSet(longTimeRun)) {
			Pause(locator1 = "60000");
		}
	}

	@description = "This will shutdown the server at a specific bundle at ${base_branch}-bundles-${bundle_index}\tomcat."
	macro shutdownSpecificServer {
		var leadingPortNumber = StringUtil.replace("${nodePort}", "080", "");
		var bundleIndex = ${leadingPortNumber} - 8;

		AntCommand(
			locator1 = "build-test.xml",
			value1 = "stop-app-server -Dapp.server.bundle.index=${bundleIndex}");
	}

	@description = "This will start the server at a specific bundle at ${base_branch}-bundles-${bundle_index}\tomcat."
	macro startSpecificServer {
		var leadingPortNumber = StringUtil.replace("${nodePort}", "080", "");
		var bundleIndex = ${leadingPortNumber} - 8;

		if ("${deleteLiferayHome}" == "false") {
			AntCommand(
				locator1 = "build-test.xml",
				value1 = "start-app-server -Dapp.server.bundle.index=${bundleIndex} -Ddelete.liferay.home=false");
		}
		else if ("${keepCachedAppServerData}" == "true") {
			AntCommand(
				locator1 = "build-test.xml",
				value1 = "start-app-server -Dapp.server.bundle.index=${bundleIndex} -Dkeep.cached.app.server.data=true");
		}
		else {
			AntCommand(
				locator1 = "build-test.xml",
				value1 = "start-app-server -Dapp.server.bundle.index=${bundleIndex}");
		}
	}

	macro updatePortalPropertiesOnSpecificServer {
		var leadingPortNumber = StringUtil.replace("${nodePort}", "080", "");
		var bundleIndex = ${leadingPortNumber} - 8;

		AntCommand(
			locator1 = "build-test.xml",
			value1 = "portal-ext-properties-update -Dapp.server.bundle.index=${bundleIndex} -Dupdate.properties.original=${propertiesOriginal} -Dupdate.properties=${propertiesUpdate}");
	}

	macro viewClusterStatusInConsole {
		var nodeLog2 = Clustering.getLogFileSpecificServer(nodePort = "9080");

		for (var consoleText : list "Accepted view,channelName: liferay-channel-control") {
			AssertConsoleTextPresent(value1 = "${consoleText}");

			if (!(contains("${nodeLog2}", "${consoleText}"))) {
				fail("Second cluster node log does not contain expected text: ${consoleText}");
			}
		}
	}

	macro viewLiveSessionCount {
		Clustering._viewLiveSession(
			count = "${count}",
			navItem = "Live Sessions");

		var countNotPresent = 1 + ${count};

		Clustering._viewNoLiveSession(count = "${countNotPresent}");
	}

	@description = "This will view the text not shows at a specific bundle at ${base_branch}-bundles-${bundle_index}/logs."
	macro viewTextNotPresentOnSpecificNode {
		if ("${nodePort}" == "8080") {
			AssertConsoleTextNotPresent(value1 = "${exceptionText}");
		}
		else {
			var nodeLog = Clustering.getLogFileSpecificServer(nodePort = "${nodePort}");

			if (contains("${nodeLog}", "${exceptionText}")) {
				fail("Able to find exception: ${exceptionText} on cluser node at ${nodePort}");
			}
		}
	}

	@description = "This will view the text shows at a specific bundle at ${base_branch}-bundles-${bundle_index}/logs."
	macro viewTextPresentOnSpecificNode {
		if ("${nodePort}" == "8080") {
			AssertConsoleTextPresent(value1 = "${expectedText}");
		}
		else {
			var nodeLog = Clustering.getLogFileSpecificServer(nodePort = "${nodePort}");

			if (!(contains("${nodeLog}", "${expectedText}"))) {
				fail("Failed to find expected text: ${expectedText} on cluser node at ${nodePort}");
			}
		}
	}

	@description = "This will wait 5 minutes for node at port ${nodePort} to shutdown"
	macro waitForNodeShutdown {
		AntCommand(
			locator1 = "build-test.xml",
			value1 = "wait-for-server-shutdown -Dapp.server.port.number=${nodePort}");

		Pause(locator1 = "30000");
	}

}
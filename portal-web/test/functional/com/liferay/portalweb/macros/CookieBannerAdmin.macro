definition {

	@description = "Configure Cookie Banner to be visible"
	macro configuredToShowBanner {
		CookieNavigator.gotoInstanceSettings(portlet = "Banner");

		CookieBannerAdmin.saveSettings();
	}

	@description = "Disable Decline All button for Cookie Banner"
	macro disableDeclineAllButton {
		AssertClick(
			locator1 = "CookieBannerAdmin#INCLUDE_DECLINE_ALL_BUTTON",
			value1 = "Include \\"Decline All\\" Button");

		Button.clickSave();
	}

	macro saveConfiguration {
		if (IsElementPresent(locator1 = "Button#UPDATE")) {
			AssertClick.assertTextClickNoMouseOver(
				locator1 = "Button#UPDATE",
				value1 = "Update");
		}
		else if (IsElementPresent(locator1 = "Button#SAVE")) {
			AssertClick.assertTextClickNoMouseOver(
				locator1 = "Button#SAVE",
				value1 = "Save");
		}
	}

	@description = "Save Cookie Banner settings"
	macro saveSettings {
		WaitForElementPresent(locator1 = "CookieBannerAdmin#MENU_TITLE");

		if (isSet(content)) {
			Type(
				locator1 = "CookieBannerAdmin#CONTENT",
				value1 = "${content}");
		}

		if (isSet(privacyPolicyLink)) {
			Type(
				locator1 = "CookieBannerAdmin#PRIVACY_POLICY_LINK",
				value1 = "${privacyPolicyLink}");
		}

		if (isSet(linkDisplayText)) {
			Type(
				locator1 = "CookieBannerAdmin#LINK_DISPLAY_TEXT",
				value1 = "${linkDisplayText}");
		}

		if (isSet(includeDeclineAllButton)) {
			AssertClick(
				locator1 = "CookieBannerAdmin#INCLUDE_DECLINE_ALL_BUTTON",
				value1 = "Include \\"Decline All\\" Button");
		}

		CookieBannerAdmin.saveConfiguration();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#SUCCESS",
			value1 = "Your request completed successfully.");
	}

	@description = "Set custom texts for Cookie Banner at Instance Level"
	macro setCustomTextForAllFields {
		Variables.assertDefined(parameterList = "${text}");

		CookieNavigator.gotoInstanceSettings(portlet = "Banner");

		Type(
			locator1 = "CookieBannerAdmin#CONTENT",
			value1 = "${text}_1");

		Type(
			locator1 = "CookieBannerAdmin#PRIVACY_POLICY_LINK",
			value1 = "${text}_2");

		Type(
			locator1 = "CookieBannerAdmin#LINK_DISPLAY_TEXT",
			value1 = "${text}_3");

		if (isSet(cancel)) {
			AssertClick(
				locator1 = "Button#CANCEL",
				value1 = "Cancel");
		}
		else {
			CookieBannerAdmin.saveConfiguration();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#SUCCESS",
				value1 = "Your request completed successfully.");
		}
	}

	@description = "Set custom texts for Cookie Banner at Site Level"
	macro setCustomTextForAllFieldsAtSiteLevel {
		Variables.assertDefined(parameterList = "${text}");

		CookieNavigator.gotoSiteSettings(
			baseURL = "${baseURL}",
			portlet = "Banner",
			siteURLKey = "${siteURLKey}");

		Type(
			locator1 = "CookieBannerAdmin#CONTENT",
			value1 = "${text}_1");

		Type(
			locator1 = "CookieBannerAdmin#PRIVACY_POLICY_LINK",
			value1 = "${text}_2");

		Type(
			locator1 = "CookieBannerAdmin#LINK_DISPLAY_TEXT",
			value1 = "${text}_3");

		if (isSet(cancel)) {
			AssertClick(
				locator1 = "Button#CANCEL",
				value1 = "Cancel");
		}
		else {
			CookieBannerAdmin.saveConfiguration();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#SUCCESS",
				value1 = "Your request completed successfully.");
		}
	}

	@description = "Verify all elements are present and configured at Cookie Banner"
	macro verifyAllConfigurationElementsAreConfigured {
		WaitForElementPresent(locator1 = "CookieBannerAdmin#MENU_TITLE");

		while ((IsTextNotEqual(locator1 = "CookieBannerAdmin#CONTENT", value1 = "${text}")) && (maxIterations = "10")) {
			Refresh();
		}

		AssertTextEquals(
			locator1 = "CookieBannerAdmin#PRIVACY_POLICY_LINK",
			value1 = "${text}");

		AssertTextEquals(
			locator1 = "CookieBannerAdmin#LINK_DISPLAY_TEXT",
			value1 = "${text}");

		AssertChecked.assertCheckedNotVisible(locator1 = "CookieBannerAdmin#INCLUDE_DECLINE_ALL_BUTTON");
	}

}
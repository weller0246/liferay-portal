definition {

	macro addCP {
		LexiconEntry.gotoAdd();

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Type(
			locator1 = "TextInput#WEB_ID",
			value1 = "${webId}");

		Type(
			locator1 = "TextInput#VIRTUAL_HOST",
			value1 = "${virtualHost}");

		Type(
			locator1 = "TextInput#MAIL_DOMAIN",
			value1 = "${mailDomain}");

		if ("${maxUsers}" == "true") {
			Type(
				locator1 = "TextInput#MAX_USERS",
				value1 = "${maxUserNumber}");
		}

		if ("${portalInstanceActive}" == "false") {
			Uncheck(locator1 = "Checkbox#ACTIVE");
		}

		if (isSet(screenName)) {
			Type(
				locator1 = "TextInput#VIRTUAL_INSTANCE_SCREEN_NAME",
				value1 = "${instanceScreenName}");

			Type(
				locator1 = "TextInput#VIRTUAL_INSTANCE_EMAIL",
				value1 = "${instanceAdminEmail}");

			Type(
				locator1 = "TextInput#VIRTUAL_INSTANCE_PASSWORD",
				value1 = "${instancePassword}");
		}

		if (isSet(instanceAdminFirstName)) {
			Type(
				locator1 = "TextInput#VIRTUAL_INSTANCE_FIRST_NAME",
				value1 = "${instanceAdminEmail}");
		}

		if (isSet(instanceAdminLastName)) {
			Type(
				locator1 = "TextInput#VIRTUAL_INSTANCE_LAST_NAME",
				value1 = "${instanceAdminEmail}");
		}

		Button.clickAdd();

		SelectFrameTop();

		Alert.viewSuccessMessage();
	}

	macro deactivatePortalInstance {
		var key_virtualHost = "${virtualHost}";

		LexiconEntry.openEntryMenu(rowEntry = "${virtualHost}");

		MenuItem.click(menuItem = "Edit");

		Uncheck.uncheckToggleSwitch(locator1 = "Checkbox#ACTIVE");

		Button.clickSave();

		AssertTextEquals(
			locator1 = "PortalInstances#INSTANCE_TABLE_ACTIVE",
			value1 = "No");
	}

	macro deleteCP {
		var key_virtualHost = "${virtualHost}";

		LexiconTable.clickEllipsisItem(
			item = "Delete",
			tableEntry = "${virtualHost}");

		AssertTextEquals(
			locator1 = "Modal#BODY",
			value1 = "Deleting an instance is an action impossible to revert.");

		Click(
			key_text = "Delete",
			locator1 = "Modal#MODAL_FOOTER_BUTTON");

		Alert.viewSuccessMessage();

		AssertTextNotPresent(
			locator1 = "PortalInstances#INSTANCE_TABLE_VIRTUAL_HOST",
			value1 = "${virtualHost}");
	}

	macro editCP {
		var key_virtualHost = "${virtualHost}";

		AssertTextEquals(
			locator1 = "PortalInstances#INSTANCE_TABLE_VIRTUAL_HOST",
			value1 = "${virtualHost}");

		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Edit");

		Type(
			locator1 = "TextInput#MAIL_DOMAIN",
			value1 = "${mailDomain}");

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");

		Alert.viewSuccessMessage();
	}

	macro openVirtualInstancesAdmin {
		if (isSet(baseURL)) {
			var baseURL = "${baseURL}";

			Navigator.openWithAppendToBaseURL(
				baseURL = "${baseURL}",
				urlAppend = "group/control_panel/manage?p_p_id=com_liferay_portal_instances_web_portlet_PortalInstancesPortlet");
		}
		else {
			Navigator.openWithAppendToBaseURL(urlAppend = "group/control_panel/manage?p_p_id=com_liferay_portal_instances_web_portlet_PortalInstancesPortlet");
		}
	}

	@summary = "Clean up Portal Instances as part of standard teardown"
	macro tearDownCP {
		JSONUser.tearDownNonAdminUsers(authenticationMethod = "${authenticationMethod}");

		HeadlessPortalInstanceAPI.cleanAllPortalInstances(authenticationMethod = "${authenticationMethod}");
	}

	macro viewCP {
		var key_virtualHost = "${virtualHost}";

		AssertTextEquals(
			locator1 = "PortalInstances#INSTANCE_TABLE_WEB_ID",
			value1 = "${virtualHost}");

		AssertTextEquals(
			locator1 = "PortalInstances#INSTANCE_TABLE_VIRTUAL_HOST",
			value1 = "${virtualHost}");

		AssertTextEquals(
			locator1 = "PortalInstances#INSTANCE_TABLE_MAIL_DOMAIN",
			value1 = "${virtualHost}");
	}

}
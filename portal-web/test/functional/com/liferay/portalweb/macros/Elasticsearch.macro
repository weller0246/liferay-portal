definition {

	macro assertElasticsearchIsReady {
		var port = Elasticsearch.getServerPort();
		var portalHostname = PropsUtil.get("portal.hostname");

		while ((IsElementPresent(locator1 = "Message#ERROR")) && (maxIterations = "15")) {
			Refresh();

			echo("Elasticsearch is not available");
		}

		Navigator.openSpecificURL(url = "http://${portalHostname}:${port}");

		Elasticsearch.viewClusterStatus(clusterStatus = "\"cluster_name\" : \"LiferayElasticsearchCluster\"");

		Navigator.openSpecificURL(url = "http://${portalHostname}:${port}/_cluster/health?pretty");

		while (((IsTextNotEqual(locator1 = "//pre", value1 = "\"status\" : \"green\"")) || (IsTextNotEqual(locator1 = "//pre", value1 = "\"status\" : \"yellow\"")) || (IsElementNotPresent(locator1 = "//pre"))) && (maxIterations = "10")) {
			Refresh();

			echo("## Elasticsearch cluster not stable");
		}
	}

	macro getClientVersion {
		var projectDir = PropsUtil.get("project.dir");

		var fileContent = FileUtil.read("${projectDir}/modules/apps/portal-search-elasticsearch7/portal-search-elasticsearch7-impl/build.gradle");

		var clientVersion = RegexUtil.replace("${fileContent}", "elasticsearchClientVersion = \"(\d+\.\d+\.\d+)", "1");

		echo("Elasticsearch client version: ${clientVersion}");

		return "${clientVersion}";
	}

	macro getServerPort {
		var remoteEnabled = Elasticsearch.isRemoteMode();

		if ("${remoteEnabled}" == "true") {
			return "9200";
		}
		else {
			return "9201";
		}
	}

	macro getServerVersion {
		if (!(isSet(port))) {
			var port = Elasticsearch.getServerPort();
		}

		var portalHostname = PropsUtil.get("portal.hostname");

		var curl = "http://${portalHostname}:${port}";

		var serverInfo = JSONCurlUtil.get("${curl}");

		var serverVersion = RegexUtil.replace("${serverInfo}", "number\" : \"(\d+\.\d+\.\d+)", "1");

		echo("Version of the Elasticsearch server on port ${port}: ${serverVersion}");

		return "${serverVersion}";
	}

	macro isRemoteMode {
		var liferayHome = PropsUtil.get("liferay.home.dir.name");

		var fileLocation = "${liferayHome}/osgi/configs/com.liferay.portal.search.elasticsearch7.configuration.ElasticsearchConfiguration.config";

		var fileExists = FileUtil.exists("${fileLocation}");

		if ("${fileExists}" == "true") {
			var fileContent = FileUtil.read("${fileLocation}");

			var productionModeEnabled = RegexUtil.replace("${fileContent}", "productionModeEnabled=\"(.*)\"", "1");
			var updatedProductionMode = RegexUtil.replace("${fileContent}", "productionModeEnabled=B\"(.*)\"", "1");

			if (("${productionModeEnabled}" == "true") || ("${updatedProductionMode}" == "true")) {
				return "true";
			}
			else {
				return "false";
			}
		}
		else {
			return "false";
		}
	}

	macro viewClusterStatus {
		AssertTextPresent(
			locator1 = "//pre",
			value1 = "${clusterStatus}");
	}

	@description = "Elasticsearch provides a Cluster Health API (http://localhost:${nodePort}/_cluster/health?pretty=true) to easily view the current status of the cluster."
	macro viewClusterStatusViaClusterHealthAPI {
		var portalHostname = PropsUtil.get("portal.hostname");

		Navigator.openSpecificURL(url = "http://${portalHostname}:${nodePort}/_cluster/health?level=indices&pretty");

		AssertTextEquals.assertPartialText(
			locator1 = "//body",
			value1 = "\"cluster_name\" : \"LiferayElasticsearchCluster\"");

		AssertTextEquals.assertPartialText(
			locator1 = "//body",
			value1 = "\"number_of_nodes\" : ${numberOfNodes}");

		AssertTextEquals.assertPartialText(
			locator1 = "//body",
			value1 = "\"number_of_data_nodes\" : ${numberOfNodes}");
	}

}
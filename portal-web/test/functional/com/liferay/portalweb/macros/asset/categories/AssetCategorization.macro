definition {

	macro _addTag {
		Type(
			locator1 = "AssetCategorization#TAGS_FIELD",
			value1 = "${tagName}");

		ScrollWebElementIntoView(locator1 = "AssetCategorization#TAGS_FIELD");

		if (IsElementPresent(key_tagName = "${tagName}", locator1 = "AssetCategorization#TAG_AUTOCOMPLETE_SPECIFIC")) {
			Click(
				key_tagName = "${tagName}",
				locator1 = "AssetCategorization#TAG_AUTOCOMPLETE_SPECIFIC");
		}
		else {
			KeyPress(
				locator1 = "AssetCategorization#TAGS_FIELD",
				value1 = ",");
		}
	}

	macro addCategory {
		var key_categoryName = "${categoryName}";

		Type(
			key_vocabularyName = "${vocabularyName}",
			locator1 = "AssetCategorization#CATEGORIES_FIELD",
			value1 = "${categoryName}");

		Click.mouseDownMouseUp(locator1 = "AssetCategorization#CATEGORY_AUTOCOMPLETE_SPECIFIC");

		AssertTextEquals(
			locator1 = "AssetCategorization#CATEGORY",
			value1 = "${categoryName}");
	}

	macro addCategoryViaSelect {
		Panel.expandPanel(panel = "Categorization");

		Click(
			key_vocabularyName = "${vocabularyName}",
			locator1 = "AssetCategorization#CATEGORY_SELECT");

		IFrame.selectCategoryFrame();

		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${categoryName}");

		IFrame.selectTopFrame();

		Click(locator1 = "Modal#SAVE_BUTTON");

		IFrame.closeFrame();
	}

	macro addDuplicateTag {
		AssetCategorization._addTag(tagName = "${tagName}");

		AssertTextEquals(
			locator1 = "//div[contains(@class,'form-feedback-item')]",
			value1 = "Duplicate Tag: ${tagName}");
	}

	macro addMultipleCategories {
		for (var categoryName : list "${categoryNameList}") {
			AssetCategorization.addCategory(
				categoryName = "${categoryName}",
				vocabularyName = "${vocabularyName}");
		}
	}

	macro addTag {
		AssetCategorization._addTag(tagName = "${tagName}");

		AssertTextEquals(
			key_tagName = "${tagName}",
			locator1 = "AssetCategorization#TAG",
			value1 = "${tagName}");
	}

	macro gotoSelectCategory {
		IFrame.selectCategoryFrame();

		Portlet.expandTree();
	}

	macro removeCategories {
		for (var categoryName : list "${categoryNameList}") {
			var key_categoryName = "${categoryName}";

			AssertTextEquals(
				locator1 = "AssetCategorization#CATEGORY",
				value1 = "${categoryName}");

			Click(
				key_itemName = "${categoryName}",
				locator1 = "AssetCategorization#ITEM_REMOVE");

			AssertElementNotPresent(locator1 = "AssetCategorization#CATEGORY");
		}
	}

	macro removeTag {
		var key_tagName = "${tagName}";

		AssertTextEquals(
			locator1 = "AssetCategorization#TAG",
			value1 = "${tagName}");

		Click(
			key_itemName = "${tagName}",
			locator1 = "AssetCategorization#ITEM_REMOVE");

		AssertElementNotPresent(locator1 = "AssetCategorization#TAG");
	}

	macro selectCategory {
		AssetCategorization.gotoSelectCategory();

		AssertVisible(
			key_nodeName = "${categoryName}",
			locator1 = "Treeview#NODE_ITEM");

		Portlet.selectTreeNode(nodeName = "${categoryName}");

		SelectFrameTop();

		if (IsElementPresent(key_text = "Add", locator1 = "Modal#MODAL_FOOTER_BUTTON")) {
			Click(
				key_text = "Add",
				locator1 = "Modal#MODAL_FOOTER_BUTTON");
		}
		else if (IsElementPresent(key_text = "Done", locator1 = "Modal#MODAL_FOOTER_BUTTON")) {
			Click(
				key_text = "Done",
				locator1 = "Modal#MODAL_FOOTER_BUTTON");
		}
	}

	macro selectTag {
		Click(locator1 = "AssetCategorization#TAGS_SELECT_BUTTON");

		IFrame.selectTagFrame();

		for (var tagName : list "${tagNameList}") {
			Check(
				key_rowEntry = "${tagName}",
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_CHECKBOX");
		}

		SelectFrameTop();

		SelectFrame(
			key_title = "Asset Publisher",
			locator1 = "IFrame#MODAL_ANY");

		AssertClick(
			locator1 = "Button#DONE",
			value1 = "Done");

		AssertTextEquals(
			key_tagName = "${tagName}",
			locator1 = "AssetCategorization#TAG",
			value1 = "${tagName}");
	}

	macro viewCategory {
		AssertTextEquals(
			key_categoryName = "${categoryName}",
			locator1 = "AssetCategorization#CATEGORY",
			value1 = "${categoryName}");
	}

	macro viewNoAddPermissions {
		Panel.expandPanel(panel = "Categorization");

		Click(
			key_vocabularyName = "${vocabularyName}",
			locator1 = "AssetCategorization#CATEGORY_SELECT");

		IFrame.selectCategoryFrame();

		AssertElementNotPresent(locator1 = "Button#PLUS");
	}

	macro viewNoTag {
		var tagName = StringUtil.lowerCase("${tagName}");

		AssertElementNotPresent(
			key_tagName = "${tagName}",
			locator1 = "AssetCategorization#TAG",
			value1 = "${tagName}");
	}

	macro viewNoVocabularyField {
		Panel.expandPanel(panel = "Categorization");

		AssertElementNotPresent(
			key_vocabularyName = "${vocabularyName}",
			locator1 = "AssetCategorization#CATEGORIES_FIELD");
	}

	macro viewTag {
		var tagName = StringUtil.lowerCase("${tagName}");

		AssertTextEquals(
			key_tagName = "${tagName}",
			locator1 = "AssetCategorization#TAG",
			value1 = "${tagName}");
	}

	macro viewVocabulary {
		AssertTextEquals(
			key_vocabularyName = "${vocabularyName}",
			locator1 = "UsersAndOrganizationsEditUser#VOCABULARY_LABEL",
			value1 = "${vocabularyName}");
	}

}
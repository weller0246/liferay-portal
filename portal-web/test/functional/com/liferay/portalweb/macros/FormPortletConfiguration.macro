definition {

	var formName = "Created Form Name";

	macro changeLanguage {
		Click(
			key_language = "${currentLanguage}",
			locator1 = "FormPortletConfiguration#SELECT_LANGUAGE");
		Click(
			key_language = "${newLanguage}",
			locator1 = "FormPortletConfiguration#SELECT_LANGUAGE");
	}

	macro configureScopePG {
		Portlet.gotoPortletOptions(
			portletName = "${portletName}",
			portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#CONFIGURATION");

		Navigator.gotoNavItem(navItem = "Scope");

		Select(
			locator1 = "Select#SCOPE",
			value1 = "${scopeSelection}");

		PortletEntry.save();
	}

	macro selectForm {
		if (isSet(formTitle)) {
			formName = "${formTitle}";
		}

		Navigator.gotoNavItem(navItem = "Setup");

		AssertClick(
			key_formName = "${formName}",
			locator1 = "FormPortletConfiguration#FORMS_TABLE_NAME",
			value1 = "${formName}");

		AssertTextEquals(
			locator1 = "Message#INFO",
			value1 = "Displaying Form: ${formName} (Modified)");
	}

	macro viewTranslationLanguage {
		AssertElementPresent(
			key_language = "${language}",
			locator1 = "FormPortletConfiguration#SELECT_LANGUAGE");
	}

}
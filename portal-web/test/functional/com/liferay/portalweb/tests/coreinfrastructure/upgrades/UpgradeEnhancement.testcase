@component-name = "portal-upgrades"
definition {

	property app.server.types = "tomcat";
	property database.types = "mysql";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Upgrades Core Infrastructure";

	@priority = "4"
	test DecrementCoreSchemaMajorVersion {
		property test.name.skip.portal.instance = "UpgradeEnhancement#DecrementCoreSchemaMajorVersion";

		var script = '''
			import com.liferay.portal.kernel.service.ReleaseLocalServiceUtil;
			import com.liferay.portal.kernel.model.*;
			import com.liferay.portal.kernel.version.Version;

			Release release = ReleaseLocalServiceUtil.fetchRelease(ReleaseConstants.DEFAULT_SERVLET_CONTEXT_NAME);

			String originalSchemaVersion = release.getSchemaVersion();

			Version version = Version.parseVersion(originalSchemaVersion);

			version = new Version(version.getMajor() - 1, version.getMinor(), version.getMicro());

			out.println("Version decremented to: " + version.toString());

			ReleaseLocalServiceUtil.updateRelease(
				ReleaseConstants.DEFAULT_SERVLET_CONTEXT_NAME, version.toString(), originalSchemaVersion);
		''';

		User.firstLoginPG();

		ServerAdministration.openServerAdmin();

		ServerAdministration.executeScript(
			language = "Groovy",
			script = "${script}");

		Portlet.shutdownServer();

		Portlet.startServer(deleteLiferayHome = "false");

		AssertConsoleTextPresent(value1 = "You must first upgrade the portal to the required schema version");
	}

	@priority = "4"
	test DecrementCoreSchemaMicroVersion {
		property test.name.skip.portal.instance = "UpgradeEnhancement#DecrementCoreSchemaMicroVersion";

		var script = '''
			import com.liferay.portal.kernel.service.ReleaseLocalServiceUtil;
			import com.liferay.portal.kernel.model.*;
			import com.liferay.portal.kernel.version.Version;

			Release release = ReleaseLocalServiceUtil.fetchRelease(ReleaseConstants.DEFAULT_SERVLET_CONTEXT_NAME);

			String originalSchemaVersion = release.getSchemaVersion();

			Version version = Version.parseVersion(originalSchemaVersion);

			if (version.getMicro() != 0) {
				version = new Version(version.getMajor(), version.getMinor(), version.getMicro() - 1);

				out.println("Version decremented to: " + version.toString());
			} else {
				out.println("Cannot decrement micro version, it is already at the minimum value");
			}

			ReleaseLocalServiceUtil.updateRelease(
				ReleaseConstants.DEFAULT_SERVLET_CONTEXT_NAME, version.toString(), originalSchemaVersion);
		''';

		User.firstLoginPG();

		ServerAdministration.openServerAdmin();

		ServerAdministration.executeScript(
			language = "Groovy",
			script = "${script}");

		var output = selenium.getText("ServerAdministrationScript#OUTPUT_FIELD");

		if ("${output}" == "Cannot decrement micro version, it is already at the minimum value") {
			echo("This testcase is currently not testable.");
		}
		else {
			Portlet.shutdownServer();

			Portlet.startServer(deleteLiferayHome = "false");

			User.firstLoginPG();
		}
	}

	@priority = "3"
	test DecrementCoreSchemaMinorVersion {
		property test.name.skip.portal.instance = "UpgradeEnhancement#DecrementCoreSchemaMinorVersion";

		var script = '''
			import com.liferay.portal.kernel.service.ReleaseLocalServiceUtil;
			import com.liferay.portal.kernel.model.*;
			import com.liferay.portal.kernel.version.Version;

			Release release = ReleaseLocalServiceUtil.fetchRelease(ReleaseConstants.DEFAULT_SERVLET_CONTEXT_NAME);

			String originalSchemaVersion = release.getSchemaVersion();

			Version version = Version.parseVersion(originalSchemaVersion);

			if (version.getMinor() != 0) {
				version = new Version(version.getMajor(), version.getMinor() - 1, version.getMicro());

				out.println("Version decremented to: " + version.toString());
			} else {
				out.println("Cannot decrement minor version, it is already at the minimum value");
			}

			ReleaseLocalServiceUtil.updateRelease(
				ReleaseConstants.DEFAULT_SERVLET_CONTEXT_NAME, version.toString(), originalSchemaVersion);
		''';

		User.firstLoginPG();

		ServerAdministration.openServerAdmin();

		ServerAdministration.executeScript(
			language = "Groovy",
			script = "${script}");

		var output = selenium.getText("ServerAdministrationScript#OUTPUT_FIELD");

		if ("${output}" != "Cannot decrement minor version, it is already at the minimum value") {
			Portlet.shutdownServer();

			Portlet.startServer(deleteLiferayHome = "false");

			AssertConsoleTextPresent(value1 = "You must first upgrade the portal to the required schema version");
		}
		else {
			echo("This testcase is currently not testable.");
		}
	}

	@ignore = "true"
	@priority = "4"
	@refactorneeded
	test DirectUpgradeFromArchive6012 {
		property data.archive.type = "data-archive-portal";
		property portal.upgrades = "false";
		property portal.version = "6.0.12";
		property test.assert.warning.exceptions = "true";

		var liferayHome = PropsUtil.get("liferay.home.dir.name");
		var fileContent = FileUtil.read("${liferayHome}/tools/portal-tools-db-upgrade-client/logs/upgrade.log");

		if (contains("${fileContent}", "You must first upgrade to Liferay Portal 6200")) {
			echo("You must first upgrade to Liferay Portal 6200.");
		}
		else {
			fail("FAIL! Cannot find the warning message.");
		}
	}

	@priority = "4"
	@refactorneeded
	test DirectUpgradeFromArchive6130 {
		property data.archive.type = "data-archive-portal";
		property portal.upgrades = "false";
		property portal.version = "6.1.30";
		property test.assert.warning.exceptions = "true";

		var liferayHome = PropsUtil.get("liferay.home.dir.name");
		var fileContent = FileUtil.read("${liferayHome}/tools/portal-tools-db-upgrade-client/logs/upgrade.log");

		if (contains("${fileContent}", "You must first upgrade to Liferay Portal 6200")) {
			echo("You must first upgrade to Liferay Portal 6200.");
		}
		else {
			fail("FAIL! Cannot find the warning message.");
		}
	}

	@priority = "4"
	test RemoveModulesWithSchemaVersionChange {
		property osgi.modules.includes = "revert-schema-version-api,revert-schema-version-service";

		User.firstLoginPG();

		GogoShell.openGogoShellAdmin();

		GogoShell.executeCommand(commandName = "dm wtf");

		GogoShell.viewOutput(outputContent = "No unregistered components found");

		// Change schema version from 2.1.1 to 2.1.0.

		AntCommand(
			locator1 = "build-test.xml",
			value1 = "redeploy-osgi-module -Dmodule.base.dir=modules/test/upgrade/revert-schema-version-service -Dupdate.properties.original=2.1.1 -Dupdate.properties=2.1.0");

		Pause(locator1 = "15000");

		GogoShell.openGogoShellAdmin();

		GogoShell.executeCommand(commandName = "dm wtf");

		GogoShell.viewOutput(outputContent = "No unregistered components found");

		// Change schema version from 2.1.0 to 2.0.0.

		AntCommand(
			locator1 = "build-test.xml",
			value1 = "redeploy-osgi-module -Dmodule.base.dir=modules/test/upgrade/revert-schema-version-service -Dupdate.properties.original=2.1.0 -Dupdate.properties=2.0.0");

		Pause(locator1 = "15000");

		GogoShell.openGogoShellAdmin();

		GogoShell.executeCommand(commandName = "dm wtf");

		GogoShell.viewOutput(outputContent = "No unregistered components found");

		// Change schema version from 2.0.0 to 1.0.0.

		AntCommand(
			locator1 = "build-test.xml",
			value1 = "redeploy-osgi-module -Dmodule.base.dir=modules/test/upgrade/revert-schema-version-service -Dupdate.properties.original=2.0.0 -Dupdate.properties=1.0.0");

		Pause(locator1 = "15000");

		GogoShell.openGogoShellAdmin();

		GogoShell.executeCommand(commandName = "dm wtf");

		GogoShell.viewOutput(outputContent = "com.liferay.portal.kernel.model.Release (&(release.bundle.symbolic.name=com.liferay.revert.schema.version.service)(&(release.schema.version>=1.0.0)(!(release.schema.version>=2.0.0)))(|(!(release.state=*))(release.state=0))) for bundle com.liferay.revert.schema.version.service");
	}

	@priority = "4"
	test UpgradeCoreSchemaVersion {
		property test.name.skip.portal.instance = "UpgradeEnhancement#UpgradeCoreSchemaVersion";

		var printSchemaVersionScript = '''
			import com.liferay.portal.kernel.service.ReleaseLocalServiceUtil;
			import com.liferay.portal.kernel.model.*;
			import com.liferay.portal.kernel.version.Version;

			Release release = ReleaseLocalServiceUtil.fetchRelease(ReleaseConstants.DEFAULT_SERVLET_CONTEXT_NAME);

			String originalSchemaVersion = release.getSchemaVersion();

			Version version = Version.parseVersion(originalSchemaVersion);

			out.println("Schema Version is " + version.toString());
		''';

		var decrementSchemaVersionScript = '''
			import com.liferay.portal.kernel.service.ReleaseLocalServiceUtil;
			import com.liferay.portal.kernel.model.*;
			import com.liferay.portal.kernel.version.Version;

			Release release = ReleaseLocalServiceUtil.fetchRelease(ReleaseConstants.DEFAULT_SERVLET_CONTEXT_NAME);

			String originalSchemaVersion = release.getSchemaVersion();

			Version version = Version.parseVersion(originalSchemaVersion);

			if (version.getMinor() != 0) {
				version = new Version(version.getMajor(), version.getMinor() - 1, version.getMicro());

				out.println("Schema Version is changed to " + version.toString());
			} else if (version.getMicro() != 0) {
				version = new Version(version.getMajor(), version.getMinor(), version.getMicro() - 1);

				out.println("Schema Version is changed to " + version.toString());
			} else {
				out.println("Cannot decrement minor version or micro version, they are already at the minimum value");
			}

			ReleaseLocalServiceUtil.updateRelease(
			ReleaseConstants.DEFAULT_SERVLET_CONTEXT_NAME, version.toString(), originalSchemaVersion);
		''';

		User.firstLoginPG();

		ServerAdministration.openServerAdmin();

		ServerAdministration.executeScript(
			language = "Groovy",
			script = "${printSchemaVersionScript}");

		var originalSchemaVersion = selenium.getText("ServerAdministrationScript#OUTPUT_FIELD");
		var originalSchemaVersion = StringUtil.extractLast("${originalSchemaVersion}", "Schema Version is");
		var originalSchemaVersion = StringUtil.trim("${originalSchemaVersion}");

		ServerAdministration.openServerAdmin();

		ServerAdministration.executeScript(
			language = "Groovy",
			script = "${decrementSchemaVersionScript}");

		var output = selenium.getText("ServerAdministrationScript#OUTPUT_FIELD");

		if ("${output}" != "Cannot decrement minor version or micro version, they are already at the minimum value") {
			Portlet.shutdownServer();

			AntCommand(
				locator1 = "build-test.xml",
				value1 = "upgrade-legacy-database");

			var liferayHome = PropsUtil.get("liferay.home.dir.name");
			var fileContent = FileUtil.read("${liferayHome}/tools/portal-tools-db-upgrade-client/logs/upgrade.log");

			if (!(contains("${fileContent}", "Completed upgrade process com.liferay.portal.upgrade.PortalUpgradeProcess"))) {
				fail("Didn't run upgrade process for Core from the latest failing point.");
			}

			Portlet.startServer(deleteLiferayHome = "false");

			User.firstLoginPG();

			ServerAdministration.openServerAdmin();

			ServerAdministration.executeScript(
				language = "Groovy",
				script = "${printSchemaVersionScript}");

			AssertTextEquals.assertPartialText(
				locator1 = "ServerAdministrationScript#OUTPUT_FIELD",
				value1 = "Schema Version is ${originalSchemaVersion}");
		}
		else {
			echo("This scenario is currently not testable.");
		}

		Portlet.shutdownServer();

		AntCommand(
			locator1 = "build-test.xml",
			value1 = "rebuild-legacy-database -Ddata.archive.type=data-archive-form-entry -Dportal.version=7.0.4");

		AntCommand(
			locator1 = "build-test.xml",
			value1 = "upgrade-legacy-database");

		Pause(locator1 = "120000");

		Portlet.startServer(
			deleteLiferayHome = "true",
			keepCachedAppServerData = "false");

		User.firstLoginPG();

		ServerAdministration.openServerAdmin();

		ServerAdministration.executeScript(
			language = "Groovy",
			script = "${printSchemaVersionScript}");

		AssertTextEquals.assertPartialText(
			locator1 = "ServerAdministrationScript#OUTPUT_FIELD",
			value1 = "Schema Version is ${originalSchemaVersion}");
	}

}
@component-name = "portal-user-management-and-portal-configuration"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Roles";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.firstLoginPG();

			Role.tearDownCP();
		}
	}

	@description = "This is a use case for LPS-132482."
	@priority = "5"
	test AssertSelectAllCheckboxDisplayPorperlyUnderDefinePermissionsTab {
		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Roles");

		RoleNavigator.gotoDefineRolePermissions(
			permissionNavigationKey = "CONTROL_PANEL_USERS_USERS_AND_ORGANIZATIONS",
			portlet = "Users and Organizations",
			roleTitle = "Guest");

		AssertElementPresent.assertVisible(locator1 = "RolesPermissions#CONTROL_PANEL_CUSTOM_OBJECTS_PERMISSIONS_APPLICATION_PERMISSION_ACTION_CHECKBOX");
	}

	@priority = "3"
	test CanAddRoleWithSameNameDifferentKey {
		Role.openRolesAdmin();

		Role.add(roleTitle = "Role Name");

		Role.openRolesAdmin();

		LexiconEntry.gotoAdd();

		PortletEntry.inputTitle(title = "Role Name");

		Type(
			locator1 = "TextInput#KEY",
			value1 = "Role Name-2");

		AssertClick(
			locator1 = "Button#SUBMIT",
			value1 = "Save");

		Alert.viewSuccessMessage();

		Role.openRolesAdmin();

		Search.searchCP(searchTerm = "Name-2");

		LexiconEntry.gotoEntry(rowEntry = "Role Name");

		AssertTextEquals(
			locator1 = "TextInput#KEY",
			value1 = "Role Name-2");
	}

	@priority = "4"
	test CanEditTheRoleTitle {
		JSONRole.addRegularRole(
			roleKey = "Update Role Title",
			roleTitle = "Update Role Title");

		Role.openRolesAdmin();

		RoleNavigator.gotoRoleType();

		LexiconEntry.gotoEntry(rowEntry = "Update Role Title");

		Role.editRole(
			roleDescriptionEdit = "This is an updated role description.",
			roleKeyEdit = "This is an updated Role Key",
			roleTitleEdit = "This is an updated Role Title");

		Role.openRolesAdmin();

		RoleNavigator.gotoRoleType();

		Role.viewCP(
			roleDescription = "This is an updated role description.",
			roleKey = "This is an updated Role Key",
			roleTitle = "This is an updated Role Title");
	}

	@priority = "4"
	test CannotAddRoleIfSimilarKeyExists {
		Role.openRolesAdmin();

		Role.add(roleTitle = "Role Name");

		Role.openRolesAdmin();

		LexiconEntry.gotoAdd();

		PortletEntry.inputTitle(title = "Role Name2");

		Type(
			locator1 = "TextInput#KEY",
			value1 = "Role Name");

		AssertClick(
			locator1 = "Button#SUBMIT",
			value1 = "Save");

		Alert.viewRequestFailedToComplete();

		Alert.viewDuplicateErrorMessage();

		Role.openRolesAdmin();

		Search.searchCP(searchTerm = "Name2");

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No roles were found.");
	}

	@priority = "4"
	test CanRemoveSiteInScope {
		property test.name.skip.portal.instance = "RolesAdmin#CanRemoveSiteInScope";

		JSONRole.addRegularRole(
			roleKey = "Remove Permission Scope",
			roleTitle = "Remove Permission Scope");

		Role.openRolesAdmin();

		RoleNavigator.gotoRoleType();

		LexiconEntry.gotoEntry(rowEntry = "Remove Permission Scope");

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		// The define permission via json macro is not able to change the permission scope

		Role.editPermissionCmdCPUI(
			define = "true",
			permissionNavHeader = "Content & Data",
			permissionNavItem = "Documents and Media",
			permissionSheetSubtitle = "Documents",
			permissionValues = "View",
			siteNameScope = "${siteName}");

		Role.viewPermissionCP(
			permissionDefinitionValue = "View",
			permissionNavigationValue = "Documents and Media > Document",
			siteNameScope = "${siteName}");

		Role.openRolesAdmin();

		RoleNavigator.gotoRoleType();

		LexiconEntry.gotoEntry(rowEntry = "Remove Permission Scope");

		// The define permission via json macro is not able to change the permission scope

		Role.editPermissionCmdCPUI(
			permissionNavHeader = "Content & Data",
			permissionNavItem = "Documents and Media",
			permissionSheetSubtitle = "Documents",
			permissionValues = "View",
			removeSiteScope = "${siteName}");

		Role.viewPermissionCP(
			permissionDefinitionValue = "View",
			permissionNavigationValue = "Documents and Media > Document",
			siteNameScope = "All Sites and Asset Libraries");
	}

	@description = "This test asserts that user can search role with different title and key."
	@priority = "3"
	test CanSearchRoleWithDifferentTitleAndKey {
		task ("Given a new role with different title and key is created") {
			Role.openRolesAdmin();

			Role.add(
				roleKey = "Role2 Key",
				roleTitle = "Role1 Name");
		}

		task ("When searching for role title") {
			Role.openRolesAdmin();

			Search.searchCP(searchTerm = "Role1 Name");
		}

		task ("Then only one result is displayed and the result is the newly created role") {
			AssertTextEquals(
				locator1 = "Search#SEARCH_INFO",
				value1 = "1 Result for \"Role1 Name\"");

			LexiconEntry.viewEntryName(rowEntry = "Role1 Name");
		}

		task ("When clearing search results and searching for role key") {
			Search.clearSearchResult();

			Search.searchCP(searchTerm = "Role2 Key");
		}

		task ("Then only one result is displayed and the result is the newly created role") {
			AssertTextEquals(
				locator1 = "Search#SEARCH_INFO",
				value1 = "1 Result for \"Role2 Key\"");

			LexiconEntry.viewEntryName(rowEntry = "Role1 Name");
		}
	}

	@description = "This is a use case for LPS-116055."
	@priority = "5"
	test ChangeScope {
		property test.name.skip.portal.instance = "RolesAdmin#ChangeScope";

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		JSONRole.addRegularRole(
			roleKey = "Permission Scope",
			roleTitle = "Permission Scope");

		Role.openRolesAdmin();

		RoleNavigator.gotoRoleType();

		LexiconEntry.gotoEntry(rowEntry = "Permission Scope");

		Role.editPermissionCmdCPUI(
			define = "true",
			permissionNavHeader = "Applications",
			permissionNavItem = "Documents and Media",
			permissionSheetSubtitle = "Document",
			permissionValues = "View",
			siteNameScope = "${siteName}");

		Role.viewPermissionCP(
			permissionDefinitionValue = "View",
			permissionNavigationValue = "Documents and Media > Document",
			siteNameScope = "${siteName}");

		AssertConsoleTextNotPresent(value1 = "java.lang.NumberFormatException");
	}

	@description = "This test automates LPS-141903 and asserts that a deactivated user is not counted in Site Roles"
	@priority = "3"
	test DeactivatedUserIsNotCountedInSiteRoles {
		task ("Given two users are assigned to corresponding sites as a Site Content Reviewer") {
			for (var userNum : list "1,2") {
				JSONUser.addUser(
					userEmailAddress = "user${userNum}@liferay.com",
					userFirstName = "user${userNum}",
					userLastName = "user${userNum}",
					userScreenName = "user${userNum}");

				JSONGroup.addGroup(groupName = "Site${userNum}");

				JSONGroup.assignUserToGroup(
					groupName = "Site${userNum}",
					userEmailAddress = "user${userNum}@liferay.com");

				JSONGroup.assignGroupRoleToUser(
					groupName = "Site${userNum}",
					roleTitle = "Site Content Reviewer",
					userEmailAddress = "user${userNum}@liferay.com");
			}

			Role.openRolesAdmin();

			Navigator.gotoNavTab(navTab = "Site Roles");

			LexiconList.viewListEntrySubtitle(
				listEntry = "Site Content Reviewer",
				listEntrySubtitle = "2 Assignees");
		}

		task ("When the first user is deactivated") {
			User.openUsersAdmin();

			User.singleDeactivateCP(userFirstName = "user1");
		}

		task ("Then that user should no longer be counted in Site Roles and no longer be listed as a member of the site") {
			Role.openRolesAdmin();

			Navigator.gotoNavTab(navTab = "Site Roles");

			LexiconList.viewListEntrySubtitle(
				listEntry = "Site Content Reviewer",
				listEntrySubtitle = "1 Assignee");

			ProductMenu.gotoPortlet(
				category = "People",
				portlet = "Memberships",
				site = "Site1");

			LexiconEntry.viewNoEntry(rowEntry = "user1 user1");
		}
	}

	@priority = "4"
	test ExistingSegmentCanBeAssignedToARegularRole {
		ProductMenu.gotoSite(site = "Global");

		ProductMenu.gotoPortlet(
			category = "People",
			portlet = "Segments");

		LexiconEntry.gotoAdd();

		Segmentation.addProperties(propertyOption = "Email Address");

		Segmentation.fillTitle(titleField = "RoleSegment Test");

		Segmentation.changeCriterionInput(criterionInputOption = "Contains");

		Segmentation.fillProperty(propertyField = "liferay");

		Button.clickSave();

		Alert.viewSuccessMessage();

		JSONRole.addRegularRole(
			roleKey = "Roles Regrole Name",
			roleTitle = "Roles Regrole Name");

		Role.openRolesAdmin();

		Role.addAssignee(
			assigneeName = "RoleSegment Test",
			assigneeType = "Segments",
			roleTitle = "Roles Regrole Name");

		Role.openRolesAdmin();

		Role.viewAssignee(
			assigneeName = "RoleSegment Test",
			assigneeType = "Segments",
			roleTitle = "Roles Regrole Name");
	}

	@priority = "4"
	test NewSegmentCanBeAssignedToARegularRole {
		JSONRole.addRegularRole(
			roleKey = "Roles Regrole Name",
			roleTitle = "Roles Regrole Name");

		Role.openRolesAdmin();

		LexiconEntry.gotoEntry(rowEntry = "Roles Regrole Name");

		Navigator.gotoNavItem(navItem = "Assignees");

		Navigator.gotoNavItem(navItem = "Segments");

		LexiconEntry.gotoAdd();

		SelectFrame(locator1 = "IFrame#SELECT_ASSIGNEES");

		LexiconEntry.gotoAdd();

		Segmentation.addProperties(propertyOption = "Email Address");

		Segmentation.fillTitle(titleField = "New RoleSegment Test");

		Segmentation.changeCriterionInput(criterionInputOption = "Contains");

		Segmentation.fillProperty(propertyField = "liferay");

		Button.clickSave();

		Role.openRolesAdmin();

		Role.addAssignee(
			assigneeName = "New RoleSegment Test",
			assigneeType = "Segments",
			roleTitle = "Roles Regrole Name");

		Role.openRolesAdmin();

		Role.viewAssignee(
			assigneeName = "New RoleSegment Test",
			assigneeType = "Segments",
			roleTitle = "Roles Regrole Name");
	}

	@description = "This test assigns and unassigns an organization to a regular role via UI."
	@priority = "5"
	test OrganizationCanBeRemovedFromRegularRole {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		JSONOrganization.addOrganization(
			organizationName = "Organization Name",
			organizationSite = "true");

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		JSONOrganization.assignUserToOrganization(
			organizationName = "Organization Name",
			userEmailAddress = "userea@liferay.com");

		JSONRole.addRegularRole(
			roleKey = "Remove Org Assignee",
			roleTitle = "Remove Org Assignee");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "VIEW_CONTROL_PANEL",
			resourceName = "90",
			roleTitle = "Remove Org Assignee");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "VIEW",
			resourceName = "com.liferay.portal.kernel.model.UserGroup",
			roleTitle = "Remove Org Assignee");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "ACCESS_IN_CONTROL_PANEL",
			resourceName = "com_liferay_user_groups_admin_web_portlet_UserGroupsAdminPortlet",
			roleTitle = "Remove Org Assignee");

		Role.openRolesAdmin();

		Role.addAssignee(
			assigneeName = "Organization Name",
			assigneeType = "Organizations",
			roleTitle = "Remove Org Assignee");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		UserGroup.openUserGroupsAdmin();

		AssertElementNotPresent(
			locator1 = "Message#ERROR",
			value1 = "You do not have the roles required to access this portlet.");

		User.logoutAndLoginPG(
			userLoginEmailAddress = "test@liferay.com",
			userLoginFullName = "test test");

		Role.openRolesAdmin();

		Role.removeAssignee(
			assigneeName = "Organization Name",
			assigneeType = "Organizations",
			roleTitle = "Remove Org Assignee");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		UserGroup.openUserGroupsAdmin();

		AssertElementPresent(
			locator1 = "Message#ERROR",
			value1 = "You do not have the roles required to access this portlet.");
	}

	@priority = "5"
	test OrganizationRoleCanBeDeleted {
		property portal.acceptance = "true";
		property testray.component.names = "Training";

		Role.openRolesAdmin();

		RoleNavigator.gotoRoleType(roleType = "Organization");

		Role.add(roleTitle = "Roles Orgrole Name");

		Role.openRolesAdmin();

		Role.viewCP(
			roleTitle = "Roles Orgrole Name",
			roleType = "Organization");

		Role.openRolesAdmin();

		RoleNavigator.gotoRoleType(roleType = "Organization");

		LexiconEntry.delete(rowEntry = "Roles Orgrole Name");

		Role.openRolesAdmin();

		RoleNavigator.gotoRoleType(roleType = "Organization");

		Role.viewDefaultCP(roleTitle = "Roles Orgrole Name");
	}

	@priority = "5"
	test RegularRoleCanBeDeleted {
		property app.server.types = "jboss,tcserver,tomcat,weblogic,websphere,wildfly";
		property database.types = "db2,hypersonic,mariadb,mysql,oracle,postgresql,sqlserver,sybase";
		property environment.acceptance = "true";
		property portal.acceptance = "true";

		Role.openRolesAdmin();

		Role.add(roleTitle = "Roles Regrole Name");

		Role.openRolesAdmin();

		Role.viewCP(
			roleTitle = "Roles Regrole Name",
			roleType = "Regular");

		Role.openRolesAdmin();

		LexiconEntry.delete(rowEntry = "Roles Regrole Name");

		Role.openRolesAdmin();

		Role.viewDefaultCP(roleTitle = "Roles Regrole Name");
	}

	@priority = "4"
	test RolesCanBeOrderedByTitle {
		Role.openRolesAdmin();

		Pagination.changePagination(itemsPerPage = "4");

		AssertElementPresent(
			key_itemName = "Administrator",
			locator1 = "ListGroupItem#ITEM_NAME_LINK",
			value1 = "Administrator");

		AssertElementNotPresent(
			key_itemName = "User",
			locator1 = "ListGroupItem#ITEM_NAME_LINK",
			value1 = "User");

		ManagementBar.setFilterAndOrder(orderBy = "Title");

		Click(locator1 = "ManagementBar#ORDER_ICON_GENERIC");

		AssertElementPresent(
			key_itemName = "User",
			locator1 = "ListGroupItem#ITEM_NAME_LINK",
			value1 = "User");

		AssertElementNotPresent(
			key_itemName = "Administrator",
			locator1 = "ListGroupItem#ITEM_NAME_LINK",
			value1 = "Administrator");
	}

	@priority = "3"
	test RolesCanBeViewedInTableView {
		Role.openRolesAdmin();

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertTextEquals(
			key_tableEntry = "Administrator",
			locator1 = "LexiconTable#TABLE_ENTRY_TITLE_LINK",
			value1 = "Administrator");

		AssertTextEquals(
			key_columnName = "description",
			key_tableEntry = "Administrators",
			locator1 = "LexiconTable#TABLE_ENTRY_COLUMN_LINK",
			value1 = "Administrators are super users who can do anything.");

		AssertTextEquals(
			key_columnName = "assignees",
			key_tableEntry = "Administrators",
			locator1 = "LexiconTable#TABLE_ENTRY_COLUMN_LINK",
			value1 = "1");
	}

	@priority = "4"
	test SegmentCanBeRemovedFromRegularRole {
		ProductMenu.gotoSite(site = "Global");

		ProductMenu.gotoPortlet(
			category = "People",
			portlet = "Segments");

		LexiconEntry.gotoAdd();

		Segmentation.addProperties(propertyOption = "Email Address");

		Segmentation.fillTitle(titleField = "Role Assignee Test");

		Segmentation.changeCriterionInput(criterionInputOption = "Contains");

		Segmentation.fillProperty(propertyField = "liferay");

		Button.clickSave();

		Alert.viewSuccessMessage();

		JSONRole.addRegularRole(
			roleKey = "Remove Regrole Assignee",
			roleTitle = "Remove Regrole Assignee");

		Role.openRolesAdmin();

		Role.addAssignee(
			assigneeName = "Role Assignee Test",
			assigneeType = "Segments",
			roleTitle = "Remove Regrole Assignee");

		Role.openRolesAdmin();

		Role.removeAssignee(
			assigneeName = "Role Assignee Test",
			assigneeType = "Segments",
			roleTitle = "Remove Regrole Assignee");
	}

	@description = "This test assigns and unassigns a site to a regular role via UI."
	@priority = "5"
	test SiteCanBeRemovedFromRegularRole {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		JSONGroup.addGroup(groupName = "Site Name");

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		User.openUsersAdmin();

		User.editUserSiteCP(
			siteName = "Site Name",
			userScreenName = "usersn");

		JSONRole.addRegularRole(
			roleKey = "Remove Site Assignee",
			roleTitle = "Remove Site Assignee");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "VIEW_CONTROL_PANEL",
			resourceName = "90",
			roleTitle = "Remove Site Assignee");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "VIEW",
			resourceName = "com.liferay.portal.kernel.model.UserGroup",
			roleTitle = "Remove Site Assignee");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "ACCESS_IN_CONTROL_PANEL",
			resourceName = "com_liferay_user_groups_admin_web_portlet_UserGroupsAdminPortlet",
			roleTitle = "Remove Site Assignee");

		Role.openRolesAdmin();

		Role.addAssignee(
			assigneeName = "Site Name",
			assigneeType = "Sites",
			roleTitle = "Remove Site Assignee");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		UserGroup.openUserGroupsAdmin();

		AssertElementNotPresent(
			locator1 = "Message#ERROR",
			value1 = "You do not have the roles required to access this portlet.");

		User.logoutAndLoginPG(
			userLoginEmailAddress = "test@liferay.com",
			userLoginFullName = "test test");

		Role.openRolesAdmin();

		Role.removeAssignee(
			assigneeName = "Site Name",
			assigneeType = "Sites",
			roleTitle = "Remove Site Assignee");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		UserGroup.openUserGroupsAdmin();

		AssertElementPresent(
			locator1 = "Message#ERROR",
			value1 = "You do not have the roles required to access this portlet.");
	}

	@priority = "5"
	test SiteRoleCanBeDeleted {
		property portal.acceptance = "true";

		Role.openRolesAdmin();

		RoleNavigator.gotoRoleType(roleType = "Site");

		Role.add(roleTitle = "Roles Siterole Name");

		Role.openRolesAdmin();

		Role.viewCP(
			roleTitle = "Roles Siterole Name",
			roleType = "Site");

		Role.openRolesAdmin();

		RoleNavigator.gotoRoleType(roleType = "Site");

		LexiconEntry.delete(rowEntry = "Roles Siterole Name");

		Role.openRolesAdmin();

		RoleNavigator.gotoRoleType(roleType = "Site");

		Role.viewDefaultCP(roleTitle = "Roles Siterole Name");
	}

	@description = "This is a use case for LPS-105001 and LPS-104999."
	@priority = "3"
	test UserCanAddARoleWithoutATitle {
		Role.openRolesAdmin();

		Role.viewCP(roleKey = "Administrator");

		Role.openRolesAdmin();

		Role.add(roleKey = "Role Key Value");

		Role.openRolesAdmin();

		Role.viewCP(roleKey = "Role Key Value");
	}

	@description = "This is a use case for LPS-109572."
	@priority = "5"
	test UserCanBeAssignedToARegularRole {
		property portal.acceptance = "true";

		JSONRole.addRegularRole(
			roleKey = "Regular Role",
			roleTitle = "Regular Role");

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		Role.openRolesAdmin();

		JSONRole.assignRoleToUser(
			roleTitle = "Regular Role",
			userEmailAddress = "userea@liferay.com");

		Role.openRolesAdmin();

		Role.viewAssignee(
			assigneeName = "userfn userln",
			assigneeType = "Users",
			roleTitle = "Regular Role");
	}

	@description = "This test assigns and unassigns a user to a regular role via UI."
	@priority = "5"
	test UserCanBeRemovedFromRegularRole {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		JSONRole.addRegularRole(
			roleKey = "Remove User Assignee",
			roleTitle = "Remove User Assignee");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "VIEW_CONTROL_PANEL",
			resourceName = "90",
			roleTitle = "Remove User Assignee");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "VIEW",
			resourceName = "com.liferay.portal.kernel.model.UserGroup",
			roleTitle = "Remove User Assignee");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "ACCESS_IN_CONTROL_PANEL",
			resourceName = "com_liferay_user_groups_admin_web_portlet_UserGroupsAdminPortlet",
			roleTitle = "Remove User Assignee");

		Role.openRolesAdmin();

		Role.addAssignee(
			assigneeName = "userfn userln",
			assigneeType = "Users",
			roleTitle = "Remove User Assignee");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		UserGroup.openUserGroupsAdmin();

		AssertElementNotPresent(
			locator1 = "Message#ERROR",
			value1 = "You do not have the roles required to access this portlet.");

		User.logoutAndLoginPG(
			userLoginEmailAddress = "test@liferay.com",
			userLoginFullName = "test test");

		Role.openRolesAdmin();

		Role.removeAssignee(
			assigneeName = "userfn userln",
			assigneeType = "Users",
			roleTitle = "Remove User Assignee");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		UserGroup.openUserGroupsAdmin();

		AssertElementPresent(
			locator1 = "Message#ERROR",
			value1 = "You do not have the roles required to access this portlet.");
	}

	@description = "This test asserts that Organization Role table is empty and select button does not display if user is not a member of an organization."
	@priority = "3"
	test UserCanNotSelectOrgRoleIfUserIsNotAnOrgMember {
		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONOrganization.addOrganization(organizationName = "Organization Name");

		User.openUsersAdmin();

		UserNavigator.gotoUser(userScreenName = "usersn");

		UserNavigator.gotoRoles();

		User.viewUnableToAssignRoleToUserCP(roleType = "Organization");

		AssertElementNotPresent(
			key_roleType = StringUtil.replace("Organization Role", " ", ""),
			locator1 = "UsersAndOrganizationsEditUser#ROLES_SELECT_LINK",
			value1 = "Select");
	}

	@description = "This test asserts that Site Role table is empty and select button does not display if user is not a member of a site."
	@priority = "3"
	test UserCanNotSelectSiteRoleIfUserIsNotASiteMember {
		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONGroup.addGroup(groupName = "Site Name");

		User.openUsersAdmin();

		UserNavigator.gotoUser(userScreenName = "usersn");

		UserNavigator.gotoRoles();

		User.viewUnableToAssignRoleToUserCP(roleType = "Site");

		AssertElementNotPresent(
			key_roleType = StringUtil.replace("Site Role", " ", ""),
			locator1 = "UsersAndOrganizationsEditUser#ROLES_SELECT_LINK",
			value1 = "Select");
	}

	@description = "This test assigns and unassigns a user group to a regular role via UI."
	@priority = "5"
	test UserGroupCanBeRemovedFromRegularRole {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		JSONUserGroup.addUserGroup(userGroupName = "UG UserGroup Name");

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		JSONUser.addUserToUserGroup(
			userEmailAddress = "userea@liferay.com",
			userGroupName = "UG UserGroup Name");

		JSONRole.addRegularRole(
			roleKey = "Remove User Group Assignee",
			roleTitle = "Remove User Group Assignee");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "VIEW_CONTROL_PANEL",
			resourceName = "90",
			roleTitle = "Remove User Group Assignee");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "VIEW",
			resourceName = "com.liferay.portal.kernel.model.UserGroup",
			roleTitle = "Remove User Group Assignee");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "ACCESS_IN_CONTROL_PANEL",
			resourceName = "com_liferay_user_groups_admin_web_portlet_UserGroupsAdminPortlet",
			roleTitle = "Remove User Group Assignee");

		Role.openRolesAdmin();

		Role.addAssignee(
			assigneeName = "UG UserGroup Name",
			assigneeType = "User Groups",
			roleTitle = "Remove User Group Assignee");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		UserGroup.openUserGroupsAdmin();

		AssertElementNotPresent(
			locator1 = "Message#ERROR",
			value1 = "You do not have the roles required to access this portlet.");

		User.logoutAndLoginPG(
			userLoginEmailAddress = "test@liferay.com",
			userLoginFullName = "test test");

		Role.openRolesAdmin();

		Role.removeAssignee(
			assigneeName = "UG UserGroup Name",
			assigneeType = "User Groups",
			roleTitle = "Remove User Group Assignee");

		User.logoutPG();

		User.loginUserPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		UserGroup.openUserGroupsAdmin();

		AssertElementPresent(
			locator1 = "Message#ERROR",
			value1 = "You do not have the roles required to access this portlet.");
	}

	@description = "This is a use case for LPS-97321. All subdirectories in Define Permissions should display."
	@priority = "4"
	test ViewDefinePermissionsSubdirectoriesDisplay {
		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		JSONRole.addRegularRole(
			roleKey = "User Permission",
			roleTitle = "User Permission");

		Role.openRolesAdmin();

		Role.viewCP(roleTitle = "User Permission");

		Navigator.gotoNavItem(navItem = "Define Permissions");

		Role.viewPermissionSubdirectory(
			navItem = "Control Panel",
			subItemList = "General Permissions,Users,Sites,Configuration,Security,System,Marketplace,Accounts");

		Role.viewPermissionSubdirectory(
			navItem = "Site and Asset Library Administration",
			subItemList = "Site Builder,Content & Data,Categorization,Recycle Bin,People,Configuration,Publishing,Applications");

		Role.viewPermissionSubdirectory(
			navItem = "User",
			subItemList = "Notifications,My Submissions,My Workflow Tasks,Account Settings,My Organizations");

		Role.viewPermissionSubdirectory(
			navItem = "Applications Menu",
			subItemList = "Content,Workflow,Communication");
	}

}
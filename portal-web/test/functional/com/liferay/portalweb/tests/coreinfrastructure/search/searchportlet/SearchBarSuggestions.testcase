@component-name = "portal-search"
definition {

	property custom.properties = "feature.flag.LPS-152597=true";
	property portal.release = "true";
	property portal.suite.search.engine = "elasticsearch7";
	property portal.upstream = "true";
	property testray.main.component.name = "Web Search";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			BlogsEntry.tearDownCP();

			MessageboardsThread.tearDownCP();

			JSONUser.tearDownNonAdminUsers();

			WebContent.tearDownCP();

			DMDocument.tearDownCP();

			WikiPage.tearDownCP();

			Vocabulary.tearDownCP();

			Tag.tearDownCP(site = "Guest");

			PagesAdmin.tearDownCP();
		}
	}

	@priority = "4"
	test CanViewSearchBarSuggestionsResultDetails {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			description = "WC WebContent Description",
			groupName = "Guest",
			title = "Apple");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Apple Pie",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "Apple");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "WC WebContent Description",
			searchAssetTitle = "Apple");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "DM Document Description",
			searchAssetTitle = "Apple Pie");

		SearchBarSuggestions.showMoreSuggestions();

		SearchResultPortlet.viewSearchResults(
			searchAssetTitle = "Apple",
			searchAssetType = "Web Content Article");

		SearchResultPortlet.viewSearchResults(
			searchAssetTitle = "Apple Pie",
			searchAssetType = "Document");

		Type(
			locator1 = "Search#SEARCH_BAR_WIDGET",
			value1 = "Apple");

		SearchBarSuggestions.gotoSuggestionResultDetails(searchAssetTitle = "Apple Pie");

		SearchBarSuggestions.viewSuggestionResultDetails(searchAssetTitle = "Apple Pie");
	}

	@description = "This is a use case for LPS-155317"
	@priority = "4"
	test DisablingSuggestionsSAPHidesSuggestionsFromGuests {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			description = "WC WebContent Description",
			groupName = "Guest",
			title = "Apple");

		ApplicationsMenu.gotoPortlet(
			category = "Security",
			panel = "Control Panel",
			portlet = "Service Access Policy");

		ServiceAccessPolicy.editServiceAccessPolicy(
			enabled = "false",
			policyName = "SEARCH_SUGGESTIONS");

		Navigator.openURL();

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "App");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "WC WebContent Description",
			searchAssetTitle = "Apple");

		SignOut.signOut();

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "App");

		SearchBarSuggestions.viewNoResultAssetTitle(searchAssetTitle = "Apple");

		SignIn.signIn(userEmailAddress = "test@liferay.com");
	}

	@priority = "4"
	test DisplaySuggestionsAfterCharacterThresholdIsMet {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			description = "WC WebContent Description",
			groupName = "Guest",
			title = "Apple");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Apple",
			groupName = "Guest",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		SearchPortlets.configureEmbeddedSearchBar(characterThresholdForDisplayingSuggestions = "3");

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "App");

		SearchBarSuggestions.viewNoResultAssetTitle(searchAssetTitle = "Apple");

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "Appl");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "DM Document Description",
			searchAssetTitle = "Apple");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "WC WebContent Description",
			searchAssetTitle = "Apple");
	}

	@priority = "5"
	test ShowBasicSuggestionsAcrossDifferentScopes {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			description = "WC WebContent Description",
			groupName = "Guest",
			title = "Apple");

		JSONGroup.addGroup(groupName = "Site Name");

		JSONDocument.addFileWithUploadedFile(
			dmDocumentDescription = "DM Document Description",
			dmDocumentTitle = "Apple Pie",
			groupName = "Site Name",
			mimeType = "application/msword",
			sourceFileName = "Document_1.doc");

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "Apple");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "WC WebContent Description",
			searchAssetTitle = "Apple");

		SearchBarSuggestions.viewNoResultAssetTitle(searchAssetTitle = "Apple Pie");

		Refresh();

		SearchPortlets.configureEmbeddedSearchBar(searchScope = "Everything");

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "Apple");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "WC WebContent Description",
			searchAssetTitle = "Apple");

		SearchBarSuggestions.viewSuggestions(
			searchAssetDescription = "DM Document Description",
			searchAssetTitle = "Apple Pie");
	}

}
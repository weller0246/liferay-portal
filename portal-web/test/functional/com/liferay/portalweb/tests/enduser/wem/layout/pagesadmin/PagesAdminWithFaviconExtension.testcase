@component-name = "portal-wcm"
definition {

	property custom.properties = "feature.flag.LPS-153457=true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Site Pages Administration";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Add two remote apps for favicon extension") {
			JSONRemoteApp.addRemoteAppEntry(
				name = "Google Favicon",
				type = "themeFavicon",
				url = "https://www.google.com/favicon.ico");

			JSONRemoteApp.addRemoteAppEntry(
				name = "NBA Favicon",
				type = "themeFavicon",
				url = "https://www.nba.com/favicon.ico");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");

			RemoteApps.tearDown();
		}
	}

	@description = "This is a test for LPS-153654. The site designer could change favicon from favicon extension for page, page template, and group of pages."
	@priority = "5"
	test ChangeFavicons {
		task ("Given a site designer has a widget page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Widget Page Name");
		}

		task ("When the site designer accesses to the widget page") {
			Navigator.gotoSitePage(
				pageName = "Widget Page Name",
				siteName = "Test Site name");
		}

		task ("Then the site designer could view the favicon from theme") {
			AssertElementPresent(
				imageFileName = "classic-theme/images/favicon.ico",
				locator1 = "Icon#FAVICON");
		}

		task ("When the site designer add a favicon extension to widget page") {
			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			PagesAdmin.gotoPageEllipsisMenuItem(
				menuItem = "Configure",
				pageName = "Widget Page Name");

			Navigator.gotoNavTab(navTab = "Look and Feel");

			AssertTextEquals(
				faviconName = "Favicon from Theme",
				locator1 = "PagesAdmin#FAVICON_NAME",
				value1 = "Favicon from Theme");

			PagesAdmin.selectFaviconExtension(extensionName = "Google Favicon");

			AssertTextEquals(
				faviconName = "Google Favicon",
				locator1 = "PagesAdmin#FAVICON_NAME",
				value1 = "Google Favicon");

			PortletEntry.save();
		}

		task ("Then the site designer could view the new favicon on widget page") {
			Navigator.gotoSitePage(
				pageName = "Widget Page Name",
				siteName = "Test Site name");

			AssertElementPresent(
				imageFileName = "https://www.google.com/favicon.ico",
				locator1 = "Icon#FAVICON");
		}

		task ("Given a site designer has a content page template") {
			JSONLayoutpagetemplate.addLayoutPageTemplateCollection(
				groupName = "Test Site Name",
				layoutPageTemplateCollectionName = "Page Template Collection");

			PageTemplatesAdmin.openPageTemplatesAdmin(siteURLKey = "test-site-name");

			Navigator.gotoNavNested(navNested = "Page Template Collection");

			PageTemplatesAdmin.addPageTemplate(
				pageTemplateName = "Page Template Name",
				pageTemplateType = "Content Page Template");

			PageEditor.assertFragmentSidebarLoaded();

			AssertElementPresent(
				imageFileName = "classic-theme/images/favicon.ico",
				locator1 = "Icon#FAVICON");
		}

		task ("When the site designer changes favicon of content page template") {
			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			AssertTextEquals(
				faviconName = "Favicon from Theme",
				locator1 = "PagesAdmin#FAVICON_NAME",
				value1 = "Favicon from Theme");

			PagesAdmin.selectFaviconExtension(extensionName = "Google Favicon");

			AssertTextEquals(
				faviconName = "Google Favicon",
				locator1 = "PagesAdmin#FAVICON_NAME",
				value1 = "Google Favicon");

			PortletEntry.save();
		}

		task ("Then the site designer could view the new favicon on content page template") {
			Navigator.gotoBack();

			PageEditor.assertFragmentSidebarLoaded();

			AssertElementPresent(
				imageFileName = "https://www.google.com/favicon.ico",
				locator1 = "Icon#FAVICON");
		}

		task ("When the site designer changes favicon of pages") {
			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			WaitForPageLoad();

			Click(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS");

			DropdownMenuItem.click(menuItem = "Configuration");

			AssertTextEquals(
				faviconName = "Favicon from Theme",
				locator1 = "PagesAdmin#FAVICON_NAME",
				value1 = "Favicon from Theme");

			PagesAdmin.selectFaviconExtension(extensionName = "NBA Favicon");

			AssertTextEquals(
				faviconName = "NBA Favicon",
				locator1 = "PagesAdmin#FAVICON_NAME",
				value1 = "NBA Favicon");

			PortletEntry.save();
		}

		task ("Then the site designer could view the new favicon on new page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Content Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Test Site Name");

			AssertElementPresent(
				imageFileName = "https://www.nba.com/favicon.ico",
				locator1 = "Icon#FAVICON");

			Navigator.gotoSitePage(
				pageName = "Widget Page Name",
				siteName = "Test Site name");

			AssertElementPresent(
				imageFileName = "https://www.google.com/favicon.ico",
				locator1 = "Icon#FAVICON");
		}
	}

	@description = "This is a test for LPS-153654. The site designer could clear the favicon extenion selection."
	@priority = "4"
	test ClearFaviconExtensionSelection {
		task ("Given a site designer has a content page with a favicon extension") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();

			PagesAdmin.selectFaviconExtension(extensionName = "Google Favicon");

			PortletEntry.save();
		}

		task ("When the site designer accesses to the Look and Feel settings") {
			Navigator.gotoBack();

			PageEditor.assertFragmentSidebarLoaded();

			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();
		}

		task ("Then the site designer could clear the favicon extension selection") {
			AssertTextEquals(
				faviconName = "Google Favicon",
				locator1 = "PagesAdmin#FAVICON_NAME",
				value1 = "Google Favicon");

			Click(
				buttonId = "clearFaviconButton",
				locator1 = "Button#BUTTON_ID");

			AssertTextEquals(
				faviconName = "Favicon from Theme",
				locator1 = "PagesAdmin#FAVICON_NAME",
				value1 = "Favicon from Theme");

			PortletEntry.save();

			Navigator.gotoBack();

			PageEditor.assertFragmentSidebarLoaded();

			AssertElementPresent(
				imageFileName = "classic-theme/images/favicon.ico",
				locator1 = "Icon#FAVICON");
		}
	}

	@description = "This is a test for LPS-153654. The site designer could apply a different favicon extension to an inheritor page from master page."
	@priority = "4"
	test OverwriteInheritedFavionExtensionFromMasterPageTemplate {
		task ("Given a site designer has a content page with inherited favicon extension from master page template") {
			JSONLayoutpagetemplate.addMasterPageTemplateEntry(
				groupName = "Test Site Name",
				masterPageTemplateEntryName = "Master Page Template");

			MastersPageTemplatesAdmin.openMastersAdmin(siteURLKey = "test-site-name");

			MastersPageTemplatesAdmin.gotoMastersEllipsisItem(
				item = "Edit",
				masterLayoutName = "Master Page Template");

			PageEditor.assertFragmentSidebarLoaded();

			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();

			PagesAdmin.selectFaviconExtension(extensionName = "Google Favicon");

			PortletEntry.save();

			Navigator.gotoBack();

			PageEditor.assertFragmentSidebarLoaded();

			Button.click(button = "Publish Master");

			Alert.viewSuccessMessage();

			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(
				masterLayoutName = "Master Page Template",
				pageName = "Test Page Name");
		}

		task ("When the site designer accesses to the Look and Feel settings of content page") {
			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();

			AssertTextEquals(
				faviconName = "Favicon from Master",
				locator1 = "PagesAdmin#FAVICON_NAME",
				value1 = "Favicon from Master");
		}

		task ("Then the site designer could apply a new favicon extension") {
			PagesAdmin.selectFaviconExtension(extensionName = "NBA Favicon");

			AssertTextEquals(
				faviconName = "NBA Favicon",
				locator1 = "PagesAdmin#FAVICON_NAME",
				value1 = "NBA Favicon");

			PortletEntry.save();

			Navigator.gotoBack();

			PageEditor.assertFragmentSidebarLoaded();

			AssertElementPresent(
				imageFileName = "https://www.nba.com/favicon.ico",
				locator1 = "Icon#FAVICON");
		}
	}

	@description = "This is a test for LPS-153654. The site designer could apply a different favicon extension to an inheritor page from page template."
	@priority = "4"
	test OverwriteInheritedFavionExtensionFromPageTemplate {
		task ("Given a site designer has a content page with inherited favicon extension from content page template") {
			JSONLayoutpagetemplate.addLayoutPageTemplateCollection(
				groupName = "Test Site Name",
				layoutPageTemplateCollectionName = "Page Template Collection");

			PageTemplatesAdmin.openPageTemplatesAdmin(siteURLKey = "test-site-name");

			Navigator.gotoNavNested(navNested = "Page Template Collection");

			PageTemplatesAdmin.addPageTemplate(
				pageTemplateName = "Page Template Name",
				pageTemplateType = "Content Page Template");

			PageEditor.assertFragmentSidebarLoaded();

			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();

			PagesAdmin.selectFaviconExtension(extensionName = "Google Favicon");

			PortletEntry.save();

			Navigator.gotoBack();

			PageEditor.assertFragmentSidebarLoaded();

			PageEditor.publish();

			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(
				collectionName = "Page Template Collection",
				pageName = "Test Page Name",
				pageTemplateName = "Page Template Name");
		}

		task ("When the site designer accesses to the Look and Feel settings of content page") {
			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();
		}

		task ("Then the site designer could apply a new favicon extension") {
			PagesAdmin.selectFaviconExtension(extensionName = "NBA Favicon");

			AssertTextEquals(
				faviconName = "NBA Favicon",
				locator1 = "PagesAdmin#FAVICON_NAME",
				value1 = "NBA Favicon");

			PortletEntry.save();

			Navigator.gotoBack();

			PageEditor.assertFragmentSidebarLoaded();

			AssertElementPresent(
				imageFileName = "https://www.nba.com/favicon.ico",
				locator1 = "Icon#FAVICON");
		}
	}

	@description = "This is a test for LPS-153654. Page should inherit the favicon extension from page template."
	@priority = "4"
	test ViewInheritedFavionExtensionFromPageTemplate {
		task ("Given a site designer has a content page template with a favicon extension") {
			JSONLayoutpagetemplate.addLayoutPageTemplateCollection(
				groupName = "Test Site Name",
				layoutPageTemplateCollectionName = "Page Template Collection");

			PageTemplatesAdmin.openPageTemplatesAdmin(siteURLKey = "test-site-name");

			Navigator.gotoNavNested(navNested = "Page Template Collection");

			PageTemplatesAdmin.addPageTemplate(
				pageTemplateName = "Page Template Name",
				pageTemplateType = "Content Page Template");

			PageEditor.assertFragmentSidebarLoaded();

			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();

			PagesAdmin.selectFaviconExtension(extensionName = "Google Favicon");

			PortletEntry.save();

			Navigator.gotoBack();

			PageEditor.assertFragmentSidebarLoaded();

			PageEditor.publish();
		}

		task ("When the site designer creates a content page based on content page template") {
			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPage(
				collectionName = "Page Template Collection",
				pageName = "Test Page Name",
				pageTemplateName = "Page Template Name");
		}

		task ("Then the site designer see the inherited favicon extension from content page template") {
			AssertElementPresent(
				imageFileName = "https://www.google.com/favicon.ico",
				locator1 = "Icon#FAVICON");

			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			WaitForPageLoad();

			AssertTextEquals(
				faviconName = "Google Favicon",
				locator1 = "PagesAdmin#FAVICON_NAME",
				value1 = "Google Favicon");
		}
	}

}
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Remote Apps";
	property testray.main.component.name = "Remote Apps";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Navigator.gotoPage(pageName = "Home");

			RemoteApps.tearDown();
		}
	}

	@description = "LPS-170678 - Assert if 'Custom Element' can be categorized as 'Client Extension'"
	@priority = "3"
	test CanBeCategorizedInAddWidgetList {
		property app.server.types = "tomcat";
		property database.types = "mysql";
		property environment.acceptance = "true";
		property operating.system.types = "alpine,amazonlinux,centos,debian,fedora,orcllinux,osx,redhat,solaris,suse,ubuntu,windows";
		property portal.acceptance = "true";

		task ("When client extension has portal category 'Client Extensions'") {
			RemoteApps.goToRemoteAppsPortlet();

			RemoteApps.addCustomElementAllFields(
				entryCssurl = "https://liferay.github.io/liferay-frontend-projects/index.css",
				entryHtmlName = "vanilla-counter",
				entryName = "Vanilla Counter",
				entryProperties = "test-data-user=QAuser",
				entryURL = "https://liferay.github.io/liferay-frontend-projects/vanilla-counter/index.js");
		}

		task ("And When on Content Page") {
			Navigator.openURL();

			ContentPagesNavigator.openEditContentPage(
				pageName = "Home",
				siteName = "guest");
		}

		task ("And When access add widget list") {
			Type(
				locator1 = "PageEditor#FRAGMENTS_AND_WIDGETS_SEARCH_FIELD",
				value1 = "Client Extensions");
		}

		task ("Then custom element is available under category 'Client Extensions'") {
			AssertTextEquals(
				key_collectionName = "Client Extensions",
				locator1 = "PageEditor#FRAGMENT_SIDEBAR_COLLECTION_PANEL",
				value1 = "Client Extensions");

			AssertTextEquals(
				key_fragmentName = "Vanilla Counter",
				locator1 = "PageEditor#FRAGMENTS_AND_WIDGETS_SEARCH_RESULTS",
				value1 = "Vanilla Counter");
		}
	}

}
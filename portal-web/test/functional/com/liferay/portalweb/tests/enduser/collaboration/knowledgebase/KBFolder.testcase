@component-name = "portal-knowledge-base"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Knowledge Base";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		Refresh();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			KBFolder.tearDownCP();
		}
	}

	@e2etest
	@priority = "5"
	@refactordone
	test CanAddFolder {
		property custom.properties = "feature.flag.LPS-156421=true";
		property portal.acceptance = "true";

		KBFolder.addCP(
			kbFolderDescription = "KB Folder 1 Description",
			kbFolderName = "KB Folder 1");

		KBFolder.gotoCP(
			kbFolderName = "KB Folder 1",
			pageTreePanel = "true");

		KBArticle.viewDefaultCP(featureFlag = "true");
	}

	@description = "This test ensures that users can add folder via center pane button."
	@e2etest
	@priority = "5"
	test CanAddFolderViaCenterPaneButton {
		property custom.properties = "feature.flag.LPS-156421=true";

		KBArticle.openPageTreePanel(section = "Folders and Articles");

		KBFolder.addCP(
			centerPaneButton = "true",
			kbFolderDescription = "KB Folder Description",
			kbFolderName = "KB Folder");

		KBFolder.viewFolderThroughPageTreePanel(kbFolderName = "KB Folder");

		KBFolder.viewFolderNameCP(kbFolderName = "KB Folder");

		Button.viewNotPresent(button = "New");
	}

	@priority = "4"
	@refactordone
	test CanDeleteFolder {
		property portal.acceptance = "true";

		JSONKnowledgeBase.addkBFolder(
			groupName = "Guest",
			kbFolderDescription = "KB Folder 1 Description",
			kbFolderName = "KB Folder 1");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBFolder.deleteCP(kbFolderName = "KB Folder 1");
	}

	@description = "This test ensures that users can delete a KB folder from the navigation pane."
	@priority = "5"
	test CanDeleteFolderViaNavigationPane {
		property custom.properties = "feature.flag.LPS-156421=true";

		JSONKnowledgeBase.addkBFolder(
			groupName = "Guest",
			kbFolderName = "KB Folder");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.openPageTreePanel(section = "Folders and Articles");

		KBFolder.deleteCP(
			kbFolderName = "KB Folder",
			menuItem = "Delete",
			navigationPane = "true");

		KBArticle.viewDefaultCP(
			defaultHome = "true",
			featureFlag = "true");
	}

	@priority = "4"
	@refactordone
	test CanEditFolder {
		property portal.acceptance = "true";

		JSONKnowledgeBase.addkBFolder(
			groupName = "Guest",
			kbFolderDescription = "KB Folder 1 Description",
			kbFolderName = "KB Folder 1");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBFolder.editCP(
			kbFolderDescription = "KB Folder 1 Description Edited",
			kbFolderEditedName = "KB Folder 1 Edited",
			kbFolderName = "KB Folder 1");

		KBFolder.viewFolderNameCP(kbFolderName = "KB Folder 1 Edited");
	}

	@description = "This test ensures that users can manage permissions for a KB folder from the navigation pane."
	@priority = "5"
	test CanEditFolderPermissionsViaNavigationPane {
		property custom.properties = "feature.flag.LPS-156421=true";

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Knowledge Base Display Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Knowledge Base Display Page",
			widgetName = "Knowledge Base Display");

		JSONKnowledgeBase.addkBFolder(
			groupName = "Guest",
			kbFolderName = "KB Folder");

		JSONKnowledgeBase.addkBArticle(
			folderName = "KB Folder",
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.selectPGViaKBArticle(
			kbArticleTitle = "KB Folder",
			kbDisplay = "true",
			pageName = "Knowledge Base Article Page");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.openPageTreePanel(section = "Folders and Articles");

		KBFolder.gotoMenuItemViaNavigationPane(
			kbFolderName = "KB Folder",
			menuItem = "Permissions");

		PermissionsInline.addOrRemoveAnyPermissionsFromSpecificRole(
			addOrRemoveViewPermission = "Remove",
			permissionsKeyList = "INLINE_PERMISSIONS_VIEW_CHECKBOX",
			roleTitle = "Guest");

		User.logoutPG();

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");
	}

	@description = "This test ensures that users can edit a KB folder from the navigation pane."
	@priority = "5"
	test CanEditFolderViaNavigationPane {
		property custom.properties = "feature.flag.LPS-156421=true";

		JSONKnowledgeBase.addkBFolder(
			groupName = "Guest",
			kbFolderDescription = "KB Folder Description",
			kbFolderName = "KB Folder");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.openPageTreePanel(section = "Folders and Articles");

		KBFolder.editCP(
			kbFolderDescription = "KB Folder Description Edited",
			kbFolderEditedName = "KB Folder Edited",
			kbFolderName = "KB Folder",
			navigationPane = "true");

		KBFolder.viewFolderNameCP(kbFolderName = "KB Folder Edited");
	}

	@description = "This test ensures that users can manage permissions for the KB home folder from the navigation pane."
	@priority = "5"
	test CanEditHomeFolderPermissionsViaNavigationPane {
		property custom.properties = "feature.flag.LPS-156421=true";

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.openPageTreePanel(section = "Folders and Articles");

		KBFolder.gotoMenuItemViaNavigationPane(
			kbFolderName = "Home",
			menuItem = "Permissions");

		PermissionsInline.addOrRemoveAnyPermissionsFromSpecificRole(
			addOrRemoveViewPermission = "Add",
			permissionsKeyList = "INLINE_PERMISSIONS_PERMISSIONS_CHECKBOX",
			roleTitle = "Site Member");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		LexiconEntry.gotoHeaderMenuItem(menuItem = "Home Folder Permissions");

		IFrame.selectPermissionsFrame();

		PermissionsInline.viewPermissionsChecked(
			permissionDefinitionKey = "INLINE_PERMISSIONS_PERMISSIONS_CHECKBOX",
			roleTitle = "site member");
	}

	@description = "This test ensures that users can move a KB folder from the navigation pane."
	@priority = "5"
	test CanMoveFolderViaNavigationPane {
		property custom.properties = "feature.flag.LPS-156421=true";

		JSONKnowledgeBase.addkBFolder(
			groupName = "Guest",
			kbFolderName = "KB Folder 1");

		JSONKnowledgeBase.addkBFolder(
			groupName = "Guest",
			kbFolderName = "KB Folder 2");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.openPageTreePanel(section = "Folders and Articles");

		KBFolder.goToMoveCPViaNavigationPane(kbFolderName = "KB Folder 1");

		KBFolder.selectParentCP(kbFolderName = "KB Folder 2");

		KBFolder.gotoCP(
			highlight = "true",
			kbFolderName = "KB Folder 2");

		KBFolder.viewFolderNameCP(kbFolderName = "KB Folder 1");
	}

	@priority = "3"
	@refactordone
	test CannotAddDuplicateFolder {
		JSONKnowledgeBase.addkBFolder(
			groupName = "Guest",
			kbFolderDescription = "KB Folder 1 Description",
			kbFolderName = "KB Folder 1");

		KBFolder.addCP(
			kbAddDuplicate = "true",
			kbFolderDescription = "KB Folder 1 Description",
			kbFolderName = "KB Folder 1");
	}

	@priority = "3"
	@refactorneeded
	test EditKBFolderWithCustomField {
		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Custom Fields");

		CustomFields.addCP(
			customFieldName = "Date",
			customFieldType = "Date",
			resourceName = "Knowledge Base Folder");

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Knowledge Base Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Knowledge Base Page",
			widgetName = "Knowledge Base Display");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBFolder.addCP(
			customField = "01/01/2017",
			kbFolderDescription = "KB Folder 2017 Description",
			kbFolderName = "KB Folder 2017 Name");

		KBFolder.addCP(
			customField = "01/01/2010",
			kbFolderDescription = "KB Folder 2010 Description",
			kbFolderName = "KB Folder 2010 Name");

		JSONKnowledgeBase.addkBArticle(
			folderName = "KB Folder 2017 Name",
			groupName = "Guest",
			kbArticleContent = "Knowledge Base 2017 Article Content",
			kbArticleTitle = "Knowledge Base 2017 Article Title");

		JSONKnowledgeBase.addkBArticle(
			folderName = "KB Folder 2010 Name",
			groupName = "Guest",
			kbArticleContent = "Knowledge Base 2010 Article Content",
			kbArticleTitle = "Knowledge Base 2010 Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Page");

		KBFolder.gotoViaKBDisplayPG(kbFolderName = "KB Folder 2017 Name");

		Refresh();

		KBDisplayWidget.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base 2017 Article Content",
			kbArticleTitle = "Knowledge Base 2017 Article Title");

		KBFolder.gotoViaKBDisplayPG(kbFolderName = "KB Folder 2010 Name");

		Refresh();

		KBDisplayWidget.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base 2010 Article Content",
			kbArticleTitle = "Knowledge Base 2010 Article Title");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		var entries = "2010,2017";

		for (var kbEntry : list "${entries}") {
			KBAdmin.openKBAdmin(siteURLKey = "guest");

			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Edit",
				rowEntry = "${kbEntry}");

			AssertTextEquals(
				locator1 = "TextInput#CUSTOM_FIELD",
				value1 = "01/01/${kbEntry}");
		}
	}

}
@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Fragments";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a new site and page in it") {
			JSONGroup.addGroup(groupName = "Test Site Name");

			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");
		}

		task ("Add a company Object with attachment fields") {
			ObjectAdmin.addObjectViaAPI(
				labelName = "Upload Object",
				objectName = "UploadObject",
				pluralLabelName = "Upload Object");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");

			ObjectAdmin.deleteObjectViaAPI(objectName = "UploadObject");
		}
	}

	@description = "This is a test for LPS-157806. Add custom text on Button Text field of File Upload fragment on content page."
	@priority = "4"
	test AddCustomTextOnButtonTextFieldOnContentPage {
		task ("Given a content page has a File Upload fragment mapped to Attachment field with Show Files in Documents and Media") {
			task ("Add an Attachment field to object") {
				ObjectAdmin.addObjectFieldViaAPI(
					fieldBusinessType = "Attachment",
					fieldLabelName = "From Computer And Show Files In DM",
					fieldName = "fromComputerAndShowFilesInDM",
					fieldType = "Long",
					fileSource = "userComputer",
					isRequired = "false",
					objectName = "UploadObject",
					showFilesInDocumentsAndMedia = "true");

				ObjectAdmin.publishObjectViaAPI(objectName = "UploadObject");
			}

			task ("Add a Form Container framgment to content page") {
				ContentPagesNavigator.openEditContentPage(
					pageName = "Test Page Name",
					siteName = "Test Site Name");

				PageEditor.addFragment(
					collectionName = "Form Components",
					fragmentName = "Form Container");
			}

			task ("Add a File Upload fragment and Submit Button fragment into Form Container") {
				PageEditor.editFormContainer(
					contentType = "Upload Object",
					panel = "General");

				PageEditor.publish();
			}
		}

		task ("When adding a custom text on Button Text field") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.editFragmentText(
				className = "button",
				fragmentName = "Submit Button",
				id = "text",
				text = "Custom Text");
		}

		task ("Then the custom text is shown") {
			task ("View custom text is shown at edit mode") {
				PageEditor.viewFragmentEditableText(
					className = "button",
					editableText = "Custom Text",
					fragmentName = "Submit Button",
					id = "submit-button-text");
			}

			task ("View custom text is shown at preview") {
				PageEditor.previewInANewTab();

				ContentPages.viewFragmentText(
					fragmentName = "button",
					id = "submit-button-text",
					text = "Custom Text");

				Close.closeWindow();
			}

			task ("View custom text is shown at view mode") {
				PageEditor.publish();

				ContentPages.viewFragmentText(
					fragmentName = "button",
					id = "submit-button-text",
					text = "Custom Text");
			}
		}
	}

	@description = "This is a test for LPS-157806. Add custom text on Button Text field of File Upload fragment on display page template."
	@priority = "4"
	test AddCustomTextOnButtonTextFieldOnDisplayPage {
		task ("Given a display page has a File Upload fragment mapped to Attachment field with Show Files in Documents and Media") {
			task ("Add an Attachment field to object") {
				ObjectAdmin.addObjectFieldViaAPI(
					fieldBusinessType = "Attachment",
					fieldLabelName = "From Computer And Show Files In DM",
					fieldName = "fromComputerAndShowFilesInDM",
					fieldType = "Long",
					fileSource = "userComputer",
					isRequired = "false",
					objectName = "UploadObject",
					showFilesInDocumentsAndMedia = "true");

				ObjectAdmin.publishObjectViaAPI(objectName = "UploadObject");
			}

			task ("Add a web content based on Basic Web Content") {
				JSONWebcontent.addWebContent(
					content = "Web Content Content",
					groupName = "Test Site Name",
					title = "Web Content Title");
			}

			task ("Add a display page template for Web Content Article and Basic Web Content") {
				JSONLayoutpagetemplate.addDisplayPageTemplateEntry(
					contentType = "Web Content Article",
					displayPageTemplateEntryName = "Display Page Name",
					groupName = "Test Site Name",
					subType = "Basic Web Content");
			}

			task ("Add a Form Container framgment to display page") {
				DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

				DisplayPageTemplatesAdmin.gotoDisplayPage(displayPageName = "Display Page Name");

				PageEditor.addFragment(
					collectionName = "Form Components",
					fragmentName = "Form Container");
			}

			task ("Add a File Upload fragment and Submit Button fragment into Form Container") {
				PageEditor.editFormContainer(
					contentType = "Upload Object",
					panel = "General");

				PageEditor.publish();
			}
		}

		task ("When adding a custom text on Button Text field") {
			DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

			DisplayPageTemplatesAdmin.gotoDisplayPageEllipsisItem(
				displayPageName = "Display Page Name",
				item = "Edit");

			PageEditor.assertFragmentSidebarLoaded();

			PageEditor.editFragmentText(
				className = "button",
				fragmentName = "Submit Button",
				id = "text",
				text = "Custom Text");
		}

		task ("Then the custom text is shown") {
			task ("View custom text is shown at edit mode") {
				PageEditor.viewFragmentEditableText(
					className = "button",
					editableText = "Custom Text",
					fragmentName = "Submit Button",
					id = "submit-button-text");
			}

			task ("View custom text is shown at preview") {
				PageEditor.previewInANewTab();

				ContentPages.viewFragmentText(
					fragmentName = "button",
					id = "submit-button-text",
					text = "Custom Text");

				Close.closeWindow();
			}

			task ("Publish the display page template and mark it as default") {
				PageEditor.publish();

				DisplayPageTemplatesAdmin.markDisplayPageAsDefault(displayPageName = "Display Page Name");
			}

			task ("Navigate to the display page template at view mode") {
				Navigator.openWithAppendToBaseURL(urlAppend = "web/test-site-name/w/web-content-title");
			}

			task ("View custom text is shown at view mode") {
				ContentPages.viewFragmentText(
					fragmentName = "button",
					id = "submit-button-text",
					text = "Custom Text");
			}
		}
	}

	@description = "This is a test for LPS-155170. File Upload fragment could add file from computer."
	@priority = "3"
	test AddFileFromComputerDiectly {
		task ("Given a page creator has a File Upload fragment") {
			task ("Add an Attachment field to object") {
				ObjectAdmin.addObjectFieldViaAPI(
					fieldBusinessType = "Attachment",
					fieldLabelName = "Custom Attachment From Computer",
					fieldName = "customAttachmentFromComputer",
					fieldType = "Long",
					fileSource = "userComputer",
					isRequired = "false",
					objectName = "UploadObject");

				ObjectAdmin.publishObjectViaAPI(objectName = "UploadObject");
			}

			task ("Add a File Upload fragment to content page") {
				ContentPagesNavigator.openEditContentPage(
					pageName = "Test Page Name",
					siteName = "Test Site Name");

				PageEditor.addFragment(
					collectionName = "Form Components",
					fragmentName = "Form Container");

				PageEditor.editFormContainer(
					contentType = "Upload Object",
					panel = "General");
			}
		}

		task ("When uploading file from computer") {
			task ("Publish page and go to view mode") {
				PageEditor.publish();

				ContentPagesNavigator.openViewContentPage(
					pageName = "Test Page Name",
					siteName = "Test Site Name");
			}
		}

		task ("Then the file from computer is added") {
			task ("Add file from computer") {
				UploadDependencyFile.uploadFile(fileName = "Document_1.jpg");
			}

			task ("Assert upload is successful") {
				AssertTextEquals.assertPartialText(
					locator1 = "TextInput#FILE_PRESENT",
					value1 = "Document_1.jpg");
			}
		}
	}

	@description = "This is a test for LPS-155170. File Upload fragment could map to attachment type object."
	@priority = "5"
	test CanAddFileFromComputerAndDMInMappedObject {
		property portal.acceptance = "true";

		task ("Given a content page has a File Upload fragment mapped to Attachment field") {
			task ("Add Attachment fields to object") {
				ObjectAdmin.addObjectFieldViaAPI(
					fieldBusinessType = "Attachment",
					fieldLabelName = "Custom Attachment From Computer",
					fieldName = "customAttachmentFromComputer",
					fieldType = "Long",
					fileSource = "userComputer",
					isRequired = "false",
					objectName = "UploadObject");

				ObjectAdmin.addObjectFieldViaAPI(
					fieldBusinessType = "Attachment",
					fieldLabelName = "Custom Attachment From DM",
					fieldName = "customAttachmentFromDM",
					fieldType = "Long",
					fileSource = "documentsAndMedia",
					isRequired = "false",
					objectName = "UploadObject");

				ObjectAdmin.publishObjectViaAPI(objectName = "UploadObject");
			}

			task ("Add a Form Container framgment to content page") {
				ContentPagesNavigator.openEditContentPage(
					pageName = "Test Page Name",
					siteName = "Test Site Name");

				PageEditor.addFragment(
					collectionName = "Form Components",
					fragmentName = "Form Container");

				PageEditor.editFormContainer(
					contentType = "Upload Object",
					panel = "General");

				PageEditor.removeFragment(
					fragmentName = "File Upload",
					index = "2");

				for (var fragmentName : list "File Upload,Submit Button") {
					PageEditor.removeFragment(fragmentName = "${fragmentName}");
				}
			}

			task ("Add a File Upload fragment to Form Container") {
				PageEditor.addFragmentToFormContainer(
					collectionName = "Form Components",
					fragmentName = "File Upload");
			}
		}

		task ("When map the File Upload fragment to Attachment field with file source from computer") {
			PageEditor.editInput(
				field = "Custom Attachment From Computer",
				fragmentName = "File Upload");

			PageEditor.publish(objectName = "Upload Object");
		}

		task ("Then the files from computer are added in object") {
			task ("Assert could add file from computer") {
				ContentPagesNavigator.openViewContentPage(
					pageName = "Test Page Name",
					siteName = "Test Site Name");

				UploadDependencyFile.uploadFile(fileName = "Document_1.jpg");

				AssertTextEquals.assertPartialText(
					locator1 = "TextInput#FILE_PRESENT",
					value1 = "Document_1.jpg");
			}

			task ("Assert "Show Supported File Info" toggle displayed and is set to "ON" by default") {
				ContentPagesNavigator.openEditContentPage(
					pageName = "Test Page Name",
					siteName = "Test Site Name");

				PageEditor.gotoConfiguration(fragmentName = "File Upload");

				AssertChecked.assertCheckedNotVisible(
					key_toggleSwitchLabel = "Show Supported File Info",
					locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
			}

			task ("Assert "Label" and "Show Help Text" toggles and configs displayed") {
				AssertElementPresent(
					key_toggleSwitchLabel = "Show Label",
					locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

				AssertElementPresent(
					key_toggleSwitchLabel = "Show Help Text",
					locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

				AssertTextEquals(
					key_text = "Help Text",
					locator1 = "TextInput#ANY",
					value1 = "Add your help text here.");
			}

			task ("Assert there's a help text displayed") {
				PageEditor.viewInputStyle(
					helpText = "Upload a .jpeg,.jpg,.pdf,.png no larger than 100 MB.",
					type = "file-upload");
			}
		}

		task ("When map the File Upload fragment to Attachment field with file source from DM") {
			task ("Select the second field from the object") {
				PageEditor.editInput(
					field = "Custom Attachment From DM",
					fragmentName = "File Upload");

				PageEditor.publish(objectName = "Upload Object");
			}
		}

		task ("Then the files from DM are added in object") {
			task ("Assert could add file from Documents and Media") {
				ContentPagesNavigator.openViewContentPage(
					pageName = "Test Page Name",
					siteName = "Test Site Name");

				Click(locator1 = "Button#SELECT_FILE");

				SelectFrame(locator1 = "IFrame#MODAL_BODY");

				UploadDependencyFile.uploadFile(fileName = "Document_2.jpg");

				WaitForVisible(
					key_text = "Add",
					locator1 = "Button#ANY");

				Button.clickAdd();

				SelectFrameTop(value1 = "relative=top");

				AssertTextEquals.assertPartialText(
					locator1 = "TextInput#FILE_PRESENT",
					value1 = "Document_2.jpg");
			}

			task ("Assert "Show Supported File Info" toggle displayed and is set to "ON" by default") {
				ContentPagesNavigator.openEditContentPage(
					pageName = "Test Page Name",
					siteName = "Test Site Name");

				PageEditor.gotoConfiguration(fragmentName = "File Upload");

				AssertChecked.assertCheckedNotVisible(
					key_toggleSwitchLabel = "Show Supported File Info",
					locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
			}

			task ("Assert "Label" and "Show Help Text" toggles and configs displayed") {
				AssertElementPresent(
					key_toggleSwitchLabel = "Show Label",
					locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

				AssertElementPresent(
					key_toggleSwitchLabel = "Show Help Text",
					locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

				AssertTextEquals(
					key_text = "Help Text",
					locator1 = "TextInput#ANY",
					value1 = "Add your help text here.");
			}

			task ("Assert there's a help text displayed") {
				PageEditor.viewInputStyle(
					helpText = "Upload a .jpeg,.jpg,.pdf,.png no larger than 100 MB.",
					type = "file-upload");
			}
		}
	}

	@description = "This is a test for LPS-155170. File Upload fragment could add file from computer to DM when enable Show Files in Documents and Media."
	@priority = "3"
	test CanAddFileFromComputerToDMWhenEnableShowFilesInDocumentsAndMedia {
		task ("Given a content page has a File Upload fragment mapped to Attachment field with Show Files in Documents and Media") {
			task ("Add an Attachment field to object") {
				ObjectAdmin.addObjectFieldViaAPI(
					fieldBusinessType = "Attachment",
					fieldLabelName = "From Computer And Show Files In DM",
					fieldName = "fromComputerAndShowFilesInDM",
					fieldType = "Long",
					fileSource = "userComputer",
					isRequired = "false",
					objectName = "UploadObject",
					showFilesInDocumentsAndMedia = "true");

				ObjectAdmin.publishObjectViaAPI(objectName = "UploadObject");
			}

			task ("Add a Form Container framgment to content page") {
				ContentPagesNavigator.openEditContentPage(
					pageName = "Test Page Name",
					siteName = "Test Site Name");

				PageEditor.addFragment(
					collectionName = "Form Components",
					fragmentName = "Form Container");

				PageEditor.editFormContainer(
					contentType = "Upload Object",
					panel = "General");

				PageEditor.publish();
			}
		}

		task ("When upload file to File Upload on content page") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			UploadDependencyFile.uploadFile(fileName = "Document_1.jpg");

			AssertTextEquals.assertPartialText(
				locator1 = "TextInput#FILE_PRESENT",
				value1 = "Document_1.jpg");

			Button.clickSubmitButton();
		}

		task ("Then the file from computer is added to DM") {
			DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "test-site-name");

			DMNavigator.gotoFolder(dmFolderName = "UploadObject");

			DMDocument.viewPG(dmDocumentTitle = "Document_1");
		}
	}

	@description = "This is a test for LPS-151402. The user could see error message when submit a form with attached file size larger than allowed maximum upload size."
	@priority = "4"
	test ViewErrorMessageWhenSubmitFormWithAttachedFileSizeLargerThanAllowedMaximumUploadSize {
		task ("Given a user has a content page where a File Upload and Submit Button fragments are inside a Form Container") {
			task ("Add an Attachment field to object") {
				ObjectAdmin.addObjectFieldViaAPI(
					fieldBusinessType = "Attachment",
					fieldLabelName = "Custom Attachment From Computer",
					fieldName = "customAttachmentFromComputer",
					fieldType = "Long",
					fileSource = "userComputer",
					isRequired = "false",
					objectName = "UploadObject");

				ObjectAdmin.publishObjectViaAPI(objectName = "UploadObject");
			}

			task ("Change the Maximum File Size to 2 MB") {
				ObjectAdmin.openObjectAdmin();

				ObjectPortlet.selectCustomObject(label = "Upload Object");

				ObjectAdmin.goToFieldsTab();

				ObjectAdmin.goToFieldsDetails(label = "Custom Attachment From Computer");

				Type(
					key_text = "Maximum File Size",
					locator1 = "TextInput#ANY",
					value1 = "2");

				ObjectField.save();
			}

			task ("Add a Form Container framgment to content page") {
				JSONLayout.addPublicLayout(
					groupName = "Test Site Name",
					layoutName = "Test Page Name",
					type = "content");

				ContentPagesNavigator.openEditContentPage(
					pageName = "Test Page Name",
					siteName = "Test Site Name");

				PageEditor.addFragment(
					collectionName = "Form Components",
					fragmentName = "Form Container");

				PageEditor.editFormContainer(
					contentType = "Upload Object",
					panel = "General");

				PageEditor.publish();
			}
		}

		task ("When the user submits form with attached file size larger than allowed maximum upload size") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			UploadDependencyFile.uploadFile(fileName = "high_resolution_photo.jpg");

			Button.clickSubmitButton();
		}

		task ("Then the user should see an error message") {
			Alert.viewErrorMessage(errorMessage = "File size is larger than the allowed maximum upload size (2 MB).");
		}
	}

	@description = "This is a test for LPS-151402. The user could see error message when submit a form with invalid extension of attached file."
	@priority = "4"
	test ViewErrorMessageWhenSubmitFormWithInvalidExtensionOfAttachedFile {
		task ("Given a user has a content page where a File Upload and Submit Button fragments are inside a Form Container") {
			task ("Add an Attachment field to object") {
				ObjectAdmin.addObjectFieldViaAPI(
					fieldBusinessType = "Attachment",
					fieldLabelName = "Custom Attachment From Computer",
					fieldName = "customAttachmentFromComputer",
					fieldType = "Long",
					fileSource = "userComputer",
					isRequired = "false",
					objectName = "UploadObject");

				ObjectAdmin.publishObjectViaAPI(objectName = "UploadObject");
			}

			task ("Add a Form Container framgment to content page") {
				JSONLayout.addPublicLayout(
					groupName = "Test Site Name",
					layoutName = "Test Page Name",
					type = "content");

				ContentPagesNavigator.openEditContentPage(
					pageName = "Test Page Name",
					siteName = "Test Site Name");

				PageEditor.addFragment(
					collectionName = "Form Components",
					fragmentName = "Form Container");

				PageEditor.editFormContainer(
					contentType = "Upload Object",
					panel = "General");

				PageEditor.publish();
			}
		}

		task ("When the user submits form with invalid extension of attached file") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			UploadDependencyFile.uploadFile(fileName = "Document_1.doc");

			Button.clickSubmitButton();
		}

		task ("Then the user should see an error message") {
			Alert.viewErrorMessage(errorMessage = "Please enter a file with a valid extension (jpeg, jpg, pdf, png).");
		}
	}

}
@component-name = "portal-upgrades"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Fragments,Upgrades WEM";
	property testray.main.component.name = "Upgrades WEM";

	setUp {
		SignIn.signIn();

		var portalURL = PropsUtil.get("portal.url");

		AssertLocation(value1 = "${portalURL}/web/guest?SM_USER=test@liferay.com");

		SearchAdministration.executeReindex();
	}

	@description = "This is a test for LPS-164498. Verify that it's possible to import a lar file with both page template and fragment after upgrade from 6.2.10.21."
	@priority = "3"
	test CanImportFileWithPageTemplateAndFragmentAfterUpgradeFrom621021 {
		property data.archive.type = "data-archive-portal";
		property database.types = "mysql";
		property portal.version = "6.2.10.21";

		task ("Given: Add a new site") {
			JSONGroup.addGroup(groupName = "Import Site Name");
		}

		task ("When: Import lar file from dependencies") {
			LAR.importSiteCP(
				larFileName = "page-template-collection-fragment.lar",
				siteName = "Import Site Name",
				uploadFrom = "dependencies");
		}

		task ("Then: Import should be successful without error message on console") {
			AssertConsoleTextNotPresent(value1 = "java.lang.RuntimeException: Unable to get class name");
		}
	}

	@description = "This is a test for LPS-153154. View fragment with embedded instantiable widget after upgrade from 7.1.10.5"
	@priority = "4"
	test ViewFragmentWithEmbeddedInstantiableWidgetAfterUpgradeFrom71105 {
		property data.archive.type = "data-archive-fragment-with-instantiable-widget";
		property database.types = "db2,mariadb,mysql,oracle,postgresql,sqlserver,sybase";
		property portal.version = "7.1.10.5";

		task ("View instatiable fragment is successfully upgraded on the page at view mode") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			WebContentDisplayPortlet.viewContent(webContentContent = "Web Content Content");
		}

		task ("View instatiable fragment is successfully upgraded on the page at edit mode") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.viewFragment(fragmentName = "Fragment Name");

			WebContentDisplayPortlet.viewContent(webContentContent = "Web Content Content");
		}
	}

}
@component-name = "portal-content-dashboard"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Content Dashboard";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		Navigator.openURL();

		var categoryId1 = JSONCategory.addCategory(
			groupName = "Global",
			title = "Audience 1",
			vocabularyName = "Audience");

		var categoryId2 = JSONCategory.addCategory(
			groupName = "Global",
			title = "Stage 1",
			vocabularyName = "Stage");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "When a user clicks on one of the colored bars of the audit graph, the filters corresponding to the categories in that bar are applied, they are shown as filter labels in the management tool bar and the list of contents is filtered by them"
	@priority = "5"
	test AuditGraphClickOnBar {
		property portal.acceptance = "true";

		JSONCategory.addCategory(
			groupName = "Global",
			title = "Audience 2",
			vocabularyName = "Audience");

		JSONCategory.addCategory(
			groupName = "Global",
			title = "Stage 2",
			vocabularyName = "Stage");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddCP();

		WebContent.addWithCategoryCP(
			categoryName = "Audience 1",
			vocabularyName = "Audience",
			webContentContent = "Web Content Content",
			webContentTitle = "WC Title");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoEditCP(webContentTitle = "WC Title");

		WebContent.selectCategory(
			categoryName = "Stage 1",
			vocabularyName = "Stage");

		PortletEntry.publish();

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddCP();

		WebContent.addWithCategoryCP(
			categoryName = "Audience 2",
			vocabularyName = "Audience",
			webContentContent = "Web Content Content",
			webContentTitle = "WC Title 2");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoEditCP(webContentTitle = "WC Title 2");

		WebContent.selectCategory(
			categoryName = "Stage 2",
			vocabularyName = "Stage");

		PortletEntry.publish();

		ApplicationsMenu.gotoPortlet(
			category = "Content",
			panel = "Applications",
			portlet = "Content Dashboard");

		Pause(locator1 = "3000");

		ContentDashboard.clickOnFirstBar();

		AssertTextPresent(
			locator1 = "ContentDashboard#BAR_SELECTED_MESSAGE",
			value1 = "Press escape to remove the bar's category filters.");

		AssertTextPresent(
			categoryName = "Audience 1",
			locator1 = "ContentDashboard#FILTER_CATEGORY_NAME",
			value1 = "Category:Audience 1");

		AssertTextPresent(
			categoryName = "Stage 1",
			locator1 = "ContentDashboard#FILTER_CATEGORY_NAME",
			value1 = "Category:Stage 1");

		ContentDashboard.clearFilters();

		AssertElementNotPresent(
			categoryName = "Audience 1",
			locator1 = "ContentDashboard#FILTER_CATEGORY_NAME");

		AssertElementNotPresent(
			categoryName = "Stage 1",
			locator1 = "ContentDashboard#FILTER_CATEGORY_NAME");

		AssertElementNotPresent(locator1 = "ContentDashboard#BAR_SELECTED_MESSAGE");
	}

	@description = "When a user clicks on one of the colored bars of the audit graph, the filters corresponding to the categories in that bar are applied, they are shown as filter labels in the management tool bar and the list of contents is filtered by them"
	@priority = "5"
	test AuditGraphClickOnBarDeleteOneFilter {
		property portal.acceptance = "true";

		JSONCategory.addCategory(
			groupName = "Global",
			title = "Audience 2",
			vocabularyName = "Audience");

		JSONCategory.addCategory(
			groupName = "Global",
			title = "Stage 2",
			vocabularyName = "Stage");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddCP();

		WebContent.addWithCategoryCP(
			categoryName = "Audience 1",
			vocabularyName = "Audience",
			webContentContent = "Web Content Content",
			webContentTitle = "WC Title");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoEditCP(webContentTitle = "WC Title");

		WebContent.selectCategory(
			categoryName = "Stage 1",
			vocabularyName = "Stage");

		PortletEntry.publish();

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddCP();

		WebContent.addWithCategoryCP(
			categoryName = "Audience 2",
			vocabularyName = "Audience",
			webContentContent = "Web Content Content",
			webContentTitle = "WC Title 2");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoEditCP(webContentTitle = "WC Title 2");

		WebContent.selectCategory(
			categoryName = "Stage 2",
			vocabularyName = "Stage");

		PortletEntry.publish();

		ApplicationsMenu.gotoPortlet(
			category = "Content",
			panel = "Applications",
			portlet = "Content Dashboard");

		Pause(locator1 = "3000");

		ContentDashboard.clickOnFirstBar();

		AssertTextPresent(
			locator1 = "ContentDashboard#BAR_SELECTED_MESSAGE",
			value1 = "Press escape to remove the bar's category filters.");

		AssertTextPresent(
			categoryName = "Audience 1",
			locator1 = "ContentDashboard#FILTER_CATEGORY_NAME",
			value1 = "Category:Audience 1");

		AssertTextPresent(
			categoryName = "Stage 1",
			locator1 = "ContentDashboard#FILTER_CATEGORY_NAME",
			value1 = "Category:Stage 1");

		ContentDashboard.deleteSecondCategoryFilter();

		AssertTextPresent(
			categoryName = "Audience 1",
			locator1 = "ContentDashboard#FILTER_CATEGORY_NAME",
			value1 = "Category:Audience 1");

		AssertElementNotPresent(
			categoryName = "Stage 1",
			locator1 = "ContentDashboard#FILTER_CATEGORY_NAME");

		AssertElementNotPresent(locator1 = "ContentDashboard#BAR_SELECTED_MESSAGE");
	}

	@description = "When the user clicks on the Flip axes button, the vocabularies will be exchanged, the vocabulary represented in the X (the first in the list in the "In use" box) is sent to the Y, and the other way around"
	@priority = "5"
	test AuditGraphFlipAxes {
		property portal.acceptance = "true";

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddCP();

		WebContent.addWithCategoryCP(
			categoryName = "Audience 1",
			vocabularyName = "Audience",
			webContentContent = "Web Content Content",
			webContentTitle = "WC Title");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoEditCP(webContentTitle = "WC Title");

		WebContent.selectCategory(
			categoryName = "Stage 1",
			userSegmentDetailTitle = "${userSegmentDetailTitle}",
			vocabularyName = "Stage");

		PortletEntry.publish();

		ApplicationsMenu.gotoPortlet(
			category = "Content",
			panel = "Applications",
			portlet = "Content Dashboard");

		Pause(locator1 = "3000");

		AssertTextPresent(
			locator1 = "ContentDashboard#X_AXIS_VOCABULARY_NAME",
			value1 = "WC Title",
			xAxisCtegoryName = "Stage 1");

		ContentDashboard.flipAxesButtonClick();

		AssertTextPresent(
			locator1 = "ContentDashboard#X_AXIS_VOCABULARY_NAME",
			value1 = "WC Title",
			xAxisCtegoryName = "Audience 1");
	}

	@description = "When there are not web content associated to the categories, the Flip axes button will be disabled"
	@priority = "5"
	test AuditGraphFlipAxesDisabled {
		property portal.acceptance = "true";

		ApplicationsMenu.gotoPortlet(
			category = "Content",
			panel = "Applications",
			portlet = "Content Dashboard");

		Pause(locator1 = "3000");

		AssertElementPresent(locator1 = "ContentDashboard#FLIP_AXES_BUTTON_DISABLED");
	}

	@description = "When there are not contents with Audience's categories, the X axis will show just one bar per each one of Stage's categories representing the total number of contents labeled with those categories"
	@priority = "5"
	test AuditGraphNoContentsForAudience {
		property portal.acceptance = "true";

		Category.openCategoriesAdmin(siteURLKey = "global");

		Category.deleteCategoriesCP(vocabularyName = "Audience");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddCP();

		WebContent.addWithCategoryCP(
			categoryName = "Stage 1",
			vocabularyName = "Stage",
			webContentContent = "Web Content Content",
			webContentTitle = "WC Title");

		ApplicationsMenu.gotoPortlet(
			category = "Content",
			panel = "Applications",
			portlet = "Content Dashboard");

		AssertTextPresent(
			locator1 = "ContentDashboard#X_AXIS_VOCABULARY_NAME",
			value1 = "WC Title",
			xAxisCategoryName = "Stage 1");

		AssertTextPresent(
			auditGraphTitle = "Content per Stage",
			locator1 = "ContentDashboard#AUDIT_GRAPH_TITLE",
			value1 = "Content per Stage");

		ContentDashboard.stageFirstVocabularyConfiguration();

		AssertTextPresent(
			locator1 = "ContentDashboard#X_AXIS_VOCABULARY_NAME",
			value1 = "WC Title",
			xAxisCategoryName = "Stage 1");

		AssertTextPresent(
			auditGraphTitle = "Content per Stage",
			locator1 = "ContentDashboard#AUDIT_GRAPH_TITLE",
			value1 = "Content per Stage");
	}

	@description = "When one or more web contents have not an assigned category in the Vocab1 or in the Vocab2, but they have at least one assigned category in one of them, these contents are represented by a "No [VocabularyName] Category" label in the X axis (assets with no category in the Vocab2), or a "No [VocabularyName] Category" checkbox above the chart (assets with no category in the Vocab1)"
	@priority = "5"
	test AuditGraphNoContentsForEachCategory {
		property portal.acceptance = "true";

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddCP();

		WebContent.addWithCategoryCP(
			categoryName = "Audience 1",
			vocabularyName = "Audience",
			webContentContent = "Web Content Content",
			webContentTitle = "WC Title");

		WebContentNavigator.gotoAddCP();

		WebContent.addWithCategoryCP(
			categoryName = "Stage 1",
			vocabularyName = "Stage",
			webContentContent = "Web Content Content",
			webContentTitle = "WC Title 2");

		ApplicationsMenu.gotoPortlet(
			category = "Content",
			panel = "Applications",
			portlet = "Content Dashboard");

		Pause(locator1 = "3000");

		AssertTextPresent(
			locator1 = "ContentDashboard#X_AXIS_VOCABULARY_NAME",
			value1 = "No Audience",
			xAxisCategoryName = "No Audience");

		AssertTextPresent(
			auditGraphCheckboxName = "No Stage Specified",
			locator1 = "ContentDashboard#AUDIT_GRAPH_CATEGORY_CHECKBOX",
			value1 = "No Stage Specified");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContent.viewTitle(webContentTitle = "WC Title 2");

		WebContent.deleteCP(
			siteURLKey = "test-site-name",
			webContentTitle = "WC Title 2");
	}

	@description = "When there are not contents with Stage's categories, the X axis will show just one bar per each one of Audience's categories representing the total number of contents labeled with those categories"
	@priority = "5"
	test AuditGraphNoContentsForStage {
		property portal.acceptance = "true";

		Category.openCategoriesAdmin(siteURLKey = "global");

		Category.deleteCategoriesCP(vocabularyName = "Stage");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddCP();

		WebContent.addWithCategoryCP(
			categoryName = "Audience 1",
			vocabularyName = "Audience",
			webContentContent = "Web Content Content",
			webContentTitle = "WC Title");

		ApplicationsMenu.gotoPortlet(
			category = "Content",
			panel = "Applications",
			portlet = "Content Dashboard");

		AssertTextPresent(
			locator1 = "ContentDashboard#X_AXIS_VOCABULARY_NAME",
			value1 = "WC Title",
			xAxisCategoryName = "Audience 1");

		AssertTextPresent(
			auditGraphTitle = "Content per Audience",
			locator1 = "ContentDashboard#AUDIT_GRAPH_TITLE",
			value1 = "Content per Audience");

		ContentDashboard.stageFirstVocabularyConfiguration();

		AssertTextPresent(
			locator1 = "ContentDashboard#X_AXIS_VOCABULARY_NAME",
			value1 = "WC Title",
			xAxisCategoryName = "Audience 1");

		AssertTextPresent(
			auditGraphTitle = "Content per Audience",
			locator1 = "ContentDashboard#AUDIT_GRAPH_TITLE",
			value1 = "Content per Audience");
	}

	@description = "Validate if the filters are saved after swapping vocabularies"
	@priority = "5"
	test AuditGraphSaveFiltersAfterSwapping {
		property portal.acceptance = "true";

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddCP();

		WebContent.addWithCategoryCP(
			categoryName = "Audience 1",
			vocabularyName = "Audience",
			webContentContent = "Web Content Content",
			webContentTitle = "WC Title");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoEditCP(webContentTitle = "WC Title");

		WebContent.selectCategory(
			categoryName = "Stage 1",
			userSegmentDetailTitle = "${userSegmentDetailTitle}",
			vocabularyName = "Stage");

		PortletEntry.publish();

		ApplicationsMenu.gotoPortlet(
			category = "Content",
			panel = "Applications",
			portlet = "Content Dashboard");

		Pause(locator1 = "3000");

		ContentDashboard.filterByAuthorTable(filterOption = "Author");

		ContentDashboard.swapVocabularies();

		AssertTextPresent(
			locator1 = "ContentDashboard#FILTER_TEXT",
			value1 = "Author:Test Test");
	}

	@description = "When an asset has S1 and S2 (Stage) assigned categories, but no "Audience" (Vocab2) categories, it will be represented in two different bars in the No Audience Category"
	@priority = "5"
	test AuditGraphTwoBarsForNoSpecifiedCategory {
		property portal.acceptance = "true";

		JSONCategory.addCategory(
			groupName = "Global",
			title = "Audience 2",
			vocabularyName = "Audience");

		JSONCategory.addCategory(
			groupName = "Global",
			title = "Stage 2",
			vocabularyName = "Stage");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddCP();

		WebContent.addWithCategoryCP(
			categoryName = "Audience 1",
			vocabularyName = "Audience",
			webContentContent = "Web Content Content",
			webContentTitle = "WC A1 A2");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoEditCP(webContentTitle = "WC A1 A2");

		WebContent.selectCategory(
			categoryName = "Audience 2",
			userSegmentDetailTitle = "${userSegmentDetailTitle}",
			vocabularyName = "Audience");

		PortletEntry.publish();

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddCP();

		WebContent.addWithCategoryCP(
			categoryName = "Stage 1",
			vocabularyName = "Stage",
			webContentContent = "Web Content Content",
			webContentTitle = "WC S1 S2");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoEditCP(webContentTitle = "WC S1 S2");

		WebContent.selectCategory(
			categoryName = "Stage 2",
			userSegmentDetailTitle = "${userSegmentDetailTitle}",
			vocabularyName = "Stage");

		PortletEntry.publish();

		ApplicationsMenu.gotoPortlet(
			category = "Content",
			panel = "Applications",
			portlet = "Content Dashboard");

		Pause(locator1 = "3000");

		AssertElementPresent(
			fillColor = "4B9FFF",
			locator1 = "ContentDashboard#BAR_FROM_NO_VOCABULARY_SPECIFIED");

		AssertElementPresent(
			fillColor = "FFB46E",
			locator1 = "ContentDashboard#BAR_FROM_NO_VOCABULARY_SPECIFIED");

		ContentDashboard.flipAxesButtonClick();

		AssertElementPresent(
			fillColor = "4B9FFF",
			locator1 = "ContentDashboard#BAR_FROM_NO_VOCABULARY_SPECIFIED");

		AssertElementPresent(
			fillColor = "FFB46E",
			locator1 = "ContentDashboard#BAR_FROM_NO_VOCABULARY_SPECIFIED");
	}

	@description = "When the user unchecks all of the checkboxes of the content audit graph, all bars in the graph disappear and a message is shown: No Stage Categories selected. Select any stage category from the checkboxes in the legend above."
	@priority = "5"
	test AuditGraphUncheckCheckbox {
		property portal.acceptance = "true";

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoAddCP();

		WebContent.addWithCategoryCP(
			categoryName = "Audience 1",
			vocabularyName = "Audience",
			webContentContent = "Web Content Content",
			webContentTitle = "WC Title");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

		WebContentNavigator.gotoEditCP(webContentTitle = "WC Title");

		WebContent.selectCategory(
			categoryName = "Stage 1",
			userSegmentDetailTitle = "${userSegmentDetailTitle}",
			vocabularyName = "Stage");

		PortletEntry.publish();

		ApplicationsMenu.gotoPortlet(
			category = "Content",
			panel = "Applications",
			portlet = "Content Dashboard");

		Pause(locator1 = "3000");

		ContentDashboard.uncheckCheckbox(auditGraphCheckboxName = "Stage 1");

		AssertTextPresent(
			locator1 = "ContentDashboard#NO_CATEGORIES_SELECTED_TEXT",
			value1 = "There are no categories selected");
	}

	@description = "LPS-133797 When the user selects the option Available, the modal lists all the existing Global Vocabularies"
	@priority = "5"
	test CheckAvailableVocabulariesDisplayed {
		task ("Create 2 new vocabularies") {
			for (var vocabularyNumber : list "1,2") {
				JSONCategory.addVocabulary(
					groupName = "Global",
					title = "Vocabulary Name ${vocabularyNumber}");
			}
		}

		task ("Check if vocabularies are avalible") {
			ApplicationsMenu.gotoPortlet(
				category = "Content",
				panel = "Applications",
				portlet = "Content Dashboard");

			ContentDashboard.openAuditGraphConfiguration();

			AssertElementPresent(
				locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_VOCABULARY_AVAILABLE",
				vocabularyName = "Vocabulary Name 1");

			AssertElementPresent(
				locator1 = "ContentDashboard#AUDIT_GRAPH_CONFIGURATION_VOCABULARY_AVAILABLE",
				vocabularyName = "Vocabulary Name 2");
		}
	}

	@description = "LPS-134606 When there's a vocabulary in the "In use" box is in the second position of the list and user clicks on Save, the graph will feature that vocabulary in the checkbox legend above"
	@priority = "5"
	test CheckSecondPositionVocabularyCheckbox {
		property portal.acceptance = "true";

		task ("Create a Web Content with Audience and Stage categories") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			WebContentNavigator.gotoAddCP();

			WebContent.addWithCategoryCP(
				categoryName = "Audience 1",
				vocabularyName = "Audience",
				webContentContent = "Web Content Content",
				webContentTitle = "WC Title");

			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			WebContentNavigator.gotoEditCP(webContentTitle = "WC Title");

			WebContent.selectCategory(
				categoryName = "Stage 1",
				vocabularyName = "Stage");

			PortletEntry.publish();
		}

		task ("Go to Content Dashboard > Audic Graph configuration") {
			ApplicationsMenu.gotoPortlet(
				category = "Content",
				panel = "Applications",
				portlet = "Content Dashboard");
		}

		task ("Check which vocabulary is displayed in x-axis") {
			ContentDashboard.setVocabularyInUseSecondPosition(vocabularyName = "Stage");

			AssertTextPresent(
				auditGraphCheckboxName = "Stage 1",
				locator1 = "ContentDashboard#AUDIT_GRAPH_CATEGORY_CHECKBOX",
				value1 = "WC Title");

			ContentDashboard.setVocabularyInUseSecondPosition(vocabularyName = "Audience");

			AssertTextPresent(
				auditGraphCheckboxName = "Audience 1",
				locator1 = "ContentDashboard#AUDIT_GRAPH_CATEGORY_CHECKBOX",
				value1 = "WC Title");
		}
	}

	@description = "LPS-134606 When there's a vocabulary in the "In use" box is in the top position of the list and user clicks on Save, the graph will feature that vocabulary in the X axis"
	@priority = "5"
	test CheckTopPositionVocabularyXAxis {
		property portal.acceptance = "true";

		task ("Create a Web Content with Audience and Stage categories") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			WebContentNavigator.gotoAddCP();

			WebContent.addWithCategoryCP(
				categoryName = "Audience 1",
				vocabularyName = "Audience",
				webContentContent = "Web Content Content",
				webContentTitle = "WC Title");

			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			WebContentNavigator.gotoEditCP(webContentTitle = "WC Title");

			WebContent.selectCategory(
				categoryName = "Stage 1",
				vocabularyName = "Stage");

			PortletEntry.publish();
		}

		task ("Go to Content Dashboard > Audic Graph configuration") {
			ApplicationsMenu.gotoPortlet(
				category = "Content",
				panel = "Applications",
				portlet = "Content Dashboard");
		}

		task ("Check which vocabulary is displayed in x-axis") {
			ContentDashboard.setVocabularyInUseTopPosition(vocabularyName = "Stage");

			AssertTextPresent(
				locator1 = "ContentDashboard#X_AXIS_VOCABULARY_NAME",
				value1 = "WC Title",
				xAxisCategoryName = "Stage 1");

			ContentDashboard.setVocabularyInUseTopPosition(vocabularyName = "Audience");

			AssertTextPresent(
				locator1 = "ContentDashboard#X_AXIS_VOCABULARY_NAME",
				value1 = "WC Title",
				xAxisCategoryName = "Audience 1");
		}
	}

	@priority = "4"
	test DocumentWithNoCategories {
		property osgi.module.configuration.file.names = "com.liferay.content.dashboard.web.internal.configuration.FFContentDashboardDocumentConfiguration.config";
		property osgi.module.configurations = "enabled=B&quot;true&quot;";
		property portal.acceptance = "true";

		task ("Create a new Document") {
			JSONDocument.addFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "DM Document Title",
				groupName = "Test Site Name");
		}

		task ("Go to Content Dashboard") {
			ApplicationsMenu.gotoPortlet(
				category = "Content",
				panel = "Applications",
				portlet = "Content Dashboard");
		}

		task ("Assert the are no info in the Audit Graph") {
			AssertTextPresent(
				locator1 = "ContentDashboard#EMPTY_AUDIT_GRAPH_TEXT",
				value1 = "There is no data.");
		}
	}

	@priority = "4"
	test DocumentWithOneCategory {
		property osgi.module.configuration.file.names = "com.liferay.content.dashboard.web.internal.configuration.FFContentDashboardDocumentConfiguration.config";
		property osgi.module.configurations = "enabled=B&quot;true&quot;";
		property portal.acceptance = "true";

		task ("Create a new Document") {
			JSONDocument.addFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "DM Document Title",
				groupName = "Test Site Name");
		}

		task ("Add public and internal categories to Document") {
			DMDocument.openDocumentsAndMediaAdmin(siteURLKey = "test-site-name");

			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Edit",
				rowEntry = "DM Document Title");

			DMDocument.addCategories(
				categoryNameList = "Audience 1",
				vocabularyName = "Audience");

			Button.clickSubmitButton();

			Alert.viewSuccessMessage();
		}

		task ("Go to Content Dashboard") {
			ApplicationsMenu.gotoPortlet(
				category = "Content",
				panel = "Applications",
				portlet = "Content Dashboard");
		}

		task ("Assert the document is counted as an asset") {
			AssertTextPresent(
				locator1 = "ContentDashboard#X_AXIS_VOCABULARY_NAME",
				value1 = "DM Document Title",
				xAxisCategoryName = "Audience 1");
		}
	}

	@description = "LPS-133797 When clicks on the left or right arrows within Configuration and selects one or more vocabularies, the vocabularies go from Available to In use and vice versa"
	@priority = "3"
	test MoveVocabulariesAcrossConfiguration {
		task ("Go to Content Dashboard > Audic Graph configuration") {
			ApplicationsMenu.gotoPortlet(
				category = "Content",
				panel = "Applications",
				portlet = "Content Dashboard");
		}

		task ("Move vocabulary from in use to available") {
			ContentDashboard.moveVocabularyDisplayConfiguration(vocabularyName = "Audience");
		}

		task ("Move vocabulary from available to in use") {
			ContentDashboard.moveVocabularyDisplayConfiguration(vocabularyName = "Topic");
		}
	}

	@description = "LPS-134606 When the user has removed all vocabularies from the In Use box and clicks on Save , a warning message will show: Select at least one vocabulary to show on the chart "
	@priority = "3"
	test ShowWarningMessageNoVocabulariesInUse {
		task ("Create a Web Content with Audience and Stage categories") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			WebContentNavigator.gotoAddCP();

			WebContent.addWithCategoryCP(
				categoryName = "Audience 1",
				vocabularyName = "Audience",
				webContentContent = "Web Content Content",
				webContentTitle = "WC Title");

			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			WebContentNavigator.gotoEditCP(webContentTitle = "WC Title");

			WebContent.selectCategory(
				categoryName = "Stage 1",
				vocabularyName = "Stage");

			PortletEntry.publish();
		}

		task ("Go to Content Dashboard > Audic Graph configuration") {
			ApplicationsMenu.gotoPortlet(
				category = "Content",
				panel = "Applications",
				portlet = "Content Dashboard");
		}

		task ("Move all vocabularies from In Use and check warning message") {
			ContentDashboard.moveVocabularyDisplayConfiguration(vocabularyName = "Audience");

			ContentDashboard.moveVocabularyDisplayConfiguration(
				notCloseFrame = "true",
				vocabularyName = "Stage");

			Alert.viewWarningSpecific(warningMessage = "You have not selected any vocabularies");
		}
	}

}
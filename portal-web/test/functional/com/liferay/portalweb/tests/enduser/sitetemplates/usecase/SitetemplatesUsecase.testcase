@component-name = "portal-staging"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Site Templates";
	property testray.main.component.name = "Site Templates";

	var siteTemplateName = "Site Template Name";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Navigator.openURL();

			User.logoutAndLoginPG(
				password = "test",
				userLoginEmailAddress = "test@liferay.com",
				userLoginFullName = "Test Test");

			JSONUser.tearDownNonAdminUsers();

			UserGroup.tearDownCP();

			Site.tearDownCP();

			SiteTemplates.tearDownCP();

			DMDocument.tearDownCP();
		}
	}

	@description = "This is a use case for LPS-87055."
	@priority = "3"
	test AddPageTemplatesWithDuplicateNameViaSiteTemplate {
		SiteTemplates.addCP(
			siteTemplateActive = "Yes",
			siteTemplateName = "Site Template Name");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Manage",
			rowEntry = "Site Template Name");

		ProductMenu.gotoPortlet(
			category = "Design",
			portlet = "Page Templates");

		NavItem.gotoPageTemplates();

		PageTemplatesAdmin.addCollection(collectionName = "Page Template Collection");

		Navigator.gotoNavNested(navNested = "Page Template Collection");

		PageTemplatesAdmin.addPageTemplate(
			pageTemplateName = "Test",
			siteTemplate = "true");

		Navigator.openURL();

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Manage",
			rowEntry = "Site Template Name");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		ProductMenu.gotoPortlet(
			category = "Design",
			portlet = "Page Templates");

		NavItem.gotoPageTemplates();

		Navigator.gotoNavNested(navNested = "Page Template Collection");

		PageTemplatesAdmin.addDuplicatePageTemplate(
			duplicatePageTemplate = "true",
			pageTemplateName = "Test",
			siteTemplate = "true");
	}

	@description = "This is a use case for LPS-51432 and LPS-95113."
	@priority = "4"
	test AdminCanNotAddASubpageOfAChildPageLinkedToSiteTemplatePage {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "SitetemplatesUsecase#AdminCanNotAddASubpageOfAChildPageLinkedToSiteTemplatePage";

		SiteTemplates.addCP(siteTemplateName = "Site Template Name");

		SiteTemplates.addSitePageViaSiteTemplateCP(
			pageName = "Site Page Name",
			siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates");

		SiteTemplates.addChildPageViaSiteTemplateCP(
			childSitepageName = "true",
			pageName = "Site Page Name",
			siteChildPageName = "Site Child Page Name",
			siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addSiteTemplateCP(
			siteName = "Site Name",
			siteTemplateName = "Site Template Name");

		Navigator.gotoSitePage(
			pageName = "Home",
			siteName = "Site Name");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		SitePages.togglePrivacy(privacy = "Public Pages");

		Page.viewNoDeletePagesAssociatedWithSiteTemplate(
			page = "Site Child Page Name",
			pageName = "Site Page Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates");

		SiteTemplates.deletePage(
			hasChildren = "true",
			pageName = "Site Page Name",
			siteTemplateName = "Site Template Name");

		Navigator.openSiteURL(siteName = "Site Name");

		Refresh();

		Page.viewNoPG(pageName = "Site Page Name");

		Page.viewNoPG(pageName = "Site Child Page Name");

		Page.viewNoPage(
			pageName = "Site Page Name",
			privacy = "Public Pages");

		Page.viewNoPage(
			pageName = "Site Child Page Name",
			privacy = "Public Pages");

		AssertConsoleTextNotPresent(value1 = "java.lang.NullPointerException");
	}

	@priority = "4"
	test EditSiteTemplateGuestPermissions {
		SiteTemplates.addCP(siteTemplateName = "${siteTemplateName}");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addSiteTemplateCP(
			siteName = "Site Name",
			siteTemplateName = "Site Template Name");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		SitePages.gotoPageMenuItem(
			menuItem = "Permissions",
			pageName = "Home");

		Role.removePermissionPG(
			permissionDefinitionKey = "CONTENT_PERMISSIONS_VIEW_CHECKBOX",
			roleTitle = "Guest");

		Navigator.gotoSitePage(
			pageName = "Home",
			siteName = "Site Name");

		PermissionsInline.gotoSiteTemplateCP(
			page = "Home",
			site = "Site Name");

		Role.definePermissionPG(
			permissionDefinitionKey = "CONTENT_PERMISSIONS_VIEW_CHECKBOX",
			roleTitle = "Guest");
	}

	@priority = "4"
	test ImportOldSchemaVerion {
		property test.name.skip.portal.instance = "SitetemplatesUsecase#ImportOldSchemaVerion";

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates");

		LAR.importPortlet(
			importSchemaVersionMismatching = "true",
			larFileName = "class-site-template-7.0.lar",
			uploadFrom = "dependencies");
	}

	@description = "This is a use case for LPS-81629"
	@priority = "4"
	test PortletPreferencesPersistWithSiteTemplateWhenSamePortletAddedOnSecondPage {
		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		SiteTemplates.addCP(siteTemplateName = "Site Template Name");

		SiteTemplates.addSitePageViaSiteTemplateCP(
			pageName = "Page1 Name",
			siteTemplateName = "Site Template Name");

		SiteTemplatesNavigator.gotoSiteTemplatesPage(
			pageName = "Page1 Name",
			siteName = "${siteName}",
			siteTemplateName = "Site Template Name");

		Portlet.addPG(
			defaultURL = "false",
			portletName = "IFrame");

		SiteTemplates.addSitePageViaSiteTemplateCP(
			pageName = "Page2 Name",
			siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addSiteTemplateCP(
			siteName = "Site Name",
			siteTemplateName = "Site Template Name");

		Navigator.gotoSitePage(
			pageName = "Page1 Name",
			siteName = "Site Name");

		PermissionsInline.gotoConfigurationPG(portletName = "IFrame");

		Role.removePermissionPG(
			permissionDefinitionKey = "CONTENT_PERMISSIONS_VIEW_CHECKBOX",
			roleTitle = "Guest");

		Refresh();

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates");

		SiteTemplates.gotoViewPages(
			siteName = "${siteName}",
			siteTemplateName = "Site Template Name");

		AssertClick(
			locator1 = "Home#PAGE_TEMPLATE_3",
			value1 = "Page2 Name");

		Portlet.addPG(
			defaultURL = "false",
			portletName = "IFrame");

		Navigator.gotoSitePage(
			pageName = "Page1 Name",
			siteName = "Site Name");

		PermissionsInline.gotoConfigurationPG(portletName = "IFrame");

		Role.viewPermissionPG(
			permissionDefinitionKey = "CONTENT_PERMISSIONS_VIEW_CHECKBOX",
			permissionFlagged = "false",
			roleTitle = "Guest");
	}

	@priority = "3"
	test SiteAdminCannotDeletePagesThatWereCreatedBySiteTemplates {
		SiteTemplates.addCP(siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "User Groups");

		UserGroup.addCP(
			selectSiteTemplate = "true",
			siteTemplateName = "Site Template Name",
			userGroupName = "UG UserGroup Name");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "User Groups");

		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Manage Pages");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		Page.viewNoDeletePagesAssociatedWithSiteTemplate(
			page = "Home",
			pageName = "Public Pages");
	}

	@priority = "4"
	test SitesDynamicTemplateInheritance {
		property custom.properties = "jsonws.web.service.paths.excludes=";
		property portal.acceptance = "true";

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		SiteTemplates.addCP(siteTemplateName = "Site Template Name");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Site Templates");

		SiteTemplates.gotoViewPages(
			siteName = "${siteName}",
			siteTemplateName = "Site Template Name");

		ContentPages.clickPencil();

		PageEditor.addWidget(portletName = "Message Boards");

		PageEditor.clickPublish();

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		JSONUser.addUserToSite(
			groupName = "Guest",
			userEmailAddress = "userea@liferay.com");

		JSONRole.assignRoleToUser(
			roleTitle = "Administrator",
			userEmailAddress = "userea@liferay.com");

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "User Groups");

		UserGroup.addCP(
			selectSiteTemplate = "true",
			siteTemplateName = "Site Template Name",
			userGroupName = "UG UserGroup Name");

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "test@liferay.com");
	}

	@priority = "3"
	test SitesTemplateResourceInheritance {
		property portal.release = "false";

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		SiteTemplates.addCP(siteTemplateName = "Site Template Name");

		SiteTemplates.addSitePageViaSiteTemplateCP(
			pageName = "Test Page Name",
			siteTemplateName = "Site Template Name");

		SiteTemplatesNavigator.gotoSiteTemplatesPage(
			pageName = "Test Page Name",
			siteName = "${siteName}",
			siteTemplateName = "Site Template Name");

		Portlet.addPG(portletName = "Documents and Media");

		SiteTemplates.enableActionsOnDMInSiteTemplate();

		SiteTemplatesNavigator.gotoSiteTemplatesPage(
			pageName = "Test Page Name",
			siteName = "${siteName}",
			siteTemplateName = "Site Template Name");

		DMDocument.addPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.png",
			dmDocumentTitle = "Document_1.png");

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.addPersonalSiteCP(
			siteTemplateName = "Site Template Name",
			userScreenName = "usersn");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordCP(
			userEmailAddress = "userea@liferay.com",
			userScreenName = "usersn");

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "userea@liferay.com");

		UserBar.gotoDropdownItem(dropdownItem = "My Dashboard");

		Navigator._gotoPage(pageName = "Test Page Name");

		DMDocument.viewPG(
			dmDocumentDescription = "DM Document Description",
			dmDocumentFile = "Document_1.png",
			dmDocumentTitle = "Document_1.png");

		User.firstLoginPG(
			password = "test",
			userEmailAddress = "test@liferay.com");
	}

	@description = "This is a use case for LPS-86487."
	@priority = "4"
	test SiteTemplatePageLinkInWebContentRedirectCorrectly {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "SitetemplatesUsecase#SiteTemplatePageLinkInWebContentRedirectCorrectly";

		var portalURL = PropsUtil.get("portal.url");
		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		SiteTemplates.addCP(siteTemplateName = "Template Site");

		SiteTemplates.addSitePageViaSiteTemplateCP(
			pageName = "Page1 Name",
			siteTemplateName = "Template Site");

		Navigator.openURL();

		SiteTemplates.addSitePageViaSiteTemplateCP(
			pageName = "Page2 Name",
			siteTemplateName = "Template Site");

		SiteTemplatesNavigator.gotoSiteTemplatesPage(
			pageName = "Page2 Name",
			siteName = "${siteName}",
			siteTemplateName = "Template Site");

		var page2URL = Navigator.getCurrentURL();
		var entryExternalURL = StringUtil.replace("${page2URL}", "http://", "");

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			portlet = "Web Content");

		WebContentNavigator.gotoAddCP();

		PortletEntry.inputTitle(title = "Web Content Title");

		CKEditor.addEntryExternalURL(
			displayText = "${page2URL}",
			entryExternalURL = "${entryExternalURL}",
			fieldLabel = "content");

		PortletEntry.publish();

		Close.closeAndSelectWindow(locator1 = "Site Templates - ${siteName}");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addCP(
			siteName = "Site Name",
			siteType = "Template");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "Page1 Name",
			widgetName = "Web Content Display");

		Navigator.gotoSitePage(
			pageName = "Page1 Name",
			siteName = "Site Name");

		WebContentDisplayPortlet.selectWebContent(
			portletName = "Web Content Display",
			webContentTitle = "Web Content Title");

		Navigator.gotoSitePage(
			pageName = "Page1 Name",
			siteName = "Site Name");

		Click(locator1 = "WCD#WEB_CONTENT_LINK");

		Pause(locator1 = "1000");

		AssertLocation(value1 = "${portalURL}/web/site-name/page2-name");
	}

	@description = "This is a use case for LPS-74081."
	@priority = "4"
	test ViewImportedSitePageWithNumericFriendlyURL {
		var portalURL = PropsUtil.get("portal.url");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addCP(
			siteName = "Export",
			siteType = "Blank");

		JSONLayout.addPublicLayout(
			groupName = "Export",
			layoutName = "1");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "1");

		AssertTextEquals(
			locator1 = "TextInput#FRIENDLY_URL",
			value1 = "/1");

		JSONLayout.addPublicLayout(
			groupName = "Export",
			layoutName = "2");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "2");

		AssertTextEquals(
			locator1 = "TextInput#FRIENDLY_URL",
			value1 = "/2");

		JSONLayout.addPublicLayout(
			groupName = "Export",
			layoutName = "Hello");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Hello");

		AssertTextEquals(
			locator1 = "TextInput#FRIENDLY_URL",
			value1 = "/hello");

		LAR.exportSiteCP(
			larFileName = "Export",
			siteScopeName = "Export");

		var larFileName = LAR.getLarFileName();

		LAR.downloadLar();

		SiteTemplates.addCP(siteTemplateName = "Template Site");

		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addCP(
			siteName = "New Site",
			siteType = "Template");

		LAR.importSiteCP(
			larFileName = "${larFileName}",
			siteName = "New Site");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		Page.viewPagesOrder(
			depth = "2",
			pageName1 = "Home",
			pageName2 = "1",
			pageName3 = "2",
			pageName4 = "Hello");
	}

	@description = "This is a use case for LPS-107677."
	@priority = "3"
	test ViewImportedSitePageWithoutInfoMessage {
		SiteTemplates.addCP(
			siteTemplateActive = "Yes",
			siteTemplateName = "Site Template Name");

		Site.openSitesAdmin();

		Site.addSiteTemplateCP(
			siteName = "Site A",
			siteTemplateName = "Site Template Name");

		Navigator.gotoSitePage(
			pageName = "Home",
			siteName = "Site A");

		SiteTemplates.viewNoInfoMessage();

		SiteTemplatesNavigator.openSiteTemplatesAdmin();

		LAR.exportPortlet(
			larFileName = "site-template-name.lar",
			portletName = "Site Templates");

		LAR.exportSiteCP(siteScopeName = "Site A");

		var key_larFileName = "${larFileName}";

		var larFileName = LAR.getLarFileName();

		LAR.downloadLar();

		JSONGroup.deleteGroupByName(groupName = "Site A");

		SiteTemplatesNavigator.openSiteTemplatesAdmin();

		SiteTemplates.deleteCP(siteTemplateName = "Site Template Name");

		SiteTemplatesNavigator.openSiteTemplatesAdmin();

		LAR.importPortlet(larFileName = "site-template-name.lar");

		SiteTemplates.viewCP(siteTemplateName = "Site Template Name");

		JSONGroup.addGroup(groupName = "Site B");

		LAR.importSiteCP(
			larFileName = "${larFileName}",
			siteName = "Site B");

		Navigator.gotoSitePage(
			pageName = "Home",
			siteName = "Site B");

		SiteTemplates.viewNoInfoMessage();
	}

	@priority = "3"
	test ViewInlineGuestPermissionsInSiteTemplatesViaWCD {
		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		SiteTemplates.addCP(siteTemplateName = "Site Template Name");

		SiteTemplates.addSitePageViaSiteTemplateCP(
			pageName = "Test Page Name",
			siteTemplateName = "Site Template Name");

		SiteTemplatesNavigator.gotoSiteTemplatesPage(
			pageName = "Test Page Name",
			siteName = "${siteName}",
			siteTemplateName = "Site Template Name");

		Portlet.addPG(
			defaultURL = "false",
			portletName = "Web Content Display");

		SiteTemplatesNavigator.gotoSiteTemplatesPage(
			pageName = "Test Page Name",
			siteName = "${siteName}",
			siteTemplateName = "Site Template Name");

		PermissionsInline.gotoConfigurationPG(
			portletName = "Web Content Display",
			siteTemplate = "true");

		Role.viewPermissionPG(
			permissionDefinitionKey = "CONTENT_PERMISSIONS_VIEW_CHECKBOX",
			permissionFlagged = "true",
			roleTitle = "Guest");
	}

	@description = "This is a use case for LPS-92663."
	@priority = "4"
	test ViewNavigationMenuCopyFromSitesTemplate {
		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		SiteTemplates.addCP(
			siteTemplateActive = "Yes",
			siteTemplateName = "Site Template Name");

		var siteTemplateID = SiteTemplates.getSiteTemplateID(siteTemplateName = "Site Template Name");

		SiteTemplates.addSitePageViaSiteTemplateCP(
			pageName = "Page1 Name",
			siteTemplateName = "Site Template Name");

		SiteTemplates.addSitePageViaSiteTemplateCP(
			pageName = "Page2 Name",
			siteTemplateName = "Site Template Name");

		NavigationMenusAdmin.openNavigationMenusAdmin(siteURLKey = "${siteTemplateID}");

		NavigationMenusAdmin.addMenu(menuName = "Test Navigation Menu Name");

		NavigationMenusAdmin.addItem(
			item = "Page",
			pageNames = "Page1 Name,Page2 Name",
			privacy = "Private Pages");

		NavigationMenusAdmin.viewItem(itemName = "Page1 Name");

		NavigationMenusAdmin.viewItem(itemName = "Page2 Name");

		NavigationMenusAdmin.addItem(
			item = "URL",
			url = "https://www.liferay.com/",
			urlName = "Liferay");

		NavigationMenusAdmin.viewItem(itemName = "Liferay");

		NavigationMenusAdmin.addItem(
			item = "URL",
			url = "https://www.google.com/",
			urlName = "Google");

		NavigationMenusAdmin.viewItem(itemName = "Google");

		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteTemplateID}");

		NavigationMenusWidget.gotoNavigationMenuWidgetConfig();

		NavigationMenusWidget.chooseMenu(menuName = "Test Navigation Menu Name");

		Portlet.saveAndCloseConfig();

		AssertVisible(
			key_pageName = "Page1 Name",
			locator1 = "Home#PAGE");

		AssertVisible(
			key_pageName = "Page2 Name",
			locator1 = "Home#PAGE");

		AssertVisible(
			key_pageName = "Liferay",
			locator1 = "Home#PAGE");

		AssertVisible(
			key_pageName = "Google",
			locator1 = "Home#PAGE");

		Site.openSitesAdmin();

		Site.addSiteTemplateCP(
			siteName = "Site Name",
			siteTemplateName = "Site Template Name");

		Navigator.openSiteURL(siteName = "Site Name");

		AssertVisible(
			key_pageName = "Page1 Name",
			locator1 = "Home#PAGE");

		AssertVisible(
			key_pageName = "Page2 Name",
			locator1 = "Home#PAGE");

		AssertVisible(
			key_pageName = "Liferay",
			locator1 = "Home#PAGE");

		AssertVisible(
			key_pageName = "Google",
			locator1 = "Home#PAGE");
	}

	@description = "This is a use case for LPS-113145."
	@priority = "3"
	test ViewPortletConfigurationOfGlobalTemplatesPageCopyFromSitesTemplate {
		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		SiteTemplates.addCP(
			siteTemplateActive = "Yes",
			siteTemplateName = "Site Template Name");

		var siteTemplateID = SiteTemplates.getSiteTemplateID(siteTemplateName = "Site Template Name");

		SiteTemplates.addSitePageViaSiteTemplateCP(
			inheritChanges = "disable",
			pageName = "Page Name",
			pageTemplateName = "Blog",
			siteTemplateName = "Site Template Name");

		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteTemplateID}/page-name");

		Portlet.addPG(portletName = "Asset Publisher");

		AssetPublisherPortlet.configureManualAssetSelectionPG();

		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteTemplateID}/page-name");

		AssetPublisherPortlet.viewManualAssetSelectionConfigurationPG(portletName = "Asset Publisher");

		Site.openSitesAdmin();

		Site.addSiteTemplateCP(
			siteName = "Site Name",
			siteTemplateName = "Site Template Name");

		Navigator.gotoSitePage(
			pageName = "Page Name",
			siteName = "Site Name");

		AssetPublisherPortlet.viewManualAssetSelectionConfigurationPG(portletName = "Asset Publisher");
	}

}
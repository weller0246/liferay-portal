@component-name = "portal-frontend-infrastructure"
definition {

	property custom.properties = "upgrade.database.auto.run=false";
	property osgi.modules.includes = "frontend-data-set-sample-web";
	property portal.acceptance = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Frontend Dataset";
	property testray.main.component.name = "User Interface";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Frontend Data Set Test Page");

		JSONLayout.updateLayoutTemplateOfPublicLayout(
			groupName = "Guest",
			layoutName = "Frontend Data Set Test Page",
			layoutTemplate = "1 Column");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Guest",
			layoutName = "Frontend Data Set Test Page",
			widgetName = "Frontend Data Set Sample");

		Navigator.gotoPage(pageName = "Frontend Data Set Test Page");

		WaitForElementPresent(
			key_itemName = "Sample1",
			locator1 = "FrontendDataSet#TABLE_ITEM_ROW");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONLayout.deletePublicLayout(
				groupName = "Guest",
				layoutName = "Frontend Data Set Test Page");
		}
	}

	@description = "LPS-147914: Verify alert message is present when action button is clicked"
	@priority = "4"
	test ActionsCanBeCustomized {
		property custom.properties = "upgrade.database.auto.run=false";
		property test.name.skip.portal.instance = "FrontendDataSet#ActionsCanBeCustomized";

		task ("When Click on 1st action button in FDS sample") {
			MouseOver(
				key_itemName = "Sample1",
				locator1 = "FrontendDataSet#TABLE_ITEM_ROW");

			ClickNoError(
				key_title = "Sample1",
				locator1 = "FrontendDataSet#VIEW_ACTION_BUTTON");
		}

		task ("Then 1st alert message is present") {
			AssertConfirm.assertConfirmationNoError(value1 = "Hello Sample1!");
		}

		task ("When Click on 3rd action button in FDS sample") {
			MouseOver(
				key_itemName = "Sample3",
				locator1 = "FrontendDataSet#TABLE_ITEM_ROW");

			ClickNoError(
				key_title = "Sample3",
				locator1 = "FrontendDataSet#VIEW_ACTION_BUTTON");
		}

		task ("Then 3rd alert message is present") {
			AssertConfirm.assertConfirmationNoError(value1 = "Hello Sample3!");
		}
	}

}
@component-name = "portal-segmentation"
definition {

	property custom.properties = "jsonws.web.service.paths.excludes=";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Segmentation";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		task ("Add segment in Test Site Name") {
			JSONSegmentsentry.addSegment(
				fieldName = "Email Address",
				groupName = "Test Site Name",
				operator = "contains",
				segmentName = "EditSegment Test",
				text = "liferay");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		Navigator.openURL();

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "LPS-94874 Bug: Can't edit segment if the segment has a select input"
	@priority = "5"
	@uitest
	test CanEditSegmentIfHaveASelectInput {
		property portal.acceptance = "true";

		task ("Go to segments portlet") {
			Segmentation.openSegmentsAdmin(siteURLKey = "test-site-name");
		}

		task ("Edit the segment with a select input") {
			Segmentation.editSegment(entry = "EditSegment Test");

			AssertChecked.assertCheckedNotVisible(
				checkboxName = "",
				locator1 = "Checkbox#ANY_CHECKBOX_CHECKED");

			Segmentation.deleteProperties();

			Segmentation.addProperties(propertyOption = "Regular Role");

			Segmentation.fillProperty(selectCheckbox = "Administrator");

			Segmentation.fillTitle(titleField = "EditSegmentIfHaveASelectInput Test");

			Segmentation.viewMembers(memberName = "Test Test");

			Button.clickSave();
		}

		task ("Assert segment edited") {
			Segmentation.editSegment(entry = "EditSegmentIfHaveASelectInput Test");

			Segmentation.viewSegment(
				criterion = "select-input",
				value = "Administrator");

			Segmentation.fillProperty(selectCheckbox = "Power User");

			Button.clickSave();

			Segmentation.editSegment(entry = "EditSegmentIfHaveASelectInput Test");

			AssertChecked.assertCheckedNotVisible(
				checkboxName = "",
				locator1 = "Checkbox#ANY_CHECKBOX_CHECKED");

			AssertValue(
				key_criterion = "select-input",
				locator1 = "Segmentation#CRITERION_INPUT_FORM_CONTROL",
				value1 = "Power User");
		}

		task ("Assert the segment cannot be edited with the toggle off") {
			Uncheck.uncheckNotVisible(
				checkboxName = "",
				locator1 = "Checkbox#ANY_CHECKBOX_CHECKED");

			AssertNotChecked.assertNotCheckedNotVisible(
				checkboxName = "",
				locator1 = "Checkbox#ANY_CHECKBOX_CHECKED");

			AssertElementNotPresent(locator1 = "Button#SELECT");
		}
	}

	@description = "LPS-102740 Validate if the user can edit a segment with Properties Country"
	@priority = "5"
	@uitest
	test EditSegmentUserByOrganizationCountry {
		property portal.acceptance = "true";

		task ("Go to segments portlet") {
			Segmentation.openSegmentsAdmin(siteURLKey = "test-site-name");
		}

		task ("Edit the segment") {
			Segmentation.editSegment(entry = "EditSegment Test");

			Segmentation.fillTitle(titleField = "EditSegmentUserByCountry Test");

			Segmentation.deleteProperties();

			Segmentation.gotoSidebar(fieldType = "Organization");

			Segmentation.addProperties(propertyOption = "Country");

			Segmentation.changeCriterionInput(criterionInputOption = "Equals");

			Segmentation.fillProperty(optionsValue = "China");

			Button.clickSave();
		}

		task ("Assert segment edited") {
			Segmentation.editSegment(entry = "EditSegmentUserByCountry Test");

			AssertValue(
				key_criterion = "options",
				locator1 = "Segmentation#CRITERION_INPUT_FORM_CONTROL",
				value1 = "china");
		}
	}

	@description = "LPS-102740 Validate if the user can edit a segment with Properties Region"
	@priority = "5"
	@uitest
	test EditSegmentUserByOrganizationRegion {
		property portal.acceptance = "true";

		task ("Go to segments portlet") {
			Segmentation.openSegmentsAdmin(siteURLKey = "test-site-name");
		}

		task ("Edit the segment") {
			Segmentation.editSegment(entry = "EditSegment Test");

			Segmentation.deleteProperties();

			Segmentation.gotoSidebar(fieldType = "Organization");

			Segmentation.addProperties(propertyOption = "Region");

			Segmentation.fillProperty(optionsValue = "Spain - Madrid");

			Segmentation.fillTitle(titleField = "EditSegmentUserByRegion Test");

			Button.clickSave();

			Alert.viewSuccessMessage();
		}

		task ("Assert segment edited") {
			Segmentation.editSegment(entry = "EditSegmentUserByRegion Test");

			AssertValue(
				key_criterion = "options",
				locator1 = "Segmentation#CRITERION_INPUT_FORM_CONTROL",
				value1 = "madrid");
		}
	}

	@description = "LPS-102743 Validate if the user can edit a segment with Properties Session URL"
	@priority = "5"
	@uitest
	test EditSegmentUserBySessionURL {
		property portal.acceptance = "true";

		task ("Go to segments portlet") {
			Segmentation.openSegmentsAdmin(siteURLKey = "test-site-name");
		}

		task ("Edit the segment") {
			Segmentation.editSegment(entry = "EditSegment Test");

			Segmentation.fillTitle(titleField = "EditSegmentUserBySessionURL Test");

			Segmentation.deleteProperties();

			Segmentation.gotoSidebar(fieldType = "Session");

			Segmentation.addProperties(propertyOption = "URL");

			Segmentation.fillProperty(propertyField = "http://localhost:8080");

			Button.clickSave();
		}

		task ("Assert segment edited") {
			Segmentation.editSegment(entry = "EditSegmentUserBySessionURL Test");

			AssertValue(
				locator1 = "Segmentation#PROPERTY_FIELD",
				value1 = "http://localhost:8080");
		}
	}

	@description = "Validate if the user can edit a segment with User Tag"
	@priority = "5"
	@uitest
	test EditSegmentUserByUserTag {
		property portal.acceptance = "true";

		task ("Add user with tag") {
			JSONUser.addUser(
				assetTagNames = "tagname",
				userEmailAddress = "userea@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");
		}

		task ("Go to segments portlet") {
			Segmentation.openSegmentsAdmin(siteURLKey = "test-site-name");
		}

		task ("Edit the segment") {
			Segmentation.editSegment(entry = "EditSegment Test");

			Segmentation.deleteProperties();

			Segmentation.addProperties(propertyOption = "Tag");

			Segmentation.fillProperty(selectLink = "tagname");

			Segmentation.fillTitle(titleField = "EditSegmentUserByUserTag Test");

			Button.clickSave();

			Alert.viewSuccessMessage();
		}

		task ("Assert segment edited") {
			Segmentation.editSegment(entry = "EditSegmentUserByUserTag Test");

			AssertValue(
				key_criterion = "select-input",
				locator1 = "Segmentation#CRITERION_INPUT_FORM_CONTROL",
				value1 = "tagname");
		}
	}

	@description = "Validate if the user can edit a segment with Properties User and Email Segment with Contains to Equals Option"
	@priority = "5"
	@uitest
	test EditSegmentUserEmailAddressContainsToEquals {
		property portal.acceptance = "true";

		task ("Go to segments portlet") {
			Segmentation.openSegmentsAdmin(siteURLKey = "test-site-name");
		}

		task ("Edit the segment") {
			Segmentation.editSegment(entry = "EditSegment Test");

			Segmentation.fillTitle(titleField = "EditSegmentUserEmailAddressContainsToEquals Test");

			Segmentation.changeCriterionInput(criterionInputOption = "Equals");

			Button.clickSave();
		}

		task ("Assert segment edited") {
			Segmentation.editSegment(entry = "EditSegmentUserEmailAddressContainsToEquals Test");

			AssertValue(
				locator1 = "Segmentation#CRITERION_INPUT",
				value1 = "eq");
		}
	}

}